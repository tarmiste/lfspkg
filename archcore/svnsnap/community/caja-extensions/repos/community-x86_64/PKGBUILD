# Maintainer:
# Contributor: Martin Wimpress <code@flexion.org>

pkgbase=caja-extensions
pkgname=(caja-extensions-common caja-gksu caja-image-converter caja-open-terminal caja-sendto caja-share caja-wallpaper caja-xattr-tags)
pkgver=1.18.2
pkgrel=1
pkgdesc="Caja extensions"
url="https://mate-desktop.org"
arch=('i686' 'x86_64')
license=('GPL')
depends=('caja' 'dbus-glib' 'gksu' 'gupnp' 'imagemagick' 'samba')
makedepends=('intltool')
source=("https://pub.mate-desktop.org/releases/${pkgver%.*}/$pkgbase-$pkgver.tar.xz")
sha256sums=('6aa9e7da54c1e660eb714a934654cd7d44303a7d4df7be5d2ee3fe337d1bb218')

build() {
    cd ${pkgbase}-${pkgver}
    PYTHON=/usr/bin/python2 ./configure \
        --prefix=/usr

    #https://bugzilla.gnome.org/show_bug.cgi?id=656231
    sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

    make
}

package_caja-extensions-common() {
    pkgdesc="Caja extensions (common files)"
    depends=()

    cd ${pkgbase}-${pkgver}
    make -C po DESTDIR="${pkgdir}" install
}

package_caja-gksu() {
    pkgdesc="Privilege granting extension for Caja using gksu"
    depends=('caja' 'caja-extensions-common' 'gksu')
    groups=('mate-extra')
    conflicts=('caja-gksu-gtk3')
    replaces=('caja-gksu-gtk3')

    cd ${pkgbase}-${pkgver}
    make -C gksu DESTDIR="${pkgdir}" install
}

package_caja-image-converter() {
    pkgdesc="A Caja extension for simple image conversions"
    depends=('caja' 'caja-extensions-common' 'imagemagick')
    groups=('mate-extra')
    conflicts=('caja-image-converter-gtk3')
    replaces=('caja-image-converter-gtk3')

    cd ${pkgbase}-${pkgver}
    make -C image-converter DESTDIR="${pkgdir}" install
}

package_caja-open-terminal() {
    pkgdesc="A Caja extension for opening terminals in arbitrary local paths"
    depends=('caja' 'caja-extensions-common')
    groups=('mate-extra')
    conflicts=('caja-open-terminal-gtk3')
    replaces=('caja-open-terminal-gtk3')

    cd "${srcdir}/${pkgbase}-${pkgver}"
    make -C open-terminal DESTDIR="${pkgdir}" install
}

package_caja-sendto() {
    pkgdesc="A Caja extension for sending files"
    depends=('caja' 'caja-extensions-common' 'dbus-glib' 'gupnp')
    groups=('mate-extra')
    optdepends=('pidgin: Support for sending to instant messaging networks.'
                'gajim: Support for sending to Jabber.')
    conflicts=('caja-sendto-gtk3')
    replaces=('caja-sendto-gtk3')

    cd ${pkgbase}-${pkgver}
    make -C sendto DESTDIR="${pkgdir}" install
}

package_caja-share() {
    pkgdesc="A Caja extension to quickly share a folder"
    depends=('caja' 'caja-extensions-common' 'samba')
    groups=('mate-extra')
    conflicts=('caja-share-gtk3')
    replaces=('caja-share-gtk3')    

    cd ${pkgbase}-${pkgver}
    make -C share DESTDIR="${pkgdir}" install
}

package_caja-wallpaper() {
    pkgdesc="A Caja extension to quickly set wallpaper"
    depends=('caja' 'caja-extensions-common')
    groups=('mate-extra')
    conflicts=('caja-wallpaper-gtk3')
    replaces=('caja-wallpaper-gtk3')
    
    cd ${pkgbase}-${pkgver}
    make -C wallpaper DESTDIR="${pkgdir}" install
}

package_caja-xattr-tags() {
    pkgdesc="A Caja extension to see tags stored on xattrs"
    depends=('caja' 'caja-extensions-common')
    groups=('mate-extra')
    
    cd ${pkgbase}-${pkgver}
    make -C xattr-tags DESTDIR="${pkgdir}" install
}
