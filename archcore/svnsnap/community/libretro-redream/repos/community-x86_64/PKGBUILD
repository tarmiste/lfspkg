# $Id: PKGBUILD 277614 2017-12-31 19:35:09Z alucryd $
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=libretro-redream
pkgver=1077
pkgrel=1
pkgdesc='Sega Dreamcast core'
arch=('x86_64')
url='https://github.com/libretro/redream'
license=('GPL2')
groups=('libretro')
depends=('gcc-libs' 'glibc' 'libgl' 'libretro-core-info')
makedepends=('git')
_commit='e032445a089b90e597eed77baa0aa316614b92b7'
source=("libretro-redream::git+https://github.com/libretro/redream.git#commit=${_commit}")
sha256sums=('SKIP')

pkgver() {
  cd libretro-redream

  git rev-list --count HEAD
}

build() {
  cd libretro-redream/deps/libretro

  make
}

package() {
  cd libretro-redream/deps/libretro

  install -Dm 644 redream_libretro.so -t "${pkgdir}"/usr/lib/libretro/
}

# vim: ts=2 sw=2 et:
