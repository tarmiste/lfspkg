# Maintainer: Thore BÃ¶decker <foxxx0@archlinux.org>
# Contributor: Tim Meusel <tim@bastelfreak.de>
pkgbase='python-pecan'
pkgname=('python2-pecan' 'python-pecan')
pkgdesc='A WSGI object-dispatching web framework, designed to be lean and fast with few dependencies.'
pkgver=1.2.1
pkgrel=4
arch=('any')
url='https://github.com/pecan/pecan'
license=('BSD')
makedepends=('python' 'python2' 'python-setuptools' 'python2-setuptools' 'python-sphinx' 'python2-sphinx' 'python-webob' 'python2-webob' 'python-mako' 'python2-mako' 'python-webtest' 'python2-webtest')
checkdepends=('python2-tox' 'python2-singledispatch' 'python2-waitress' 'python2-markupsafe' 'python2-six' 'python2-beautifulsoup4' 'python2-gunicorn' 'uwsgi-plugin-python2')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/pecan/pecan/archive/${pkgver}.tar.gz")
sha512sums=('eced82e4c3ceaaf9b2d6135da9e108b6334f56fc9e356ea05cfa7a7048c41eba019b87e1e793e310b95aa0740314aa6235ff24fc1735f222da741390c44a8735')

prepare() {
  cd "${srcdir}/pecan-${pkgver}"

  # fix manpage build
  sed -i '/^dist = /d' docs/source/conf.py
  sed -i "s/^version = release = .*\$/version = release = '${pkgver}'/" docs/source/conf.py

  # run tests only on platforms we support on Arch Linux
  # upstream currently ships no tests for python3.6, only 3.5 and older 3.X
  sed -i 's/envlist.*/envlist = py27,scaffolds-27,scaffolds-27-rest-api,pep8/' tox.ini
}

check() {
  cd "${srcdir}/pecan-${pkgver}"
  # upstream tests only support python 2
  python2 setup.py test
}

package_python2-pecan() {
  depends=('python2')
  cd "${srcdir}/pecan-${pkgver}"
  python2 setup.py install --root="${pkgdir}/" --optimize=1
  install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"

  cd docs
  PYTHONPATH="${PWD}/../" make man
  install -D -m644 "${srcdir}/pecan-${pkgver}/docs/build/man/pecan.1" "${pkgdir}/usr/share/man/man1/${pkgname}.1"

  # fix conflicting files
  for file in gunicorn_pecan pecan; do
    mv "${pkgdir}/usr/bin/${file}" "${pkgdir}/usr/bin/${file}2"
  done
}

package_python-pecan() {
  depends=('python')
  cd "${srcdir}/pecan-${pkgver}"
  python setup.py install --root="${pkgdir}/" --optimize=1
  install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"

  cd docs
  PYTHONPATH="${PWD}/../" make man
  install -D -m644 "${srcdir}/pecan-${pkgver}/docs/build/man/pecan.1" "${pkgdir}/usr/share/man/man1/${pkgname}.1"
}
