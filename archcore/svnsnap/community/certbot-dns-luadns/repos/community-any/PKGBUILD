# $Id: PKGBUILD 272770 2017-12-07 07:14:38Z felixonmars $
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=certbot-dns-luadns
pkgver=0.20.0
pkgrel=1
pkgdesc="LuaDNS DNS Authenticator plugin for Certbot"
arch=('any')
license=('Apache')
url="https://pypi.python.org/pypi/$pkgname"
depends=("certbot=$pkgver" 'dns-lexicon' "python-acme=$pkgver" 'python-mock' 'python-setuptools'
         'python-zope-interface')
source=("https://pypi.io/packages/source/c/$pkgname/$pkgname-$pkgver.tar.gz"{,.asc})
validpgpkeys=('148C30F6F7E429337A72D992B00B9CC82D7ADF2C'
              '1B41B758F9A78C7F444C20FA3E5B4152E3ADB1FD'
              'A2CFB51FA275A7286234E7B24D17C995CD9775F2')
sha512sums=('d23b710ba4159917f56690b9df35e5528b7127bc82ac62190900589e5b2ee054e357b15e672a72ecee6777784af1e1d3057ec3934f2f9d084030b1a9c10d02d5'
            'SKIP')

build() {
  cd "$srcdir"/$pkgname-$pkgver
  python setup.py build
}

check() {
  cd "$srcdir"/$pkgname-$pkgver
  python setup.py test
}

package() {
  cd "$srcdir"/$pkgname-$pkgver
  python setup.py install --root="$pkgdir"
}
