# $Id: PKGBUILD 286056 2018-01-23 19:09:17Z felixonmars $
# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=ListLike
pkgname=haskell-listlike
pkgver=4.6
pkgrel=3
pkgdesc="Generic support for list-like structures"
url="https://github.com/sjoerdvisscher/listlike"
license=('custom:BSD3')
arch=('x86_64')
depends=('ghc-libs' 'haskell-dlist' 'haskell-fmlist' 'haskell-semigroups' 'haskell-text'
         'haskell-utf8-string' 'haskell-vector')
makedepends=('ghc' 'haskell-hunit' 'haskell-quickcheck' 'haskell-random')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha512sums=('47f32716b95e1c520701d63c6bebe0be3cb9d5d5dbead3a16f70dc5ddef985d00a414e469e9674036714b7ad70f18cc077893bc03a6d77634b078ce1a730b26d')

build() {
    cd $_hkgname-$pkgver
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir="/usr/share/doc/${pkgname}" --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test
}

package() {
    cd $_hkgname-$pkgver
    
    install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
    install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
    runhaskell Setup copy --destdir="${pkgdir}"
    install -D -m644 "COPYRIGHT" "${pkgdir}/usr/share/licenses/${pkgname}/COPYRIGHT"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/COPYRIGHT"
}
