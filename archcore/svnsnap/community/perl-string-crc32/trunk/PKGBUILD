# $Id: PKGBUILD 270675 2017-11-29 12:57:54Z spupykin $
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: Charles Mauch <cmauch@gmail.com>

pkgname=perl-string-crc32
pkgver=1.5
pkgrel=7
pkgdesc="Perl/CPAN Module String::CRC32 : ZMODEMlike CRC32 generation"
arch=("x86_64")
url="http://search.cpan.org/dist/String-CRC32"
license=("GPL" "PerlArtistic")
options=('!emptydirs')
source=("https://www.cpan.org/authors/id/S/SO/SOENKE/String-CRC32-${pkgver}.tar.gz")
sha256sums=('5a812f1a7b08330fe49f64cd479970cbc2193285c8bb6951f4dd4291c4947054')

build() {
  cd String-CRC32-${pkgver}

  PERL_MM_USE_DEFAULT=1 perl Makefile.PL INSTALLDIRS=vendor

  make
}

package() {
  cd String-CRC32-${pkgver}

  make pure_install doc_install DESTDIR="$pkgdir"

  find "$pkgdir" -name '.packlist' -delete
  find "$pkgdir" -name '*.pod' -delete
# template start; name=perl-binary-module-dependency; version=1;
if [[ $(find "$pkgdir/usr/lib/perl5/" -name "*.so") ]]; then
	_perlver_min=$(perl -e '$v = $^V->{version}; print $v->[0].".".($v->[1]);')
	_perlver_max=$(perl -e '$v = $^V->{version}; print $v->[0].".".($v->[1]+1);')
	depends+=("perl>=$_perlver_min" "perl<$_perlver_max")
fi
# template end;
}
