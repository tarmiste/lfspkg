# $Id: PKGBUILD 264770 2017-10-30 12:21:12Z arodseth $
# Maintainer: Alexander F RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>
# Contributor: arjan <arjan@archlinux.org>

pkgname=allegro
pkgver=5.2.3
pkgrel=2
pkgdesc='Portable library mainly aimed at video game and multimedia programming'
arch=('x86_64' 'i686')
url='http://liballeg.org/'
license=('custom')
makedepends=('cmake' 'git' 'glu' 'libtheora' 'mesa-libgl' 'ninja' 'opusfile')
depends=('dumb' 'gtk2' 'jack' 'libgl' 'libpulse' 'libtheora' 'libxpm' 'libxxf86dga' 'opusfile' 'physfs')
source=("git+https://github.com/liballeg/${pkgname}${pkgver%%.*}#tag=$pkgver.0")
md5sums=('SKIP')

build() {
  mkdir -p build
  cd build

  cmake "../${pkgname}${pkgver%%.*}" \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DWANT_DOCS=OFF \
    -DWANT_PHYSFS=ON \
    -G Ninja
  ninja
}

package() {
  DESTDIR="$pkgdir" ninja -C build install

  install -Dm644 "${pkgname}${pkgver%%.*}/LICENSE.txt" \
    "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# getver: liballeg.org/a5docs/trunk
# vim: ts=2 sw=2 et:
