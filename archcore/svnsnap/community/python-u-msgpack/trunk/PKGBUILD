# Maintainer: Eli Schwartz <eschwartz@archlinux.org>

_pkgname=u-msgpack-python
pkgbase='python-u-msgpack'
pkgname=('python-u-msgpack' 'python2-u-msgpack')
pkgver=2.4.0
pkgrel=1
arch=('any')
url="https://github.com/vsergeev/${_pkgname}"
license=('MIT')
makedepends=('python' 'python2')
source=("https://pypi.io/packages/source/${_pkgname:0:1}/${_pkgname}/${_pkgname}-${pkgver}.tar.gz")
sha256sums=('9c9b0c5ca358c61d9fe9940e84f49b208f17706a883377bfb793637013e3673c')

build() {
  cd "${srcdir}/${_pkgname}-${pkgver}"

  python setup.py build
  python2 setup.py build
}

package_python-u-msgpack() {
  pkgdesc="portable, lightweight MessagePack serializer and deserializer for Python"
  depends=('python')

  cd "${srcdir}/${_pkgname}-${pkgver}"
  python setup.py install --root="${pkgdir}" --optimize=1 --skip-build

  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

package_python2-u-msgpack() {
  pkgdesc="portable, lightweight MessagePack serializer and deserializer for Python2"
  depends=('python2')

  cd "${srcdir}/${_pkgname}-${pkgver}"
  python2 setup.py install --root="${pkgdir}" --optimize=1 --skip-build

  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
