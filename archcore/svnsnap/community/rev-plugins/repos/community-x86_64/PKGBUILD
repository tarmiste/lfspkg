# $Id: PKGBUILD 275813 2017-12-24 16:10:46Z dvzrv $
# Maintainer: David Runge <dave@sleepmap.de>
# Contributor: speps <speps at aur dot archlinux dot org>

pkgname=rev-plugins
_name=REV-plugins
pkgver=0.7.1
pkgrel=4
pkgdesc="LADSPA stereo and ambisonic reverb plugin based on zita-rev1"
arch=('x86_64')
url="https://kokkinizita.linuxaudio.org/linuxaudio/ladspa/"
license=('GPL2')
groups=('ladspa-plugins')
depends=('ladspa')
source=("${url}../downloads/${_name}-${pkgver}.tar.bz2")
sha512sums=('00f984b47347a4f75d809587c654e3e078cb37a6c324bcec09382d1181bd8665a58044505054de75c9ba58972af7e1d3f964c3a5bd925fed4af5cccd1233bd19')

build() {
  cd "${_name}-${pkgver}"
  make
}

package() {
  cd "${_name}-${pkgver}"
  # plugin
  install -Dm755 zita-reverbs.so "${pkgdir}/usr/lib/ladspa/zita-reverbs.so"
  # docs
  install -Dm644 AUTHORS "${pkgdir}/usr/share/doc/${pkgname}/AUTHORS"
  install -Dm644 README "${pkgdir}/usr/share/doc/${pkgname}/README"
}
