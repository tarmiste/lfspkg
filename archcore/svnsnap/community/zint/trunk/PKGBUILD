# $Id: PKGBUILD 268032 2017-11-22 14:29:44Z bgyorgy $
# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgbase=zint
pkgname=(zint zint-qt)
pkgver=2.6.2
pkgrel=1
pkgdesc="Barcode encoding library supporting over 50 symbologies"
arch=('x86_64')
url="http://zint.org.uk/"
license=('GPL')
depends=('qt5-base')
makedepends=('cmake' 'qt5-tools')
source=("https://downloads.sourceforge.net/$pkgname/$pkgname-$pkgver.src.tar.gz")
sha256sums=('bcbaad61f64bf9153603fe453924038aeefabc62c674a178b4af9dd95a779530')

build() {
	cd $pkgbase-$pkgver.src
	cmake -DCMAKE_INSTALL_PREFIX=/usr
	make
}

package_zint() {
	depends=('libpng')

	cd $pkgbase-$pkgver.src
	make DESTDIR="$pkgdir" install

	rm "$pkgdir/usr/bin/zint-qt" \
	   "$pkgdir/usr/include/qzint.h" \
	   "$pkgdir/usr/lib/libQZint.so"*
}

package_zint-qt() {
	pkgdesc="Zint Barcode Studio GUI"
	depends=('qt5-base' 'zint')

	cd $pkgbase-$pkgver.src
	make -C backend_qt DESTDIR="$pkgdir" install
	make -C frontend_qt DESTDIR="$pkgdir" install
	install -Dm644 zint.png "$pkgdir/usr/share/pixmaps/zint.png"
	install -Dm644 zint-qt.desktop "$pkgdir/usr/share/applications/zint-qt.desktop"
}
