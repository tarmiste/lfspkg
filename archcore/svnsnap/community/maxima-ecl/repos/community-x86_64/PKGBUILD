# $Id: PKGBUILD 150832 2012-02-23 12:01:17Z juergen $
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Ronald van Haren <ronald.archlinux.org>
# Contributor: Damir Perisa <damir@archlinux.org>
# Modified to compile against ecl by: maribu

pkgname=maxima-ecl
_pkgname=maxima
pkgver=5.41.0
_eclver=16.1.2
pkgrel=1
pkgdesc="A sophisticated computer algebra system (compiled against ecl)"
arch=(i686 x86_64)
license=(GPL)
url="https://maxima.sourceforge.net"
depends=(ecl-16.1.2 texinfo shared-mime-info)
makedepends=(python emacs patch)
optdepends=('gnuplot: plotting capabilities' 'rlwrap: readline support via /usr/bin/rmaxima' 'tk: graphical xmaxima interface')
conflicts=(maxima)
provides=(maxima)
options=(!zipman) # don't zip info pages or they won't work inside maxima
source=("https://downloads.sourceforge.net/sourceforge/${_pkgname}/${_pkgname}-${pkgver}.tar.gz"
        'build-fasl.patch' 'matrixexp.patch')
sha256sums=('daf8254600f4c7d9f079a0084178aaf036bd97a63b1e77b945f4120d7e401674'
            '90ced3b33361fa24c2b417e0aeba8956892f0965b4a22d57d0c04115f2a3274b'
            'ef1bc6a15fc982ff8c6aa1800bbbd3284d9e060ca27abf9d8c1266632c0c2619')

prepare() {
  cd $_pkgname-$pkgver

# set correct python executable to create docs
  sed -i "s|${PYTHONBIN:-python}|python2|" doc/info/extract_categories.sh

# build maxima ecl library
  patch -p1 -i ../build-fasl.patch
# fix matrix exponentiation
  patch -p1 -i ../matrixexp.patch
}

build() {
  cd $_pkgname-$pkgver

  # set correct python executable to create docs
  sed -i "s|${PYTHONBIN:-python}|python2|" doc/info/extract_categories.sh

  ./configure --prefix=/usr --mandir=/usr/share/man --infodir=/usr/share/info \
	--libexecdir=/usr/lib --enable-ecl --with-default-lisp=ecl
  make -j1
}

package() {
  cd $_pkgname-$pkgver
  make DESTDIR="$pkgdir" emacsdir=/usr/share/emacs/site-lisp/maxima install

  _ecldir="/usr/lib/ecl-$_eclver"
  mkdir -p "$pkgdir/$_ecldir"
  install src/binary-ecl/maxima.fas "$pkgdir/$_ecldir"
}
