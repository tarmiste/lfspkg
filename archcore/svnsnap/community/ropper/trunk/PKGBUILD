# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

pkgbase=ropper
pkgname=('ropper' 'python-ropper' 'python2-ropper')
_pkgname=Ropper
pkgver=1.10.10
pkgrel=2
pkgdesc='Show information about binary files and find gadgets to build rop chains for different architectures'
url='https://github.com/sashs/Ropper'
arch=('any')
license=('GPL2')
makedepends=('python-setuptools' 'python-capstone' 'python-filebytes'
             'python2-setuptools' 'python2-capstone' 'python2-filebytes')
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/sashs/${_pkgname}/archive/v${pkgver}.tar.gz)
sha512sums=('5ef06837a5ceeb61cf12dbf3d529fad5f98b581a655490ee9d62909ace9b2a31885be2c972eba448252299a1de3417452ba5ec33235c8dfb50d477279c7c34b8')

prepare() {
  cp -ra ${_pkgname}-${pkgver}{,-py2}
  sed -r 's|(env python)|\12|' -i ${_pkgname}-${pkgver}-py2/sample.py
}

build() {
  (cd ${_pkgname}-${pkgver}
    python setup.py build
  )
  (cd ${_pkgname}-${pkgver}-py2
    python2 setup.py build
  )
}

check() {
  (cd ${_pkgname}-${pkgver}
    python test.py
  )
  (cd ${_pkgname}-${pkgver}-py2
    python2 test.py
  )
}

package_ropper() {
  depends=('python' "python-ropper>=${pkgver}")
  optdepends=('python-keystone: assemble command support')
  cd ${_pkgname}-${pkgver}
  python setup.py install -O1 --root="${pkgdir}" --skip-build
  install -Dm 644 README.md "${pkgdir}/usr/share/doc/${pkgname}/README.md"
  rm -r "${pkgdir}/usr/lib"
}

package_python-ropper() {
  depends=('python-capstone' 'python-filebytes')
  optdepends=('python-keystone: assemble command support')
  cd ${_pkgname}-${pkgver}
  python setup.py install -O1 --root="${pkgdir}" --skip-build
  install -Dm 644 README.md "${pkgdir}/usr/share/doc/${pkgname}/README.md"
  install -Dm 644 sample.py "${pkgdir}/usr/share/doc/${pkgname}/sample.py"
  rm -r "${pkgdir}/usr/bin"
}

package_python2-ropper() {
  depends=('python2-capstone' 'python2-filebytes')
  optdepends=('python2-keystone: assemble command support')
  cd ${_pkgname}-${pkgver}-py2
  python2 setup.py install -O1 --root="${pkgdir}" --skip-build
  install -Dm 644 README.md "${pkgdir}/usr/share/doc/${pkgname}/README.md"
  install -Dm 644 sample.py "${pkgdir}/usr/share/doc/${pkgname}/sample.py"
  rm -r "${pkgdir}/usr/bin"
}

# vim: ts=2 sw=2 et:
