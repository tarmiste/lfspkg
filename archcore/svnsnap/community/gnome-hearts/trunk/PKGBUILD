# $Id: PKGBUILD 266875 2017-11-15 14:29:11Z foutrelis $
# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>
# Contributor: Neil Darlow <neil@darlow.co.uk>

pkgname=gnome-hearts
pkgver=0.3.1
pkgrel=3
pkgdesc="A card game for GNOME"
arch=('x86_64')
url="http://www.jejik.com/gnome-hearts/"
license=('GPL')
depends=('python2' 'libglade')
makedepends=('intltool' 'rarian')
source=(http://www.jejik.com/files/$pkgname/$pkgname-$pkgver.tar.gz
        drop-libgnomeui.patch)
sha256sums=('4b9477e8bb2d48a86021ae512a713fd405a96d52cf711ff549cfe0d119bf56b3'
            'cc2743c2efbabdc0e40c404ce3d6155d85a959474401823d075bcbb15dae424e')

prepare() {
  cd "$srcdir/$pkgname-$pkgver"

  # Remove libgnomeui dependency
  patch -Np1 -i ../drop-libgnomeui.patch

  autoreconf -fi
}

build() {
  cd "$srcdir/$pkgname-$pkgver"

  ./configure --prefix=/usr
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
