# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Joris Steyn <jorissteyn@gmail.com>
# Contributor: fnord0 <fnord0 AT riseup DOT net>

pkgname=zaproxy
pkgver=2.7.0
pkgrel=1
pkgdesc='Integrated penetration testing tool for finding vulnerabilities in web applications'
url='https://www.owasp.org/index.php/ZAP'
arch=('any')
license=('Apache')
depends=('java-runtime' 'ttf-font')
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/zaproxy/zaproxy/releases/download/${pkgver}/ZAP_${pkgver}_Linux.tar.gz)
sha512sums=('7d98fb930bd179af141215387fb0f62b49e1040cb9942abe09dba1a2bb4df2af05cab18e4b67fbdfd29a3171fd2a8e6e59e7b17aedf9dd7030dfb16e95919488')

prepare() {
  cd ZAP_${pkgver}
  cat > ${pkgname} <<EOF
#!/bin/sh
cd /usr/share/zaproxy
./zap.sh
cd -
EOF
}

package() {
  cd ZAP_${pkgver}
  install -d "${pkgdir}/usr/share/${pkgname}"
  cp -pR . "${pkgdir}/usr/share/${pkgname}"
  install -Dm 755 ${pkgname} "${pkgdir}/usr/bin/${pkgname}"
}

# vim: ts=2 sw=2 et:
