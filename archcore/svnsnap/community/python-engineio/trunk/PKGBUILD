# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

pkgbase=python-engineio
pkgname=('python-engineio' 'python2-engineio')
pkgver=2.0.1
pkgrel=1
pkgdesc='Python implementation of the Engine.IO realtime server'
url='https://github.com/miguelgrinberg/python-engineio'
arch=('any')
license=('MIT')
makedepends=('python-setuptools' 'python-sphinx' 'python-six'
             'python2-setuptools' 'python2-sphinx' 'python2-six')
checkdepends=('python-mock' 'python-eventlet' 'python-pytest' 'python-aiohttp'
              'python2-mock' 'python2-eventlet' 'python2-pytest')
options=('!makeflags')
source=(${pkgbase}-${pkgver}.tar.gz::https://github.com/miguelgrinberg/python-engineio/archive/v${pkgver}.tar.gz)
sha256sums=('0402619cacd202ed24da4a3e6958104913abeab12b7c2ac6551cadbf568527a7')
sha512sums=('799676a6e63e231459013f987bde4af705fb3ad8e807809cff418a46d7301d18681ce4012f8c02ff2da3628a142983e9a165e16e2cdb5839e745ee3eceeb07b0')

prepare() {
  cp -ra ${pkgbase}-${pkgver}{,-py2}
}

build() {
  (cd ${pkgbase}-${pkgver}
    python setup.py build
    make -C docs man text SPHINXBUILD=sphinx-build
  )
  (cd ${pkgbase}-${pkgver}-py2
    python2 setup.py build
    make -C docs man text SPHINXBUILD=sphinx-build2
  )
}

check() {
  (cd ${pkgbase}-${pkgver}
    py.test
  )
  (cd ${pkgbase}-${pkgver}-py2
    py.test2
  )
}

package_python-engineio() {
  depends=('python-six')
  optdepends=(
    'python-eventlet: eventlet support'
    'python-aiohttp: aiohttp support'
  )

  cd ${pkgbase}-${pkgver}
  python setup.py install -O1 --root="${pkgdir}" --skip-build
  install -Dm 644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  install -Dm 644 README.rst "${pkgdir}/usr/share/doc/${pkgname}/README"
  install -Dm 644 docs/_build/text/index.txt "${pkgdir}/usr/share/doc/${pkgname}/DOCUMENTATION"
  install -Dm 644 docs/_build/man/engineio.1 "${pkgdir}/usr/share/man/man1/${pkgname}.1"
}

package_python2-engineio() {
  depends=('python2-six')
  optdepends=(
    'python2-eventlet: eventlet support'
  )

  cd ${pkgbase}-${pkgver}-py2
  python2 setup.py install -O1 --root="${pkgdir}" --skip-build
  install -Dm 644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  install -Dm 644 README.rst "${pkgdir}/usr/share/doc/${pkgname}/README"
  install -Dm 644 docs/_build/text/index.txt "${pkgdir}/usr/share/doc/${pkgname}/DOCUMENTATION"
  install -Dm 644 docs/_build/man/engineio.1 "${pkgdir}/usr/share/man/man1/${pkgname}.1"
}

# vim: ts=2 sw=2 et:
