# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Carsten Feuls <archlinux@carstenfeuls.de>

_gemname=eventmachine
pkgname=ruby-${_gemname}
pkgver=1.2.5
pkgrel=2
pkgdesc='EventMachine: fast, simple event-processing library'
url='http://rubyeventmachine.com'
arch=('x86_64')
license=('Ruby' 'GPL2')
depends=('ruby' 'openssl')
options=('!emptydirs')
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/eventmachine/eventmachine/archive/v${pkgver}.tar.gz)
sha256sums=('2e2e3ef78390a2ed54fc72095f584a12f71392c3cabedf270e3a598571b910a9')
sha512sums=('e924a5bc84d7b8b7fd4e2433229932c91320a1b811ae10f5a23f6ebde0a0a3113064b91174d09e079b24318d8d3e71a5f9dc0287dc3d76458e907cf1890c4dcf')

prepare() {
  cd ${_gemname}-${pkgver}
  sed 's|git ls-files README.md CHANGELOG.md GNU LICENSE|find|' -i ${_gemname}.gemspec
  sed 's|git ls-files|find|' -i ${_gemname}.gemspec
}

build() {
  cd ${_gemname}-${pkgver}
  gem build ${_gemname}.gemspec
}

package() {
  cd ${_gemname}-${pkgver}
  local _gemdir="$(ruby -e'puts Gem.default_dir')"
  gem install --ignore-dependencies --no-user-install -i "${pkgdir}/${_gemdir}" -n "${pkgdir}/usr/bin" ${_gemname}*.gem
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  rm "${pkgdir}/${_gemdir}/cache/${_gemname}-${pkgver}.gem"
  find "${pkgdir}/${_gemdir}" \( -name 'mkmf.log' -or -name 'gem_make.out' \) -delete
}
