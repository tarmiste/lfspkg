# $Id: PKGBUILD 280738 2018-01-09 18:11:51Z spupykin $
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Corrado Primier <cp663137@stud.polimi.it>

pkgname=drupal
pkgver=8.4.4
pkgrel=1
pkgdesc="A PHP-based content management platform"
arch=('any')
url="http://www.drupal.org/"
license=('GPL')
depends=()
install='drupal.install'
backup=('etc/webapps/drupal/.htaccess')
options=(!strip)
source=(https://www.drupal.org/files/projects/$pkgname-$pkgver.tar.gz)
sha256sums=('d2441f8e42f3dcb26f8ef00c8e07ee25c57f5437b0d9ac56119a4aa3930e7a5b')

package() {
  depends=('php71' 'php71-gd')

  _instdir="$pkgdir"/usr/share/webapps/drupal
  mkdir -p ${_instdir}
  cd ${_instdir}
  cp -r "$srcdir"/$pkgname-$pkgver/{*,.htaccess} ${_instdir}

  # move .htaccess
  mkdir -p "$pkgdir"/etc/webapps/drupal
  sed -i '1,1iRequire all denied' ${_instdir}/.htaccess
  mv ${_instdir}/.htaccess "$pkgdir"/etc/webapps/drupal/.htaccess
  ln -s /etc/webapps/drupal/.htaccess ${_instdir}/.htaccess

  # move sites/
  mkdir -p "$pkgdir"/var/lib/drupal/
  mv ${_instdir}/sites "$pkgdir"/var/lib/drupal/sites
  ln -s /var/lib/drupal/sites ${_instdir}/sites

  # apache config example
  cat >"$pkgdir"/etc/webapps/drupal/apache.example.conf <<EOF
	Alias /drupal "/usr/share/webapps/drupal"
	<Directory "/usr/share/webapps/drupal">
		AllowOverride All
		Options FollowSymlinks
		Require all granted
		php_admin_value open_basedir "/srv/:/tmp/:/usr/share/webapps/:/etc/webapps:/usr/share/pear/:/var/lib/drupal"
	</Directory>
EOF
}
