# Maintainer: Jiachen Yang <farseerfc@gmail.com>
# AUR Maintainer: Aetf <aetf at unlimitedcodeworks dot xyz>
# Contributor: carstene1ns <arch carsten-teibes de>

pkgbase=python-smartypants
pkgname=(python-smartypants python2-smartypants)
_pkgname=smartypants
pkgver=2.0.0
pkgrel=2
pkgdesc="Python with the SmartyPants"
url="https://bitbucket.org/livibetter/smartypants.py"
license=('BSD')
arch=('any')
makedepends=('python' 'python2')
md5sums=('00b9105ac7ee1d53ca50c05dca4d5ad8')
source=("https://pypi.python.org/packages/09/dd/08848ea21422a585ecd2dd32c032fd8f75f0f8345225b455f3bf9e5a0feb/${_pkgname}-${pkgver}.tar.gz#md5=${md5sums[0]}")

package_python-smartypants() {
  depends=('python')

  cd "$srcdir/$_pkgname-$pkgver"
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}

package_python2-smartypants() {
  depends=('python2')

  cd "$srcdir/$_pkgname-$pkgver"
  install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"

  # python2 fix
  sed -i 's|#!/usr/bin/env python|#!/usr/bin/env python2|' setup.py
  sed -i 's|#!/usr/bin/python|#!/usr/bin/env python2|' smartypants{.py,}
  python2 setup.py install --root="${pkgdir}" -O1

  # Prevent conflict with python-smartypants
  mv "${pkgdir}/usr/bin/${_pkgname}" "${pkgdir}/usr/bin/${_pkgname}2"
}
