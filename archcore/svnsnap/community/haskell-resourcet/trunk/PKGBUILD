# $Id: PKGBUILD 282951 2018-01-16 06:29:18Z felixonmars $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

_hkgname=resourcet
pkgname=haskell-resourcet
pkgver=1.1.11
pkgrel=1
pkgdesc="Deterministic allocation and freeing of scarce resources."
url="https://github.com/snoyberg/conduit"
license=("custom:BSD3")
arch=('x86_64')
depends=('ghc-libs' 'haskell-exceptions' 'haskell-lifted-base' 'haskell-mmorph'
         'haskell-monad-control' 'haskell-mtl' 'haskell-transformers-base'
         'haskell-transformers-compat' 'haskell-unliftio-core')
makedepends=('ghc' 'haskell-hspec')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha512sums=('1c7428dae7f6c8cc04c942008d859e54313d32ee972229f817c04a8cef29c38e38c095d42bb953b86b8be0090c8c39cef49854658169da710f1501d8e144d4c7')

build() {
    cd "${srcdir}/${_hkgname}-${pkgver}"

    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir="/usr/share/doc/${pkgname}" --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test
}

package() {
    cd "${srcdir}/${_hkgname}-${pkgver}"

    install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
    install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
    runhaskell Setup copy --destdir="${pkgdir}"
    install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"
}
