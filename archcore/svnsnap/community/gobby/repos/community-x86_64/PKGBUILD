# Maintainer: Baptiste Jonglez <archlinux at bitsofnetworks dot org>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: William Rea <sillywilly@gmail.com>
# Contributor: Angel 'angvp' Velasquez <angvp[at]archlinux.com.ve>

pkgname=gobby
_gitver=e5c2d145d020e080c8612f80a3d123f3a024a13a
pkgver=0.6.0+e5c2d1
pkgrel=1
pkgdesc="A collaborative text editor"
arch=('x86_64')
url="https://gobby.github.io/"
license=('GPL')
depends=('gtkmm3' 'gtksourceview3' 'libinfinity' 'libxml++2.6')
makedepends=('gnome-doc-utils' 'intltool')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/gobby/gobby/archive/${_gitver}.tar.gz")
sha512sums=('9f67bc5704caa23b4b3a2b8c64a69c619d0f6aea1295a74b9c7e5fda5a28939b6157957690af68dc19338a7833a07744ec5aac7b38c98df17ddcd4dd797ee425')
validpgpkeys=('B63A7560B25B619CA11E219225A941E879B57591') # Armin Burgmeier <armin@arbur.net>

build() {
  cd "$srcdir"/gobby-$_gitver
  [ -x configure ] || ./autogen.sh
  ./configure --prefix=/usr
  make
}

package() {
  cd "$srcdir"/gobby-$_gitver
  make DESTDIR="$pkgdir" install
}
