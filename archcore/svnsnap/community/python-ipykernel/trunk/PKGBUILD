# $Id: PKGBUILD 269079 2017-11-26 21:24:25Z kkeen $
# Maintainer: Kyle Keen <keenerd@gmail.com>

pkgbase=python-ipykernel
pkgname=(python-ipykernel python2-ipykernel)
pkgver=4.6.1
pkgrel=1
pkgdesc="The ipython kernel for Jupyter"
arch=('any')
url="https://pypi.python.org/pypi/ipykernel"
license=('BSD')
depends=('python-traitlets' 'python-tornado' 'ipython' 'python-jupyter_client')
makedepends=('python-setuptools' 'python2-setuptools' 'ipython2' 'python2-jupyter_client')
source=("$pkgbase-$pkgver.tgz::https://github.com/ipython/ipykernel/archive/$pkgver.tar.gz")
md5sums=('7fdff19d6d687a993052dc6a31a78560')

# dep cycle with python-jupyter_client

prepare() {
  cd "$srcdir"
  cp -r ipykernel-$pkgver python2-ipykernel-$pkgver
}

package_python-ipykernel() {
  cd "$srcdir/ipykernel-$pkgver"
  python3 setup.py install --prefix=/usr --root="$pkgdir" --optimize=0

  cd "$srcdir"
  install -Dm644 "ipykernel-$pkgver/COPYING.md" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

package_python2-ipykernel() {
  depends=('python2-traitlets' 'python2-tornado' 'ipython2' 'python2-jupyter_client')

  cd "$srcdir/python2-ipykernel-$pkgver"
  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=0

  cd "$srcdir"
  install -d "$pkgdir/usr/share/jupyter/kernels/python2"
  echo '{"display_name": "Python 2", "language": "python2", "argv": ["python2", "-m", "ipykernel", "-f", "{connection_file}"],"codemirror_mode": {"version": 2, "name": "ipython2"}}' > "$pkgdir/usr/share/jupyter/kernels/python2/kernel.json"

  install -Dm644 "ipykernel-$pkgver/COPYING.md" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

