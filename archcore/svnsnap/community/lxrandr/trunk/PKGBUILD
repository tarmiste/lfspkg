# $Id: PKGBUILD 281639 2018-01-12 15:48:24Z bgyorgy $
# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Geoffroy Carrier <geoffroy.carrier@koon.fr>

pkgbase=lxrandr
pkgname=(lxrandr lxrandr-gtk3)
pkgver=0.3.1
pkgrel=1
pkgdesc="Monitor configuration tool (part of LXDE)"
arch=('x86_64')
license=('GPL2')
url="https://lxde.org/"
depends=('gtk2' 'gtk3' 'xorg-xrandr')
makedepends=('intltool')
source=("https://downloads.sourceforge.net/lxde/$pkgbase-$pkgver.tar.xz")
md5sums=('b327938f18a4baac85c4707f927d606e')

build() {
  # GTK+ 2 version
  [ -d gtk2 ] || cp -r $pkgbase-$pkgver gtk2
  cd gtk2
  ./configure --sysconfdir=/etc --prefix=/usr
  make

  cd "$srcdir"
  # GTK+ 3 version
  [ -d gtk3 ] || cp -r $pkgbase-$pkgver gtk3
  cd gtk3
  ./configure --sysconfdir=/etc --prefix=/usr --enable-gtk3
  make
}

package_lxrandr() {
  groups=('lxde')
  depends=('gtk2' 'xorg-xrandr')

  cd gtk2
  make DESTDIR="$pkgdir" install
}

package_lxrandr-gtk3() {
  groups=('lxde-gtk3')
  pkgdesc+=' (GTK+ 3 version)'
  depends=('gtk3' 'xorg-xrandr')
  conflicts=('lxrandr')

  cd gtk3
  make DESTDIR="$pkgdir" install
}
