# $Id: PKGBUILD 229853 2017-05-23 21:20:35Z kkeen $
# Maintainer: Kyle Keen <keenerd@gmail.com>
# Contributor: wizzomafizzo <wizzomafizzo@gmail.com>
# Contributor: Mateusz Herych <heniekk@gmail.com>
# Contributor: Benjamin Andresen <benny AT klapmuetz DOT org>
# Contributor: Douglas Thrift <douglas@douglasthrift.net>

pkgname=rlwrap
pkgver=0.43
pkgrel=1
pkgdesc="Adds readline-style editing and history to programs."
arch=('i686' 'x86_64')
url="https://github.com/hanslub42/rlwrap"
license=('GPL')
depends=('perl')
optdepends=('python: filters')
source=($pkgname-$pkgver.tar.gz::https://github.com/hanslub42/rlwrap/archive/v$pkgver.tar.gz)
md5sums=('361d54b7127374e5761e0738eb7b88fb')

prepare() {
  cd "$srcdir/$pkgname-$pkgver"
  autoreconf -vi
}

build() {
  cd "$srcdir/$pkgname-$pkgver"
  ./configure --prefix=/usr
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}
