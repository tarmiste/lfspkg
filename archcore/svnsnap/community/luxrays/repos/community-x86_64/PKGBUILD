# $Id: PKGBUILD 279615 2018-01-06 19:40:17Z bpiotrowski $
# Maintainer: Lukas Jirkovsky <l.jirkovsky@gmail.com>
pkgname='luxrays'
pkgver=1.6
_pkgver=61352e6aae3f
pkgrel=13
pkgdesc="Accelerate the ray intersection process by using GPUs"
arch=('x86_64')
url="http://www.luxrender.net/"
license=('GPL')
depends=('embree' 'opencl-icd-loader' 'libgl' 'openimageio')
makedepends=('boost' 'cmake' 'freetype2' 'gtk3' 'libpng' 'mesa' 'opencl-headers' 'glew' 'freeglut')
optdepends=('amdapp-sdk: OpenCL support for AMD GPUs' \
            'intel-opencl-runtime: OpenCL support for Intel CPUs' \
            'glew: demos (SmallLuxGPU etc.)' \
            'gtk3: luxcoreui'
            'freeglut: demos (SmallLuxGPU etc.)' \
            'boost-libs: demos (SmallLuxGPU etc.)')
options=('staticlibs')
source=($pkgname-$pkgver.tar.bz2::https://bitbucket.org/luxrender/luxrays/get/luxrender_v$pkgver.tar.bz2 \
        force_python3.diff)
md5sums=('4c8df34d90dc04b50a180b945041cd92'
         '36c9823246c2b575415c1709e065727c')

prepare() {
  cd "$srcdir"/luxrender-luxrays-$_pkgver

  # force python3 for boost
  patch -Np1 < "$srcdir/force_python3.diff" || true
}

build() {
  cd "$srcdir/luxrender-luxrays-$_pkgver"

  cmake . \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_SKIP_RPATH=ON \
    -DLUXRAYS_DISABLE_OPENCL=OFF
  # this sucks, but luxrays doesn't seem to honor  CMAKE_*_LINKER_FLAGS
  # thanks bartus from AUR for the fix
  for file in `grep lboost_python -l -R ` ;do sed -i 's/$/ -lpython3/' $file ; done
  make
}

package() {
  cd "$srcdir/luxrender-luxrays-$_pkgver"

  install -d -m755 "$pkgdir"/usr/{bin,include,lib}
  install -m755 bin/* "$pkgdir"/usr/bin
  install -m644 lib/* "$pkgdir"/usr/lib
  cp -a include "$pkgdir"/usr

  # install pyluxcore to the Python search path
  install -d -m755 "$pkgdir"/usr/lib/python3.6/
  mv "$pkgdir"/usr/lib/pyluxcore.so "$pkgdir"/usr/lib/python3.6/
}

# vim:set ts=2 sw=2 et:
