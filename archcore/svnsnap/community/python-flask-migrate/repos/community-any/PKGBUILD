# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Anthony25 <Anthony Ruhier>

_pkgname=Flask-Migrate
pkgbase=python-flask-migrate
pkgname=('python-flask-migrate' 'python2-flask-migrate')
pkgver=2.1.1
pkgrel=2
pkgdesc='SQLAlchemy database migrations for Flask applications using Alembic..'
url='https://github.com/miguelgrinberg/Flask-Migrate/'
arch=('any')
license=('MIT')
makedepends=('python-alembic' 'python-flask' 'python-flask-script' 'python-flask-sqlalchemy' 'python-setuptools' 'python-sphinx'
             'python2-alembic' 'python2-flask' 'python2-flask-script' 'python2-flask-sqlalchemy' 'python2-setuptools' 'python2-sphinx')
source=(${pkgbase}-${pkgver}.tar.gz::https://github.com/miguelgrinberg/Flask-Migrate/archive/v${pkgver}.tar.gz)
sha256sums=('883854c9c811746c1d989f4e707e3815d7d3e43316d6cee832e53897737a90e5')
sha512sums=('b7f81f0b256ac975d287d591e6bc9c2adb50579872d7546512b081d687c1b74002491705ce8ff09ad2ef038f8b120af546f331b5315cd876a4446acd9d2d65ba')

prepare() {
  cp -ra ${_pkgname}-${pkgver}{,-py2}
}

build() {
  (cd ${_pkgname}-${pkgver}
    python setup.py build
    make -j1 -C docs man text SPHINXBUILD=sphinx-build
  )
  (cd ${_pkgname}-${pkgver}-py2
    python2 setup.py build
    make -j1 -C docs man text SPHINXBUILD=sphinx-build2
  )
}

check() {
  (cd ${_pkgname}-${pkgver}
    python setup.py test
  )
  (cd ${_pkgname}-${pkgver}-py2
    python2 setup.py test
  )
}

package_python-flask-migrate() {
  depends=('python-alembic' 'python-flask' 'python-flask-script' 'python-flask-sqlalchemy' 'python-setuptools')

  cd ${_pkgname}-${pkgver}
  python setup.py install --root="${pkgdir}/" --optimize=1 --skip-build
  install -Dm 644 README.md CHANGELOG.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm 644 docs/_build/text/* -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 docs/_build/man/flask-migrate.1 "${pkgdir}/usr/share/man/man1/${pkgname}.1"
}

package_python2-flask-migrate() {
  depends=('python2-alembic' 'python2-flask' 'python2-flask-script' 'python2-flask-sqlalchemy' 'python2-setuptools')

  cd ${_pkgname}-${pkgver}
  python2 setup.py install --root="${pkgdir}/" --optimize=1 --skip-build
  install -Dm 644 README.md CHANGELOG.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm 644 docs/_build/text/* -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 docs/_build/man/flask-migrate.1 "${pkgdir}/usr/share/man/man1/${pkgname}.1"
}

# vim: ts=2 sw=2 et:
