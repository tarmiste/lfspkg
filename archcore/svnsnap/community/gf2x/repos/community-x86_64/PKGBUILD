# $Id: PKGBUILD 242199 2017-07-03 17:18:07Z arojas $
# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=gf2x
pkgver=1.2
pkgrel=1
pkgdesc="A library for multiplying polynomials over the binary field"
arch=(i686 x86_64)
url="http://gforge.inria.fr/projects/gf2x/"
license=(GPL LGPL)
depends=(glibc)
source=("https://gforge.inria.fr/frs/download.php/36934/gf2x-$pkgver.tar.gz")
sha256sums=('61427ffa03b5006aa154def6dce8bcff0fdefb3bd72f43fb1a7b4fdd6b80db34')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --enable-sse2 --disable-pclmul CFLAGS="$CFLAGS -msse2"
  make
}

check() {
  cd $pkgname-$pkgver
  make -k check
}

package() {
  cd $pkgname-$pkgver
  make prefix="$pkgdir"/usr install
}
