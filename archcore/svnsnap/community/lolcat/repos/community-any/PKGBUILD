# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Gordin <9ordin @t gmail dot com>

pkgname=lolcat
pkgver=90.8.8
pkgrel=2
pkgdesc='Okay, no unicorns. But rainbows!!'
url='https://github.com/busyloop/lolcat'
arch=('any')
license=('custom')
depends=('ruby' 'ruby-paint' 'ruby-trollop')
options=('!emptydirs')
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/busyloop/lolcat/archive/v${pkgver}.tar.gz)
sha256sums=('3925c462d594b1882ce6b5378d4526c52103676ebce4fe940527c9768fc6ecff')
sha512sums=('5f9acbaafc41c5035da43fcff03b637206940980bc0a6226bc4aef2d5b2604a8ee10c62fa50c61cc0a89a9c8bd3efd3393d017b94f3fc338a826d83c2b36fb2d')

prepare() {
  cd ${pkgname}-${pkgver}
  sed -r 's|~>|>=|g' -i lolcat.gemspec # don't give a fuck about rubys bla bla
  sed 's|git ls-files -- bin/\*|find bin -type f|' -i lolcat.gemspec
  sed 's|git ls-files -- {test,spec,features}/*|echo|' -i lolcat.gemspec
  sed 's|git ls-files|find -type f|' -i lolcat.gemspec
}

build() {
  cd ${pkgname}-${pkgver}
  gem build lolcat.gemspec
}

package() {
  cd ${pkgname}-${pkgver}
  local _gemdir="$(gem env gemdir)"
  gem install --ignore-dependencies --no-user-install -i "${pkgdir}${_gemdir}" -n "${pkgdir}/usr/bin" ${pkgname}-${pkgver}.gem
  rm "${pkgdir}/${_gemdir}/cache/${pkgname}-${pkgver}.gem"
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: ts=2 sw=2 et:
