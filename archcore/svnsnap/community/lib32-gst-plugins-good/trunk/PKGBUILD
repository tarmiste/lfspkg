# $Id: PKGBUILD 246392 2017-07-26 07:15:59Z felixonmars $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

_basename=gst-plugins-good
pkgname=lib32-gst-plugins-good
pkgver=1.12.2
pkgrel=1
pkgdesc="GStreamer Multimedia Framework Good Plugins (32-bit)"
url="https://gstreamer.freedesktop.org/"
arch=('x86_64')
license=('LGPL')
depends=(lib32-libpulse lib32-jack lib32-libsoup lib32-gst-plugins-base-libs lib32-wavpack
	 lib32-aalib lib32-taglib lib32-libdv lib32-libshout lib32-libvpx lib32-gdk-pixbuf2
         lib32-libcaca lib32-libavc1394 lib32-libiec61883 lib32-libxdamage lib32-v4l-utils
         lib32-cairo lib32-libgudev lib32-speex lib32-flac lib32-libraw1394 gst-plugins-good)
makedepends=(lib32-gstreamer python autoconf-archive git)
checkdepends=(lib32-gst-plugins-base)
options=(!emptydirs)
_commit=188ab74da41dc0d0c62e7918327fba7bdabdf314 # tags/1.12.2^0
source=("git+https://anongit.freedesktop.org/git/gstreamer/gst-plugins-good#commit=$_commit"
        "gst-common::git+https://anongit.freedesktop.org/git/gstreamer/common"
        0001-tests-souphttpsrc-Update-test-cert.pem.patch
        0002-tests-souphttpsrc-Make-ssl_cert-key_file-static.patch
        0003-tests-souphttpsrc-Avoid-deprecated-server-ssl-proper.patch
        0004-tests-souphttpsrc-Avoid-deprecated-ssl-ca-file-prope.patch)
sha512sums=('SKIP'
            'SKIP'
            'b5e675448dc80f32207046a5fa6bd8b59da8d63bf2a0fc35763e39dac343cef4e88564e7fba4aa8ac0408370cf3d50b2f081c139c353e9b79982a03d58d77ef2'
            'b795d1f17c134b2cf3d9ccd1a016f54ab1a963dcd37386f8ec32a80af74844caaa1493a66cf3ed55ca5c9372d67e0bfd3e60671c4e2ec4f09d9a865b3b6a6975'
            'f55e304abda9b62e6bc811f0224585bbfb92de6802dabd24d08b0ff03dcfa97c8857f0be86a5af23ead46373c130f06264e435a44f0aac88b794112a9691245d'
            '672f7ef0d2e5352de589640537a00de9a22fc615568440761b46fee8645989da2653baa73e330f841cb1180acad9779b2067d8084c04cd884a1acacd6fbc3509')

pkgver() {
  cd $_basename
  git describe --tags | sed 's/-//g'
}

prepare() {
  cd $_basename

  # https://bugzilla.gnome.org/show_bug.cgi?id=784005
  patch -Np1 -i ../0001-tests-souphttpsrc-Update-test-cert.pem.patch
  patch -Np1 -i ../0002-tests-souphttpsrc-Make-ssl_cert-key_file-static.patch
  patch -Np1 -i ../0003-tests-souphttpsrc-Avoid-deprecated-server-ssl-proper.patch
  patch -Np1 -i ../0004-tests-souphttpsrc-Avoid-deprecated-ssl-ca-file-prope.patch

  git submodule init
  git config --local submodule.common.url "$srcdir/gst-common"
  git submodule update

  NOCONFIGURE=1 ./autogen.sh
}
 
build() {
  cd $_basename

  export CC='gcc -m32'
  export CXX='g++ -m32'
  export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
    --build=i686-pc-linux-gnu --libdir=/usr/lib32 \
    --with-package-name="GStreamer Good Plugins (Arch Linux)" \
    --with-package-origin="https://www.archlinux.org/" \
    --enable-experimental --disable-static --disable-gtk-doc \

  # https://bugzilla.gnome.org/show_bug.cgi?id=655517
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

  make
}

# FAIL: elements/rtp-payloading
#check() {
# cd $_basename
# make check
#}

package() {
  cd $_basename
  make DESTDIR="${pkgdir}" install

  rm -r "$pkgdir"/usr/share
}
