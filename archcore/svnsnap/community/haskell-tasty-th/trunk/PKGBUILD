# $Id: PKGBUILD 280852 2018-01-10 04:48:29Z felixonmars $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

_hkgname=tasty-th
pkgname=haskell-tasty-th
pkgver=0.1.7
pkgrel=4
pkgdesc="Automatic tasty test case discovery using TH"
url="http://github.com/bennofs/tasty-th"
license=("custom:BSD3")
arch=('x86_64')
depends=('ghc-libs' 'haskell-src-exts' 'haskell-tasty')
makedepends=('ghc' 'haskell-tasty-hunit')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha512sums=('7302220ab27aaed22e064518e915610fd5dc7033a931f862fbc63e8a0b1cadcf9306ab3d54d48a10a200c8c8a62b9259462f513a6350df5a02836c6a29f51aef')

build() {
    cd "${srcdir}/${_hkgname}-${pkgver}"

    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir="/usr/share/doc/${pkgname}" --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test
}

package() {
    cd "${srcdir}/${_hkgname}-${pkgver}"

    install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
    install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
    runhaskell Setup copy --destdir="${pkgdir}"
    install -D -m644 "BSD3.txt" "${pkgdir}/usr/share/licenses/${pkgname}/BSD3.txt"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/BSD3.txt"
}
