# $Id: PKGBUILD 266875 2017-11-15 14:29:11Z foutrelis $
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Gustavo Alvarez <sl1pkn07@gmail.com>

pkgname=vapoursynth-plugin-lsmashsource
pkgver=929
pkgrel=1
pkgdesc='L-SMASH source plugin for Vapoursynth'
arch=('x86_64')
url='https://github.com/VFR-maniac/L-SMASH-Works'
license=('custom')
depends=('glibc' 'vapoursynth'
         'libavcodec.so' 'libavformat.so' 'libavutil.so' 'liblsmash.so'
         'libswscale.so')
makedepends=('git')
_commit='fba174b2606236f526a6863b6baf5983efa08250'
source=("vapoursynth-plugin-lsmashsource::git+https://github.com/VFR-maniac/L-SMASH-Works.git#commit=${_commit}")
sha256sums=('SKIP')

pkgver() {
  cd vapoursynth-plugin-lsmashsource/VapourSynth

  git rev-list --count HEAD
}

build() {
  cd vapoursynth-plugin-lsmashsource/VapourSynth

  ./configure \
    --prefix='/usr'
  make
}

package(){
  cd vapoursynth-plugin-lsmashsource/VapourSynth

  make DESTDIR="${pkgdir}" install

  # Mark library as executable so sodeps get detected and versioned
  chmod +x "${pkgdir}"/usr/lib/libvslsmashsource.so.*

  install -dm 755 "${pkgdir}"/usr/share/doc/vapoursynth/plugins/lsmashsource
  install -m 644 README "${pkgdir}"/usr/share/doc/vapoursynth/plugins/lsmashsource/

  install -dm 755 "${pkgdir}"/usr/share/licenses/vapoursynth-plugin-lsmashsource
  install -m 644 LICENSE "${pkgdir}"/usr/share/licenses/vapoursynth-plugin-lsmashsource/
}

# vim: ts=2 sw=2 et:
