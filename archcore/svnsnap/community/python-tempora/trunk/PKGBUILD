# Maintainer: Thore BÃ¶decker <foxxx0@archlinux.org>
# Contributor: Laurent Soest <laurent.soest(at)gmail.com>

pkgbase='python-tempora'
_pkgbase="${pkgbase//python-/}"
pkgname=('python-tempora' 'python2-tempora')
pkgver=1.9
pkgrel=1
pkgdesc="Objects and routines pertaining to date and time (tempora)."
arch=('any')
url="https://github.com/jaraco/tempora"
license=('MIT')
makedepends=('python-setuptools' 'python2-setuptools' 'python-setuptools-scm'
  'python2-setuptools-scm' 'python-six' 'python2-six' 'python-pytz'
  'python2-pytz')
checkdepends=('python-tox' 'python2-tox' 'python2-backports.unittest_mock')
source=("${pkgbase}-${pkgver}.tar.gz::https://github.com/jaraco/${_pkgbase}/archive/${pkgver}.tar.gz"
        'LICENSE')
sha512sums=('50c11499a02645dddc1a447f16a29920baea6fc0d9d56d84c2a90f87d8d727ddd700e6b274d9f0931d70377154e058ca9f5a1a56a63c61127c5147cdee5a43a7'
            '1106afed483b7258e4ae89c5d9459c3834412b31aac90169725ed62d2ab44f61f6f79e894d4c9e4d8bd99e14530ab778df2187784f0b25eaab86d312fad68944')

prepare() {
    cp -r "${srcdir}/${_pkgbase}-${pkgver}" "${srcdir}/${_pkgbase}-${pkgver}-py2"
}

build() {
    # setuptools wont find version from git tag
    export SETUPTOOLS_SCM_PRETEND_VERSION="${pkgver}"
    cd "${srcdir}/${_pkgbase}-${pkgver}"
    python ./setup.py build

    cd "${srcdir}/${_pkgbase}-${pkgver}-py2"
    python2 ./setup.py build
}

check() {
    cd "${srcdir}/${_pkgbase}-${pkgver}"
    tox

    cd "${srcdir}/${_pkgbase}-${pkgver}-py2"
    tox2
}

package_python-tempora() {
    depends=('python' 'python-six' 'python-pytz')
    cd "${srcdir}/${_pkgbase}-${pkgver}"
    python ./setup.py install --root="${pkgdir}" --optimize=1

    # the author has promised to include a LICENSE file in future releases:
    # https://github.com/jaraco/skeleton/issues/1
    # for now the LICENSE file has been taken from:
    # https://github.com/jaraco/skeleton/blob/skeleton/LICENSE
    install -D -m644 "${srcdir}/LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

package_python2-tempora() {
    depends=('python2' 'python2-six' 'python2-pytz')
    cd "${srcdir}/${_pkgbase}-${pkgver}-py2"
    python2 ./setup.py install --root="${pkgdir}" --optimize=1
    mv "${pkgdir}/usr/bin/calc-prorate" "${pkgdir}/usr/bin/calc-prorate2"

    # the author has promised to include a LICENSE file in future releases:
    # https://github.com/jaraco/skeleton/issues/1
    # for now the LICENSE file has been taken from:
    # https://github.com/jaraco/skeleton/blob/skeleton/LICENSE
    install -D -m644 "${srcdir}/LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
