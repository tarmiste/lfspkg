# $Id: PKGBUILD 266875 2017-11-15 14:29:11Z foutrelis $
# Maintainer: Timothy Redaelli <timothy.redaelli@gmail.com>
# Contributor: DonVla <donvla@users.sourceforge.net>

pkgname=i2c-tools
pkgver=4.0
pkgrel=1
pkgdesc="Heterogeneous set of I2C tools for Linux that used to be part of lm-sensors"
arch=('x86_64')
url="http://www.lm-sensors.org/wiki/I2CTools"
license=('GPL')
depends=('perl')
makedepends=('python2' 'python' 'git')
optdepends=('read-edid: for decode-edid script'
            'python: for smbus module'
            'python2: for smbus module')
_commit=629185af1552b1866052c6973c930f1a26992dfc
source=("git+https://git.kernel.org/pub/scm/utils/i2c-tools/i2c-tools.git#commit=$_commit")
sha256sums=('SKIP')

build() {
  cd "${srcdir}/${pkgname}"
  make
  make -C eepromer CFLAGS="$CFLAGS -I../include"
  cd py-smbus
  CPPFLAGS="$CPPFLAGS -I../include" python setup.py build
  CPPFLAGS="$CPPFLAGS -I../include" python2 setup.py build
}

package() {
  cd "${srcdir}/${pkgname}"
  make prefix="${pkgdir}/usr" sbindir="$pkgdir/usr/bin" install
  install -Dm755 eepromer/eeprom eepromer/eepromer "${pkgdir}/usr/bin"
  cd py-smbus
  python setup.py install --prefix "${pkgdir}/usr"
  python2 setup.py install --prefix "${pkgdir}/usr"
  rm -rf "${pkgdir}/usr/include"
}

# vim:set ts=2 sw=2 et:
