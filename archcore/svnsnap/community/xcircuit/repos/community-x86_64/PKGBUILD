# $Id: PKGBUILD 255681 2017-09-04 17:27:35Z spupykin $
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: Giovanni Scafora <linuxmania@gmail.com>
# Contributor: simo <simo@archlinux.org>

pkgname=xcircuit
pkgver=3.9.70
_pkgver=3.9
pkgrel=1
pkgdesc="A program for drawing publishable-quality electrical circuit schematic diagrams"
arch=('i686' 'x86_64')
url="http://opencircuitdesign.com/xcircuit"
license=('GPL2')
depends=('tk' 'libxpm' 'zlib' 'cairo')
makedepends=('chrpath' 'gendesk')
_categories='Application;Electronics;Engineering'
source=(http://opencircuitdesign.com/xcircuit/archive/$pkgname-$pkgver.tgz)
sha256sums=('277b4239e9fe38eeb1ebbf2062e017dc3ae3b5f473a1173aa03e0ea88c610278')

build() {
  cd "$srcdir"/$pkgname-$pkgver
  gendesk $startdir/PKGBUILD
  ./configure \
    --prefix=/usr \
    --mandir=/usr/share/man \
    --with-cairo \
    --with-tcl=/usr/lib \
    --with-tk=/usr/lib
  make ACLOCAL=aclocal AUTOMAKE=automake
}

package() {
  cd "$srcdir"/$pkgname-$pkgver
  make DESTDIR="$pkgdir" install
  mkdir -p "$pkgdir"/usr/share/man/man1
  mv "$pkgdir"/usr/lib/xcircuit-${_pkgver}/man/* "$pkgdir"/usr/share/man/man1/
  rm -rf "$pkgdir"/usr/lib/xcircuit-${_pkgver}/man/
  chrpath -d "$pkgdir"/usr/lib/xcircuit-${_pkgver}/xcircexec
  install -Dm0644 xcircuit.desktop "$pkgdir"/usr/share/applications/xcircuit.desktop
  install -Dm0644 xcircuit.png "$pkgdir"/usr/share/icons/xcircuit.png
}
