# $Id: PKGBUILD 284265 2018-01-20 10:48:55Z bgyorgy $
# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Alexander F Rødseth <xyproto@archlinux.org>
# Contributor: Bartłomiej Piotrowski <nospam@bpiotrowski.pl>
# Contributor: Robert Hollencamp <rhollencamp@gmail.com>
# Contributor: Daniel J Griffiths <griffithsdj@inbox.com>
# Contributor: Stefan Husmann <stefan-husmann@t-online.de>
# Contributor: Roman Mikhayloff <rimf@inbox.ru>

pkgname=codeblocks
pkgver=17.12
pkgrel=1
pkgdesc='Cross-platform C/C++ IDE'
arch=('x86_64')
url='http://codeblocks.org/'
license=('GPL3')
depends=('wxgtk3')
makedepends=('boost' 'gamin' 'hunspell' 'zip')
optdepends=('boost-libs: NassiShneiderman plugin'
            'gamin: FileManager plugin'
            'hunspell: SpellChecker plugin')
source=("https://downloads.sourceforge.net/codeblocks/${pkgname}_$pkgver.tar.xz")
sha256sums=('13881a0a72769694e82e531b8e7814d51fbf1fa122c73c5004e186560fbc57e0')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --with-contrib-plugins=all --with-wx-config=/usr/bin/wx-config-gtk3
  sed -i 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
