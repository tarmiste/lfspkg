# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Christoph W <c w e g e n e r at gmail dot com>

pkgbase=('python-requests-credssp')
pkgname=('python-requests-credssp' 'python2-requests-credssp')
_module='requests-credssp'
pkgver=0.1.0
pkgrel=1
pkgdesc="HTTPS CredSSP authentication with the requests library"
url="https://github.com/jborean93/requests-credssp"
makedepends=('python-setuptools' 'python2-setuptools')
license=('ISC')
arch=('any')
source=("https://github.com/jborean93/requests-credssp/archive/v${pkgver}.tar.gz")
sha512sums=('a3fb18924101a6fe3bbfb6e95a9abdd58eaceb8d91ce62639782ff17cdee96143c02d5cc6d77d6655f199a75dc55b0bb8eee6daa7a496dc705f5badc1b7d6580')

prepare() {
    cp -a "${srcdir}/${_module}-${pkgver}"{,-python2}
}

build() {
    cd "${srcdir}/${_module}-${pkgver}"
    python setup.py build

    cd "${srcdir}/${_module}-${pkgver}-python2"
    python2 setup.py build
}

package_python-requests-credssp() {
    depends+=('python' 'python-pyopenssl' 'python-requests' 'python-ntlm-auth')
    cd "${srcdir}/${_module}-${pkgver}"
    python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
}

package_python2-requests-credssp() {
    depends+=('python2' 'python2-pyopenssl' 'python2-requests' 'python2-ordereddict' 'python2-ntlm-auth')
    cd "${srcdir}/${_module}-${pkgver}-python2"
    python2 setup.py install --root="${pkgdir}" --optimize=1 --skip-build
}
