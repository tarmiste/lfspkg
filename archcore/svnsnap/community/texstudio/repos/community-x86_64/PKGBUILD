# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Cl√©ment DEMOULINS <clement@archivel.fr>
pkgname=texstudio
pkgver=2.12.6
pkgrel=5
pkgdesc="Integrated writing environment for creating LaTeX documents"
arch=('x86_64')
url="http://texstudio.sourceforge.net/"
license=('GPL')
depends=('poppler-qt5' 'qt5-svg' 'qt5-script' 'libxkbcommon-x11' 'hicolor-icon-theme')
makedepends=('qt5-tools' 'mercurial' 'imagemagick' 'librsvg')
optdepends=('evince: pdf reader'
            'okular: alternate pdf reader')
replaces=('texmakerx')
source=("hg+http://hg.code.sf.net/p/texstudio/hg#tag=${pkgver}"
        fs54269.patch
        fs56433.patch)
sha512sums=('SKIP'
            '247e85f668b06c8a2def4e7456f0ddc8d35c7990484c1c037f7cf43fdee91419b5df66e968ef1e0c637fe685722bbad9171bdd79fad7a10912ae329aff088b68'
            'a098c45440077db6c137bbeeb76d266242b80d51c3721aed30987d22b3e381b2a490b685aff4897d203a17256c64a9254360e3ac6f962675ea4b9a575a29a808')

prepare() {
    cd hg
    patch -Np1 < "${srcdir}"/fs54269.patch
    patch -Np1 < "${srcdir}"/fs56433.patch
}

build() {
    cd hg
    qmake-qt5 CONFIG-="debug" texstudio.pro
    make
}

package() {
    cd hg
    make INSTALL_ROOT="${pkgdir}" install

    for res in $(/bin/ls /usr/share/icons/hicolor | grep '[0-9]\+x[0-9]\+'); do
        mkdir -p "${pkgdir}"/usr/share/icons/hicolor/${res}/apps
        convert -background none -density 1200 -resize ${res} \
            "${pkgdir}"/usr/share/icons/hicolor/scalable/apps/texstudio.svg \
            "${pkgdir}"/usr/share/icons/hicolor/${res}/apps/texstudio.png
    done
}
