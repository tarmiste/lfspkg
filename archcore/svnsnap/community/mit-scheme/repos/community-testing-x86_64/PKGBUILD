# $Id: PKGBUILD 281335 2018-01-11 14:06:39Z bpiotrowski $
# Maintainer: Alad Wenter <alad@mailbox.org>
# Contributor: Bart≈Çomiej Piotrowski <nospam@bpiotrowski.pl>
# Contributor: peter feigl <peter.feigl@gmail.com>

pkgname=mit-scheme
pkgver=9.2
pkgrel=2
pkgdesc='MIT/GNU Scheme'
url='http://www.gnu.org/software/mit-scheme/'
arch=('x86_64')
license=('GPL')
depends=('glibc' 'ncurses' 'zlib')
optdepends=('openssl: support for openssl')
source_x86_64=("https://ftp.gnu.org/gnu/mit-scheme/stable.pkg/$pkgver/$pkgname-$pkgver-x86-64.tar.gz"
               "https://ftp.gnu.org/gnu/mit-scheme/stable.pkg/$pkgver/$pkgname-$pkgver-x86-64.tar.gz.sig")
validpgpkeys=('424626C56C4AA9DB11194C07E3AA82D81B352C01') # Chris Hanson <cph@chris-hanson.org>
# https://ftp.gnu.org/gnu/mit-scheme/stable.pkg/9.2/md5sums.txt
md5sums_x86_64=('9fcc6c156e53efeb0560996551fa0a57'
                'SKIP')

build() {
    cd "$pkgname-$pkgver"/src
    ./configure --prefix=/usr \
        --with-x \
        --enable-native-code
    make
}

package() {
    cd "$pkgname-$pkgver"/src
    make DESTDIR="$pkgdir" install
}
