# Maintainer: Alexander F RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Bastien Dejean <nihilhill@gmail.com>

pkgname=sxhkd
pkgver=0.5.8
pkgrel=1
pkgdesc='Simple X hotkey daemon'
arch=('x86_64' 'i686')
url='https://github.com/baskerville/sxhkd'
license=('custom:BSD')
depends=('bash' 'xcb-util-keysyms')
makedepends=('xcb-util' 'git')
source=("git://github.com/baskerville/sxhkd.git#tag=$pkgver")
md5sums=('SKIP')

build() {
  make -C "$pkgname" PREFIX=/usr
}

package() {
  make -C "$pkgname" PREFIX=/usr DESTDIR="$pkgdir" install
  install -Dm644 "$pkgname/LICENSE" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim: ts=2 sw=2 et:
