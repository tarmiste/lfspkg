# $Id: PKGBUILD 252446 2017-08-18 12:04:17Z arodseth $
# Maintainer: Alexander F RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Paul Mattal <pjmattal@elys.com>

pkgname=dcfldd
pkgver=1.3.4.1
pkgrel=5
pkgdesc='DCFL (DoD Computer Forensics Lab) dd replacement with hashing'
arch=('x86_64' 'i686')
url='https://dcfldd.sourceforge.net/'
license=('GPL')
depends=('glibc')
source=("https://downloads.sourceforge.net/sourceforge/$pkgname/$pkgname-${pkgver%.*}-${pkgver##*.}.tar.gz")
sha256sums=('f5143a184da56fd5ac729d6d8cbcf9f5da8e1cf4604aa9fb97c59553b7e6d5f8')

build() {
  cd "$pkgname-${pkgver%.*}-${pkgver##*.}"

  ./configure --prefix=/usr --mandir=/usr/share/man
  make
}

package() {
  cd "$pkgname-${pkgver%.*}-${pkgver##*.}"

  make DESTDIR="$pkgdir" install
}

# vim:ts=2 sw=2 et:
