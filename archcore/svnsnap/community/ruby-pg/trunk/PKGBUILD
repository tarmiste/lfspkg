# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Niels Abspoel <aboe76 (at) Gmail.com>

_gemname=pg
pkgname=ruby-pg
pkgver=1.0.0
_commit=9a388d1023ec
pkgrel=1
pkgdesc='Pg is the Ruby interface to the Postgresql RDBMS'
url='https://bitbucket.org/ged/ruby-pg'
arch=('x86_64')
license=('custom:BSDL')
depends=('ruby' 'ruby-rspec-core' 'postgresql-libs')
options=('!emptydirs')
source=(${pkgname}-${pkgver}.tar.bz2::https://bitbucket.org/ged/ruby-pg/get/v${pkgver}.tar.bz2)
sha256sums=('a3e18534feefc6eeff27c5621f63c23509cddd7a5ea0227caababd311f2333ba')
sha512sums=('eb01ffa74186b3216e4fbb694820c02907af5f0c5c9fd77645d97396286ff810d858717de25b348d5144043f883e641bc5249901d0845e69cef2d4e94e9487f1')

prepare() {
  cd ged-${pkgname}-${_commit}
  sed 's|"ChangeLog".freeze, ||' -i ${_gemname}.gemspec
  sed 's|"lib/pg/deprecated_constants.rb".freeze, ||' -i ${_gemname}.gemspec
  sed "s|^  s.version = .*$|  s.version = '${pkgver}'|" -i ${_gemname}.gemspec
}

build() {
  cd ged-${pkgname}-${_commit}
  gem build ${_gemname}.gemspec
}

package() {
  cd ged-${pkgname}-${_commit}
  local _gemdir="$(gem env gemdir)"
  gem install --ignore-dependencies --no-user-install -i "${pkgdir}/${_gemdir}" -n "${pkgdir}/usr/bin" ${_gemname}-${pkgver}.gem
  install -Dm 644 README.rdoc -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  find "${pkgdir}/${_gemdir}" \( -name 'mkmf.log' -or -name 'gem_make.out' \) -delete
  rm "${pkgdir}"/${_gemdir}/cache/${_gemname}-${pkgver}.gem
}

# vim: ts=2 sw=2 et:
