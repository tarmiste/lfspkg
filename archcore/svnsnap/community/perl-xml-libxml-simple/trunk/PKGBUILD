# $ID$
# Maintainer: RÃ©my Oudompheng <remy@archlinux.org>

pkgname=perl-xml-libxml-simple
_dist=XML-LibXML-Simple
pkgver=0.97
pkgrel=3
pkgdesc="An analogue of XML::Simple using libxml"
arch=('any')
url="https://metacpan.org/release/${_dist}"
license=('PerlArtistic' 'GPL')
depends=('perl' 'perl-xml-libxml' 'perl-file-slurp-tiny')
options=('!emptydirs')
source=(http://search.cpan.org/CPAN/authors/id/M/MA/MARKOV/${_dist}-${pkgver}.tar.gz)
md5sums=('402da3e6575af4fac52dc911c9935fb0')
sha512sums=('5607218a0ee7cd08f6e166af30f38ac7942e36e32469836f9b09d3802f32d8c5ca965fe6870823679523721241530eab9a5235dc376b2ce362d70a78b9c2d8f7')

build() {
    cd "${srcdir}/${_dist}-${pkgver}"
    unset PERL5LIB PERL_MM_OPT PERL_MB_OPT PERL_LOCAL_LIB_ROOT
    export PERL_MM_USE_DEFAULT=1 PERL_AUTOINSTALL=--skipdeps
    perl Makefile.PL
    make
}

check() {
    cd "${srcdir}/${_dist}-${pkgver}"
    unset PERL5LIB PERL_MM_OPT PERL_MB_OPT PERL_LOCAL_LIB_ROOT
    export PERL_MM_USE_DEFAULT=1
    make test
}

package() {
    cd "${srcdir}/${_dist}-${pkgver}"
    unset PERL5LIB PERL_MM_OPT PERL_MB_OPT PERL_LOCAL_LIB_ROOT
    make install INSTALLDIRS=vendor DESTDIR="$pkgdir"
}
