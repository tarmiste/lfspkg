# $Id: PKGBUILD 266875 2017-11-15 14:29:11Z foutrelis $
# Maintainer: Alad Wenter <alad (at) mailbox.org>
# Contributor: Chris Brannon <cmbrannon (at) cox.net>
# Contributor: joyfulgirl <joyfulgirl (at) archlinux.us>
# Contributor: Chuck Boucher <jofywyxaxukyvofa (at) tempomail.fr>
# Contributor: aksr <aksr at t-com dot me>

pkgname=edbrowse
pkgver=3.7.1
pkgrel=2
pkgdesc='Line-oriented editor, browser and mail client'
arch=('x86_64')
url="http://edbrowse.org/"
license=('GPL' 'MPL')
depends=('duktape' 'tidy' 'curl' 'pcre')
source=("$pkgname-$pkgver.tar.gz::https://github.com/CMB/edbrowse/archive/v$pkgver.tar.gz")
sha256sums=('12edf1834bbfb93335002c222a0f472eb4551a5b3a99d67c8a5e9dc05f97e37a')

build() {
    cd "$pkgname-$pkgver"
    make CFLAGS="-I/usr/include/tidy" all
}

package() {
    cd "$pkgname-$pkgver"
    make -C src DESTDIR="$pkgdir" prefix=/usr install

    install -Dm644 README CHANGES -t "$pkgdir/usr/share/doc/$pkgname/README"
    install -Dm644 COPYING -t "$pkgdir/usr/share/licenses/$pkgname"

    install -Dm644 doc/usersguide.html doc/sample.ebrc -t "$pkgdir/usr/share/doc/edbrowse"
    install -Dm644 doc/man-edbrowse-debian.1 -T "$pkgdir/usr/share/man/man1/edbrowse.1"
}

