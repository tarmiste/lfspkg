# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

_gemname=daemons
pkgname=ruby-${_gemname}
pkgver=1.2.5
pkgrel=2
pkgdesc='Easy way to wrap existing ruby scripts to be run as a daemon'
url='https://github.com/thuehlinger/daemons'
arch=('any')
license=('MIT')
depends=('ruby')
options=('!emptydirs')
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/thuehlinger/daemons/archive/v${pkgver}.tar.gz)
sha256sums=('7ebf1fe54b24c45d168e3b897547b29fda5f84b0fc723f74abeaf81d852a5dfa')
sha512sums=('5d3db8e5917eff5a3946c385a1ac6f561d1e9d1d6cd87c0deb4c4777bdab33a5e4f889300ee61a2e68df0200d51b3ddd0aa01d9f2841104d01a866f3ee8c09ac')

prepare() {
  cd ${_gemname}-${pkgver}
  sed -r 's|~>|>=|g' -i ${_gemname}.gemspec # don't give a fuck about rubys bla bla
  sed 's|README.md LICENSE Releases ||' -i ${_gemname}.gemspec
  sed -r 's|`git ls-files (.+)`|`find \1 -type f`|' -i ${_gemname}.gemspec
}

build() {
  cd ${_gemname}-${pkgver}
  gem build ${_gemname}.gemspec
}

package() {
  cd ${_gemname}-${pkgver}
  local _gemdir="$(gem env gemdir)"
  gem install --ignore-dependencies --no-user-install -i "${pkgdir}${_gemdir}" -n "${pkgdir}/usr/bin" ${_gemname}-${pkgver}.gem
  install -Dm 644 README.md Releases -t "${pkgdir}/usr/share/doc/${pkgname}"
  cp -a examples "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  rm "${pkgdir}/${_gemdir}/cache/${_gemname}-${pkgver}.gem"
}

# vim: ts=2 sw=2 et:
