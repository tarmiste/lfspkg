# $Id: PKGBUILD 260728 2017-09-28 05:10:09Z anatolik $
# Maintainer: Anatol Pomozov
# Contributor: Nicolas Quiénot < niQo at aur >
# Contributor: Damián Nohales <damiannohales at gmail.com>
 
pkgname=evemu
pkgver=2.7.0
pkgrel=1
pkgdesc='Tools and bindings for kernel input event device emulation and data capture and replay'
arch=(i686 x86_64)
url='http://www.freedesktop.org/wiki/Evemu'
license=(GPL3)
depends=(libevdev python2)
source=(https://www.freedesktop.org/software/$pkgname/$pkgname-$pkgver.tar.xz{,.sig})
validpgpkeys=(0A75E35E0FAEE97EC769103E2F2670AC164DB36F) # Benjamin Tissoires <benjamin.tissoires@gmail.com>
sha1sums=('16c6e1484253e1610a019dcca22b1698002824ca'
          'SKIP')
 
build() {
  cd $pkgname-$pkgver
  PYTHON=/usr/bin/python2 ./configure --prefix=/usr
  make
}

check() {
  cd $pkgname-$pkgver
  # Tests have to be run as 'root'
  # make check
}
 
package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
