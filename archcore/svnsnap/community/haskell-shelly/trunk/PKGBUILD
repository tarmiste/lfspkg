# $Id: PKGBUILD 276719 2017-12-29 00:59:16Z felixonmars $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

_hkgname=shelly
pkgname=haskell-shelly
pkgver=1.7.0
pkgrel=2
pkgdesc="Shell-like (systems) programming in Haskell"
url="https://github.com/yesodweb/Shelly.hs"
license=("custom:BSD3")
arch=('x86_64')
depends=('ghc-libs' 'haskell-mtl' 'haskell-unix-compat' 'haskell-system-filepath'
         'haskell-system-fileio' 'haskell-monad-control' 'haskell-lifted-base'
         'haskell-lifted-async' 'haskell-exceptions' 'haskell-enclosed-exceptions' 'haskell-text'
         'haskell-async' 'haskell-transformers-base')
makedepends=('ghc' 'haskell-hunit' 'haskell-hspec')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha512sums=('ffb0af966b43ddaec9ec03e105b1f3d02205f581063fe8971dcca9d30d3f15f1b1c23ae786203f7a1260d47e50e5c7fc110b60e52a20e442bd67b9ef3c3ff6f4')

prepare() {
    cd $_hkgname-$pkgver
    mkdir test/data/{dir,symlinked_dir}
    ln -s ../symlinked_dir test/data/dir/
    touch test/data/symlinked_dir/hoge_file
}

build() {
    cd "${srcdir}/${_hkgname}-${pkgver}"

    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir="/usr/share/doc/${pkgname}" --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid \
            -f-lifted -f-build-examples
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test
}

package() {
    cd "${srcdir}/${_hkgname}-${pkgver}"

    install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
    install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
    runhaskell Setup copy --destdir="${pkgdir}"
    install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"
}
