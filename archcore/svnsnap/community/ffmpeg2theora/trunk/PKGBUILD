# $Id: PKGBUILD 266875 2017-11-15 14:29:11Z foutrelis $
# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: Eric Belanger <eric@archlinux.org>
# Contributor: Angelo Theodorou <encelo@users.sourceforge.net>
# Contributor: Alexander RÃ¸dseth <rodseth@gmail.com>

pkgname=ffmpeg2theora
pkgver=0.30
pkgrel=3
pkgdesc="Simple converter to create Ogg Theora files"
arch=('x86_64')
url="http://www.v2v.cc/~j/ffmpeg2theora/"
license=('GPL')
depends=('ffmpeg' 'libkate')
makedepends=('scons' 'git')
source=($pkgname-$pkgver::git+https://git.xiph.org/ffmpeg2theora.git#tag=${pkgver}
        ffmpeg2theora-0.29-underlinking.patch)
sha256sums=('SKIP'
            '3b937c7e3a5afe327ee75e246ef606a747b8fb5880b43a864b3b9db4a245ef7f')

prepare() {
  cd "${srcdir}"/$pkgname-$pkgver

  echo "#define AVCODEC_MAX_AUDIO_FRAME_SIZE 192000" >> src/ffmpeg2theora.h
  patch -p0 -i ../ffmpeg2theora-0.29-underlinking.patch
}

build() {
  cd "${srcdir}"/$pkgname-$pkgver

  scons
}

package() {
  cd "${srcdir}"/$pkgname-$pkgver

  scons install destdir="${pkgdir}" prefix="/usr" mandir="PREFIX/share/man"
}
