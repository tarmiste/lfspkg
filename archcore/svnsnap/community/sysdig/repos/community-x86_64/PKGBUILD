# $Id: PKGBUILD 284178 2018-01-19 17:57:26Z mtorromeo $
# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>
# Contribuitor: Alfredo Palhares <masterkorp@masterkorp.net>
# Contribuitor: Christian Babeux <christian.babeux@0x80.ca>

pkgname=sysdig
pkgver=0.20.0
pkgrel=1
pkgdesc="Open source system-level exploration and troubleshooting tool"
arch=('x86_64')
url="http://www.sysdig.org/"
license=('GPL2')
depends=('dkms' 'jsoncpp' 'luajit' 'curl' 'jq' 'libb64')
makedepends=('cmake' 'pandoc')
source=("https://github.com/draios/sysdig/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha256sums=('e1f7e824831a9b902f6df0e818c41bdfc21175398d58318d7892df9c601e7e96')

prepare() {
  cd "$srcdir"/$pkgname-$pkgver
  sed 's|${DIR_ETC}/bash_completion.d|share/bash-completion/completions/|' -i \
      scripts/CMakeLists.txt
  sed 's|curl/curlbuild\.h|curl/system.h|' -i \
      userspace/libsinsp/marathon_http.cpp \
      userspace/libsinsp/mesos_http.cpp
}

build() {
  cd "$srcdir"/$pkgname-$pkgver
  rm -rf build
  mkdir build
  cd build
  cmake .. \
        -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DSYSDIG_VERSION=$pkgver \
        -DUSE_BUNDLED_DEPS=OFF \
        -DBUILD_DRIVER=OFF
  make
}

package() {
  cd "$srcdir"/$pkgname-$pkgver
  cd build
  make install DESTDIR="$pkgdir"
}
