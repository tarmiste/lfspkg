# $Id: PKGBUILD 270675 2017-11-29 12:57:54Z spupykin $
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: Charles Mauch <cmauch@gmail.com>
# Contributor: Francois Charette <firmicus@gmx.net>

pkgname=perl-crypt-cbc
pkgver=2.33
pkgrel=2
pkgdesc="Cipherblock chaining for Crypt::DES/IDEA"
arch=('any')
depends=('perl')
url="http://search.cpan.org/dist/Crypt-CBC"
license=('GPL' 'PerlArtistic')
options=('!emptydirs')
source=(https://www.cpan.org/authors/id/L/LD/LDS/Crypt-CBC-$pkgver.tar.gz)
sha256sums=('6a70de21b6cc7f2b100067e8e188db966e9a8001b5db6fa976e7cb5b294ae645')

build() {
  cd  "$srcdir"/Crypt-CBC-$pkgver
  PERL_MM_USE_DEFAULT=1 perl Makefile.PL INSTALLDIRS=vendor
  make
}

package() {
  cd  "$srcdir"/Crypt-CBC-$pkgver
  make pure_install doc_install DESTDIR="$pkgdir"
  find "$pkgdir" -name '.packlist' -delete
  find "$pkgdir" -name '*.pod' -delete
}
