# $Id: PKGBUILD 265218 2017-11-02 22:05:21Z kkeen $
# Maintainer: Kyle Keen <keenerd@gmail.com>
# Contributor: PepeSmith
# Contributor: Aron Asor <aronasorman at gmail.com>
# Contributor: Chris Brannon <chris@the-brannons.com>
# Contributor : Douglas Soares de Andrade <dsa@aur.archlinux.org>

pkgname=ipython
pkgver=6.2.1
pkgrel=2
pkgdesc="An enhanced Interactive Python shell."
arch=('any')
url="http://ipython.org"
license=('BSD')
depends=('python' 'python-traitlets' 'python-pexpect' 'sqlite'
         'python-setuptools' 'python-pickleshare' 'python-prompt_toolkit'
         'python-jedi' 'python-pygments')
makedepends=('python-setuptools')
optdepends=("python-nose: for IPython's test suite")

source=("ipython-$pkgver.tgz::https://github.com/ipython/ipython/archive/$pkgver.tar.gz"
        "https://pypi.python.org/packages/source/s/simplegeneric/simplegeneric-0.8.1.zip"
        "http://www.packal.org/sites/default/files/public/styles/icon_large/public/workflow-files/nkeimipynbworkflow/icon/icon.png")
md5sums=('fe8704b6b28c05cf608e0747a7c983ec'
         'f9c1fab00fd981be588fc32759f474e3'
         '2901d65f1b1fe354e72850085cd1c072')

# confirm that an update does not break sage?

prepare() {
  cd "$srcdir"

  # python 3.5 does not need a 3.3 backport hack
  cd "$srcdir/ipython-$pkgver"
  sed -i 's|backports.shutil_get_terminal_size|shutil|' IPython/utils/terminal.py
}

build() {
  cd "$srcdir"
}

package() {
  cd "$srcdir/ipython-$pkgver"

  # see https://github.com/ipython/ipython/issues/2057
  #export LC_ALL=en_US.UTF-8
  python3 setup.py install --prefix=/usr --root="$pkgdir" --optimize=0

  cd "$srcdir/simplegeneric-0.8.1"
  python3 setup.py install --prefix=/usr --root="$pkgdir" --optimize=0

  cd "$srcdir/ipython-$pkgver"
  install -Dm644 docs/source/about/license_and_copyright.rst "$pkgdir/usr/share/licenses/ipython/LICENSE"

  cd "$srcdir/ipython-$pkgver/examples/IPython Kernel/"
  # FS#45120
  sed -i 's/gnome-netstatus-idle/ipython/' *.desktop
  install -Dm644 ipython.desktop "$pkgdir/usr/share/applications/ipython.desktop"
  #install -Dm644 ipython-qtconsole.desktop "$pkgdir/usr/share/applications/ipython-qtconsole.desktop"
  # FS#47046
  install -Dm644 "$srcdir/icon.png" "$pkgdir/usr/share/pixmaps/ipython.png"

  #cd $srcdir/ipython-$pkgver/IPython/qt/console/resources/icon/
  #install -Dm644 IPythonConsole.svg "$pkgdir/usr/share/icons/hicolor/scalable/apps/ipython.svg"
}

