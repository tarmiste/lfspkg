# $Id: PKGBUILD 213374 2017-02-21 18:43:35Z jlichtblau $
# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: graysky <graysky AT archlnux DOT us>
# Contributor: Geoffroy Carrier <geoffroy@archlinux.org>
# Contributor: JoÃ«l Schaerer <joel.schaerer@laposte.net>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=autojump
pkgver=22.5.1
pkgrel=1
pkgdesc="A faster way to navigate your filesystem from the command line"
arch=('any')
url="https://github.com/wting/autojump"
license=('GPL3')
depends=('python')
source=($pkgname-$pkgver.tar.gz::https://github.com/wting/$pkgname/archive/release-v$pkgver.tar.gz)
sha256sums=('765fabda130eb4df70d1c1e5bc172e1d18f8ec22c6b89ff98f1674335292e99f')

prepare() {
  cd $pkgname-release-v$pkgver
  sed -i "s:/env python:/python3:g" bin/$pkgname
}

package() {
  cd $pkgname-release-v$pkgver

  SHELL=/bin/bash ./install.py --destdir "${pkgdir}" \
                               --prefix 'usr/' \
                               --zshshare 'usr/share/zsh/site-functions'

  cd "${pkgdir}"/usr/share/$pkgname
  for i in $pkgname.*
    do ln -s /usr/share/$pkgname/$i "${pkgdir}"/etc/profile.d/$i
  done
#FS#49601
  install -d "${pkgdir}"/usr/share/fish/completions
  mv "${pkgdir}"/etc/profile.d/$pkgname.fish "${pkgdir}"/usr/share/fish/completions

#https://github.com/joelthelion/autojump/pull/339
  sed -i "s:/usr/local/:/usr/:g" "${pkgdir}"/etc/profile.d/$pkgname.sh
  sed -i "s:/build/autojump/pkg/autojump/:/:g" "${pkgdir}"/etc/profile.d/$pkgname.sh
#FS#43762
  sed -i '27,31d' "${pkgdir}"/etc/profile.d/$pkgname.sh
}
