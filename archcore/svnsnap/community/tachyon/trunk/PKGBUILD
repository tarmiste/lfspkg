# $Id: PKGBUILD 266875 2017-11-15 14:29:11Z foutrelis $
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: RÃ©my Oudompheng <remy@archlinux.org>

pkgname=tachyon
pkgver=0.98.9
pkgrel=4
pkgdesc="Multithreaded ray tracing software"
arch=(x86_64)
url="http://jedi.ks.uiuc.edu/~johns/raytracer/"
license=(BSD)
depends=(libjpeg-turbo libpng)
makedepends=()
source=("http://jedi.ks.uiuc.edu/~johns/raytracer/files/$pkgver/$pkgname-$pkgver.tar.gz" 'enable-jpeg-png.patch')
sha256sums=('c6a3298ded4dc3e9390447819e1dac59b62c3d2d46d4154312ffe71549ee40d7'
            'ecd6ab6274879e0f971e2eba17871fffe2e24dcc9a332013a0b100ee4857cc2c')

case "$CARCH" in
  "i686") _target=linux-thr ;;
  "x86_64") _target=linux-64-thr ;;
esac

prepare(){
  cd $pkgname
# enable JPEG and PNG support
  patch -p0 -i "$srcdir"/enable-jpeg-png.patch
}

build() {
  cd $pkgname/unix
  make $_target
}

package() {
  cd $pkgname/compile/$_target
  install -D -m 755 tachyon $pkgdir/usr/bin/tachyon
  install -D -m 644 libtachyon.a $pkgdir/usr/lib/libtachyon.a

  cd $srcdir/$pkgname/scenes
  install -d "$pkgdir"/usr/share/tachyon/scenes/imaps
  install -m 644 README *.dat *.cam *.ac "$pkgdir"/usr/share/tachyon/scenes
  install -m 644 imaps/* "$pkgdir"/usr/share/tachyon/scenes/imaps

  install -d "$pkgdir"/usr/share/licenses/tachyon
  install $srcdir/$pkgname/Copyright "$pkgdir"/usr/share/licenses/tachyon
}
