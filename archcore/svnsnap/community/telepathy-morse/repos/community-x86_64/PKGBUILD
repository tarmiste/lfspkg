# $Id$
# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=telepathy-morse
pkgver=0.1.0
pkgrel=1
pkgdesc="Telepathy Connection Manager for the Telegram network"
arch=(i686 x86_64)
url="https://github.com/TelepathyQt/telepathy-morse"
license=(GPL)
depends=(telepathy-qt5 telegram-qt)
makedepends=(cmake)
source=("https://github.com/TelepathyQt/telepathy-morse/archive/$pkgname-$pkgver.tar.gz")
md5sums=('3e3250e6fca470546bb26eff76ff6d10')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DCMAKE_INSTALL_LIBEXECDIR=lib/telepathy \
    -DCMAKE_BUILD_TYPE=Release
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}

