# $Id: PKGBUILD 268245 2017-11-23 23:04:36Z muflone $
# Maintainer: Muflone http://www.muflone.com/contacts/english/

pkgname=dbeaver-plugin-apache-poi
pkgver=3.16.0
pkgrel=3
pkgdesc='DBeaver library for Microsoft Office documents'
arch=('any')
url='http://dbeaver.jkiss.org/'
license=('Apache')
makedepends=('unzip')
depends=('dbeaver>=4.2.5')
source=("https://dbeaver.jkiss.org/update/office/latest/plugins/org.jkiss.bundle.apache.poi_${pkgver}.jar"
        "${pkgname}.info")
sha256sums=('d6f1dd96b99fe8fbb80c025fb714e06b0d9f6c6ed46a3ceb0f1dc273802a7626'
            'fc7127a98a8de5347c9ce31f78d1d55c911d86477825a5632d9d53d89d4f8490')
noextract=("org.jkiss.bundle.apache.poi_${pkgver}.jar")

package() {
  # Extract plugin
  install -m 755 -d "${pkgdir}/usr/lib/dbeaver/plugins/org.jkiss.bundle.apache.poi_${pkgver}"
  unzip -q "${srcdir}/org.jkiss.bundle.apache.poi_${pkgver}.jar" \
        -d "${pkgdir}/usr/lib/dbeaver/plugins/org.jkiss.bundle.apache.poi_${pkgver}"

  # Add configuration file (handled by the hook)
  install -m 755 -d "${pkgdir}/etc/dbeaver/bundles.d"
  install -m 644 "${srcdir}/${pkgname}.info" "${pkgdir}/etc/dbeaver/bundles.d/20-${pkgname}.info"
}
