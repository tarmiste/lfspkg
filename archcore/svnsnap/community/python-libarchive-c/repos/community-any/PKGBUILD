# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

pkgbase=python-libarchive-c
pkgname=('python-libarchive-c' 'python2-libarchive-c')
pkgver=2.7
pkgrel=2
pkgdesc='Python interface to libarchive'
url='https://github.com/Changaco/python-libarchive-c'
arch=('any')
license=('CC0')
makedepends=('python-setuptools' 'python2-setuptools' 'libarchive')
checkdepends=('python-pytest-cov' 'python2-pytest-cov')
source=(${pkgbase}-${pkgver}.tar.gz::https://github.com/Changaco/python-libarchive-c/archive/${pkgver}.tar.gz)
sha512sums=('15ef4e3689d412fa524858067fc7f68cc909ffb916367d7edb3e0940f114385bab5d0f97a89f90ced59a5cf6d63e78c89d652f7817d6e49f38d244cc6316a424')

prepare() {
  cd ${pkgbase}-${pkgver}
  sed '/os.umask(2)/d' -i setup.py
}

check() {
  cd ${pkgbase}-${pkgver}
  LC_CTYPE=en_US.UTF-8 py.test
  LC_CTYPE=en_US.UTF-8 py.test2
}

package_python-libarchive-c() {
  depends=('python' 'libarchive')
  cd ${pkgbase}-${pkgver}
  python setup.py install -O1 --root="${pkgdir}"
  install -Dm 644 README.rst "${pkgdir}/usr/share/doc/${pkgname}/README"
}

package_python2-libarchive-c() {
  depends=('python2' 'libarchive')
  cd ${pkgbase}-${pkgver}
  python2 setup.py install -O1 --root="${pkgdir}"
  install -Dm 644 README.rst "${pkgdir}/usr/share/doc/${pkgname}/README"
}

# vim: ts=2 sw=2 et:
