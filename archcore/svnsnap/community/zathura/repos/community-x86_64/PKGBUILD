# $Id: PKGBUILD 275760 2017-12-24 14:13:51Z demize $
# Maintainer: Johannes LÃ¶thberg <johannes@kyriasis.com>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: ML <neldoreth>

pkgname=zathura
pkgver=0.3.8
pkgrel=1

pkgdesc="Minimalistic document viewer"
url="http://pwmt.org/projects/zathura/"
arch=('x86_64')
license=('custom')

makedepends=('python2-docutils' 'texlive-bin')
depends=('girara>=0.2.7' 'sqlite' 'desktop-file-utils' 'file' 'libsynctex')
optdepends=('zathura-djvu: DjVu support'
            'zathura-pdf-poppler: PDF support using Poppler'
            'zathura-pdf-mupdf: PDF support using MuPDF'
            'zathura-ps: PostScript support'
            'zathura-cb: Comic book support')

source=(https://pwmt.org/projects/zathura/download/zathura-$pkgver.tar.gz
        bash-completion)

sha1sums=('f96df67bd12068af8ed14671877b1ebe64931241'
          '94a8236c483626a7f3acee053a1ea885aed45a82')

build() {
  cd zathura-$pkgver
  make WITH_SYNCTEX=1
}

package() {
  cd zathura-$pkgver

  make install DESTDIR="$pkgdir"
  install -D -m664 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
  install -Dm0644 "$srcdir"/bash-completion "$pkgdir"/usr/share/bash-completion/completions/zathura
}
