# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

_gemname=rubydns
pkgname=ruby-${_gemname}
pkgver=2.0.1
pkgrel=3
pkgdesc='High-performance asynchronous DNS server and resolver'
url='https://github.com/ioquatix/rubydns'
arch=('any')
license=('MIT')
depends=('ruby' 'ruby-celluloid-dns' 'ruby-async-dns')
options=('!emptydirs')
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/ioquatix/rubydns/archive/v${pkgver}.tar.gz)
sha256sums=('2190a5b769b9396e739ebf6d43878294662e25c863d845a32ec7f68129b60bf2')
sha512sums=('2a0e674a1cc58c9f9a80500882af34306e73a177f26ce745e9501db996cd5e9697442a3c5f7b04b5c5fccd8f972795e629b8d130ecebd38d3dfa9c058c26fb56')

prepare() {
  cd ${_gemname}-${pkgver}
  sed 's|git ls-files|find|' -i ${_gemname}.gemspec
  sed 's|~>|>=|g' -i ${_gemname}.gemspec
  sed -r "s|(VERSION =) '2.0.1'|\1 '${pkgver}'|" -i lib/rubydns/version.rb
}

build() {
  cd ${_gemname}-${pkgver}
  gem build ${_gemname}.gemspec
}

package() {
  cd ${_gemname}-${pkgver}
  local _gemdir="$(gem env gemdir)"
  gem install --ignore-dependencies --no-user-install -i "${pkgdir}/${_gemdir}" -n "${pkgdir}/usr/bin" ${_gemname}-${pkgver}.gem
  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  cp -r examples "${pkgdir}/usr/share/doc/${pkgname}"
  rm "${pkgdir}"/${_gemdir}/cache/${_gemname}-${pkgver}.gem
}

# vim: ts=2 sw=2 et:
