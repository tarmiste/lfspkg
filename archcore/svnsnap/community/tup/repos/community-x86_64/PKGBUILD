# $Id: PKGBUILD 187747 2016-08-26 20:01:16Z anatolik $
# Maintainer: Anatol Pomozov <anatol.pomozov@gmail.com>
# Contributor: Arvid Norlander <anmaster A_T tele2 d_o_t (ccTLD for Sweden, which is se)>
# Contributor: Patrick McCarty <pnorcks at gmail dot com>

pkgname=tup
pkgver=0.7.5
pkgrel=1
pkgdesc='A fast, file-based build system'
arch=(i686 x86_64)
url='http://gittup.org/tup/index.html'
license=(GPL2)
depends=(fuse)
install=tup.install
source=($pkgname-$pkgver.zip::https://github.com/gittup/tup/archive/v$pkgver.zip)
sha256sums=('1cc35012fcb7a675f0f9827e3acbeba17a12fb449c2671638f19ea22f75f88e8')

prepare() {
  cd tup-$pkgver

  sed "s/\`git describe\`/$pkgver/" -i Tupfile
  echo "CFLAGS += $CFLAGS" >> Tuprules.tup
  echo "LDFLAGS += $LDFLAGS" >> Tuprules.tup
}

build() {
  cd tup-$pkgver

  # Clean environment does not allow to use fuse so ./bootstrap does not work
  ./build.sh
  ./build/tup generate runme.sh
  ./runme.sh
}

package() {
  cd tup-$pkgver

  install -d "$pkgdir"/usr/bin "$pkgdir"/usr/share/man/man1 "$pkgdir"/usr/share/vim/vimfiles/{syntax,ftdetect}
  install -m755 -t "$pkgdir"/usr/bin tup
  install -m644 -t "$pkgdir"/usr/share/man/man1 tup.1
  install -m644 -t "$pkgdir"/usr/share/vim/vimfiles/syntax contrib/syntax/tup.vim
  echo 'au BufNewFile,BufRead Tupfile,*.tup setf tup' > "$pkgdir"/usr/share/vim/vimfiles/ftdetect/tup.vim
}
