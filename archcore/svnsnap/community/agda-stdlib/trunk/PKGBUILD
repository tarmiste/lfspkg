# $Id: PKGBUILD 152508 2015-12-10 18:09:47Z fyan $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

pkgname=agda-stdlib
pkgver=0.14
pkgrel=1
pkgdesc="Agda standard libraries"
url="https://github.com/agda/agda-stdlib"
license=("MIT")
arch=('x86_64')
depends=('agda')
makedepends=('ghc' 'haskell-filemanip')
options=('staticlibs')
source=("$pkgname-$pkgver.tar.gz::https://github.com/agda/agda-stdlib/archive/v$pkgver.tar.gz")
sha512sums=('fcb785110c72fa8d298346150827766bdcb308502a4b915b56b6bb43aff9b5f93fd23932e08a78d97ec723ad0f2ea49c7f132b3b8cc8a535f9cde49387c95c51')

build() {
    cd $pkgname-$pkgver

    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanill
    runhaskell Setup build

    dist/build/GenerateEverything/GenerateEverything

    agda -i. -isrc Everything.agda
    agda --html -i. -isrc README.agda
}

package() {
    cd $pkgname-$pkgver
    install -dm755 "$pkgdir"/usr/share/agda/lib/prim
    cp -pr Everything.agda* src/* "$pkgdir"/usr/share/agda/lib/prim
    install -D -m644 LICENCE "$pkgdir"/usr/share/licenses/$pkgname/LICENCE
}
