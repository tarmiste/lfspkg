# $Id: PKGBUILD 272762 2017-12-07 07:14:08Z felixonmars $
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=certbot-dns-digitalocean
pkgver=0.20.0
pkgrel=1
pkgdesc="DigitalOcean DNS Authenticator plugin for Certbot"
arch=('any')
license=('Apache')
url="https://pypi.python.org/pypi/$pkgname"
depends=("certbot=$pkgver" "python-acme=$pkgver" 'python-digitalocean' 'python-mock'
         'python-setuptools' 'python-zope-interface')
source=("https://pypi.io/packages/source/c/$pkgname/$pkgname-$pkgver.tar.gz"{,.asc})
validpgpkeys=('148C30F6F7E429337A72D992B00B9CC82D7ADF2C'
              '1B41B758F9A78C7F444C20FA3E5B4152E3ADB1FD'
              'A2CFB51FA275A7286234E7B24D17C995CD9775F2')
sha512sums=('27f0db97a9eefe7a4d2c0056228f7dc8b3693a5e99c5825c6df8ad243a7536a54d2fc8bcc83673b277158e0dcf2a01a10f3734f92106909c3f0ebde301f2b2aa'
            'SKIP')

build() {
  cd "$srcdir"/$pkgname-$pkgver
  python setup.py build
}

check() {
  cd "$srcdir"/$pkgname-$pkgver
  python setup.py test
}

package() {
  cd "$srcdir"/$pkgname-$pkgver
  python setup.py install --root="$pkgdir"
}
