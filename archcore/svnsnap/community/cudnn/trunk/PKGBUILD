# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Adria Arrufat <adria.arrufat AT protonmail+aur DOT com>
# Contributor: Sami B. <spidermario@free.fr>
# Contributor: Yunhui Fu <yhfdev@gmail.com>

pkgname=cudnn
pkgver=7.0.5
pkgrel=2
pkgdesc="NVIDIA CUDA Deep Neural Network library"
arch=('x86_64')
url="https://developer.nvidia.com/cuDNN"
license=('proprietary')
depends=('cuda')
# To figure out these URLs, check out the Dockerfiles at
# https://gitlab.com/nvidia/cuda/tree/centos7 for the appropriate cuda version.
source=("${pkgname}-${pkgver}::https://developer.download.nvidia.com/compute/redist/cudnn/v${pkgver}/cudnn-9.1-linux-x64-v7.tgz"
        "NVIDIA_SLA+cuDNN_Supp_Feb2017_release.pdf")
sha512sums=('7eadb64a3d5e49aec2761e6f7dc0295c1d356910b114eed450c47081fc81b6e3b7748f3a4153f6a9d957691e3689cd52823bfa12816b1950dfc8794d6f332749'
            '38bbb7eb287914e4d7ba79da20a47222382687832d4ba0290715199a129c08715a23681800cf8d381f40e24202470a7b20505a31c8cea51a78762a740860251b')

package() {
  mkdir -p "${pkgdir}/opt"
  mkdir -p "${pkgdir}/etc"

  cp -r cuda "${pkgdir}/opt"

  install -d ${pkgdir}/etc/ld.so.conf.d
  echo "/opt/cuda/lib64/" > ${pkgdir}/etc/ld.so.conf.d/cudnn.conf

  install -Dm644 ${srcdir}/NVIDIA_SLA+cuDNN_Supp_Feb2017_release.pdf ${pkgdir}/usr/share/licenses/${pkgname}/NVIDIA_SLA+cuDNN_Supp_Feb2017_release.pdf
}

# vim: ft=sh syn=sh et
