# Maintainer: Florian Pritz <bluewind@xinu.at>
# Contributor: Giuseppe Borzi <gborzi@ieee.org>
# Contributor: Brice M<E9>alier <mealier_brice@yahoo.fr>
# Contributor: Michele Mocciola <mickele>
pkgname=opencascade
pkgver=7.0.0
pkgrel=1
pkgdesc="Open CASCADE Technology, 3D modeling & numerical simulation"
arch=('i686' 'x86_64')
url="http://www.opencascade.org"
license=('custom')
depends=('tk' 'mesa' 'libxmu' 'ftgl' 'libxi')
makedepends=('cmake')
source=("https://sources.archlinux.org/other/community/opencascade/opencascade-$pkgver.tgz" "env.sh" "opencascade.sh" "opencascade.conf")
md5sums=('fa1458aba1ce96c85d7876b35a04af6c'
         'a96f28ee7f4273ae1771ee033a2a3af3'
         'd9368b8d348ced3ec4462012977552d2'
         '2924ecf57c95d25888f51071fdc72ad0')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  #export CFLAGS+=" -L/opt/vtk6/lib"
  #export CXXFLAGS+=" -L/opt/vtk6/lib"

  mkdir build
  cd build
  cmake \
    -DCMAKE_INSTALL_PREFIX="$pkgdir/opt/$pkgname" \
    -DCMAKE_BUILD_TYPE=Release \
    -DUSE_VTK=OFF \
    ..
    #-D3RDPARTY_VTK_INCLUDE_DIR=/opt/vtk6/include/vtk/ \
    #-D3RDPARTY_VTK_LIBRARY_DIR=/opt/vtk6/lib/ \
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  cd build

  # no DESTDIR support so use prefix. This has to suffix match the prefix in ./configure
  make install/fast

  cd ..
  cp -r src/UnitsAPI/ "${pkgdir}/opt/$pkgname/src"
  install -D -m644 "${srcdir}/opencascade.conf" "${pkgdir}/etc/ld.so.conf.d/opencascade.conf"
  install -D -m 755 "${srcdir}/opencascade.sh" "${pkgdir}/etc/profile.d/opencascade.sh"
  install -m 755 "${srcdir}/env.sh" "${pkgdir}/opt/$pkgname"
  install -dm755 "$pkgdir/usr/share/licenses/$pkgname/"
  install -m644 LICENSE_LGPL_21.txt OCCT_LGPL_EXCEPTION.txt "$pkgdir/usr/share/licenses/$pkgname"
}

# vim:set ts=2 sw=2 et:
