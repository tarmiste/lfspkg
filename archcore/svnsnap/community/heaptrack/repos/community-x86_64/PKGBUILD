# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=heaptrack
pkgver=1.0.0
pkgrel=3
arch=(x86_64)
pkgdesc="A heap memory profiler for Linux"
url="http://milianw.de/tag/heaptrack"
license=(GPL)
depends=(kdiagram threadweaver kitemmodels kio boost-libs)
makedepends=(extra-cmake-modules boost python sparsehash)
source=(http://download.kde.org/stable/$pkgname/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig}
        heaptrack-glibc-2.26.patch)
sha256sums=('01d87ea1b424a67f943483ff141c0a32a46c5b6109834e2e832ebef898a4474f'
            'SKIP'
            '39a04e0bacb99ecd91d1d8225b944cc9c852853813520b7538b10bcd5ec6d591')
validpgpkeys=(3D8568869097C65C98F8D2760C0EB212CD1D1393) # Milian Wolff <mail@milianw.de>

prepare() {
  mkdir -p build

  cd $pkgname-$pkgver
  patch -p1 -i ../heaptrack-glibc-2.26.patch # fix build with glibc 2.26
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
   -DCMAKE_BUILD_TYPE=Release \
   -DCMAKE_INSTALL_PREFIX=/usr \
   -DBUILD_TESTING=OFF
  make
}

package(){
  cd build
  make DESTDIR="$pkgdir" install
}
