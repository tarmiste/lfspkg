# $Id: PKGBUILD 258845 2017-09-19 18:11:32Z arodseth $
# Maintainer: Alexander F RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Thomas Berryhill <tb01110100@gmail.com>
# Contributor: Bastien Dejean <baskerville@lavabit.com>

pkgname=xdo
pkgver=0.5.7
pkgrel=1
pkgdesc='Utility for performing actions on windows in X'
arch=('x86_64' 'i686')
url='https://github.com/baskerville/xdo'
license=('BSD')
depends=('libxcb' 'xcb-util-wm' 'xcb-util')
makedepends=('git')
source=("git+https://github.com/baskerville/xdo.git#tag=$pkgver")
md5sums=('SKIP')

build() {
  make -C xdo PREFIX=/usr
}

package() {
  make -C xdo PREFIX=/usr DESTDIR="$pkgdir" install
  install -Dm644 xdo/LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# getver: github.com/baskerville/xdo/blob/master/VERSION
# vim:ts=2 sw=2 et:
