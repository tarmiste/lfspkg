# $Id: PKGBUILD 271643 2017-12-02 02:53:27Z felixonmars $
# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=safe-exceptions
pkgname=haskell-safe-exceptions
pkgver=0.1.6.0
pkgrel=4
pkgdesc="Safe, consistent, and easy exception handling"
url="https://github.com/fpco/safe-exceptions"
license=('MIT')
arch=('x86_64')
depends=('ghc-libs' 'haskell-exceptions')
makedepends=('ghc')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha512sums=('a87139b1e506bb197e34e83713e29490f20f9140ecb47d979b87db4d6f7570690e2a1ea81901df28675d0b23747ddf743752d5ef26b81f90165ee010e609658f')

build() {
    cd "${srcdir}/${_hkgname}-${pkgver}"
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir="/usr/share/doc/${pkgname}" \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd "${srcdir}/${_hkgname}-${pkgver}"
    
    install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
    install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
    runhaskell Setup copy --destdir="${pkgdir}"
    install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"
}
