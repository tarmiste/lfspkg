# $Id: PKGBUILD 287015 2018-01-26 09:32:32Z felixonmars $
# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=safe-exceptions
pkgname=haskell-safe-exceptions
pkgver=0.1.7.0
pkgrel=1
pkgdesc="Safe, consistent, and easy exception handling"
url="https://github.com/fpco/safe-exceptions"
license=('MIT')
arch=('x86_64')
depends=('ghc-libs' 'haskell-exceptions')
makedepends=('ghc' 'haskell-hspec' 'haskell-void')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha512sums=('05fc32ea80ce1f733a6ea09655fecccf813ea9b7736dbd1d8ef84f54e3fb58f1017756cc9eb507d381cb73f22f100e9166bef1b7198a1d9edbda21e38626e351')

build() {
    cd $_hkgname-$pkgver
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir="/usr/share/doc/${pkgname}" --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test
}

package() {
    cd $_hkgname-$pkgver
    
    install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
    install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
    runhaskell Setup copy --destdir="${pkgdir}"
    install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"
}
