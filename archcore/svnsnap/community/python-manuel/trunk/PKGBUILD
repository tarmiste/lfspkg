# $Id: PKGBUILD 267683 2017-11-21 03:49:10Z felixonmars $
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-manuel
pkgname=('python-manuel' 'python2-manuel')
pkgver=1.9.0
pkgrel=1
pkgdesc="Manuel lets you build tested documentation"
arch=('any')
license=('Apache')
url="https://pypi.python.org/pypi/manuel"
makedepends=('python-setuptools' 'python2-setuptools' 'git')
checkdepends=('python-zope-testing' 'python2-zope-testing')
source=("git+https://github.com/benji-york/manuel.git#tag=$pkgver")
sha512sums=('SKIP')

prepare() {
  cp -a manuel{,-py2}
}

build() {
  cd "$srcdir/manuel"
  python setup.py build

  cd "$srcdir/manuel-py2"
  python2 setup.py build
}

check() {
  cd "$srcdir/manuel"
  python setup.py test

  cd "$srcdir/manuel-py2"
  python2 setup.py test
}

package_python-manuel() {
  depends=('python-setuptools')

  cd manuel
  python setup.py install --root="${pkgdir}" --optimize=1
}

package_python2-manuel() {
  depends=('python2-setuptools')

  cd manuel-py2
  python2 setup.py install --root="${pkgdir}" --optimize=1
}
