# $Id: PKGBUILD 236689 2017-06-13 20:42:38Z arojas $
# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=coin-or-osi
pkgver=0.107.9
pkgrel=1
pkgdesc="COIN-OR Open Solver Interface"
arch=(i686 x86_64)
url="https://projects.coin-or.org/Osi"
license=(EPL)
groups=(coin-or)
depends=(coin-or-coinutils)
makedepends=(gcc-fortran)
source=("http://www.coin-or.org/download/source/Osi/Osi-${pkgver}.tgz")
sha256sums=('6d61fb27e2dccf6574fcc4a03b7a0083f10e578e5e5b2abae22da11489571c05')

build() {
  cd Osi-$pkgver
  COIN_SKIP_PROJECTS="Sample" \
  ./configure --prefix=/usr --enable-dependency-linking \
              --with-coinutils-lib="$(pkg-config --libs coinutils)" \
              --with-coinutils-incdir="/usr/include/coin/"
  make
}

package() {
  cd Osi-$pkgver
  PKG_CONFIG_LIBDIR="${pkgdir}/usr/lib/pkgconfig/" \
  make DESTDIR="$pkgdir" install
}
