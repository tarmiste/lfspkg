# $Id: PKGBUILD 245610 2017-07-18 21:08:41Z arojas $
# Maintainer: Eric BÃ©langer <eric@archlinux.org>

pkgname=krename
pkgver=4.0.9
pkgrel=8
pkgdesc="A very powerful batch file renamer for KDE"
arch=('i686' 'x86_64')
url="http://www.krename.net"
license=('GPL')
depends=('kdebase-runtime' 'taglib')
makedepends=('cmake' 'automoc4')
options=('libtool')
source=(http://downloads.sourceforge.net/project/krename/KDE4%20krename-stable/${pkgver}/${pkgname}-${pkgver}.tar.bz2 krename-gcc6.patch)
sha1sums=('03886a385a16de4c9bc285d25b9816ea97768c9e'
          '87fe0f0f1f2d12496cebdb2b235a7476b9f809ea')

prepare() {
  cd $pkgname-$pkgver
# Fix build with GCC 6
  patch -p1 -i ../krename-gcc6.patch
}

build() {
  mkdir build
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release -DCMAKE_CXX_FLAGS="-I/usr/include/freetype2"
  sed -i 's|/share/locale|/share/locale/kde4|' po/cmake_install.cmake
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
