# $Id: PKGBUILD 264167 2017-10-25 01:20:15Z kkeen $
# Maintainer: Kyle Keen <keenerd@gmail.com>
# Contributor: Thomas Dziedzic < gostrc at gmail >
# Contributor: Jared Casper <jaredcasper@gmail.com>

pkgname=gtkwave
pkgver=3.3.86
pkgrel=1
pkgdesc='A wave viewer which reads LXT, LXT2, VZT, GHW and VCD/EVCD files'
arch=('i686' 'x86_64')
url='http://gtkwave.sourceforge.net'
license=('GPL' 'MIT')
depends=('bzip2' 'xz' 'gtk2' 'desktop-file-utils' 'gconf')
makedepends=('gperf')
install='gtkwave.install'
source=("http://gtkwave.sourceforge.net/gtkwave-${pkgver}.tar.gz")
md5sums=('6319a203b3aa3f5aad35e0a5a3e85fe6')

build() {
  cd "$srcdir/gtkwave-$pkgver"

  ./configure \
    --prefix=/usr \
    --disable-tcl \
    --disable-mime-update \
    --with-gconf

  make CFLAGS="-D_LARGEFILE64_SOURCE -O"
}

package() {
  cd "$srcdir/gtkwave-$pkgver"

  make DESTDIR="${pkgdir}" install

  install -D -m644 "$srcdir/gtkwave-$pkgver/LICENSE.TXT" \
    "$pkgdir/usr/share/licenses/gtkwave/LICENSE.TXT"
}

