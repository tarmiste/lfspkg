# $Id$
# Maintainer: Alexander F. Rødseth <xyproto@archlinux.org>
# Contributor: Thomas Weißschuh <thomas@t-8ch.de>

pkgname=smlnj
pkgver=110.82
pkgrel=1
pkgdesc='Standard ML compiler from New Jersey'
url='http://www.smlnj.org/'
license=('BSD')
arch=('x86_64')
provides=('sml')
install='smlnj.install'
depends_x86_64+=('lib32-glibc')
makedepends_x86_64+=('gcc-multilib')
_url="http://smlnj.cs.uchicago.edu/dist/working/$pkgver/"
source=(
  "$pkgname-$pkgver-boot.x86-unix.tgz::${_url}boot.x86-unix.tgz"
  "$pkgname-$pkgver-config.tgz::${_url}config.tgz"
  "$pkgname-$pkgver-cm.tgz::${_url}cm.tgz"
  "$pkgname-$pkgver-compiler.tgz::${_url}compiler.tgz"
  "$pkgname-$pkgver-runtime.tgz::${_url}runtime.tgz"
  "$pkgname-$pkgver-system.tgz::${_url}system.tgz"
  "$pkgname-$pkgver-MLRISC.tgz::${_url}MLRISC.tgz"
  "$pkgname-$pkgver-smlnj-lib.tgz::${_url}smlnj-lib.tgz"
  "$pkgname-$pkgver-ckit.tgz::${_url}ckit.tgz"
  "$pkgname-$pkgver-nlffi.tgz::${_url}nlffi.tgz"
  "$pkgname-$pkgver-cml.tgz::${_url}cml.tgz"
  "$pkgname-$pkgver-ml-lpt.tgz::${_url}ml-lpt.tgz"
  "$pkgname-$pkgver-ml-lex.tgz::${_url}ml-lex.tgz"
  "$pkgname-$pkgver-ml-yacc.tgz::${_url}ml-yacc.tgz"
  "$pkgname-$pkgver-ml-burg.tgz::${_url}ml-burg.tgz"
  "$pkgname-$pkgver-trace-debug-profile.tgz::${_url}trace-debug-profile.tgz"
  'urlgetter.sh'
  'profile.d-smlnj.sh'
)
sha256sums=('89e66f5a7fe7c811877d3cec4c1e3e4eb909ecca905248c2bf5f7105e2a4f9a5'
            'ceff9288e8106d94ce813181d6f4c4da02e4abf808875b27b78d9f6258d016de'
            '682fd43ef9021bb3beb62cc95fc60b47b4bf79458f4e3ca20029f1e1d57db207'
            'f7fb742608efa1ef4ed34a8791a1014c831708faa83e11d47f9d4242fbd69862'
            '39cff4308c4dea8ae59883be651478a49174ff0473edb67ff46420aaa2cddb71'
            'dee67036542d4cd0cbe18cb323db45de41083fc988f10e26c5c467c69f0d7321'
            'fb5b7e781ae2b7aef2c873bb778c08dad99b816e98d737116b79e7210213fbf2'
            '2c008ebea14d193ecea4969dcff7001b107ec724bc05562b7dde1e3dd5d4d1ae'
            '3db4e2aa95002698096b2e90c1979fedebb5c3ab0feacc374c0dfecbcb953632'
            '7789f2c2e2aee03664e69414385f09356a8779c4cf1af74caa8a49f1d913df6b'
            '092399af4825f0c352ed1cfe783bb42a44853fa8798bf8463e4279e41758d2ff'
            'fd107fcc3c51aa44b59f464bfb92bfff22131b18ddee29f361d01765f51ff286'
            'de7699715b0bbe1e9b67aa5bea5515dfe6aa0e5b2e74abe1f97168a8ad6a0ab5'
            '1ef667a170fa9c3ec87345f53b7579842c93342b4af8f8c10993551b55f4a366'
            '7f9c300c2553bf95be7f8275bf159bfb1ac7c7e24a9921b375be15c8d6606ae9'
            '37a11feacb6ecd9e3d5a727fdd1bd2d0d908f25a03642f42b42bc61839bb29bf'
            '4da78effe7d3644c28c731c8e4003a9cecec9f8f61d2fa4553981a729f2d200b'
            'dd20a81a5d2899f60183215ab6a412d522d2c6801d454c142225716899e089f3')

prepare() {
  mkdir -p install
}

build() {
  unset SMLNJ_HOME MAKEFLAGS
  export srcdir
  export pkgver
  export INSTALLDIR="$srcdir/install"
  URLGETTER="$srcdir/urlgetter.sh"
  config/install.sh
}

package() {
  install -d "$pkgdir/usr/"{lib/smlnj,share}
  install -Dm755 "$srcdir/profile.d-smlnj.sh" "$pkgdir/etc/profile.d/smlnj.sh"
  install -Dm644 compiler/LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  cp -R "$srcdir/install/"{bin,lib} "$pkgdir/usr/lib/smlnj"
  cp -R "$srcdir/doc/man" "$pkgdir/usr/share/"
  find "$pkgdir/usr/share/man" -name '._*' -delete
}

# getver: smlnj.org
# vim:set ts=2 sw=2 et:
