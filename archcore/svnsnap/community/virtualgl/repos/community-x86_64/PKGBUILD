# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: FigoDaPaura <ffigoDaPaura>
# Contributor: Andres Jimenez

pkgname=virtualgl
pkgver=2.5.2
pkgrel=2
pkgdesc="Redirects 3D commands from an OpenGL application onto a server-side 3D graphics card"
arch=('i686' 'x86_64')
url="http://virtualgl.org"
license=('LGPL')
depends=('libxv' 'glu' 'libjpeg-turbo' 'xcb-util-keysyms')
makedepends=('cmake' 'mesa')
source=("https://github.com/VirtualGL/virtualgl/archive/${pkgver}.tar.gz")
sha512sums=('124a5f8856899dc82cf87cd702239a2ca9ed3d6595884cbd96f8ca2aff52f9e38105761b5f84d0be654f9994a1b71cbee2f912106866d8bb4789d645071f5c10')

prepare() {
  # FS#48403
  export LDFLAGS="${LDFLAGS/--as-needed,}"
}

build() {
  cd "virtualgl-$pkgver"

  mkdir -p build && cd build

  cmake .. \
      -DCMAKE_INSTALL_PREFIX=/usr \
      -DTJPEG_INCLUDE_DIR=/usr/include \
      -DTJPEG_LIBRARY=/usr/lib/libturbojpeg.so \
      -DCMAKE_INSTALL_LIBDIR=/usr/lib \
      -DCMAKE_INSTALL_BINDIR=/usr/bin \
      -DCMAKE_INSTALL_DOCDIR=/usr/share/doc/${pkgname} \
      -DVGL_FAKEXCB=1
  make
}

package() {
  cd "virtualgl-$pkgver/build"
  make install DESTDIR="$pkgdir"

  mv "$pkgdir"/usr/bin/glxinfo "$pkgdir"/usr/bin/vglxinfo
}
