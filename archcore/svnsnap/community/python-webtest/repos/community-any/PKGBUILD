# $Id: PKGBUILD 263983 2017-10-22 15:13:27Z felixonmars $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Jerome Leclanche <jerome@leclan.ch>

pkgbase=python-webtest
pkgname=(python-webtest python2-webtest)
pkgver=2.0.29
pkgrel=1
pkgdesc="Helper to test WSGI applications"
arch=('any')
url='http://webtest.pythonpaste.org/'
license=('MIT')
makedepends=('python-setuptools' 'python2-setuptools' 'python-webob' 'python2-webob' 'python-six' 'python2-six'
             'python-beautifulsoup4' 'python2-beautifulsoup4' 'python-waitress' 'python2-waitress')
checkdepends=('python-nose' 'python2-nose' 'python-mock' 'python2-mock' 'python-coverage' 'python2-coverage'
              'python-pyquery' 'python2-pyquery'  'python-pastedeploy' 'python2-pastedeploy'
              'python-wsgiproxy2' 'python2-wsgiproxy2')
source=("https://pypi.io/packages/source/W/WebTest/WebTest-$pkgver.tar.gz")
sha512sums=('319700d1444f6a681f8ed8a4ee8661ba0462b3f4bfeb7510232f58be92e017066da3bac494ebe1a3c5b9f2d06c025e08e6193277e13c6113e9485108d4ccd2d2')

prepare() {
  cp -a WebTest-$pkgver{,-py2}

  export LC_CTYPE=en_US.UTF-8
}

build() {
  cd "$srcdir"/WebTest-$pkgver
  python setup.py build

  cd "$srcdir"/WebTest-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/WebTest-$pkgver
  PYTHONPATH="$PWD/build/lib.linux-$CARCH-3.6:$PYTHONPATH" nosetests3

  cd "$srcdir"/WebTest-$pkgver-py2
  PYTHONPATH="$PWD/build/lib.linux-$CARCH-2.7:$PYTHONPATH" nosetests2
}

package_python-webtest() {
  depends=('python-webob' 'python-six' 'python-beautifulsoup4' 'python-waitress')
  optdepends=('python-pyquery: for response.pyquery'
              'python-lxml: for response.lxml')

  cd WebTest-$pkgver
  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 --skip-build
  install -Dm644 "docs/license.rst" "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_python2-webtest() {
  depends=('python2-webob' 'python2-six' 'python2-beautifulsoup4' 'python2-waitress')
  optdepends=('python2-pyquery: for response.pyquery'
              'python2-lxml: for response.lxml')

  cd WebTest-$pkgver-py2
  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 --skip-build
  install -Dm644 "docs/license.rst" "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
