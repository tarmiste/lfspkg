# $Id: PKGBUILD 174008 2016-05-07 03:21:18Z fyan $
# Maintainer: Felix Yan <felixonmars@gmail.com>
# Contributor: Francois Boulogne <fboulogne at april dot org>

pkgname=pastebinit
pkgver=1.5
pkgrel=1
pkgdesc="Send anything you want directly to a pastebin from the command line"
arch=('any')
url="http://launchpad.net/pastebinit"
license=("GPL")
depends=("python")
makedepends=("asciidoc")
source=("http://launchpad.net/pastebinit/trunk/${pkgver}/+download/${pkgname}-${pkgver}.tar.gz") 

package() {
  cd ${pkgname}-${pkgver}
  install -d "$pkgdir"/usr/{bin,share/man/man1,share/locale}
  install -m 755 pastebinit "$pkgdir"/usr/bin

  cp -a pastebin.d "$pkgdir"/usr/share
  a2x -f manpage pastebinit.xml
  install -m 644 pastebinit.1 "$pkgdir"/usr/share/man/man1

  cd po
  make
  cp -a mo/* "$pkgdir"/usr/share/locale
}

sha512sums=('47eb785fbea1089ad3dd80ec7f481c553da2599a6fb3c47f0bb0c3c776a79331c63e38d4c0494253571b40c9808dbeeed62d14048e328d8562ca536bd0815986')
