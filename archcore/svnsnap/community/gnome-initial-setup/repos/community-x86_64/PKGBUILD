# $Id: PKGBUILD 280750 2018-01-09 19:12:36Z bgyorgy $
# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=gnome-initial-setup
pkgver=3.26.0
pkgrel=2
pkgdesc="Simple, easy, and safe way to prepare a new system"
arch=('x86_64')
url="http://www.gnome.org/"
license=('GPL')
depends=('nm-connection-editor' 'accountsservice' 'cheese' 'gnome-desktop' 'libgweather'
         'gnome-online-accounts' 'libgdm' 'libpwquality' 'libibus' 'libgnomekbd')
makedepends=('intltool' 'python2')
optdepends=('gdm: GDM Initial Setup mode'
            'gnome-getting-started-docs: GNOME Welcome Tour')
source=(https://download.gnome.org/sources/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.xz)
sha256sums=('65a9d5de028306e8bd017d9ae6525f46ea9ff36a0e02ddbca7fb17759be60c39')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var --libexecdir=/usr/lib/$pkgname
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
  install -d -o root -g 102 -m 750 "$pkgdir/usr/share/polkit-1/rules.d"

  # Setup system user and group
  install -dm755 "$pkgdir/usr/lib/sysusers.d"
  echo 'u gnome-initial-setup - "GNOME Initial Setup" /run/gnome-initial-setup' > "$pkgdir/usr/lib/sysusers.d/$pkgname.conf"
}
