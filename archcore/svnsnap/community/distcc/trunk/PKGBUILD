# $Id: PKGBUILD 281291 2018-01-11 08:13:10Z arodseth $
# Maintainer: Alexander F RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Judd Vinet <jvinet@zeroflux.org>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>

pkgname=distcc
pkgver=3.2rc1
pkgrel=4
pkgdesc='Distributed C, C++ and Objective-C compiler'
arch=('x86_64')
url='https://github.com/distcc/distcc'
license=('GPL')
depends=('popt' 'avahi' 'dbus')
makedepends=('gtk2' 'python2')
optdepends=('gtk2: for distccmon-gnome'
            'python2')
backup=('etc/conf.d/distccd'
        'etc/distcc/hosts')
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz"
        distccd.conf.d
        distccd.service)
sha256sums=('33e85981ff6afd94efc38b23b2d8b9036b3dff2dc6eac6982b9ff0ae1de64caa'
            '43e02b461841ca2976816c244a0eca8b24820ca143f73cc0924403d75a8c012f'
            '0f18ffb58647eeba643f28e9029a5d307100df3470eb228097f2b3f4196d8098')
prepare() {
  cd "$srcdir/$pkgname-$pkgver"

  ./autogen.sh
}

build() {
  cd "$srcdir/$pkgname-$pkgver"

  [ -f Makefile ] || PYTHON=/usr/bin/python2 \
    ./configure \
    --prefix=/usr \
    --with-gtk \
    --sysconfdir=/etc \
    --mandir=/usr/share/man \
    --enable-rfc2553

  make WERROR_CFLAGS= INCLUDESERVER_PYTHON=/usr/bin/python2
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir" INCLUDESERVER_PYTHON=/usr/bin/python2 install

  install -Dm644 "$srcdir/distccd.conf.d" "$pkgdir/etc/conf.d/distccd"
  install -Dm644 "$srcdir/distccd.service" \
    "$pkgdir/usr/lib/systemd/system/distccd.service"
  install -d "$pkgdir/usr/lib/$pkgname/bin"
  for bin in c++ cc cpp g++ gcc; do
    ln -sf "/usr/bin/$pkgname" "$pkgdir/usr/lib/$pkgname/bin/$bin"
  done

}

# vim: ts=2 sw=2 et:
