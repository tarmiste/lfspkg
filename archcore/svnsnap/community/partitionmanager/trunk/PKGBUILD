# $Id: PKGBUILD 280224 2018-01-07 20:11:56Z arojas $
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Tobias Powalowski <tpowa@archlinux.org>
# Contributor: Nick B <Shirakawasuna at gmail _dot_com>

pkgname=partitionmanager
pkgver=3.3.1
pkgrel=1
pkgdesc="A KDE utility that allows you to manage disks, partitions, and file systems"
arch=(x86_64)
url="http://kde.org/applications/system/kdepartitionmanager/"
license=(GPL2)
depends=(kpmcore hicolor-icon-theme kde-cli-tools)
makedepends=(extra-cmake-modules kdoctools python)
source=("http://download.kde.org/stable/$pkgname/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('5107955f9c70fc859bef04a54bffb28cd50efe355694232b9860e9d9c97a0f4a'
            'SKIP')
validpgpkeys=(1EE5A3205904BAA2B88C0A9D24FD31940095C0E1) # Andrius Å tikonas <andrius@stikonas.eu>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
