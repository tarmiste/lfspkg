# $Id: PKGBUILD 267444 2017-11-19 11:44:11Z aginiewicz $
# Maintainer: Andrzej Giniewicz <gginiu@gmail.com>

pkgbase=python-scikit-learn
pkgname=('python2-scikit-learn' 'python-scikit-learn')
pkgver=0.19.1
pkgrel=1
pkgdesc="A set of python modules for machine learning and data mining"
arch=('x86_64')
url="http://scikit-learn.sourceforge.net/"
license=('BSD')
makedepends=('python2-scipy' 'python-scipy' 'python2-setuptools' 'python-setuptools' 'cython' 'cython2')
options=(!emptydirs)

source=("https://github.com/scikit-learn/scikit-learn/archive/${pkgver}.tar.gz")
md5sums=('8fce5acd7d2d12c45d3381bbfa953d61')

prepare() {
  cd "$srcdir"
  cp -a scikit-learn-$pkgver scikit-learn-py2-$pkgver
  cd scikit-learn-py2-$pkgver

  sed -e "s|#![ ]*/usr/bin/python$|#!/usr/bin/python2|" \
      -e "s|#![ ]*/usr/bin/env python$|#!/usr/bin/env python2|" \
      -e "s|#![ ]*/bin/env python$|#!/usr/bin/env python2|" \
      -i $(find . -name '*.py')
}

build() {
  msg "Building Python2"
  cd "$srcdir"/scikit-learn-py2-$pkgver
  python2 setup.py build

  msg "Building Python3"
  cd "$srcdir"/scikit-learn-$pkgver
  python setup.py build
}

package_python2-scikit-learn() {
  depends=('python2-scipy')
  cd "$srcdir"/scikit-learn-py2-$pkgver

  python2 setup.py install --root="$pkgdir"/ --optimize=1

  install -D COPYING "$pkgdir"/usr/share/licenses/$pkgname/LICENSE

  # See FS#49651
  install -d "$pkgdir"/usr/share/doc/$pkgname
  cp -r doc/tutorial "$pkgdir"/usr/share/doc/$pkgname/tutorial
}

package_python-scikit-learn() {
  depends=('python-scipy')
  cd "$srcdir"/scikit-learn-$pkgver

  python setup.py install --root="$pkgdir"/ --optimize=1

  install -D COPYING "$pkgdir"/usr/share/licenses/$pkgname/LICENSE

  # See FS#49651
  install -d "$pkgdir"/usr/share/doc/$pkgname
  cp -r doc/tutorial "$pkgdir"/usr/share/doc/$pkgname/tutorial
}
