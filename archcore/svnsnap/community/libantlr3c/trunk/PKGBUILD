# $Id: PKGBUILD 266875 2017-11-15 14:29:11Z foutrelis $
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Mathieu Clabaut <mathieu.clabaut@gmail.com>
# Contributor: Artyom Smirnov <smirnoffjr@gmail.com>
# Contributor: Alexis Robert <alexis.robert@gmail.com>
# Maintainer: Daniel Nagy < danielnagy at gmx de>

pkgname=libantlr3c
pkgver=3.5.2
pkgrel=1
pkgdesc="C runtime for the ANTLR parsing library"
url="http://www.antlr.org/"
arch=("x86_64")
license=("BSD")
depends=('bash')
makedepends=()
source=("$pkgname-$pkgver.tar.gz::https://github.com/antlr/antlr3/archive/$pkgver.tar.gz")
sha256sums=('529baf7d11f06b773543e25271c1a6e1735d2f8061f4866d1ea5c99a5d71ee1f')

prepare() {
  cd "${srcdir}/antlr3-${pkgver}/runtime/C"
  cat <<EOF >antlr3config.h.in
#include <stdint.h>
#include <stdarg.h>
#include <stdlib.h>
#include <netinet/in.h>
#include <netinet/tcp.h>
#include <sys/stat.h>
#include <sys/socket.h>
#include <antlr3defs.h>
EOF
  aclocal
  libtoolize
  automake --add-missing
  autoconf
}

build() {
  cd "${srcdir}/antlr3-${pkgver}/runtime/C"
  [ "$CARCH" = "x86_64" ] && _cfgopt=--enable-64bit
  ./configure --prefix=/usr --sysconfdir=/etc ${_cfgopt}
  make
}

package() {
  cd "${srcdir}/antlr3-${pkgver}/runtime/C"
  make install DESTDIR="${pkgdir}"
  install -Dm0644 COPYING "$pkgdir"/usr/share/licenses/$pkgname/COPYING
}
