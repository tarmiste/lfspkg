# $Id: PKGBUILD 266875 2017-11-15 14:29:11Z foutrelis $
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-wsaccel
pkgname=('python-wsaccel' 'python2-wsaccel')
pkgver=0.6.2
_commit=f16d0b8cd9b54b5b2d7698a5b41d5c38e1564fb6
pkgrel=2
pkgdesc='Accelerator for ws4py and AutobahnPython'
arch=('x86_64')
url='https://github.com/methane/wsaccel'
license=('Apache')
makedepends=('python-setuptools' 'python2-setuptools' 'cython' 'cython2' 'git')
checkdepends=('python-pytest-runner' 'python2-pytest-runner')
source=("git+https://github.com/methane/wsaccel.git#commit=$_commit")
md5sums=('SKIP')

prepare() {
  cp -a wsaccel{,-py2}
}

build() {
  cd "$srcdir"/wsaccel
  python setup.py build

  cd "$srcdir"/wsaccel-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/wsaccel
  python setup.py pytest

  cd "$srcdir"/wsaccel-py2
  python2 setup.py pytest
}

package_python-wsaccel() {
  depends=('python')

  cd wsaccel
  python setup.py install --root="$pkgdir" -O1
}

package_python2-wsaccel() {
  depends=('python2')

  cd wsaccel-py2
  python2 setup.py install --root="$pkgdir" -O1
}
