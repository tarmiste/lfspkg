# $Id: PKGBUILD 256078 2017-09-06 20:13:02Z arojas $
# Maintainer: Thorsten TÃ¶pper <atsutane-tu@freethoughts.de>
# Contributor: Daniel Hommel <dhommel@gmail.com>

pkgname=mksh
_pkgver=R56b
pkgver=56.b
pkgrel=1
pkgdesc='The MirBSD Korn Shell - an enhanced version of the public domain ksh'
url='https://www.mirbsd.org/mksh.htm'
license=('custom')
arch=('i686' 'x86_64')
depends=('glibc') # Make namcap happy.
install=mksh.install
source=("https://www.mirbsd.org/MirOS/dist/mir/mksh/$pkgname-$_pkgver.tgz"
        'https://www.mirbsd.org/TaC-mksh.txt')
md5sums=('6b65b3798f5f6a057fa541fc08cc2be9'
         '0d39e63f8104667ef294165bd844c35c')

check() {
  cd "$srcdir/$pkgname"
  ./test.sh
}

build() {
  cd "$srcdir/$pkgname"
  sh Build.sh -r
}

package() {
  cd "$srcdir/$pkgname"
  install -D -m 755 mksh "$pkgdir/usr/bin/mksh"
  install -D -m 644 mksh.1 "$pkgdir/usr/share/man/man1/mksh.1"
  install -D -m 644 dot.mkshrc "$pkgdir/etc/skel/.mkshrc"
  install -D -m 644 "$srcdir/TaC-mksh.txt" "$pkgdir/usr/share/licenses/mksh/TaC-mksh.txt"
}

# vim:set ts=2 sw=2 et:
