# $Id: PKGBUILD 283276 2018-01-16 13:53:30Z arodseth $
# Maintainer: Alexander F RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Angel Velasquez <angvp@archlinux.org>
# Contributor: Douglas Soares de Andrade <dsa@aur.archlinux.br>

pkgbase=python-pyro
pkgname=("$pkgbase" "${pkgbase/-/2-}")
pkgver=4.63
pkgrel=2
pkgdesc='Distributed object middleware for Python (RPC)'
url='https://github.com/irmen/Pyro4'
arch=('any')
license=('MIT')
makedepends=('git')
depends=('python' 'python2')
source=("git+https://github.com/irmen/Pyro4#tag=$pkgver")
sha256sums=('SKIP')

package_python-pyro() {
  depends=('python' 'python-serpent')

  cd Pyro4

  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

package_python2-pyro() {
  depends=('python2' 'python2-serpent')

  cd Pyro4

  python2 setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# getver: github.com/irmen/Pyro4/tags
# vim: ts=2 sw=2 et:
