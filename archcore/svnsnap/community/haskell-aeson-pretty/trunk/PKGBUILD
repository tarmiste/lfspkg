# $Id: PKGBUILD 282047 2018-01-14 11:33:22Z felixonmars $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

_hkgname=aeson-pretty
pkgname=haskell-aeson-pretty
pkgver=0.8.5
pkgrel=18
pkgdesc="JSON pretty-printing library and command-line tool."
url="http://github.com/informatikr/aeson-pretty"
license=("custom:BSD3")
arch=('x86_64')
depends=('ghc-libs' "haskell-aeson" "haskell-attoparsec" "haskell-base-compat" "haskell-cmdargs"
         "haskell-scientific" "haskell-text" "haskell-unordered-containers" "haskell-vector")
makedepends=('ghc')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha512sums=('b429dad73923f6a7a5892b511af19b766d639c21bab8a2b8d1750a1f972c4f55c5139f37773474d303597cd4d02f168515be7b537f33ea62987e77fd3d701e8a')

build() {
    cd "${srcdir}/${_hkgname}-${pkgver}"

    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir="/usr/share/doc/${pkgname}" \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid \
            -f-lib-only
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd "${srcdir}/${_hkgname}-${pkgver}"

    install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
    install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
    runhaskell Setup copy --destdir="${pkgdir}"
    install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"
}
