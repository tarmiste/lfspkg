# $Id: PKGBUILD 284475 2018-01-21 01:01:13Z bgyorgy $
# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Jon Nordby <jononor@gmail.com>

pkgname=mypaint
pkgver=1.2.1
pkgrel=5
pkgdesc="Fast and easy painting application for digital painters, with brush dynamics"
arch=('x86_64')
url="http://mypaint.org/"
license=('GPL')
depends=('gtk3' 'python2-cairo' 'python2-gobject' 'python2-numpy')
makedepends=('scons' 'swig')
source=(https://github.com/mypaint/$pkgname/releases/download/v$pkgver/$pkgname-$pkgver.tar.xz
        fix-wayland.patch)
sha256sums=('ed2349382f6a0c583d7defae06427e97ac7d14acb77b9e2c0fc7171ddd96d3c7'
            '1ffa2e6e0deb236928f33c7f82cc037949e76600b5f7242f85b18380a241928b')

prepare() {
  cd $pkgname-$pkgver
  # Fix run under wayland
  # https://github.com/mypaint/mypaint/issues/791
  patch -Np1 -i ../fix-wayland.patch
}

build() {
  cd $pkgname-$pkgver
  scons
}

package() {
  cd $pkgname-$pkgver
  scons prefix="$pkgdir"/usr install
}
