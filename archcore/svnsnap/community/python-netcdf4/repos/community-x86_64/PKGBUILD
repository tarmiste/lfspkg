# Maintainer: Bruno Pagani <archange@archlinux.org>
# Contributor: Spyros Stathopoulos <foucault.online@gmail.com>
# Contributor: richli <rich at dranek dot com>
# Contributor: rememberthemer <rememberthemer@_GMAIL_DOT_COM_>

_pkg=netCDF4
pkgbase=python-${_pkg,,}
pkgname=("python-${_pkg,,}" "python2-${_pkg,,}")
pkgver=1.3.1
pkgrel=1
pkgdesc="Python/NumPy interface to the netCDF C library"
arch=('x86_64' 'i686')
url="https://unidata.github.io/${_pkg,,}-python"
license=('MIT')
makedepends=('cython' 'python-setuptools' 'cython2' 'python2-setuptools' 'python-numpy' 'python2-numpy' 'netcdf')
source=("https://pypi.io/packages/source/n/${_pkg}/${_pkg}-${pkgver}.tar.gz"
        'fix-cdf5-test.patch')
sha256sums=('570ea59992aa6d98a9b672c71161d11ba5683f787da53446086077470a869957'
            '5b300628ce1c22cdd58062196f3a410f673e568c04a35450baf3e75505734efc')

prepare() {
    cd ${_pkg}-${pkgver}
    patch -p1 -i ../fix-cdf5-test.patch
    cd ..
    cp -a ${_pkg}-${pkgver}{,-py2}
}

build() {
    cd ${_pkg}-${pkgver}
    USE_NCCONFIG=1 python setup.py build

    cd ../${_pkg}-${pkgver}-py2
    USE_NCCONFIG=1 python2 setup.py build
}

check() {
    cd ${_pkg}-${pkgver}/test
    NO_NET=1 PYTHONPATH="../build/lib.linux-${CARCH}-3.6" python -B ./run_all.py

    cd ../../${_pkg}-${pkgver}-py2/test
    PYTHONPATH="../build/lib.linux-${CARCH}-2.7" python2 -B ./run_all.py
}

package_python-netcdf4() {
    depends=('python-numpy' 'netcdf')

    cd ${_pkg}-${pkgver}
    USE_NCCONFIG=1 python setup.py install --prefix=/usr --root="${pkgdir}" --skip-build --optimize=2

    rm -r "${pkgdir}"/usr/bin
    install -Dm644 docs/netCDF4/* -t "${pkgdir}"/usr/share/doc/${pkgname}/docs/netCDF4
    install -Dm644 COPYING -t "${pkgdir}"/usr/share/licenses/${pkgname}
}

package_python2-netcdf4() {
    depends=('python2-numpy' 'netcdf')

    cd ${_pkg}-${pkgver}-py2
    USE_NCCONFIG=1 python2 setup.py install --prefix=/usr --root="${pkgdir}" --skip-build --optimize=2

    install -Dm644 docs/netCDF4/* -t "${pkgdir}"/usr/share/doc/${pkgname}/docs/netCDF4
    install -Dm644 COPYING -t "${pkgdir}"/usr/share/licenses/${pkgname}
}
