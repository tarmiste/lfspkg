# $Id: PKGBUILD 278566 2018-01-04 06:01:19Z anatolik $
# Maintainer: Anatol Pomozov <anatol pomozov at gmail> 
# Contributor: Daniel YC Lin <dlin.tw at gmail>
# Contributor: Konstantin Plotnikov <kostyapl at gmail dot com>

pkgname=fossil
pkgver=2.4
pkgrel=2
pkgdesc='Simple, high-reliability, distributed software configuration management'
arch=(x86_64)
license=(BSD)
url='http://www.fossil-scm.org'
depends=(openssl zlib sqlite)
makedepends=(tcl)
optdepends=(tcl)
backup=(etc/xinetd.d/fossil)
source=(
  fossil.socket
  fossil.service
  fossil-xinetd
  https://www.fossil-scm.org/index.html/uv/fossil-src-$pkgver.tar.gz
)
sha256sums=('f885e17998dc1eece1688a75e516663462fe72a7f4f132def4132055777c7ff8'
            'c4973079facf209d3757db81df08f8d0843ede242f2a0c12281720da65e3b166'
            '2f9a141fadfb7cb5225f0d65a308949bbc592bff9d567b5c472edb413c81f5aa'
            'd23498b6ae8894d18d7b73b8312c7514eda9ef91cf9f96c4c38429316919ad29')

build() {
  cd fossil-$pkgver
  ./configure --prefix=/usr --json --disable-internal-sqlite --with-tcl --with-tcl-private-stubs=1
  # headers and translate targets are problematic with parallel jobs
  #make -j1 bld bld/headers
  make
}

package() {
  install -Dm644 fossil-xinetd "$pkgdir"/etc/xinetd.d/fossil
  install -Dm644 fossil.socket "$pkgdir"/usr/lib/systemd/system/fossil.socket
  install -Dm644 fossil.service "$pkgdir"/usr/lib/systemd/system/fossil@.service

  cd fossil-$pkgver
  install -Dm755 fossil "$pkgdir"/usr/bin/fossil
  install -Dm644 tools/fossil-autocomplete.bash "$pkgdir"/usr/share/bash-completion/completions/fossil
  install -Dm644 COPYRIGHT-BSD2.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
