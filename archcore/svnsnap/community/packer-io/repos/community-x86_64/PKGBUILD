# Maintainer: Christian Rebischke <Chris.Rebischke[at]archlinux[dot]org>
_pkgname=packer
pkgname=packer-io
pkgver=1.1.3
pkgrel=2
pkgdesc="tool for creating identical machine images for multiple platforms from a single source configuration"
arch=('x86_64')
url="https://github.com/hashicorp/packer"
license=('MPL')
makedepends=('git' 'go-pie' 'go-tools')
depends=('glibc')
_packer_commit='9698d7283f790aa1d3755ccbd95a1a1cdea9390a'
_go_checkpoint_commit='1545e56e46dec3bba264e41fde2c1e2aa65b5dd4'
_go_cleanhttp_commit='3573b8b52aa7b37b9358d966a898feb387f62437'
_go_uuid_commit='64130c7a86d732268a38cb04cfbaf0cc987fda98'
_osext_commit='ae77be60afb1dcacde03767a8c37337fad28ac14'
_panicwrap_commit='fce601fe55579125e1b3cb0b992287e7290f7b83'
_cli_commit='65fcae5817c8600da98ada9d7edf26dd1a84837b'
_prefixedio_commit='6e6954073784f7ee67b28f2d22749d6479151ed7'
_go_radix_commit='1fca145dffbcaa8fe914309b1ec0cfc67500fe61'
_speakeasy_commit='4aabc24848ce5fd31929f7d1e4ea74d3709c14cd'
_go_isatty_commit='6ca4dbf54d38eea1a992b3c722a76a5d1c4cb25c'
source=("git+https://github.com/hashicorp/packer#commit=${_packer_commit}"
        "git+https://github.com/hashicorp/go-checkpoint#commit=${_go_checkpoint_commit}"
        "git+https://github.com/hashicorp/go-cleanhttp#commit=${_go_cleanhttp_commit}"
        "git+https://github.com/hashicorp/go-uuid#commit=${_go_uuid_commit}"
        "git+https://github.com/kardianos/osext#commit=${_osext_commit}"
        "git+https://github.com/mitchellh/panicwrap#commit=${_panicwrap_commit}"
        "git+https://github.com/mitchellh/cli#commit=${_cli_commit}"
        "git+https://github.com/mitchellh/prefixedio#commit=${_prefixedio_commit}"
        "git+https://github.com/armon/go-radix#commit=${_go_radix_commit}"
        "git+https://github.com/bgentry/speakeasy#commit=${_speakeasy_commit}"
        "git+https://github.com/mattn/go-isatty#commit=${_go_isatty_commit}")
sha512sums=('SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP')


prepare() {
  export GOPATH="${srcdir}"
  export PATH="$PATH:$GOPATH/bin"
  mkdir -p "src/github.com/mitchellh"
  mkdir -p "src/github.com/hashicorp"
  mkdir -p "src/github.com/kardianos"
  mkdir -p "src/github.com/armon"
  mkdir -p "src/github.com/bgentry"
  mkdir -p "src/github.com/mattn"
  mv ${_pkgname} src/github.com/hashicorp
  mv go-checkpoint src/github.com/hashicorp
  mv go-cleanhttp src/github.com/hashicorp
  mv go-uuid src/github.com/hashicorp
  mv osext src/github.com/kardianos
  mv panicwrap src/github.com/mitchellh
  mv cli src/github.com/mitchellh
  mv prefixedio src/github.com/mitchellh
  mv go-radix src/github.com/armon
  mv speakeasy src/github.com/bgentry
  mv go-isatty src/github.com/mattn
  sed -e '/VersionPrerelease = ""/ {N;N;N;d;}' -i "${srcdir}/src/github.com/hashicorp/${_pkgname}/Makefile"
}
build() {
  cd "src/github.com/hashicorp/${_pkgname}"
  make dev
}

#check() {
#  cd "src/github.com/hashicorp/${_pkgname}"
#  make test
#}

package() {
  cd "${srcdir}/bin/"
  install -Dm755 packer "${pkgdir}/usr/bin/packer-io"
}

# vim:set et sw=2 ts=2 tw=79:
