# $Id: PKGBUILD 205463 2017-01-06 08:00:56Z felixonmars $
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-pytest-isort
pkgname=('python-pytest-isort' 'python2-pytest-isort')
pkgver=0.1.0
_commit=86363b59c1c9de7b3c03362971d942e627c06a28
pkgrel=2
pkgdesc='pytest plugin to perform isort checks (import ordering)'
arch=('any')
license=('BSD')
url='http://github.com/moccu/pytest-isort'
makedepends=('python-pytest-cache' 'python2-pytest-cache' 'python-isort' 'python2-isort' 'git')
source=("git+https://github.com/moccu/pytest-isort#commit=$_commit")
md5sums=('SKIP')

prepare() {
  cp -a pytest-isort{,-py2}
}

build() {
  cd "$srcdir"/pytest-isort
  python setup.py build

  cd "$srcdir"/pytest-isort-py2
  python2 setup.py build
}

check() {
  # Hack entry points by installing it

  cd "$srcdir"/pytest-isort
  python setup.py install --root="$PWD/tmp_install" --optimize=1
  PYTHONPATH="$PWD/tmp_install/usr/lib/python3.6/site-packages:$PYTHONPATH" py.test || warning "Tests failed"

  cd "$srcdir"/pytest-isort-py2
  python2 setup.py install --root="$PWD/tmp_install" --optimize=1
  PYTHONPATH="$PWD/tmp_install/usr/lib/python2.7/site-packages:$PYTHONPATH" py.test2 || warning "Tests failed"
}

package_python-pytest-isort() {
  depends=('python-pytest-cache' 'python-isort')

  cd pytest-isort
  python setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE.rst "$pkgdir"/usr/share/licenses/$pkgname/LICENSE.rst
}

package_python2-pytest-isort() {
  depends=('python2-pytest-cache' 'python2-isort')

  cd pytest-isort-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE.rst "$pkgdir"/usr/share/licenses/$pkgname/LICENSE.rst
}

# vim:set ts=2 sw=2 et:
