# $Id: PKGBUILD 286932 2018-01-25 20:55:17Z felixonmars $
# Maintainer: David Runge <dave@sleepmap.de>
# Contributor: speps <speps at aur dot archlinux dot org>
# Contributor: Alexander Fehr <pizzapunk gmail com>
# Contributor: dorphell <dorphell@archlinux.org>

pkgname=sylpheed
pkgver=3.6.0
pkgrel=4
pkgdesc="Lightweight and user-friendly e-mail client"
arch=('x86_64')
url="https://sylpheed.sraoss.jp/en/"
license=('GPL')
depends=('compface' 'gpgme' 'gtkspell')
makedepends=('openssl')
source=("https://sylpheed.sraoss.jp/${pkgname}/v${pkgver%.*}/${pkgname}-${pkgver}.tar.bz2")
sha512sums=('84c59e327690ed7e637e9ce60e630da6cdad771ccc278e31e490ed643da55c295fb47e999ad421fc59dbbcfed990882022b96cf478bd47253692d2e86279c591')

prepare() {
  cd "${pkgname}-${pkgver}"
  # patch for enchant >= 2.1.3
  # https://www.archlinux.org/todo/enchant-221-rebuild/
  sed -i 's,enchant/,enchant-2/,g' src/compose.c
  sed -i 's/ enchant/ enchant-2/g' configure
}

build() {
  cd "${pkgname}-${pkgver}"
  ./configure --prefix=/usr \
              --enable-maintainer-mode \
              --enable-ldap
  make

  # Build Attachment-Tool Plug-in
  cd plugin/attachment_tool && make
}

package() {
  cd "${pkgname}-${pkgver}"
  make DESTDIR="$pkgdir/" LDFLAGS+="/usr/lib/enchant-2" install

  # Install Attachment-Tool Plug-in
  cd plugin/attachment_tool
  make DESTDIR="$pkgdir/" install-plugin
}
