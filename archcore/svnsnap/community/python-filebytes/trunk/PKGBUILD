# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

pkgbase=python-filebytes
pkgname=('python-filebytes' 'python2-filebytes')
_pyname=${pkgbase/python-/}
pkgver=0.9.13
pkgrel=1
pkgdesc='Library to read and edit executable binary files'
url='https://github.com/sashs/filebytes'
arch=('any')
license=('GPL2')
makedepends=('python' 'python2' 'python-setuptools' 'python2-setuptools')
source=(${pkgbase}-${pkgver}.tar.gz::https://github.com/sashs/filebytes/archive/v${pkgver}.tar.gz)
sha512sums=('e6c8fe3647fc7a600ffc26883bc90724b5cfe18c8c5603d7a184e73e5fee3ae27ab01e793ef1ddbd8a69fe294bbc33cb5b3314d815d8f498f57876f83566de16')

prepare() {
  cp -ra ${_pyname}-${pkgver}{,-py2}
  sed -r 's|env python$|env python2|' -i ${_pyname}-${pkgver}-py2/samples/*
}

package_python-filebytes() {
  depends=('python')
  cd ${_pyname}-${pkgver}
  python setup.py install -O1 --root="${pkgdir}" --prefix=/usr
  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 samples/* -t "${pkgdir}/usr/share/doc/${pkgname}/samples"
}

package_python2-filebytes() {
  depends=('python2')
  cd ${_pyname}-${pkgver}-py2
  python2 setup.py install -O1 --root="${pkgdir}" --prefix=/usr
  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 samples/* -t "${pkgdir}/usr/share/doc/${pkgname}/samples"
}

# vim: ts=2 sw=2 et:
