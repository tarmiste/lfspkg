# Maintainer: Thore BÃ¶decker <foxxx0@archlinux.org>
# Contributor: wangjiezhe <wangjiezhe AT yandex DOT com>

pkgbase='python-cheroot'
_pkgbase="${pkgbase//python-/}"
pkgname=('python-cheroot' 'python2-cheroot')
pkgdesc="Highly-optimized, pure-python HTTP server"
pkgver=6.0.0
pkgrel=1
arch=('any')
url="https://github.com/cherrypy/cheroot"
license=('MIT')
makedepends=('python-six' 'python2-six' 'python-setuptools-scm'
  'python2-setuptools-scm')
source=("${pkgbase}-${pkgver}.tar.gz::https://pypi.python.org/packages/a7/d1/27e974bd18ec4933717911a746f3b3b8a2ff66549bd7dc510054e40e42f2/cheroot-6.0.0.tar.gz"
        "LICENSE")
sha512sums=('0109fbf3b73ed3f12261eaa3c6d86b6c6b80a1ba3564548ba3288aa6c61a1c08db60886e677991bf7997e6a5b1ec7767a3e06098ec1748453e72c40fda7b82a7'
            '1106afed483b7258e4ae89c5d9459c3834412b31aac90169725ed62d2ab44f61f6f79e894d4c9e4d8bd99e14530ab778df2187784f0b25eaab86d312fad68944')

package_python-cheroot() {
  depends=('python-six')
  cd "${srcdir}/${_pkgbase}-${pkgver}"
  python setup.py install --root="$pkgdir/" --optimize=1

  # the author has promised to include a LICENSE file in future releases:
  # https://github.com/jaraco/skeleton/issues/1
  # for now the LICENSE file has been taken from:
  # https://github.com/jaraco/skeleton/blob/skeleton/LICENSE
  install -D -m644 "${srcdir}/LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

package_python2-cheroot() {
  depends=('python2-six')
  cd "${srcdir}/${_pkgbase}-${pkgver}"
  python2 setup.py install --root="$pkgdir/" --optimize=1

  # the author has promised to include a LICENSE file in future releases:
  # https://github.com/jaraco/skeleton/issues/1
  # for now the LICENSE file has been taken from:
  # https://github.com/jaraco/skeleton/blob/skeleton/LICENSE
  install -D -m644 "${srcdir}/LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

# vim:set ts=2 sw=2 et:
