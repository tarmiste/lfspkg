# $Id: PKGBUILD 286807 2018-01-25 13:32:21Z dvzrv $
# Maintainer: David Runge <dave@sleepmap.de>

_pkgver=1.3.4-2
pkgbase=fltk
pkgname=('fltk' 'fltk-docs' 'fltk-games')
pkgver=${_pkgver//-/.}
pkgrel=2
arch=('x86_64')
license=('custom:LGPL')
url="http://www.fltk.org/"
makedepends=('mesa' 'glu' 'doxygen' 'libjpeg' 'libxft' 'libxinerama'
             'alsa-lib' 'desktop-file-utils' 'libxcursor')
source=("http://fltk.org/pub/${pkgbase}/${_pkgver%"-2"}/${pkgbase}-${_pkgver}-source.tar.gz"
        fltk-config-dynlibs.patch)
sha512sums=('cc169449b71ca966b2043ceedc55e92220ccb6be07b0ac54eeec36bbed5d60e2f59c6faba2403b5292b9120f5255227880a066d98ac82e57d502522bc627fd4d'
            'c2f15a5c134a258082454dacf8db14b4d276c8c9ed207b1d2275690a2829272cfee81186caf794031339f7ed619898485bd59c6d59d2e71cd98facfbb564c1f5')

prepare() {
  cd "${pkgbase}-${_pkgver}"
  # Fix broken removal of static linking: https://bugs.archlinux.org/task/38276
  patch -p1 -i ../fltk-config-dynlibs.patch
  sed -i 's/class Fl_XFont_On_Demand/class FL_EXPORT Fl_XFont_On_Demand/' FL/x.H
  sed -i 's/x-fluid.desktop/fluid.desktop/' -i fluid/Makefile
  sed -i -e 's/$(LINKFLTK)/$(LINKSHARED)/' \
         -e 's/$(LINKFLTKIMG)/$(LINKSHARED)/' test/Makefile
}

build() {
  cd "${pkgbase}-${_pkgver}"
  ./configure --prefix=/usr \
    --enable-threads \
    --enable-xft \
    --enable-shared
  make
  make -C documentation html
}

package_fltk() {
  pkgdesc="Graphical user interface toolkit for X"
  depends=('libjpeg' 'libxft' 'libxinerama' 'hicolor-icon-theme'
           'desktop-file-utils' 'xdg-utils' 'libxcursor' 'glu')
  options=('!docs')

  cd "${pkgbase}-${_pkgver}"
  make DESTDIR="$pkgdir" install
  make DESTDIR="$pkgdir" -C fluid install install-linux
  install -Dm 644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

package_fltk-docs() {
  pkgdesc="Graphical user interface toolkit for X (documentation)"

  cd "${pkgbase}-${_pkgver}/documentation"
  install -t "${pkgdir}/usr/share/doc/fltk/" -Dm644 html/*
  make DESTDIR="$pkgdir" -C ../test install
  install -Dm 644 ../COPYING "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

package_fltk-games() {
  pkgdesc="Example games that accompany FLTK: blocks, checkers and sudoku"
  depends=('alsa-lib' 'fltk')

  cd "${pkgbase}-${_pkgver}/test"
  make DESTDIR="$pkgdir" install-linux
  make DESTDIR="$pkgdir" -C ../documentation install-linux
  install -Dm 644 ../COPYING "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
