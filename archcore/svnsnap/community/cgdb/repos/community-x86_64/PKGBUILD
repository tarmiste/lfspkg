# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Marek Kubica <marek@xivilization.net>
# Contributor: Javier ‘Phrodo_00’ Aravena <Phrodo.00@gmail.com>
pkgname=cgdb
pkgver=0.7.0
pkgrel=1
pkgdesc="Curses-based interface to the GNU Debugger"
arch=('i686' 'x86_64')
url="http://cgdb.github.io/"
license=('GPL')
depends=('readline>=5.1' 'ncurses' 'gdb')
makedepends=('help2man')
source=("https://github.com/cgdb/cgdb/archive/v${pkgver}.tar.gz")
sha512sums=('83aeb919e96cd0981d8fe5cdd257df3e8d8bfbceea720ea66c1773b28c9f0ecb0e303e0585206c55242bc4438d505e328d43b4991c6ddc4e4202e048558c30d9')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  ./autogen.sh
  ./configure --prefix=/usr
  make
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}/" install
}
