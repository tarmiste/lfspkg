# Maintainer: David Runge <dave@sleepmap.de>

pkgname=python-imdbpy
pkgver=6.2
pkgrel=1
pkgdesc="Python bindings for the Internet Movie Database (IMDb)"
url="https://imdbpy.sourceforge.io/"
arch=('any')
license=('GPL2')
depends=('python')
makedepends=('python-pytest' 'python-setuptools')
optdepends=('python-lxml' 'python-sqlalchemy')
source=("https://github.com/alberanid/${pkgname/python-/}/archive/${pkgver}.tar.gz")
sha512sums=('78b371eb73eb2eda2df465ab83a05152d156eb19dd05f317173aa15b8b56c14fa9f9d4943d053c93a29da794b608a0ea4af3353143fb76e033c6dc0d8e651cd8')

prepare() {
  cd "${pkgname/python-/}-${pkgver}"
  # fix install folder for docs
  sed -i "s/'doc'/'share\/doc'/g" setup.py
  # fix install folder for config
  sed -i "s/'etc'/'\/etc'/g" setup.py
}

build() {
  cd "${pkgname/python-/}-${pkgver}"
  python setup.py build
}

package() {
  cd "${pkgname/python-/}-${pkgver}"
  python setup.py install --skip-build \
    --optimize=1 \
    --prefix=/usr \
    --root="${pkgdir}/"
  install -Dm644 README.md "${pkgdir}/usr/share/doc/${pkgname}/README.md"
}

# vim:set ts=2 sw=2 et:
