# Maintainer: Jelle van der Waa <jelle@archlinux.org>
# Contributor: Maxim Andersson <thesilentboatman@gmail.com>

pkgbase=python-zeroconf
pkgname=('python-zeroconf' 'python2-zeroconf')
pkgver=0.19.1
pkgrel=1
pkgdesc="A pure python implementation of multicast DNS service discovery"
arch=('any')
url="https://github.com/jstasiak/python-zeroconf"
license=('LGPL')
makedepends=('python-setuptools' 'python2-setuptools')
source=("https://github.com/jstasiak/${pkgname}/archive/${pkgver}.tar.gz")
sha256sums=('ca8952a3eb04818dcb566e1344f2f15b5dd6d487770384651cdfae777d5eb848')

package_python-zeroconf() {
  depends=('python' 'python-netifaces' 'python-six')
  cd "${srcdir}/${pkgbase}-${pkgver}"

  # No need for enum-compat on Python 3.4+
  sed -i "s/'enum-compat',//" setup.py

  python setup.py install --prefix=/usr --root="${pkgdir}" --optimize=1
}

package_python2-zeroconf() {
  depends=('python2' 'python2-netifaces' 'python2-six')
  cd "${srcdir}/${pkgbase}-${pkgver}"

  python2 setup.py install --prefix=/usr --root="${pkgdir}" --optimize=1
}

# vim:set ts=2 sw=2 et:
