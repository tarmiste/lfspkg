# Maintainer: Alexander F RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Vitaliy Berdinskikh <ur6lad@archlinux.org.ua>

pkgname=apache-ivy
pkgver=2.4.0
pkgrel=2
pkgdesc='Dependency manager'
arch=('any')
url='http://ant.apache.org/ivy/'
license=('Apache')
depends=('bash' 'apache-ant')
source=("http://mirror.ventraip.net.au/apache/ant/ivy/${pkgver//_/-}/${pkgname}-${pkgver//_/-}-bin.tar.gz" 'ivy.sh')
sha256sums=('7a3d13a80b69d71608191463dfc2a74fff8ef638ce0208e70d54d28ba9785ee9'
            '8abb3c0a8f03809d665083053bb432591f1b9c8852009bf31cd61a71cc28b4f5')

package() {
  install -d "$pkgdir/usr/"{bin,share/java/apache-ivy,share/java/apache-ant}
  install -m 644 "$pkgname-${pkgver//_/-}/ivy-${pkgver//_/-}.jar" \
    "$pkgdir/usr/share/java/apache-ivy"
  install -m 755 ivy.sh "$pkgdir/usr/bin/ivy"
  ln -s "ivy-${pkgver//_/-}.jar" "$pkgdir/usr/share/java/apache-ivy/ivy.jar"
  ln -s "../apache-ivy/ivy-${pkgver//_/-}.jar" "$pkgdir/usr/share/java/apache-ant/ivy.jar"
}

# vim:set ts=2 sw=2 et:
