# Maintainer: Levente Polyak <anthraxx@archlinux.org>
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=lib32-libpsl
pkgver=0.19.1
pkgrel=1
pkgdesc='Public Suffix List library'
url='https://github.com/rockdaboot/libpsl'
arch=('x86_64')
license=('MIT')
depends=('lib32-icu' 'libpsl')
makedepends=('publicsuffix-list' 'python')
source=(https://github.com/rockdaboot/libpsl/releases/download/libpsl-${pkgver}/libpsl-${pkgver}.tar.gz)
sha512sums=('86bb2f35764491c65cacec8cd5de97c7d7e1c4d1cc2590d54f394ed17acc75922494115019631b2d930d64dbdc73c266efb323036ac7e52131111287ee6381d5')

prepare() {
  cd libpsl-${pkgver}
  rm -frv list
}

build() {
  cd libpsl-${pkgver}

  export CC='gcc -m32'
  export CXX='g++ -m32'
  export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'

  ./configure \
    --prefix='/usr' \
    --libdir='/usr/lib32' \
    --disable-static \
    --disable-dependency-tracking \
    --enable-{builtin,runtime}='libicu' \
    --with-psl-file='/usr/share/publicsuffix/effective_tld_names.dat' \
    --with-psl-testfile='/usr/share/publicsuffix/test_psl.txt'
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  LC_CTYPE=en_US.UTF-8 make
}

check() {
  cd libpsl-${pkgver}
  make check
}

package() {
  cd libpsl-${pkgver}

  make DESTDIR="${pkgdir}" install
  rm -rf "${pkgdir}"/usr/{bin,include,share}

  install -dm 755 "${pkgdir}"/usr/share/licenses
  ln -s libpsl "${pkgdir}"/usr/share/licenses/lib32-libpsl
}

# vim: ts=2 sw=2 et:
