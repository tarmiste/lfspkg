# $Id: PKGBUILD 255298 2017-09-02 11:46:10Z foutrelis $
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: Charles Mauch <cmauch@gmail.com>

pkgname=perl-authen-sasl
pkgver=2.16
pkgrel=4
pkgdesc="Perl/CPAN Module Authen::SASL : SASL authentication framework"
arch=(any)
url="http://search.cpan.org/dist/Authen-SASL"
license=("GPL" "PerlArtistic")
source=("http://www.cpan.org/authors/id/G/GB/GBARR/Authen-SASL-$pkgver.tar.gz")
md5sums=('7c03a689d4c689e5a9e2f18a1c586b2f')

build() {
  cd "$srcdir"/Authen-SASL-$pkgver
  PERL_USE_UNSAFE_INC=1 \
  PERL_MM_USE_DEFAULT=1 perl Makefile.PL INSTALLDIRS=vendor
  make
}

package() {
  cd "$srcdir"/Authen-SASL-$pkgver
  make pure_install doc_install DESTDIR="$pkgdir"
  find "$pkgdir" -name '.packlist' -delete
  find "$pkgdir" -name '*.pod' -delete
}
