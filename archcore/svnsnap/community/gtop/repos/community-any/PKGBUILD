# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Peter Cai <peter at typeblog dot net>

pkgname=gtop
pkgver=0.1.4
pkgrel=1
pkgdesc='System monitoring dashboard for terminal'
arch=('any')
url='https://github.com/aksakalli/gtop'
license=('MIT')
depends=(nodejs)
makedepends=(npm)
source=("https://github.com/aksakalli/gtop/archive/v${pkgver}.tar.gz")
sha512sums=('15d1dde481ca4504a3d78b0b8acdd672082356d69df66de67dd471504c4792e17d5521661c354d8036d5c7de371c24643a34162d401d934fc09373b6bb4c70b5')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  npm install --production
}

package() {
  local _npmdir="$pkgdir/usr/lib/node_modules"
  mkdir -p "$_npmdir"
  cp -R "$srcdir/$pkgname-$pkgver" "$_npmdir/gtop"
  local _bindir="$pkgdir/usr/bin"
  mkdir -p "$_bindir"
  ln -s /usr/lib/node_modules/gtop/bin/gtop "$_bindir/gtop"
}
