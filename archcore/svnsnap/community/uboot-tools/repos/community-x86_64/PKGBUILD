# $Id: PKGBUILD 244258 2017-07-11 14:23:57Z jelle $
# Maintainer: Anatol Pomozov
# Contributor: Michael Lass <bevan@bi-co.net>
# Contributor: Philipp Schrader <philipp.schrader+arch@gmail.com>

pkgname=uboot-tools
pkgver=2017.07
pkgrel=1
pkgdesc='U-Boot bootloader utility tools'
arch=(i686 x86_64)
url='http://www.denx.de/wiki/U-Boot/WebHome'
license=(GPL)
depends=(openssl)
source=(ftp://ftp.denx.de/pub/u-boot/u-boot-$pkgver.tar.bz2{,.sig})
validpgpkeys=('E872DB409C1A687EFBE8633687F9F635D31D7652')
sha1sums=('517c33f41e2969f3b5b88e9ae3c042acfe1bde05'
          'SKIP')

build() {
  cd u-boot-$pkgver
  make defconfig
  make tools-all
}

package() {
  install -m 755 -d "$pkgdir"/usr/bin
  install -m 755 -t "$pkgdir"/usr/bin/ u-boot-$pkgver/tools/{mk{,env}image,env/fw_printenv,img2srec,dumpimage,netconsole,jtagconsole,ncb}
  ln -s fw_printenv "$pkgdir"/usr/bin/fw_setenv
  install -m 644 -D u-boot-$pkgver/doc/mkimage.1 "$pkgdir"/usr/share/man/man1/mkimage.1
}
