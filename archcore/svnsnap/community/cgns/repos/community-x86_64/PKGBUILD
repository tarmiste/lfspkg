# Maintainer: Bruno Pagani <archange@archlinux.org>
# Contributor: eolianoe <eolianoe At GoogleMAIL DoT com>
# Contriburor: Mathias Anselmann <mathias.anselmann@gmail.com>
# Contributor: Thomas Dziedzic < gostrc at gmail >
# Contributor: lainme <lainme993@gmail.com>
# Contributor: Klimov Max <cleemmi@gmail.com>

_pkgname=CGNS
pkgname=cgns
pkgver=3.3.1
pkgrel=5
pkgdesc="Standard for recording and recovering computer data associated with the numerical solution of fluid dynamics equations"
arch=('x86_64')
url='https://cgns.github.io/'
license=('custom')
depends=('tk' 'hdf5' 'libxmu' 'glu')
makedepends=('gcc-fortran' 'cmake')
options=(!makeflags)
source=(${pkgname}-${pkgver}.tar.gz::"https://github.com/${_pkgname}/${_pkgname}/archive/v${pkgver}.tar.gz")
sha256sums=('81093693b2e21a99c5640b82b267a495625b663d7b8125d5f1e9e7aaa1f8d469')

prepare(){
    mkdir -p build
}

build() {
    cd build

    cmake ../${_pkgname}-${pkgver} \
        -DCGNS_BUILD_CGNSTOOLS=ON \
        -DCGNS_BUILD_SHARED=ON \
        -DCGNS_ENABLE_64BIT=ON \
        -DCGNS_ENABLE_FORTRAN=ON \
        -DCGNS_ENABLE_HDF5=ON \
        -DCGNS_ENABLE_LEGACY=ON \
        -DCGNS_ENABLE_SCOPING=OFF \
        -DCGNS_ENABLE_TESTS=ON \
        -DCMAKE_BUILD_TYPE="Release" \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_SKIP_RPATH=ON

    make
}


check() {
    cd build

    make test
}

package() {
    cd build

    make DESTDIR="${pkgdir}" install

    install -Dm644 "${srcdir}"/${_pkgname}-${pkgver}/license.txt -t "${pkgdir}"/usr/share/licenses/${pkgname}/
}
