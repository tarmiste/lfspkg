# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

_gemname=rouge
pkgname=ruby-rouge
pkgver=3.0.0
pkgrel=2
pkgdesc='Pure-ruby code highlighter that is compatible with pygments'
url='http://rouge.jneen.net/'
arch=('any')
license=('MIT')
depends=('ruby')
options=('!emptydirs')
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/jneen/rouge/archive/v${pkgver}.tar.gz)
sha256sums=('46f109e0b54e588376b70935556f248e9f70d79bb043677fc5c19c174ace5a7d')
sha512sums=('9ec9a22b0b4c0ca37b765b7f76104f64c46d2c6f3d7073f8ad6eb3512f5671b1d828f8bdd2073c67dba18afb682f8111e56112a23c0d66debb418573d4fb8007')

prepare() {
  cd ${_gemname}-${pkgver}
  sed -r 's|~>|>=|g' -i ${_gemname}.gemspec # don't give a fuck about rubys bla bla
}

build() {
  cd ${_gemname}-${pkgver}
  gem build ${_gemname}.gemspec
}

package() {
  cd ${_gemname}-${pkgver}
  local _gemdir="$(gem env gemdir)"
  gem install --ignore-dependencies --no-user-install -i "${pkgdir}${_gemdir}" -n "${pkgdir}/usr/bin" ${_gemname}-${pkgver}.gem
  install -Dm 644 README.md CHANGELOG.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  rm "${pkgdir}/${_gemdir}/cache/${_gemname}-${pkgver}.gem"
}

# vim: ts=2 sw=2 et:
