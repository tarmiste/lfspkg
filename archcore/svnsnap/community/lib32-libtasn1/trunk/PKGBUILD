# $Id: PKGBUILD 284263 2018-01-20 10:33:01Z lcarlier $
# Maintainer: Laurent Carlier <lordheavym@gmail.com>
# Contributor: Florian Pritz <bluewind@xinu.at>
# Contributor: josephgbr <rafael.f.f1 at gmail.com>

_pkgbase=libtasn1
pkgname=lib32-${_pkgbase}
pkgver=4.13
pkgrel=1
pkgdesc="The ASN.1 library used in GNUTLS (32 bit)"
arch=('x86_64')
license=('GPL3' 'LGPL')
url="https://www.gnu.org/software/libtasn1/"
depends=('lib32-glibc' ${_pkgbase})
makedepends=('gcc-multilib')
options=('!libtool')
source=(https://ftp.gnu.org/gnu/libtasn1/${_pkgbase}-${pkgver}.tar.gz{,.sig})
sha256sums=('7e528e8c317ddd156230c4e31d082cd13e7ddeb7a54824be82632209550c8cca'
            'SKIP')
validpgpkeys=('1F42418905D8206AA754CCDC29EE58B996865171') #Nikos Mavrogiannopoulos <nmav@gnutls.org>

build() {
  export CC='gcc -m32'
  export CXX='g++ -m32'
  export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'

  cd ${_pkgbase}-${pkgver}

  ./configure --prefix=/usr \
      --libdir=/usr/lib32 \
      --with-packager=Archlinux \
      --with-packager-bug-reports="http://bugs.archlinux.org/" \
      --with-packager-version=${pkgver}-${pkgrel}
  make
}

package() {
  cd ${_pkgbase}-${pkgver}

  make DESTDIR="${pkgdir}" install

  rm -rf "${pkgdir}/usr/"{bin,include,share}
}
