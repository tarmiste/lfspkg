# $Id: PKGBUILD 276083 2017-12-26 02:24:37Z eschwartz $
# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=python-minidb
_pkgname=minidb
pkgver=2.0.2
pkgrel=1
pkgdesc="A simple SQLite3 store for Python objects"
arch=('any')
url="https://thp.io/2010/minidb/"
license=('ISC')
depends=('python')
source=("$_pkgname-$pkgver.tar.gz::https://github.com/thp/$_pkgname/archive/$pkgver.tar.gz")
md5sums=('1aa7b7afa9c51507a595895d162ad12f')

build() {
  cd $_pkgname-$pkgver
  python3 setup.py build
}

package() {
  cd $_pkgname-$pkgver
  python3 setup.py install --root="$pkgdir"

  install -dm755 "$pkgdir/usr/share/licenses/$pkgname"
  sed -n '/# Copyright/,/OF THIS SOFTWARE./p' minidb.py > "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}
