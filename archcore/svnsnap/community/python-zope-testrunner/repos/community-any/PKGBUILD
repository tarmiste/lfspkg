# $Id: PKGBUILD 266478 2017-11-12 16:05:53Z felixonmars $
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-zope-testrunner
pkgname=('python-zope-testrunner' 'python2-zope-testrunner')
pkgver=4.8.1
pkgrel=1
pkgdesc="Zope testrunner script"
arch=('any')
url="https://github.com/zopefoundation/zope.testrunner"
license=('ZPL')
makedepends=('python-setuptools' 'python2-setuptools' 'python-zope-interface'
             'python2-zope-interface' 'python-zope-exceptions' 'python2-zope-exceptions'
             'python-six' 'python2-six')
checkdepends=('python-zope-testing' 'python2-zope-testing' 'python-zope-testrunner'
              'python2-zope-testrunner')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/zopefoundation/zope.testrunner/archive/$pkgver.tar.gz")
sha512sums=('3ea5ceacdeff59c7f393efd6ba6768bd04701739f507a21902112fd5a0b5f31ba470178e761eddb0864caaafec7fae11574d094892825603f45c3b5e4faeec71')

prepare() {
  cp -a zope.testrunner-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/zope.testrunner-$pkgver
  python setup.py build

  cd "$srcdir"/zope.testrunner-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/zope.testrunner-$pkgver
  python setup.py test

  cd "$srcdir"/zope.testrunner-$pkgver-py2
  python2 setup.py test
}

package_python-zope-testrunner() {
  depends=('python-setuptools' 'python-zope-interface' 'python-zope-exceptions' 'python-six')

  cd zope.testrunner-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}

package_python2-zope-testrunner() {
  depends=('python2-setuptools' 'python2-zope-interface' 'python2-zope-exceptions' 'python2-six')

  cd zope.testrunner-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1

  mv "$pkgdir"/usr/bin/zope-testrunner{,2}
}

# vim:set ts=2 sw=2 et:
