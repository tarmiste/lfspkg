# $Id: PKGBUILD 258619 2017-09-17 15:02:28Z felixonmars $
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-daiquiri
pkgname=('python-daiquiri' 'python2-daiquiri')
pkgver=1.3.0
pkgrel=1
pkgdesc='Library to configure Python logging easily'
arch=('any')
license=('Apache')
url='https://github.com/jd/daiquiri'
makedepends=('python-pbr' 'python2-pbr')
checkdepends=('python-testrepository' 'python2-testrepository' 'python-testtools'
              'python2-testtools' 'python-json-logger' 'python2-json-logger')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/jd/daiquiri/archive/$pkgver.tar.gz")
sha512sums=('858536bdff836655a6a3e5bdbfe125d3ced74ae19a3931450a0b7253d234ca74492a258cab8a45d75c0ca7b955970b439c786c1ab59d7901377fae7047b5cf35')

prepare() {
  cp -a daiquiri-$pkgver{,-py2}

  export PBR_VERSION=$pkgver
}

build() {
  cd "$srcdir"/daiquiri-$pkgver
  python setup.py build

  cd "$srcdir"/daiquiri-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/daiquiri-$pkgver
  python setup.py testr

  cd "$srcdir"/daiquiri-$pkgver-py2
  python2 setup.py testr
}

package_python-daiquiri() {
  depends=('python')
  optdepends=('python-json-logger: For JSON support')

  cd daiquiri-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}

package_python2-daiquiri() {
  depends=('python2')
  optdepends=('python2-json-logger: For JSON support')

  cd daiquiri-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
}

# vim:set ts=2 sw=2 et:
