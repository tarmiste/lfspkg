# $Id: PKGBUILD 234682 2017-06-06 09:02:11Z bluewind $
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: Jan-Erik Rediger <badboy at archlinux dot us>
# Contributor: Gerardo Exequiel Pozzi <vmlinuz386@yahoo.com.ar>
# Contributor: Francois Charette <firmicus@gmx.net>

pkgname=perl-ipc-run
pkgver=0.96
pkgrel=1
pkgdesc="IPC::Run - system() and background procs w/ piping, redirs, ptys"
arch=('any')
url="http://search.cpan.org/dist/IPC-Run"
license=('GPL' 'PerlArtistic')
# IPC::Run depends on IO::Pty which is provided by perl-io-tty
depends=('perl' 'perl-io-tty')
options=('!emptydirs')
source=("http://search.cpan.org/CPAN/authors/id/T/TO/TODDR/IPC-Run-$pkgver.tar.gz")
sha256sums=('bbd24ff027e1c232b3dc027562c9f4386da72b76991d30f9f3d4119e87cf4640')

build() {
  cd "$srcdir"/IPC-Run-$pkgver
  PERL_MM_USE_DEFAULT=1 perl Makefile.PL INSTALLDIRS=vendor
  make
}

package() {
  cd "$srcdir"/IPC-Run-$pkgver
  make install DESTDIR="$pkgdir"
  find "$pkgdir" -name perllocal.pod -delete
  find "$pkgdir" -name .packlist -delete
}
