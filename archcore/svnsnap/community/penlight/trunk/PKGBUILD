# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: SÃ©bastien Luttringer
# Contributor: SpepS <dreamspepser at yahoo dot it>
# Contributor: Laszlo Papp <djszapi at archlinux us>
# Contributor: Donald Ephraim Curtis <dcurtis@gmail.com>

pkgbase=penlight
pkgname=('lua-penlight')
pkgver=1.5.4
pkgrel=1
pkgdesc='Lua libraries focusing on input data handling'
url='https://github.com/stevedonovan/penlight'
arch=('any')
license=('custom')
depends=('lua' 'lua-filesystem')
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/stevedonovan/penlight/archive/${pkgver}.tar.gz)
sha256sums=('c0bcbb0874fb559ab0a3de30a21244799b7fdf8b81b34a3df0493b12ee7a5ff1')
sha512sums=('c46e7875410a90390bfbf010a53f1d536475e2094668722618d03951caad0fcb0674c9bc7119a0753e4ed3df9e4d70fef828ba4c6c24e8ada4036948d2432519')

check() {
  cd Penlight-${pkgver}
  export LUA_PATH="${PWD}/lua/?/init.lua;${PWD}/lua/?.lua;$(lua -e 'print(package.path)')"
  lua run.lua
}

package_lua-penlight() {
  cd Penlight-${pkgver}
  install -Dm 644 lua/pl/* -t "${pkgdir}/usr/share/lua/5.3/pl"
  install -Dm 644 CONTRIBUTING.md CHANGES.md README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 doc/manual/* -t "${pkgdir}/usr/share/doc/${pkgname}/manual"
  install -Dm 644 examples/* -t "${pkgdir}/usr/share/doc/${pkgname}/examples"
  install -Dm 644 LICENSE.md "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

# vim: ts=2 sw=2 et:
