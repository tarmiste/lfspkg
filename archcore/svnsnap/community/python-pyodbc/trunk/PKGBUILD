# $Id: PKGBUILD 266875 2017-11-15 14:29:11Z foutrelis $
# Maintainer: Christian Hesse <mail@eworm.de>
# Contributor: Robert Knauer <robert@privatdemail.net>

pkgbase=python-pyodbc
pkgname=(python-pyodbc python2-pyodbc)
_srcname=pyodbc
pkgver=4.0.21
pkgrel=1
pkgdesc='ODBC module for Python'
arch=('x86_64')
url='https://github.com/mkleehammer/pyodbc'
license=('MIT')
makedepends=('python' 'python2' 'unixodbc')
source=("${pkgbase}-${pkgver}.tar.gz::https://github.com/mkleehammer/pyodbc/archive/${pkgver}.tar.gz")
sha256sums=('084fa378309fb21570146aba491c14228978a7761cae8dae05ae4ebc15a3d321')

prepare() {
	echo "Version: ${pkgver}.0" > "${srcdir}/${_srcname}-${pkgver}/PKG-INFO"
}

package_python-pyodbc() {
	pkgdesc='ODBC module for Python 3.x'
	depends=('python' 'unixodbc')

	cd "${srcdir}/${_srcname}-${pkgver}"

	python setup.py install --root="${pkgdir}/" --optimize=1
}

package_python2-pyodbc() {
	pkgdesc='ODBC module for Python 2.x'
	depends=('python2' 'unixodbc')

	cd "${srcdir}/${_srcname}-${pkgver}"

	python2 setup.py install --root="${pkgdir}/" --optimize=1
}

