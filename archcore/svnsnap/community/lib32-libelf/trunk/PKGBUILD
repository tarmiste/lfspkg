# $Id: PKGBUILD 258789 2017-09-19 06:35:30Z lcarlier $
# Maintainer: St√©phane Gaudreault <stephane@archlinux.org>
# Contributor: Andrej Gelenberg <andrej.gelenberg@udo.edu>

_pkgbasename=elfutils
pkgname=lib32-libelf
pkgver=0.170
pkgrel=1
pkgdesc="Libraries to handle ELF object files and DWARF debugging information (32-bit)"
arch=('x86_64')
url="https://sourceware.org/elfutils/"
license=('LGPL3' 'GPL' 'GPL3')
depends=('lib32-bzip2' 'lib32-zlib' 'libelf')
makedepends=('gcc-multilib')
provides=('lib32-elfutils')
conflicts=('lib32-elfutils')
replaces=('lib32-elfutils')
source=(https://sourceware.org/elfutils/ftp/${pkgver}/elfutils-${pkgver}.tar.bz2{,.sig})
options=('staticlibs')
sha256sums=('1f844775576b79bdc9f9c717a50058d08620323c1e935458223a12f249c9e066'
            'SKIP')
validpgpkeys=('47CC0331081B8BC6D0FD4DA08370665B57816A6A') # Mark J. Wielaard <mark@klomp.org>

build() {
  cd ${srcdir}/${_pkgbasename}-${pkgver}
	
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"
  CFLAGS+=" -g"  # required for test-suite success
  
  ./configure --prefix=/usr --libdir=/usr/lib32
  make
}

package() {
  cd ${srcdir}/${_pkgbasename}-${pkgver}

  make DESTDIR=${pkgdir} install
  rm -rf ${pkgdir}/usr/{bin,include,share}
}
