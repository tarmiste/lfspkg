# $Id: PKGBUILD 241165 2015-06-21 20:02:47Z lcarlier $
# Maintainer: Lukas Jirkovsky <l.jirkovsky@gmail.com>

pkgname=lib32-ocl-icd
_pkgname=ocl-icd
pkgver=2.2.11
_pkgver=814
pkgrel=1
pkgdesc="OpenCL ICD Bindings (32-bit)"
arch=('x86_64')
url="https://forge.imag.fr/projects/ocl-icd/"
license=('BSD')
depends=('lib32-glibc')
makedepends=('ruby' 'lib32-mesa' 'opencl-headers>=2.1' 'gcc-multilib')
checkdepends=()
provides=('lib32-opencl-icd-loader')
conflicts=('lib32-libcl')
replaces=('lib32-libcl')
optdepends=('lib32-opencl-driver: packaged opencl driver')
source=(http://forge.imag.fr/frs/download.php/$_pkgver/$_pkgname-$pkgver.tar.gz)
md5sums=('32335dc7dd3ea2a4b994ca87f2f80554')

build() {
  cd "$srcdir/$_pkgname-$pkgver"

  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  ./configure --prefix=/usr --libdir=/usr/lib32
  make
}

check() {
  cd "$srcdir/$_pkgname-$pkgver"
  make -k check
}

package() {
  cd "$srcdir/$_pkgname-$pkgver"
  make DESTDIR="$pkgdir/" install

  rm -rf "$pkgdir"/usr/{share,include}
  install -m755 -d "$pkgdir/usr/share/licenses/lib32-ocl-icd"
  install -m644 "$srcdir/$_pkgname-$pkgver/COPYING" "${pkgdir}/usr/share/licenses/lib32-ocl-icd/"
}

# vim:set ts=2 sw=2 et:
