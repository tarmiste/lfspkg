# $Id: PKGBUILD 270675 2017-11-29 12:57:54Z spupykin $
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: Charles Mauch <cmauch@gmail.com>

pkgname=perl-mail-transport-dbx
pkgver=0.07
pkgrel=15
pkgdesc="Perl/CPAN Module Mail::Transport::Dbx : Parse Outlook Express mailboxes"
arch=("x86_64")
url="http://search.cpan.org/dist/Mail-Transport-Dbx"
license=("GPL" "PerlArtistic")
options=('!emptydirs')
source=("https://www.cpan.org/authors/id/V/VP/VPARSEVAL/Mail-Transport-Dbx-${pkgver}.tar.gz")
sha256sums=('95fd6bacf2afa0acb121eb9385d61ad4743c48240fbf5a74a731b3c1b5dfdf7d')

build() {
  cd Mail-Transport-Dbx-${pkgver}

  PERL_MM_USE_DEFAULT=1 perl Makefile.PL INSTALLDIRS=vendor

  make
}

package() {
  cd Mail-Transport-Dbx-${pkgver}

  make install DESTDIR="$pkgdir"

  find "$pkgdir" -name '.packlist' -delete
  find "$pkgdir" -name '*.pod' -delete
# template start; name=perl-binary-module-dependency; version=1;
if [[ $(find "$pkgdir/usr/lib/perl5/" -name "*.so") ]]; then
	_perlver_min=$(perl -e '$v = $^V->{version}; print $v->[0].".".($v->[1]);')
	_perlver_max=$(perl -e '$v = $^V->{version}; print $v->[0].".".($v->[1]+1);')
	depends+=("perl>=$_perlver_min" "perl<$_perlver_max")
fi
# template end;
}
