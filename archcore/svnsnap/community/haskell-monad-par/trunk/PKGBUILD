# $Id: PKGBUILD 269660 2017-11-27 10:43:18Z felixonmars $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

_hkgname=monad-par
pkgname=haskell-monad-par
pkgver=0.3.4.8
pkgrel=11
pkgdesc="A library for parallel programming based on a monad"
url="https://github.com/simonmar/monad-par"
license=("custom:BSD3")
arch=('x86_64')
depends=('ghc-libs' "haskell-abstract-deque" "haskell-abstract-par" "haskell-monad-par-extras"
         "haskell-mtl" "haskell-mwc-random" "haskell-parallel")
makedepends=('ghc')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha512sums=('6d2f6f571a3eef804e69ca892cf865bb169863d4fa97cad94cfb740925861b827fef14595c2edf235a8a60babdb854eecb87ff09bb798a6912edb7c3c6fe7027')

build() {
    cd "${srcdir}/${_hkgname}-${pkgver}"
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir="/usr/share/doc/${pkgname}" \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid \
            -f-newgeneric -f-chaselev
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd "${srcdir}/${_hkgname}-${pkgver}"
    
    install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
    install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
    runhaskell Setup copy --destdir="${pkgdir}"
    install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"
}
