# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: FigoDaPaura <ffigoDaPaura>

pkgname=lib32-virtualgl
pkgver=2.5.2
pkgrel=2
pkgdesc="32-bit serverside components for 64-bit VirtualGL servers"
arch=('x86_64')
url="http://virtualgl.org"
license=('LGPL')
makedepends=('lib32-libxv' 'lib32-glu' 'virtualgl' 'lib32-libjpeg-turbo' 'cmake' 'gcc-multilib' 'lib32-mesa' 'lib32-mesa-libgl')
depends=('virtualgl' 'lib32-libxv' 'lib32-glu' 'lib32-turbojpeg')
source=("https://downloads.sourceforge.net/project/virtualgl/${pkgver}/VirtualGL-${pkgver}.tar.gz")
sha512sums=('a3156f663c03e611a98a083bb12bea47820cdadc9f064e20431fddec55aae560f09b133f5254a24d7da01b64be2672d27713d7f6605eaca2d9d83090a070fafd')

prepare() {
  # FS#48403
  export LDFLAGS="${LDFLAGS/--as-needed,}"
}

build() {
  cd "$srcdir/VirtualGL-$pkgver"

  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  mkdir -p build && cd build

  cmake .. \
      -DCMAKE_INSTALL_PREFIX=/usr \
      -DTJPEG_INCLUDE_DIR=/usr/include \
      -DTJPEG_LIBRARY=/usr/lib32/libturbojpeg.so \
      -DCMAKE_INSTALL_LIBDIR=/usr/lib32 \
      -DCMAKE_INSTALL_BINDIR=/usr/bin \
      -DX11_X11_LIB=/usr/lib32/libX11.so \
      -DX11_Xext_LIB=/usr/lib32/libXext.so \
      -DOPENGL_gl_LIBRARY=/usr/lib32/libGL.so
  make
}

package() {
  cd "$srcdir/VirtualGL-$pkgver/build"
  make install DESTDIR="$pkgdir"

  cd "$pkgdir/usr"
  mv bin/glxspheres glxspheres32
  rm -rf bin
  mkdir -p bin
  mv glxspheres32 bin/

  rm -rf "$pkgdir/usr/share"
  rm -rf "$pkgdir/usr/include"
}
