# $Id: PKGBUILD 262649 2017-10-13 20:21:09Z eworm $
# Maintainer: Christian Hesse <mail@eworm.de>
# Contributor: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Ionut Biru <ibiru@archlinux.org>
# Contributor: Gerhard Brauer <gerbra@archlinux.de>

pkgname=nbd
pkgver=3.16.2
pkgrel=2
pkgdesc="tools for network block devices, allowing you to use remote block devices over TCP/IP"
arch=('i686' 'x86_64')
url="http://nbd.sourceforge.net"
license=('GPL')
depends=('glib2' 'gnutls')
backup=('etc/nbd-server/config')
source=("https://downloads.sourceforge.net/$pkgname/$pkgname-$pkgver.tar.xz"
	'config'
	'nbd.service'
	'sysusers_nbd.conf')
sha256sums=('422f62bdf6e6b973bd82083dc52b6577396fe00aa6607837ac464e476124155b'
            'ee2e9fbbeb8a8b9b71d16b6f32eb41788f6def9d00cc4a47897ed3cb97cdde7c'
            '1b1add55d82d7623eabf7ff3dfec15c1fbe3c340fa74160df3d60e91eb1c71ba'
            '11bbab28111c519d043bd96934e16508fe1483c9741e781eea9b9544b111505d')

build(){
  cd "${srcdir}/${pkgname}-${pkgver}"

  ./configure \
    --prefix=/usr \
    --sbindir=/usr/bin \
    --sysconfdir=/etc \
    --enable-syslog

  make
}

# checks still fail...
#check() {
#  cd "${srcdir}/${pkgname}-${pkgver}"
#
#  make check
#}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make DESTDIR="${pkgdir}" install
  install -D -m644 systemd/nbd@.service "${pkgdir}"/usr/lib/systemd/system/nbd@.service

  install -D -m644 "${srcdir}"/config "${pkgdir}"/etc/nbd-server/config
  install -D -m644 "${srcdir}"/nbd.service "${pkgdir}"/usr/lib/systemd/system/nbd.service
  install -D -m644 "${srcdir}"/sysusers_nbd.conf "${pkgdir}"/usr/lib/sysusers.d/nbd.conf

  install -D -m644 doc/README "${pkgdir}"/usr/share/doc/nbd/README
}
