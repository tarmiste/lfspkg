# $Id: PKGBUILD 263640 2017-10-19 16:52:25Z felixonmars $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>

pkgbase=python-openstacksdk
pkgname=(python-openstacksdk python2-openstacksdk)
pkgver=0.9.19
pkgrel=1
pkgdesc="An SDK for building applications to work with OpenStack"
arch=('any')
url="http://developer.openstack.org/sdks/python/openstacksdk"
license=('Apache')
makedepends=('python-setuptools' 'python2-setuptools' 'python-pbr' 'python2-pbr' 'python-jsonpatch'
             'python2-jsonpatch' 'python-stevedore' 'python2-stevedore' 'python-keystoneauth1'
             'python2-keystoneauth1' 'python-os-client-config' 'python2-os-client-config'
             'python-deprecation' 'python2-deprecation')
checkdepends=('python-testrepository' 'python2-testrepository' 'python-mock' 'python2-mock')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/openstack/python-openstacksdk/archive/$pkgver.tar.gz")
sha512sums=('78e72d0111fb462e4211f415e490064022c3ec6d4b3370d109385776eac13a2c1f43c6d89bce69e0cd10a18e88385e44bb6741b9f8aaf499f0bf994c53366623')

prepare() {
  cp -a python-openstacksdk-$pkgver{,-py2}

  export PBR_VERSION=$pkgver
}

build() {
  cd "$srcdir"/python-openstacksdk-$pkgver
  python setup.py build

  cd "$srcdir"/python-openstacksdk-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/python-openstacksdk-$pkgver
  python setup.py testr || warning "Tests failed"

  cd "$srcdir"/python-openstacksdk-$pkgver-py2
  PYTHON=python2 python2 setup.py testr
}

package_python-openstacksdk() {
  depends=('python-six' 'python-pbr' 'python-jsonpatch' 'python-stevedore' 'python-keystoneauth1'
           'python-os-client-config' 'python-deprecation')

  cd "$srcdir"/python-openstacksdk-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}

package_python2-openstacksdk() {
  depends=('python2-six' 'python2-pbr' 'python2-jsonpatch' 'python2-stevedore'
           'python2-keystoneauth1' 'python2-os-client-config' 'python2-deprecation')

  cd "$srcdir"/python-openstacksdk-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
}

# vim:set ts=2 sw=2 et:
