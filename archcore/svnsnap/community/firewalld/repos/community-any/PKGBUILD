# $Id: PKGBUILD 272311 2017-12-05 20:02:27Z alucryd $
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: unikum <unikum.pm@gmail.com>
# Contributor: speed145a <jonathan@tagchapter.com>

pkgname=firewalld
pkgver=0.4.4.6
pkgrel=1
pkgdesc='Firewall daemon with D-Bus interface'
arch=('any')
url='https://github.com/t-woerner/firewalld'
license=('GPL2')
depends=('dbus-glib' 'dconf' 'ebtables' 'hicolor-icon-theme' 'ipset' 'iptables'
         'python-slip')
makedepends=('docbook-xsl' 'intltool')
optdepends=('bash-completion: bash completion'
            'gtk3: firewall-config'
            'libnm-glib: firewall-config and firewall-applet'
            'libnotify: firewall-applet'
            'python-pyqt5: firewall-applet')
backup=('etc/conf.d/firewalld'
        'etc/firewalld/firewalld.conf')
source=("firewalld-${pkgver}.tar.gz::https://github.com/t-woerner/firewalld/archive/v${pkgver}.tar.gz"
        'firewalld-arch.patch')
sha256sums=('a7bf9dd341f3f4c261fa8a8e217160ec815e9dbf32edc25aed44719a2273e94e'
            '5c0c49e125426d561c4099df639ab6dd7073a6d17ae1c130d235cc2397c568c2')

prepare() {
  cd firewalld-${pkgver}

  patch -Np1 -i ../firewalld-arch.patch

  ./autogen.sh
}

build() {
  cd firewalld-${pkgver}

  ./configure \
    --prefix='/usr' \
    --localstatedir='/var' \
    --sbindir='/usr/bin' \
    --sysconfdir='/etc' \
    --with-sysconfigdir='/etc/conf.d' \
    --disable-schemas-compile
  make
}

package() {
  cd firewalld-${pkgver}

  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
