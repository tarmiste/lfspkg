# $Id: PKGBUILD 101980 2013-12-03 09:48:38Z spupykin $
# Maintainer: Christian Rebischke <chris.rebischke@archlinux.org>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Jonathan Wiersma <archaur at jonw dot org>

pkgbase=libvirt-python
pkgname=('libvirt-python2' 'libvirt-python')
pkgver=3.10.0
pkgrel=1
pkgdesc="libvirt python binding"
arch=('x86_64')
url="https://pypi.python.org/pypi/libvirt-python"
license=('LGPL')
makedepends=('python' 'python2' 'libvirt')
options=('emptydirs')
install="libvirt-python.install"
validpgpkeys=('C74415BA7C9C7F78F02E1DC34606B8A5DE95BC1F')
source=("http://libvirt.org/sources/python/libvirt-python-$pkgver.tar.gz")
sha512sums=('bbab01d43b1d9e682c5590c1ea1454c8891f2c545b49f48220f4e79ec4b5b01ab1736242a96653256e26fdf644389c4021747117978a9ec4e1084001de9f23f9')

package_libvirt-python2() {
  depends=('python2' 'libvirt')
  replaces=('libvirt-python<=3.2.0-1')
  conflicts=('libvirt-python<=3.2.0-1')

  cd "$srcdir/$pkgbase-$pkgver"
  python2 setup.py clean
  python2 setup.py install --root="$pkgdir"
}

package_libvirt-python() {
  depends=('python' 'libvirt')

  cd "$srcdir/$pkgbase-$pkgver"
  python setup.py clean
  python setup.py install --root="$pkgdir"
}
