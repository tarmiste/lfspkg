# $Id: PKGBUILD 262318 2017-10-11 03:08:34Z felixonmars $
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-waitress
pkgname=('python-waitress' 'python2-waitress')
pkgver=1.1.0
pkgrel=1
pkgdesc="Waitress WSGI server"
arch=('any')
license=('ZPL')
url="https://github.com/Pylons/waitress"
makedepends=('python-setuptools' 'python2-setuptools')
checkdepends=('python-nose' 'python2-nose')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/Pylons/waitress/archive/v$pkgver.tar.gz")
sha512sums=('80ada835b194b4b7b4ec443e0a5a24d3b7f5417e29bada294d225915792e24724135853484dbc700465c9ed8295b355ca34b58d46f3432b3e30e593bbec7a2d3')

prepare() {
  cp -a waitress-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/waitress-$pkgver
  python setup.py build

  cd "$srcdir"/waitress-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/waitress-$pkgver
  nosetests3

  cd "$srcdir"/waitress-$pkgver-py2
  nosetests2
}

package_python-waitress() {
  depends=('python-setuptools')

  cd waitress-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}

package_python2-waitress() {
  depends=('python2-setuptools')

  cd waitress-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1

  mv "$pkgdir"/usr/bin/waitress-serve{,2}
}
