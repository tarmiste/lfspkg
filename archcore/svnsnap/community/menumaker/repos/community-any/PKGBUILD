# $Id: PKGBUILD 206648 2017-01-10 18:41:03Z jlichtblau $
# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: Ben <contrasutra@myrealbox.com>
# Contributor: Lukasz Fidosz <virhilo@gmail.com>

pkgname=menumaker
pkgver=0.99.10
pkgrel=1
pkgdesc="Heuristics-driven menu generator for Deskmenu, FluxBox, IceWM, OpenBox, WindowMaker and XFCE"
arch=('any')
url="http://menumaker.sourceforge.net/"
license=('BSD')
depends=('python')
options=('!makeflags')
source=(http://downloads.sourceforge.net/$pkgname/$pkgname-$pkgver.tar.gz)
sha256sums=('7e22db0f0d937d33bb338e03f6eac9aa549b9a790c666e519b13de39de66a4d6')

build() {
  cd "${srcdir}"/${pkgname}-${pkgver}

  ./configure --prefix=/usr --infodir=/usr/share/info
  make
}

package() {
  cd "${srcdir}"/${pkgname}-${pkgver}

  make DESTDIR="${pkgdir}" install
  rm -rf "${pkgdir}"/usr/share/info

#license
  install -Dm644 COPYING "${pkgdir}"/usr/share/licenses/${pkgname}/license
}
