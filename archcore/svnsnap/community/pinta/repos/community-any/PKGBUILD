# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Konrad <konrad AT knauber DOT name>

pkgname=pinta
pkgver=1.6
pkgrel=2
pkgdesc="Drawing/editing program modeled after Paint.NET. It's goal is to provide a simplified alternative to GIMP for casual users"
arch=('any')
url="http://pinta-project.com/"
license=('MIT')
depends=('gtk-sharp-2' 'mono-addins')
makedepends=('intltool')
source=(https://github.com/PintaProject/Pinta/releases/download/$pkgver/$pkgname-$pkgver.tar.gz)
sha256sums=('f1ea9e95f1051b92c8d8b6f622dfb77bd26254a165a836ec179aae10eb99d881')

prepare() {
  cd $pkgname-$pkgver

  # update the project and solution files for mono4 (taken from Fedora)
  find . -name "*.sln" -print -exec sed -i 's/Format Version 10.00/Format Version 11.00/g' {} \;
  find . \( -name "*.csproj" -o -name "*.proj" \) -print -exec sed -i 's#ToolsVersion="3.5"#ToolsVersion="4.0"#g; s#<TargetFrameworkVersion>.*</TargetFrameworkVersion>##g; s#<PropertyGroup>#<PropertyGroup><TargetFrameworkVersion>v4.5</TargetFrameworkVersion>#g; s#Mono.Posix, Version.*"#Mono.Posix"#g' {} \;
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
  install -dm755 "$pkgdir"/usr/share/licenses/$pkgname
  install -m644 license-*.txt readme.md "$pkgdir"/usr/share/licenses/$pkgname/
}
