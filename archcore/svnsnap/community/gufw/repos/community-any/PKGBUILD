# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor : Martin Wimpress <code@flexion.org>

pkgname=gufw
_pkgver=17.10
pkgver=${_pkgver}.0
pkgrel=1
pkgdesc='Uncomplicated way to manage your Linux firewall'
url='https://gufw.org/'
arch=('any')
license=('GPL3')
depends=('gtk3' 'polkit' 'python-gobject' 'ufw' 'webkit2gtk' 'desktop-file-utils' 'gtk-update-icon-cache')
makedepends=('intltool' 'python-distutils-extra')
source=(https://launchpad.net/gui-ufw/trunk/${_pkgver}/+download/gui-ufw-${pkgver}.tar.gz)
sha256sums=('8632e763a07e8057f312786b8f7c4970d44c26e73f8b184be1d2fc314e9c3dac')
sha512sums=('7a2e2ff72b3f87e463027c9d8a0266ebbf43289c59684987454215bfd9f40b127b06222a36a3e13fe4325b49e14b1f0182b33fcc495ce7a2e70071aa4b03d2c9')

prepare() {
  cd gui-ufw-${pkgver}
  sed 's|python3.5|python3.6|g' -i bin/gufw-pkexec
}

package(){
  cd gui-ufw-${pkgver}
  export XDG_RUNTIME_DIR="/tmp/${pkgname}"
  python setup.py install --root="${pkgdir}" -O1
}

# vim: ts=2 sw=2 et:
