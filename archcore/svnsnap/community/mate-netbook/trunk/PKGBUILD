# Contributor: Martin Wimpress <code@flexion.org>

pkgname=mate-netbook
pkgver=1.18.2
pkgrel=1
pkgdesc="A simple window management tool (GTK2 version)"
url="https://mate-desktop.org"
arch=('x86_64')
license=('GPL')
depends=('libfakekey' 'mate-panel')
makedepends=('intltool')
groups=('mate-extra')
conflicts=('mate-netbook-gtk3')
replaces=('mate-netbook-gtk3')
source=("https://pub.mate-desktop.org/releases/${pkgver%.*}/${pkgname}-${pkgver}.tar.xz")
sha256sums=('6b9f50eda48e19704d2250d0cc34f6d0b60af177595afb4ef6cf75f026acc577')

build() {
    cd ${pkgname}-${pkgver}
    ./configure \
        --prefix=/usr \
        --libexec=/usr/lib/${pkgname} \
        --sysconfdir=/etc
    make
}

package() {
    cd ${pkgname}-${pkgver}
    make DESTDIR="${pkgdir}" install
}
