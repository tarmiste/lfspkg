# $Id: PKGBUILD 258770 2017-09-18 17:43:43Z jelle $
# Maintainer:
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>

pkgbase=ettercap
pkgname=('ettercap' 'ettercap-gtk')
pkgver=0.8.2
pkgrel=5
arch=('i686' 'x86_64')
url="http://ettercap.github.com/ettercap/"
license=('GPL')
depends=('openssl' 'libpcap' 'pcre' 'libltdl' 'libnet' 'ethtool' 'curl')
makedepends=('ghostscript' 'cmake' 'libpcap' 'libltdl' 'libnet' 'gtk2')
options=('!makeflags' '!emptydirs')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/Ettercap/ettercap/archive/v$pkgver.tar.gz" 0001-First-draft-of-openssl-1.1-compatibility-layer-from-.patch
        CVE-2017-6430.patch CVE-2017-8366.patch)
sha1sums=('7e528632ca01c5977da1a0af56a5e05fbc383832'
          '425fd778afd8fd75baccb9fb0a43c7b246304b32'
          '3a3d6423368403f03205077493e80c1c4ebaab26'
          'eabee4ad0ff0832b3cf46f453e3fa4ee8768f325')

prepare() {
  cd "${pkgbase}-${pkgver}"
  patch -Np1 -i $srcdir/0001-First-draft-of-openssl-1.1-compatibility-layer-from-.patch
  patch -Np1 -i $srcdir/CVE-2017-6430.patch
  patch -Np1 -i $srcdir/CVE-2017-8366.patch
}

build() {
  cd "${srcdir}"

  cp -r ${pkgbase}-${pkgver} ${pkgbase}-${pkgver}-gtk

  cd ${pkgbase}-${pkgver}
  mkdir build
  cd build
  cmake -D ENABLE_GTK=OFF -D CMAKE_INSTALL_PREFIX=/usr -D INSTALL_PREFIX=/usr -D CMAKE_BUILD_TYPE=Release ../
  make

  cd "$srcdir"/${pkgbase}-${pkgver}-gtk
  mkdir build
  cd build
  cmake -D ENABLE_GTK=ON -D CMAKE_INSTALL_PREFIX=/usr -D INSTALL_PREFIX=/usr -D CMAKE_BUILD_TYPE=Release ../
  make
}

package_ettercap() {
  pkgdesc="A network sniffer/interceptor/logger for ethernet LANs - console"
  backup=('etc/ettercap/etter.conf')
  conflicts=('ettercap-gtk')

  cd "${srcdir}"/${pkgbase}-${pkgver}/build
  make DESTDIR="${pkgdir}" install
}

package_ettercap-gtk() {
  pkgdesc="A network sniffer/interceptor/logger for ethernet LANs - GTK frontend"
  depends+=('gtk2')
  backup=('etc/ettercap/etter.conf')
  conflicts=('ettercap')
  provides=('ettercap')
  optdepends=('polkit: to run ettercap directly from menu')

  cd "${srcdir}"/${pkgbase}-${pkgver}-gtk/build
  make DESTDIR="${pkgdir}" install
}
