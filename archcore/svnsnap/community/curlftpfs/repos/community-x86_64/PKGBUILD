# $Id: PKGBUILD 159357 2016-01-28 21:27:44Z arojas $
# Maintainer: Chris Brannon <cmbrannon79@gmail.com>
# Contributor: Allan McRae <mcrae_allan@hotmail.com>
# Contributor: Philip Nilsson <leffeman@gmail.com>

pkgname=curlftpfs
pkgver=0.9.2
pkgrel=6
pkgdesc="A filesystem for acessing FTP hosts based on FUSE and libcurl."
url="http://curlftpfs.sourceforge.net/"
license=('GPL')
depends=('curl' 'fuse' 'glib2')
arch=('i686' 'x86_64')
source=(http://downloads.sourceforge.net/sourceforge/curlftpfs/$pkgname-$pkgver.tar.gz
        ioerror.patch no-verify-hostname.patch)
md5sums=('b452123f755114cd4461d56c648d9f12'
         '7a8db686293463ba3148c7032871c883'
         'eb1d28b9edd690aee543bd6d4f3f41ed')

prepare() {
  cd $pkgname-$pkgver
  patch -p1 -i ../ioerror.patch
  patch -p1 -i ../no-verify-hostname.patch
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install 
}
