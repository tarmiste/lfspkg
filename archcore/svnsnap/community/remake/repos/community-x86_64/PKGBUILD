# Maintainer: Baptiste Jonglez <baptiste--aur at jonglez dot org>
# Based on extra/make

pkgname=remake
pkgver=4.2.1+dbg1.1
pkgrel=1
pkgdesc="Enhanced GNU Make - tracing, error reporting, debugging, profiling and more"
arch=('i686' 'x86_64')
url="http://bashdb.sourceforge.net/remake/"
license=('GPL3')
depends=('glibc' 'guile')
source=("https://sourceforge.net/projects/bashdb/files/remake/4.2%2Bdbg-1.1/remake-4.2.1%2Bdbg1.1.tar.bz2")
sha256sums=('fdfbbceb8d1a20062d060a3498d5c86151ec1b5d9b5145379a1e6fba27f13d7b')

prepare() {
  cd ${pkgname}-${pkgver}
  autoreconf -fi
}

build() {
  cd ${pkgname}-${pkgver}
  ./configure --prefix=/usr
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install
  mv "$pkgdir/usr/include/gnumake.h" "$pkgdir/usr/include/remake.h"
}
