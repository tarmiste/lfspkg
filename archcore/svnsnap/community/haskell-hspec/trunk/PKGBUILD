# $Id: PKGBUILD 281954 2018-01-14 10:14:40Z felixonmars $
# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=hspec
pkgname=haskell-hspec
pkgver=2.4.7
pkgrel=1
pkgdesc="A Testing Framework for Haskell"
url="http://hspec.github.io/"
license=('MIT')
arch=('x86_64')
depends=('ghc-libs' 'haskell-hunit' 'haskell-quickcheck' 'haskell-call-stack' 'haskell-hspec-core'
         'haskell-hspec-discover' 'haskell-hspec-expectations')
makedepends=('ghc' 'haskell-hspec-meta' 'haskell-stringbuilder')
source=(https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz)
sha512sums=('0fe3e210f44c114d5ee816db45d836a4388fd411cbc4dd7ed5a73296fb745d99b8b6de6f7607286bf7497387eddb6c3155e7c0905f9366b52f6b861da8716571')

build() {
    cd $_hkgname-$pkgver
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir="/usr/share/doc/${pkgname}" --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test
}

package() {
    cd $_hkgname-$pkgver
    
    install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
    install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
    runhaskell Setup copy --destdir="${pkgdir}"
    install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"
}
