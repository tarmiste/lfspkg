# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Lukas Jirkovsky <l.jirkovsky@gmail.com>

pkgname=pdfcrack
pkgver=0.16
pkgrel=2
pkgdesc='Password recovery tool for PDF-files'
url='http://pdfcrack.sourceforge.net/'
arch=('i686' 'x86_64')
license=('GPL2')
depends=('glibc')
source=(https://downloads.sourceforge.net/pdfcrack/pdfcrack-${pkgver}.tar.gz{,.asc})
sha256sums=('7865b203074ccfd5c612c8ce00c46ffcb4fabaa26154ce9304dfc668c7cb73ef'
            'SKIP')
sha512sums=('460113f28249d1ea1cefc6dac8ff22b33849ccff50f61830144e41eeec47c4b9113845bb955d26757adb0552d8c0dfa43b3f5666c1419afe4063037a1e1c933d'
            'SKIP')
validpgpkeys=('987AEFC56E70D68440C0D45149241DE2BA57F699') # "Henning Nor√©n <henning.noren@gmail.com>"

build() {
  cd ${pkgname}-${pkgver}
  make
}

package() {
  cd ${pkgname}-${pkgver}
  install -Dm 755 pdfcrack -t "${pkgdir}/usr/bin"
  install -Dm 644 README changelog -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 pdfcrack.1 -t "${pkgdir}/usr/share/man/man1"
}

# vim: ts=2 sw=2 et:
