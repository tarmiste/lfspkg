# $Id: PKGBUILD 271381 2017-12-02 02:38:57Z felixonmars $
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: shild <sxp@bk.ru>
# Maintainer: Vesa Kaihlavirta <vegai@iki.fi>

_hkgname=X11
pkgname=haskell-x11
pkgver=1.8
pkgrel=6
pkgdesc="A Haskell binding to the X11 graphics library."
arch=(x86_64)
url="https://hackage.haskell.org/cgi-bin/hackage-scripts/package/X11"
license=('custom:BSD3')
depends=('ghc-libs' "libx11" "libxinerama" "libxrandr" "haskell-data-default")
conflicts=(haskell-x11-extras)
makedepends=('ghc')
source=(https://hackage.haskell.org/packages/archive/X11/$pkgver/X11-$pkgver.tar.gz)
sha512sums=('bb647bcc7504b3e4267d6a8a6ec73766fe62d7f5b3966bfa037742849c4a1128ea22608dd8353a6035aedc2046c53fae65c8b54f2933a4b896297159f5c2e59c')

build() {
    cd "${srcdir}/${_hkgname}-${pkgver}"
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir="/usr/share/doc/${pkgname}" \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd "${srcdir}/${_hkgname}-${pkgver}"
    
    install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
    install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
    runhaskell Setup copy --destdir="${pkgdir}"
    install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"
}
