# $Id: PKGBUILD 276981 2017-12-30 09:03:19Z felixonmars $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

_hkgname=uuid
pkgname=haskell-uuid
pkgver=1.3.13
pkgrel=12
pkgdesc="For creating, comparing, parsing and printing Universally Unique Identifiers"
url="https://github.com/aslatter/uuid"
license=("custom:BSD3")
arch=('x86_64')
depends=('ghc-libs' "haskell-cryptohash-md5" "haskell-cryptohash-sha1" "haskell-entropy"
         "haskell-network-info" "haskell-random" "haskell-text" "haskell-uuid-types")
makedepends=('ghc')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha512sums=('e888e6350a32c6496b0b57384797925c78ab1c4dc674d296972981fa81338b4705d21a96960a63771aa5b9a785efd507ecaad195767dba7847fcab5ca7f3f923')

prepare() {
    sed -i 's/time            >= 1.1      && < 1.8,/time            >= 1.1,/' $_hkgname-$pkgver/$_hkgname.cabal
}

build() {
    cd "${srcdir}/${_hkgname}-${pkgver}"

    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir="/usr/share/doc/${pkgname}" \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd "${srcdir}/${_hkgname}-${pkgver}"

    install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
    install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
    runhaskell Setup copy --destdir="${pkgdir}"
    install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"
}
