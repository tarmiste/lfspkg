# $Id: PKGBUILD 141139 2015-09-20 03:14:59Z fyan $
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-sortedcontainers
pkgname=('python-sortedcontainers' 'python2-sortedcontainers')
pkgver=1.5.9
pkgrel=1
pkgdesc="Python Sorted Container Types: SortedList, SortedDict, and SortedSet"
arch=('any')
license=('Apache')
url="http://www.grantjenks.com/docs/sortedcontainers/"
makedepends=('python-setuptools' 'python2-setuptools')
checkdepends=('python-nose' 'python2-nose')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/grantjenks/sorted_containers/archive/v$pkgver.tar.gz")
sha512sums=('8bcc668901673d6390e592a0a857472c0183d5988af532d6b276c724708cfb0deef19a066dfba66f3987273aad5e8d01ffbbb1592c82502da8170e2639256327')

prepare() {
  cp -a sorted_containers-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/sorted_containers-$pkgver
  python setup.py build

  cd "$srcdir"/sorted_containers-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/sorted_containers-$pkgver
  nosetests3

  cd "$srcdir"/sorted_containers-$pkgver-py2
  nosetests2
}

package_python-sortedcontainers() {
  depends=('python')

  cd sorted_containers-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}

package_python2-sortedcontainers() {
  depends=('python2')

  cd sorted_containers-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
}
