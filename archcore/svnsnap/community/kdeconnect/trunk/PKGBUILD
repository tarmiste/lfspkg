# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Kuba Serafinowski <zizzfizzix(at)gmail(dot)com>

pkgname=kdeconnect
pkgver=1.2.1
pkgrel=1
pkgdesc='Adds communication between KDE and your smartphone'
url='https://community.kde.org/KDEConnect'
license=(GPL)
arch=(x86_64)
depends=(hicolor-icon-theme kcmutils kwayland libfakekey qca-qt5)
makedepends=(extra-cmake-modules python kdoctools)
optdepends=('sshfs: remote filesystem browser' 'kde-cli-tools: configuration UI')
source=("https://download.kde.org/stable/$pkgname/$pkgver/src/$pkgname-kde-v$pkgver.tar.xz") # {.sig} key is not published
sha256sums=('ef23eb8de8676f426298b244db426ad22e5212f26b3b435fd29a539fae186407')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-kde-v$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DBUILD_TESTING=OFF \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DCMAKE_INSTALL_LIBEXECDIR=lib
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
