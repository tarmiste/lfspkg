# Maintainer: Bruno Pagani (a.k.a. ArchangeGabriel) <archange@archlinux.org>
# Contributor: Mirco Tischler <mt-ml at gmx dot de>

pkgname=pesign
pkgver=0.112
pkgrel=1
pkgdesc='Tools for manipulating signed PE-COFF binaries'
arch=('i686' 'x86_64')
url='https://github.com/rhinstaller/pesign'
license=('GPL2')
depends=('efivar' 'nss' 'libutil-linux')
source=("${url}/releases/download/${pkgver}/${pkgname}-${pkgver}.tar.bz2"
        "${pkgname}.sysusers"
        'https://src.fedoraproject.org/lookaside/pkgs/pesign/certs.tar.xz/e377e0bc924287ee09356a239c5f51a8/certs.tar.xz') # No HTTP because custom CA
sha256sums=('99fa1240311a802fa381eebba8d52b7106690322ac00395bf9fc149dc2cb363e'
            '3e016f959cbd223ecd0103ffb5186e3013bc3152dff722f1c7d67a628e68704a'
            'e0fc5b78dd6e236f87042734b880cdb2859c46817fd7c7f934b8487e0e1c001d')

build() {
    cd ${pkgname}-${pkgver}
    make
}

package() {
    cd ${pkgname}-${pkgver}

    make libdir=/usr/lib libexecdir=/usr/lib DESTDIR="${pkgdir}" install install_systemd
    rm -rf ${pkgdir}/var
    rm -rf ${pkgdir}/etc/rpm

    install -Dm644 "${srcdir}"/${pkgname}.sysusers "${pkgdir}"/usr/lib/sysusers.d/${pkgname}.conf

    # No reason those shouldnâ€™t be readable
    install -dm0755 "${pkgdir}"/etc/pki/pesign{,-rh-test}

    # Install RedHat test certificates
    install -Dm644 "${srcdir}"/etc/pki/pesign/{cert8,key3,secmod}.db -t "${pkgdir}"/etc/pki/pesign/
    install -Dm644 "${srcdir}"/etc/pki/pesign-rh-test/{cert8,key3,secmod}.db -t "${pkgdir}"/etc/pki/pesign-rh-test/
}
