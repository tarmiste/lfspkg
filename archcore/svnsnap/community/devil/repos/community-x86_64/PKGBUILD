# $Id: PKGBUILD 198286 2016-12-07 15:10:07Z anthraxx $
# Maintainer: Laurent Carlier <lordheavym@gmail.org>
# Contributor: damir <damir@archlinux.org>
# Contributor: TheHoff <forums>

pkgname=devil
pkgver=1.7.8
pkgrel=24
pkgdesc="Library for reading several different image formats"
arch=('i686' 'x86_64')
url="http://openil.sourceforge.net/"
depends=('libpng' 'libmng' 'jasper' 'lcms' 'openexr')
options=('!docs' '!emptydirs')
license=('GPL')
source=(http://downloads.sourceforge.net/openil/DevIL-$pkgver.tar.gz
        libpng14.patch
        gcc-5.0.patch
        jasper.patch)
md5sums=('7918f215524589435e5ec2e8736d5e1d'
         '0f839ccefd43b0ee8b4b3f99806147fc'
         '8d1b5d973c93927f60f775aecb448e77'
         '85d5eaf3104967dea10101aa7e5de298')

prepare() {
  cd ${srcdir}/devil-$pkgver

  patch -Np1 -i ${srcdir}/libpng14.patch
  patch -Np1 -i ${srcdir}/gcc-5.0.patch
  patch -Np1 -i ${srcdir}/jasper.patch
}

build() {
  cd ${srcdir}/devil-$pkgver

  if [[ $CARCH == x86_64 ]]; then
    ./configure --prefix=/usr --enable-ILU
  else
    ./configure --prefix=/usr --enable-ILU --disable-sse3
  fi

  make
}

package() {
  cd ${srcdir}/devil-$pkgver

  make prefix=${pkgdir}/usr install
}
