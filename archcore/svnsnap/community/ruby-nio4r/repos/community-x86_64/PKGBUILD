# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

_gemname=nio4r
pkgname=ruby-${_gemname}
pkgver=2.2.0
pkgrel=1
pkgdesc='Abstract, cross-platform stateful I/O selector API for Ruby'
url='https://github.com/celluloid/nio4r'
arch=('x86_64')
license=('MIT')
depends=('ruby')
options=('!emptydirs')
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/celluloid/nio4r/archive/v${pkgver}.tar.gz)
sha256sums=('fc6ed694a571f7cc3406581660beedfbf195eec82d01697ca202a43487bb1a46')
sha512sums=('dd884d7fa2544638c3b81903c7ce92358af4ff4c0a3b6798162d272db93dbd179509fa822e7d05cc87d16e0482763256adc3d3b923524b89bd70fe12b8060076')

prepare() {
  cd ${_gemname}-${pkgver}
  sed 's|git ls-files|find|' -i ${_gemname}.gemspec
}

build() {
  cd ${_gemname}-${pkgver}
  gem build ${_gemname}.gemspec
}

package() {
  cd ${_gemname}-${pkgver}
  local _gemdir="$(gem env gemdir)"
  gem install --ignore-dependencies --no-user-install -i "${pkgdir}/${_gemdir}" -n "${pkgdir}/usr/bin" ${_gemname}-${pkgver}.gem
  install -Dm 644 README.md CHANGES.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 LICENSE.txt -t "${pkgdir}/usr/share/licenses/${pkgname}"
  cp -r examples "${pkgdir}/usr/share/doc/${pkgname}"
  rm "${pkgdir}/${_gemdir}/cache/${_gemname}-${pkgver}.gem"
  find "${pkgdir}/${_gemdir}" \( -name 'mkmf.log' -or -name 'gem_make.out' \) -delete
}

# vim: ts=2 sw=2 et:
