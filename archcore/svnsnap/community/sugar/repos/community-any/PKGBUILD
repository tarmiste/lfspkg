# $Id: PKGBUILD 262973 2017-10-15 18:12:08Z bgyorgy $
# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=sugar
pkgver=0.112
pkgrel=1
pkgdesc="Sugar GTK shell"
arch=('any')
url="https://sugarlabs.org/"
license=('GPL')
depends=('gconf' 'gnome-keyring' 'gst-plugins-espeak' 'gtksourceview3' 'gvfs' 'libwnck3'
         'libxklavier' 'metacity' 'mobile-broadband-provider-info' 'openssh' 'python2-gwebsockets'
         'sugar-toolkit-gtk3' 'telepathy-gabble' 'telepathy-salut' 'upower' 'webkit2gtk'
         'xdg-user-dirs')
makedepends=('intltool')
source=(https://download.sugarlabs.org/sources/sucrose/glucose/$pkgname/$pkgname-$pkgver.tar.xz
        dont-overwrite-settings.patch)
sha256sums=('39aa7ffe866c9f1b4a113e7a400686d60277a3517db1fed62f536b1fdc71c38b'
            '3ce2db5d003b3172302492a2c8ae1ba34aeafdfc6038c6751eb41c56909a062c')

prepare() {
  cd $pkgname-$pkgver
  sed -i 's@^#!.*python$@#!/usr/bin/python2@' src/jarabe/model/update/aslo.py
  sed -i "s/gi.require_version('WebKit', '3.0')/gi.require_version('WebKit2', '4.0')/" src/jarabe/main.py

  # Don't overwrite default GNOME settings
  patch -Np1 -i ../dont-overwrite-settings.patch
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc \
              --disable-schemas-install --disable-schemas-compile \
              --with-gconf-schema-file-dir=/usr/share/gconf/schemas
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
  rm -r "$pkgdir"/usr/share/sugar/extensions/cpsection/updater/
}
