# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

pkgname=masscan
pkgver=1.0.4
pkgrel=1
pkgdesc='TCP port scanner, spews SYN packets asynchronously, scanning entire Internet in under 5 minutes'
url='https://github.com/robertdavidgraham/masscan'
arch=('i686' 'x86_64')
license=('AGPL3')
depends=('libpcap')
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/robertdavidgraham/${pkgname}/archive/${pkgver}.tar.gz)
sha512sums=('eb82d8ad4d6a5c77cf79197e18470a68f7c361f0785180d631e9625578e90af6ffdaa45b728349da5a1cb945a8f902541b8f708be8a4075737ccb06e948d6e2b')

prepare() {
  cd ${pkgname}-${pkgver}
  sed -r "s|(CFLAGS )=|\1+=|g" -i Makefile
}

build() {
  cd ${pkgname}-${pkgver}
  make
}

check() {
  cd ${pkgname}-${pkgver}
  make regress
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
  install -Dm 644 doc/masscan.8 "${pkgdir}/usr/share/man/man8/masscan.8"
  install -Dm 644 README.md "${pkgdir}/usr/share/doc/${pkgname}/README"
  install -Dm 644 doc/masscan.8.markdown "${pkgdir}/usr/share/doc/${pkgname}/DOCUMENTATION"
}

# vim: ts=2 sw=2 et:
