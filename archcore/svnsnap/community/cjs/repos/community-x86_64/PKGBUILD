# $Id$
# Maintainer: Alexandre Filgueira <alexfilgueira@antergos.com>
# Contributor: Ionut Biru <ibiru@archlinux.org>

pkgname=cjs
pkgver=3.6.1
pkgrel=1
pkgdesc="Javascript Bindings for Cinnamon"
arch=(i686 x86_64)
url="https://github.com/linuxmint/cjs"
license=(GPL)
depends=(gtk3 gobject-introspection-runtime js38 dbus-glib)
makedepends=(gnome-common python gobject-introspection)
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/linuxmint/cjs/archive/${pkgver}.tar.gz")
options=(!emptydirs)
sha512sums=('648997c76acb11885a53a73aa456989784fe5f5a12e0f349bce658c1eb3e3ce2b5c576ace488962c9b012fc85c2c3dd828f61f857cc9da7b60ee5fcdd7d25155')

build() {
  cd $pkgname-$pkgver

  ./autogen.sh --prefix=/usr --disable-static --libexecdir=/usr/lib

  #https://bugzilla.gnome.org/show_bug.cgi?id=656231
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

  make
}

check() {
  cd $pkgname-$pkgver
  # Needs a display
  make -k check || :
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
