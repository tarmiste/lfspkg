# $Id: PKGBUILD 73557 2012-07-09 19:15:21Z arodseth $
# Maintainer: Alexander F RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Jared Casper <jaredcasper@gmail.com>
# Contributor: Paulo Matias <matias archlinux-br org>

pkgname=iverilog
pkgver=10.2
pkgrel=1
pkgdesc='Icarus Verilog compiler and simulation tool'
arch=('x86_64' 'i686')
url='http://iverilog.icarus.com/'
license=('GPL')
makedepends=('gperf' 'git')
options=('staticlibs')
source=("git+https://github.com/steveicarus/iverilog#tag=v${pkgver/./_}")
md5sums=('SKIP')

prepare() {
  cd "$pkgname"

  aclocal
  autoconf
}

build() {
  cd "$pkgname"

  ./configure --prefix=/usr
  make
}

package() {
  make -C "$pkgname" -j1 prefix="$pkgdir/usr" install
}

# vim: ts=2 sw=2 et:
# getver: raw.githubusercontent.com/steveicarus/iverilog/master/scripts/MAKE_RELEASE.sh
