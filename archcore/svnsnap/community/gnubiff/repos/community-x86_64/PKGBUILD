# $Id: PKGBUILD 284500 2018-01-21 13:21:50Z bgyorgy $
# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Tom K <tomk@runbox.com>
# Contributor: Todd Musall <tmusall@gmail.com>

pkgname=gnubiff
pkgver=2.2.17
pkgrel=4
pkgdesc="Mail notification program that checks for mail and displays headers when new mail has arrived"
arch=('x86_64')
url="http://gnubiff.sourceforge.net/"
license=('GPL3')
depends=('gtk3')
makedepends=('gconf' 'gnome-panel' 'intltool')
optdepends=('gnome-panel: Applet for GNOME Panel')
source=(https://downloads.sourceforge.net/$pkgname/$pkgname-$pkgver.tar.gz
        panel_applet_inprocess.patch)
sha256sums=('e616ab031d493e4b9b27a2bccc810e4b2ada912b3cf8d44f39325cbc5293fb2c'
            'a6d1f34f7bcbf29c0ad48e49806755f0bdc34cc5b79082b8a9b3dad9cf317e56')

prepare() {
  cd $pkgname-$pkgver
  # Fix GNOME Panel applet
  patch -Np1 -i ../panel_applet_inprocess.patch
  sed -i 's/Icon=.*$/Icon=gnubiff/' src/org.gnome.panel.applet.GnubiffApplet.panel-applet.in.in
  autoreconf -fi
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --disable-fam --with-password --with-password-string="65423957eb027b94c9e661611c0e4271"
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" DATADIRNAME=share install
}
