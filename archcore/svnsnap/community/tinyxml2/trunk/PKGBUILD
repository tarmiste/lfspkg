# $Id: PKGBUILD 148562 2015-12-05 17:09:20Z foutrelis $
# Maintainer: Alexander F RÃ¸dseth <xyproto@archlinux.org>
# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: dhead666 <myfoolishgames@gmail.com>

pkgname=tinyxml2
pkgver=6.0.0
pkgrel=1
pkgdesc='Simple XML parser made for easy integration'
url='http://www.grinninglizard.com/tinyxml2/'
arch=('x86_64')
license=('zlib')
depends=('gcc-libs')
makedepends=('git' 'cmake' 'ninja')
source=($pkgname-$pkgver.tar.gz::https://github.com/leethomason/$pkgname/archive/$pkgver.tar.gz)
sha256sums=('9444ba6322267110b4aca61cbe37d5dcab040344b5c97d0b36c119aa61319b0f')

build() {
  cd "${srcdir}"/$pkgname-$pkgver

  install -d build
  cd build
  cmake .. \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DCMAKE_BUILD_TYPE=Release \
    -G Ninja
  ninja
}

package() {
  cd "${srcdir}"/$pkgname-$pkgver

  DESTDIR="${pkgdir}" ninja -C build install
  install -Dm644 readme.md "${pkgdir}"/usr/share/licenses/$pkgname/README.md
}

# vim:set ts=2 sw=2 et:
