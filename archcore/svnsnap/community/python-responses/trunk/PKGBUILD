# $Id: PKGBUILD 261868 2017-10-06 08:27:13Z felixonmars $
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-responses
pkgname=('python-responses' 'python2-responses')
pkgver=0.8.1
pkgrel=1
pkgdesc='A utility library for mocking out the `requests` Python library.'
arch=('any')
license=('Apache')
url='https://github.com/getsentry/responses'
makedepends=('python-setuptools' 'python2-setuptools' 'python-requests' 'python2-requests'
             'python-cookies' 'python2-cookies' 'python-six' 'python2-six' 'python2-mock')
checkdepends=('python-pytest-runner' 'python2-pytest-runner' 'python-pytest-cov'
              'python2-pytest-cov' 'flake8' 'python2-flake8' 'python-pytest-localserver'
              'python2-pytest-localserver')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/getsentry/responses/archive/$pkgver.tar.gz")
sha512sums=('62e5e08ec637c1950191fa2cc98ce279e7b909b7142ee5809ed21d22d972cfe9e4a57d41bca7c25bb920171013f483e1bd6c0d62d4c1658d02dd7437d858b9a2')

prepare() {
  cp -a responses-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/responses-$pkgver
  python setup.py build

  cd "$srcdir"/responses-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/responses-$pkgver
  python setup.py pytest

  cd "$srcdir"/responses-$pkgver-py2
  python2 setup.py pytest
}

package_python-responses() {
  depends=('python-requests' 'python-cookies' 'python-six')

  cd responses-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}

package_python2-responses() {
  depends=('python2-requests' 'python2-cookies' 'python2-six' 'python2-mock')

  cd responses-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
}

# vim:set ts=2 sw=2 et:
