# $Id: PKGBUILD 255321 2017-09-02 11:47:05Z foutrelis $
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: Charles Mauch <cmauch@gmail.com>
# Contributor: Francois Charette <firmicus@gmx.net>

pkgname=perl-curses
pkgver=1.36
pkgrel=4
pkgdesc="Character screen handling and windowing"
arch=('i686' 'x86_64')
url="http://search.cpan.org/dist/Curses"
license=('GPL' 'PerlArtistic')
depends=('perl' 'ncurses')
options=('!emptydirs')
source=(http://search.cpan.org/CPAN/authors/id/G/GI/GIRAFFED/Curses-$pkgver.tar.gz)
md5sums=('389c70ee5530b887f8e5dc1303cb5294')

build() {
  cd  "$srcdir"/Curses-$pkgver
  PERL_MM_USE_DEFAULT=1 perl Makefile.PL INSTALLDIRS=vendor
  make
}

package() {
  cd  "$srcdir"/Curses-$pkgver
  make pure_install doc_install DESTDIR="$pkgdir"
  find "$pkgdir" -name '.packlist' -delete
  find "$pkgdir" -name '*.pod' -delete
# template start; name=perl-binary-module-dependency; version=1;
if [[ $(find "$pkgdir/usr/lib/perl5/" -name "*.so") ]]; then
	_perlver_min=$(perl -e '$v = $^V->{version}; print $v->[0].".".($v->[1]);')
	_perlver_max=$(perl -e '$v = $^V->{version}; print $v->[0].".".($v->[1]+1);')
	depends+=("perl>=$_perlver_min" "perl<$_perlver_max")
fi
# template end;
}
