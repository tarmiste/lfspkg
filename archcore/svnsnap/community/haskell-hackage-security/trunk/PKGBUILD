# $Id: PKGBUILD 282455 2018-01-15 06:06:28Z felixonmars $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

_hkgname=hackage-security
pkgname=haskell-hackage-security
pkgver=0.5.2.2
pkgrel=11
pkgdesc="Hackage security library"
url="https://github.com/well-typed/hackage-security"
license=("custom:BSD3")
arch=('x86_64')
depends=('ghc-libs' "haskell-base16-bytestring" "haskell-base64-bytestring" "haskell-cryptohash-sha256"
         "haskell-ed25519" "haskell-mtl" "haskell-network" "haskell-network-uri" "haskell-parsec"
         "haskell-tar" "haskell-zlib")
makedepends=('ghc')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha512sums=('5adab3c46d7aada8668128ad8c9389ca65489eec776f8571b8ee5834d74b961f19b558cb6642cbb1891ee381b2d4f2019dddf29bad13607fe23bef49a3d6f535')

prepare() {
    sed -e 's/directory         >= 1.1.0.2 && < 1.3,/directory         >= 1.1.0.2,/' \
        -e 's/time              >= 1.2     && < 1.7,/time              >= 1.2,/' \
        -e 's/Cabal             >= 1.14    && < 1.26,/Cabal             >= 1.14,/' \
        -i ${_hkgname}-${pkgver}/${_hkgname}.cabal
}

build() {
    cd "${srcdir}/${_hkgname}-${pkgver}"

    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir="/usr/share/doc/${pkgname}" \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid \
            -fbase48 -fuse-network-uri -f-old-directory
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd "${srcdir}/${_hkgname}-${pkgver}"

    install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
    install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
    runhaskell Setup copy --destdir="${pkgdir}"
    install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"
}
