# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Marcello "mereghost" Rocha <marcello.rocha@gmail.com>
# Contributor: Gianni Vialetto <gianni at rootcube dot net>
# Contributor: Stefan J. Betz <info at stefan-betz dot net>
# Contributor: p2k <Patrick dot Schneider at uni-ulm dot de>
# Contributor: Jonathan Liu <net147@gmail.com>
# Contributor: Christopher Grebs <cg@webshox.org>
# Contributor: mutantmonkey

pkgname=rabbitmq
pkgver=3.6.14
pkgrel=1
pkgdesc='Highly reliable and performant enterprise messaging implementation of AMQP written in Erlang/OTP'
url='https://rabbitmq.com'
arch=('any')
license=('MPL')
depends=('util-linux' 'inetutils' 'erlang-nox')
optdepends=('rabbitmqadmin: CLI management tool')
backup=('etc/rabbitmq/rabbitmq-env.conf')
install=rabbitmq.install
source=(https://www.rabbitmq.com/releases/${pkgname}-server/v${pkgver}/${pkgname}-server-generic-unix-${pkgver}.tar.xz{,.asc}
        rabbitmq-env.conf
        rabbitmq.service
        rabbitmq-script-wrapper)
sha512sums=('842a30d14f78f42fe765936d9ad38612cf90c4ec5b09f52d3fb06ad4eec0eddf54244cfc43086b9949eee39dc37b7d4bc12c5ffb4339efd717b45322b3dc5784'
            'SKIP'
            '4872ff514c9f841fe4c93bc442ec4e3177d1bfa3834e8d8a26fc77a63aff1cdc14944827673a59556e26210b2b4c0091f93c8e93a334710e5bc3336a64e77140'
            'd51b43f948febb23cdd5b0c7736f966d8433b05ccd95778e90d280d45ccfd9a714e888762f458337a784fff4b86430151adb0f81d779d6b99c9154fcd7e0e319'
            '3f271d546836eccc3bcf61105c013d3b590c3e2535106d07e5605a63174fd62ef204207367c2cd39be54e0246d0bbd9aecebe8b5cefe9018a766ceb8af70bc81')
validpgpkeys=('0A9AF2115F4687BD29803A206B73A36E6026DFCA') # RabbitMQ Release Signing Key <info@rabbitmq.com>

prepare() {
  cd ${pkgname}_server-${pkgver}
  sed -r 's|^(SYS_PREFIX=).*$|\1""|' -i sbin/rabbitmq-defaults
}

package() {
  cd ${pkgname}_server-${pkgver}
  local libdir="${pkgdir}/usr/lib/rabbitmq/lib/rabbitmq-server-${pkgver}"

  install -d "${libdir}"
  install -d "${pkgdir}/usr/bin"
  install -dm 750 "${pkgdir}/var/log/rabbitmq"
  install -dm 750 "${pkgdir}/var/lib/rabbitmq"

  cp -R ebin "${libdir}"
  cp -R include "${libdir}"
  cp -R plugins "${libdir}"
  cp -R sbin "${libdir}"
  cp -R share "${pkgdir}/usr"

  install -d "${pkgdir}/usr/lib/rabbitmq/bin"
  install -Dm 755 "${srcdir}/rabbitmq-script-wrapper" -t "${pkgdir}/usr/lib/rabbitmq/bin"
  for script in "${libdir}"/sbin/*; do
    ln -s "${script#${pkgdir}}" "${pkgdir}/usr/lib/rabbitmq/bin"
    ln -s /usr/lib/rabbitmq/bin/rabbitmq-script-wrapper "${pkgdir}/usr/bin/${script#${libdir}/sbin/}"
  done

  install -Dm 644 "${srcdir}/rabbitmq-env.conf" "${pkgdir}/etc/rabbitmq/rabbitmq-env.conf"
  install -Dm 644 "${srcdir}/rabbitmq.service" "${pkgdir}/usr/lib/systemd/system/rabbitmq.service"

  chown -R 197:197 "${pkgdir}/var/log/rabbitmq"
  chown -R 197:197 "${pkgdir}/var/lib/rabbitmq"
  chown -R 197:0 "${pkgdir}/etc/rabbitmq"
}

# vim: ts=2 sw=2 et:
