# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=elisa
pkgver=0.0.80
pkgrel=2
pkgdesc='A simple music player aiming to provide a nice experience for its users'
url='https://community.kde.org/Elisa'
arch=(x86_64)
license=(LGPL3)
depends=(baloo qt5-quickcontrols qt5-quickcontrols2 kcmutils upnp-player-qt)
makedepends=(extra-cmake-modules kdoctools)
source=("https://download.kde.org/unstable/$pkgname/$pkgver/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('19cb81e456aceb9abd5c4df7e25430b4c243df1115791262fb57c043b5ac5b2a'
            'SKIP')
validpgpkeys=(267BF70F7905C2723B0243267D0F74F05C22F553) # Matthieu Gallien <matthieu_gallien@yahoo.fr>

prepare() {
  mkdir -p build
}

build() {
  cd build 
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DBUILD_TESTING=OFF 
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
