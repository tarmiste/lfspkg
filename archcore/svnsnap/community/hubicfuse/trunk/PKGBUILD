# Maintainer: Bruno Pagani <archange@archlinux.org>

pkgname=hubicfuse
pkgver=3.0.1
pkgrel=2
pkgdesc="A fuse filesystem to access HubiC cloud storage"
arch=('x86_64')
url="https://github.com/TurboGit/hubicfuse"
license=('MIT')
depends=('curl' 'file' 'fuse' 'json-c' 'libxml2')
source=(${pkgname}-${pkgver}.tar.gz::"${url}/archive/v${pkgver}.tar.gz")
sha256sums=('6c4687b07e7e3b184ad30f1d3c66932867ee6e5a38e5d153fbf408ca06d0533e')

build() {
    cd ${pkgname}-${pkgver}
    ./configure --prefix=/usr
    make
}

package() {
    cd ${pkgname}-${pkgver}
    make DESTDIR="${pkgdir}" install
    install -Dm755 hubic_token -t "${pkgdir}"/usr/bin/
    install -Dm644 LICENSE -t "${pkgdir}"/usr/share/licenses/${pkgname}/
}
