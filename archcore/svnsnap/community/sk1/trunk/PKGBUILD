# $Id: PKGBUILD 272098 2017-12-04 08:39:26Z arojas $
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: ConnorBehan <connor.behan@gmail.com>

pkgname=sk1
pkgver=2.0rc2
pkgrel=1
epoch=1
pkgdesc="Fork of skencil with improved CMYK support"
arch=(x86_64)
url="http://sk1project.net/"
depends=('zenity' 'python2-cairo' 'python2-lcms' 'python2-pillow'
	 'libmagick6' 'python2-reportlab' 'wxpython')
provides=('sk1sdk')
license=('GPL' 'LGPL')
source=("$pkgname-$pkgver.tar.gz::https://sk1project.net/dc3.php?version=$pkgver&target=sk1-$pkgver.tar.gz")
sha256sums=('ee1f03e6705e2063d0a1f032e475b24868c68fc26410fb1f2a536d3541b88a67')

build() {
  cd $pkgname-$pkgver
  export PKG_CONFIG_PATH="/usr/lib/imagemagick6/pkgconfig"
  python2 setup-sk1.py build
}

package() {
  cd $pkgname-$pkgver
  python2 setup-sk1.py install --root="$pkgdir"
}
