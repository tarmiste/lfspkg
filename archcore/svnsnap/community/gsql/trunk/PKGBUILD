# $Id: PKGBUILD 266875 2017-11-15 14:29:11Z foutrelis $
# Contributor: Michael Kanis <mkanis@gmx.de>

pkgname=gsql
pkgver=0.2.2
pkgrel=11
pkgdesc="Integrated database development tool for GNOME"
url="http://gsql.org/"
license=('GPL')
arch=('x86_64')
depends=('gtk2>=2.12' 'gtksourceview2' 'libglade' 'gconf'
	 'desktop-file-utils' 'libnotify' 'hicolor-icon-theme')
makedepends=('vte' 'libmariadbclient' 'postgresql-libs' 'libssh')
optdepends=('libmariadbclient: MySQL engine'
            'libssh: SSH tunneling plugin'
            'postgresql-libs: PostgreSQL engine'
            'vte: Terminal session plugin')
options=(!emptydirs)
source=($pkgname-$pkgver.tar.gz::"https://github.com/halturin/gsql/archive/release-$pkgver.tar.gz")
md5sums=('dbe43c17e133fdfe2372c2060a1c4ef1')

build() {
  cd "$srcdir/$pkgname-release-$pkgver"
  sed -i 's/notify_notification_new (subj, message, NULL, NULL);/notify_notification_new (subj, message, NULL);/' libgsql/notify.c
  ./autogen.sh
  LDFLAGS="$LDFLAGS `pkg-config --libs gthread-2.0`" ./configure --prefix=/usr --sysconfdir=/etc \
              --disable-static --without-gnome
  make
}

package() {
  cd "$srcdir/$pkgname-release-$pkgver"
  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="$pkgdir" install
  install -d "${pkgdir}/usr/share/gconf/schemas"
  gconf-merge-schema "${pkgdir}/usr/share/gconf/schemas/${pkgname}.schemas" "${pkgdir}"/etc/gconf/schemas/*.schemas
  rm -f "${pkgdir}"/etc/gconf/schemas/*.schemas
}
