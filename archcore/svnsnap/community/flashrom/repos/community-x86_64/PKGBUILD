# $Id: PKGBUILD 283977 2018-01-18 19:04:29Z archange $
# Maintainer: Bruno Pagani <archange@archlinux.org>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: Cilyan Olowen <gaknar@gmail.com>
# Contributor: fill <mailbox@fillo.ru>
# Contributor: Anton Shestakov <engored*ya.ru>

pkgname=flashrom
pkgver=1.0
pkgrel=2
pkgdesc="Utility for reading, writing, erasing and verifying flash ROM chips"
url="https://www.flashrom.org/"
arch=('x86_64')
license=('GPL')
depends=('libftdi' 'pciutils' 'libusb-compat')
optdepends=('dmidecode: for SMBIOS/DMI table decoder support')
source=("https://download.flashrom.org/releases/${pkgname}-${pkgver}.tar.bz2"{,.asc})
sha256sums=('3702fa215ba5fb5af8e54c852d239899cfa1389194c1e51cb2a170c4dc9dee64' 'SKIP')
validpgpkeys=('58A4868B25C7CFD662FB0132A3EB95B8D9780F68') # David Hendricks (packaging key) <david.hendricks@gmail.com>

prepare() {
  cd ${pkgname}-${pkgver}

  sed -i "s:sbin:bin:g" Makefile
}

build() {
  cd ${pkgname}-${pkgver}

  make
}

package() {
  cd ${pkgname}-${pkgver}

  make PREFIX="${pkgdir}"/usr install
}
