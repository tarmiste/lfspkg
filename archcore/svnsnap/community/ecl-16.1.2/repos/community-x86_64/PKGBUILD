# Contributor: John Proctor <jproctor@prium.net>
# Maintainer: juergen <juergen@archlinux.org>

pkgname=ecl-16.1.2
pkgver=16.1.2
pkgrel=1
pkgdesc="Embeddable Common Lisp (version 16.1.2)"
arch=('i686' 'x86_64')
url="https://common-lisp.net/project/ecl/"
license=('LGPL')
depends=('bash' 'gmp')
makedepends=('texinfo')
provides=('common-lisp' 'cl-asdf' 'ecl')
conflicts=('ecl')
options=('!makeflags')
source=("https://common-lisp.net/project/ecl/static/files/release/ecl-${pkgver}.tgz")
sha256sums=('2d482b1a0a4fbd5d881434517032279d808cb6405e22dd91ef6d733534464b99')

build() {
  cd ecl-$pkgver
  sed -i 's|-Wl,--rpath,~A|-Wl,--rpath,/usr/lib/ecl|' src/configure
  ./configure \
    --build=$CHOST \
    --prefix=/usr \
    --with-tcp \
    --with-serve-event \
    --with-clos-streams \
    --with-system-gmp \
    --without-x \
    --without-clx \
    --enable-threads \
    --enable-shared \
    --enable-boehm=included \

  make
}

package() {
  make -C ecl-$pkgver DESTDIR=$pkgdir install
}
