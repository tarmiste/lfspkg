# $Id: PKGBUILD 271109 2017-12-01 15:06:58Z svenstaro $
# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>
# Contributor: Chris <seitz.christoph@gmail.com>
# Contributor: m0ikz <ndelatorre@moikz.com.ar>
# Contributor: atweiden <archbaum@gmail.com>

pkgname=(ansible python-ansible)
pkgver=2.4.2.0
pkgrel=1
pkgdesc='Radically simple IT automation platform'
arch=('any')
url='https://www.ansible.com'
license=('GPL3')
depends=('python2' 'python2-yaml' 'python2-paramiko' 'python2-jinja' 'python2-crypto')
optdepends=('sshpass: for ssh connections with password'
            'python2-passlib: crypt values for vars_prompt'
            'python2-netaddr: for the ipaddr filter'
            'python2-systemd: log to journal'
            'python2-pywinrm: connect to Windows machines'
            'python2-dnspython: for dig lookup'
            'python2-ovirt-engine-sdk: ovirt support'
            'python2-jmespath: json_query support')
makedepends=('asciidoc' 'fakeroot' 'python' 'python2-setuptools'
             'python-setuptools')
backup=('etc/ansible/ansible.cfg')
source=("https://releases.ansible.com/ansible/ansible-${pkgver}.tar.gz")
sha512sums=('32a075959dca01e184d9c3988fc9c491e904e7627a38e3527c37a8b26340f331bd294cacfe7353d339d15488857ba5ee1b36d9ae4ebeb8b5827934a73f4b42b0')

prepare() {
  cp -r ${pkgname}-${pkgver} "${srcdir}"/${pkgname}-${pkgver}-python
}

build() {
  cd "${srcdir}"/${pkgname}-${pkgver}
  python2 setup.py build

  cd "${srcdir}"/${pkgname}-${pkgver}-python
  python setup.py build
}

package_ansible() {
  provides=('python2-ansible')

  cd ${pkgname}-${pkgver}
  python2 setup.py install -O1 --root="${pkgdir}"
  install -Dm644 COPYING "${pkgdir}"/usr/share/doc/ansible/COPYING

  install -d "${pkgdir}"/usr/share/ansible/doc
  cp -dpr --no-preserve=ownership ./examples "${pkgdir}"/usr/share/ansible/doc/
  install -Dm644 examples/ansible.cfg "${pkgdir}"/etc/ansible/ansible.cfg

  install -d "${pkgdir}"/usr/share/man/man1
  cp -dpr --no-preserve=ownership docs/man/man1/*.1 "${pkgdir}"/usr/share/man/man1
}

package_python-ansible() {
  pkgdesc='python bindings for ansible'
  depends=('python-paramiko' 'python-jinja' 'python-yaml' 'python-crypto')

  cd "${srcdir}"/ansible-${pkgver}-python
  python setup.py install --root=${pkgdir} --optimize=1
  rm "${pkgdir}"/usr/bin/ansible*
}
