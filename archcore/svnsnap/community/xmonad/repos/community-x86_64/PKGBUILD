# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Maintainer: Jelle van der Waa <jelle@archlinux.org>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Vesa Kaihlavirta <vegai@iki.fi>
# Contributor: shild <shildv@gmail.com>

pkgname=xmonad
pkgver=0.13
pkgrel=11
pkgdesc="Lightweight X11 tiled window manager written in Haskell"
arch=('x86_64')
url="http://xmonad.org/"
license=('BSD')
depends=('ghc' 'haskell-x11' 'sh' 'haskell-mtl' 'haskell-utf8-string'
         'haskell-extensible-exceptions' 'haskell-data-default' 'haskell-setlocale')
makedepends=('gendesk')
optdepends=('xorg-xmessage: for displaying visual error messages')
source=($pkgname-$pkgver.tar.gz::https://github.com/xmonad/xmonad/archive/v${pkgver}.tar.gz
       $pkgname-0.13.remove-state-file.patch::https://github.com/xmonad/xmonad/commit/89a8cc88c31163dd35d3182bbba7cdd0630ff54b.patch
        dynamic-compilation.patch
        xmonad.svg)
sha512sums=('c23daf4c187514001383e10586fc13624fd02029bc3d1b33ec6e79abfaaa14847196c1c54096734ce970391674425d614b485139a7201e0daaab4431dc7fa5d6'
            'cb9528f2789c0f9c80ea5867448a618d186f766a4d6983df45664f329febe3d051fc7a5fcc1ab921c9c09fe424fda77798759dd059029f7be9e6e8fd09c8924b'
            'abc598ebc4b55917186cd4d549fc970f53905f820d394ada9a092c08d8833050ad18b23e2f4fd7f281487da2db5b0ca058600e995ac97b18d3be4c62071562d6'
            '31f0ceb724a9931aeda75ab706c8908b5d8341af4df50ab1d749814baacb748ad3622601cd99ac03bf936b698fcd854b23d1541e1772306e3337a13a79b110b6')

prepare() {
  gendesk --pkgname "$pkgname" --pkgdesc "$pkgdesc"
  cd $pkgname-$pkgver
  patch -p1 -i "${srcdir}/dynamic-compilation.patch"
  patch -p1 -i "${srcdir}/$pkgname-0.13.remove-state-file.patch"
}

build() {
  cd $pkgname-$pkgver

  runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
    --prefix=/usr --docdir="/usr/share/doc/${pkgname}" --datasubdir="$pkgname" \
    --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
  runhaskell Setup build
  runhaskell Setup register --gen-script
  runhaskell Setup unregister --gen-script
  sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
  sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
  cd ${pkgname}-${pkgver}

  install -Dm 744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
  install -Dm 744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"

  runhaskell Setup.lhs copy --destdir="${pkgdir}"

  # Requires pandoc, regex-posix, Pretty and cabal
  #runhaskell util/GenerateManpage.hs

  install -Dm 644 man/xmonad.1 -t "${pkgdir}/usr/share/man/man1"
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/xmonad"
  install -Dm 644 "$srcdir/xmonad.svg" -t "${pkgdir}/usr/share/pixmaps"
  install -Dm 644 "$srcdir/xmonad.desktop" -t "${pkgdir}/usr/share/xsessions"
}

# vim: ts=2 sw=2 et:
