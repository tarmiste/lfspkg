# $Id: PKGBUILD 266875 2017-11-15 14:29:11Z foutrelis $
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: Angel 'angvp' Velasquez <angvp[at]archlinux.com.ve>
# Maintainer: Douglas Soares de Andrade <dsa@aur.archlinux.org>

pkgbase=python-psycopg2
pkgname=('python-psycopg2' 'python2-psycopg2')
pkgver=2.7.3.2
pkgrel=1
pkgdesc="A PostgreSQL database adapter for the Python programming language."
arch=('x86_64')
url="http://initd.org/psycopg/"
license=('LGPL3')
makedepends=('python2' 'python' 'postgresql-libs>=8.4.1')
validpgpkeys=('8AD609956CF1899418E19A856013BD3AFCF957DE')
source=(http://initd.org/psycopg/tarballs/PSYCOPG-2-7/psycopg2-$pkgver.tar.gz{,.asc})
sha256sums=('5c3213be557d0468f9df8fe2487eaf2990d9799202c5ff5cb8d394d09fad9b2a'
            'SKIP')

build() {
  cd "$srcdir/psycopg2-$pkgver"
  sed -i 's/,PSYCOPG_DEBUG$//' setup.cfg
}

package_python-psycopg2() {
  depends=('python' 'postgresql-libs>=8.4.1')

  cd "$srcdir/psycopg2-$pkgver"
  python setup.py install --root="$pkgdir"
}

package_python2-psycopg2() {
  depends=('python2' 'postgresql-libs>=8.4.1')

  cd "$srcdir/psycopg2-$pkgver"
  python2 setup.py install --root="$pkgdir"
}
