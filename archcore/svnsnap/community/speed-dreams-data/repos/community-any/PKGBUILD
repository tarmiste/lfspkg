# $Id: PKGBUILD 178608 2016-06-08 13:00:46Z svenstaro $
# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
pkgname=speed-dreams-data
_pkgname=speed-dreams
pkgver=2.2.1
_pkgver=2.2.1-r6404
pkgrel=1
pkgdesc="A racing simulator with rich graphics and physics (data files)"
arch=('any')
url="http://speed-dreams.sourceforge.net/"
license=('GPL2')
depends=()
makedepends=('sdl2' 'freealut' 'freeglut' 'libpng' 'libxmu' 'libxrandr' 'plib' 'libjpeg' 'zlib' 'enet' 'cmake' 'tar' 'libvorbis' 'glu' 'openscenegraph')
noextract=("${_pkgname}-src-base-${_pkgver}.tar.xz"
           "${_pkgname}-src-unmaintained-${_pkgver}.tar.xz"
           "${_pkgname}-src-wip-cars-and-tracks-${_pkgver}.tar.xz"
           "${_pkgname}-src-hq-cars-and-tracks-${_pkgver}.tar.xz"
           "${_pkgname}-src-more-hq-cars-and-tracks-${_pkgver}.tar.xz")
source=("http://downloads.sourceforge.net/sourceforge/${_pkgname}/${_pkgname}-src-base-${_pkgver}.tar.xz"
        "http://downloads.sourceforge.net/sourceforge/${_pkgname}/${_pkgname}-src-unmaintained-${_pkgver}.tar.xz"
        "http://downloads.sourceforge.net/sourceforge/${_pkgname}/${_pkgname}-src-wip-cars-and-tracks-${_pkgver}.tar.xz"
        "http://downloads.sourceforge.net/sourceforge/${_pkgname}/${_pkgname}-src-hq-cars-and-tracks-${_pkgver}.tar.xz"
        "http://downloads.sourceforge.net/sourceforge/${_pkgname}/${_pkgname}-src-more-hq-cars-and-tracks-${_pkgver}.tar.xz")
md5sums=('a1a296ab737d5144f1f570f4cabc7937'
         '0a9b1572d0660c60fa0553195578a36a'
         '3413d9d5e2111cd9e87628df5818d2b4'
         'e4efd3b16e53a54c23e606947739abe5'
         'b6079ca11202595ac312a28262e793ee')

prepare() {
  # extract these manually as bsdtar can't handle these
  tar xf "${_pkgname}-src-base-${_pkgver}.tar.xz"
  tar xf "${_pkgname}-src-unmaintained-${_pkgver}.tar.xz"
  tar xf "${_pkgname}-src-wip-cars-and-tracks-${_pkgver}.tar.xz"
  tar xf "${_pkgname}-src-hq-cars-and-tracks-${_pkgver}.tar.xz"
  tar xf "${_pkgname}-src-more-hq-cars-and-tracks-${_pkgver}.tar.xz"
}

build() {
  cmake . \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DSD_BINDIR=/usr/bin \
    -DSD_DATADIR=/usr/share/speed-dreams-2 \
    -DSD_LIBDIR=/usr/lib/speed-dreams-2 \
    -DOPTION_OFFICIAL_ONLY=ON
}

package() {
  make DESTDIR="${pkgdir}/" install

  rm -r "${pkgdir}/usr/bin"
  rm -r "${pkgdir}/usr/lib"
  rm -r "${pkgdir}/usr/include"
}

# vim:set ts=2 sw=2 et:
