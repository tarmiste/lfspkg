# $Id: PKGBUILD 209316 2017-01-28 20:35:29Z arojas $
# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=planarity
pkgver=3.0.0.5
pkgrel=1
pkgdesc="A library for implementing graph algorithms"
arch=(i686 x86_64)
url="https://github.com/graph-algorithms/edge-addition-planarity-suite"
license=(BSD)
depends=(glibc)
makedepends=()
source=($pkgname-$pkgver.tar.gz::"https://github.com/graph-algorithms/edge-addition-planarity-suite/archive/Version_$pkgver.tar.gz")
md5sums=('2fd16fb8bc5d8343b61fd94005475ed1')

build() {
  cd edge-addition-planarity-suite-Version_$pkgver
  ./autogen.sh
  ./configure --prefix=/usr
  make
}

package() {
  cd edge-addition-planarity-suite-Version_$pkgver
  make install DESTDIR="$pkgdir"

  install -Dm644 LICENSE.TXT "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
