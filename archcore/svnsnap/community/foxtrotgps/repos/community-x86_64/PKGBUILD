# $Id: PKGBUILD 241621 2017-06-29 18:54:18Z bgyorgy $
# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Elena ``of Valhalla'' Grandi <elena.valhalla@gmail.com>

pkgname=foxtrotgps
pkgver=1.2.0
pkgrel=2
pkgdesc="Lightweight and fast mapping application"
arch=('x86_64' 'i686')
url="https://www.foxtrotgps.org/"
license=('GPL')
depends=('curl' 'gconf' 'gpsd' 'libexif' 'libglade')
makedepends=('intltool')
optdepends=('gpscorrelate: geotag photos'
            'jhead: add photos to database'
            'python2-beautifulsoup4: georss2foxtrotgps-poi tool'
            'python2-feedparser: georss2foxtrotgps-poi tool'
            'python2-sqlalchemy: poi2osm tool')
source=(https://www.foxtrotgps.org/releases/$pkgname-$pkgver.tar.gz{,.asc}
        fixes.patch
        maps-for-free.patch)
validpgpkeys=('F0378BFD8385C9968DCBC56CFF7E9E83954BE38A') # Joshua Judson Rosen
sha256sums=('aa1ffbbf299b7b0f305a0bdbe395266e18305bb9449d621fb88bf8760b55505a'
            'SKIP'
            'd9f52319d89fb51fd8a493477a97fe7bec554555bc38f83904ef29b1e6314b3f'
            '19290db3cd2d6826b0cad7bfa45f309e13d6ba413d9da2573edae6d8e02919e8')

prepare() {
  cd $pkgname-$pkgver
  sed -i 's@^#!.*python$@#!/usr/bin/python2@' contrib/*
  patch -Np1 -i ../fixes.patch
  patch -Np0 -i ../maps-for-free.patch
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
              --disable-schemas-install \
              --with-gconf-schema-file-dir=/usr/share/gconf/schemas
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
