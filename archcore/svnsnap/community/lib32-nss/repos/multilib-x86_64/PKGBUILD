# $Id: PKGBUILD 267393 2017-11-19 01:33:18Z heftig $
# Maintainer: Daniel Wallace <danielwallace at gtmanfred dot com>
# Contributor: kfgz <kfgz at interia pl>
# Contributor: Ionut Biru <ibiru at archlinux dot org>

pkgname=lib32-nss
pkgver=3.34
pkgrel=1
pkgdesc="Network Security Services (32-bit)"
url="https://developer.mozilla.org/en-US/docs/Mozilla/Projects/NSS"
arch=(x86_64)
license=(MPL GPL)
_nsprver=4.15
depends=("lib32-nspr>=${_nsprver}" lib32-sqlite lib32-zlib lib32-p11-kit nss)
makedepends=(gcc-multilib perl gyp)
options=(!strip !makeflags staticlibs)
source=("https://ftp.mozilla.org/pub/security/nss/releases/NSS_${pkgver//./_}_RTM/src/nss-${pkgver}.tar.gz"
        nss.pc.in enable-libpkix.patch no-plt.diff)
sha256sums=('0d45954181373023c7cfc33e77c8c636d394ec7e55b93e059149ed7888652af5'
            'f2208c4f70373ff9b60f53d733f8071d4e390c384b776dfc04bf26c306882faf'
            '0b68c5ffbfeec090e950e5703e6b0519eafcb7eaf4fe2f64c355d0ddf17eed30'
            'ea8e1b871c0f1dd29cdea1b1a2e7f47bf4713e2ae7b947ec832dba7dfcc67daa')

prepare() {
  mkdir path

  ln -s /usr/bin/python2 path/python

  cd nss-$pkgver

  patch -Np1 -i ../enable-libpkix.patch
  patch -Np2 -i ../no-plt.diff
}

build() {
  export PKG_CONFIG_PATH=/usr/lib32/pkgconfig

  cd nss-$pkgver/nss
  PATH="$srcdir/path:$PATH" bash -x ./build.sh -v \
    --m32 --opt --system-sqlite --system-nspr --disable-tests
}

package() {
  cd nss-$pkgver

  sed ../nss.pc.in \
    -e "s,%libdir%,/usr/lib32,g" \
    -e "s,%prefix%,/usr,g" \
    -e "s,%exec_prefix%,/usr/bin,g" \
    -e "s,%includedir%,/usr/include/nss,g" \
    -e "s,%NSPR_VERSION%,${_nsprver},g" \
    -e "s,%NSS_VERSION%,${pkgver},g" |
    install -Dm644 /dev/stdin "$pkgdir/usr/lib32/pkgconfig/nss.pc"
  ln -s nss.pc "$pkgdir/usr/lib32/pkgconfig/mozilla-nss.pc"

  cd dist/Release/lib
  install -Dt "$pkgdir/usr/lib32" *.so
  install -Dt "$pkgdir/usr/lib32" -m644 *.chk

  ln -sf libnssckbi-p11-kit.so "$pkgdir/usr/lib32/libnssckbi.so"
}
