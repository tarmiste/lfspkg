# $Id: PKGBUILD 286864 2018-01-25 18:40:56Z bgyorgy $
# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Angel Velasquez <angvp@archlinux.org>
# Contributor: Wilson Pinto Júnior (N3RD3X) <n3rd3x@linuxmail.org>
# Contributor: Attila Bukor <r1pp3rj4ck@w4it.eu>

pkgname=guake
pkgver=3.0.4
pkgrel=1
pkgdesc='Drop-down terminal for GNOME'
arch=('any')
url="http://guake-project.org/"
license=('GPL')
depends=('libkeybinder3' 'libnotify' 'libutempter' 'python-cairo' 'python-dbus' 'python-gobject' 'vte3')
makedepends=('python-pbr' 'git')
_commit=4f4cc8b6c16a7d8598d38f6266434bfa3e533978  # tags/3.0.4
source=("git+https://github.com/Guake/guake#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname
  sed -i '/glib-compile-schemas/d' Makefile

  # Fix path
  sed -i "s|IMAGE_DIR = os.path.join(SRC_DIR, 'data/pixmaps')|IMAGE_DIR = '/usr/share/pixmaps/guake'|
          s|GLADE_DIR = os.path.join(SRC_DIR, 'data')|GLADE_DIR = '/usr/share/guake'|
          s|SCHEMA_DIR = os.path.join(SRC_DIR, 'data')|SCHEMA_DIR = '/usr/share/glib-2.0/schemas'|" guake/globals.py

  # Remove dependency on python-pbr
  sed -i "/import pbr.version/d
          s/__version__ = pbr.version.VersionInfo('guake').version_string()/__version__ = '$pkgver'/" guake/__init__.py
}

build() {
  cd $pkgname
  PBR_VERSION=$pkgver python3 setup.py build
  make generate-desktop generate-mo
}

package() {
  cd $pkgname
  PBR_VERSION=$pkgver python3 setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 -t "$pkgdir/usr/share/guake/" guake/data/*.glade 
  install -Dm644 -t "$pkgdir/usr/share/pixmaps/guake/" guake/data/pixmaps/*.png 
  make PREFIX="$pkgdir/usr" install-schemas install-locale
}
