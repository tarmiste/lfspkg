# $Id: PKGBUILD 287299 2018-01-26 23:25:41Z felixonmars $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

_hkgname=http-client
pkgname=haskell-http-client
pkgver=0.5.9
pkgrel=1
pkgdesc="An HTTP client engine"
url="https://github.com/snoyberg/http-client"
license=("MIT")
arch=('x86_64')
depends=('ghc-libs' 'haskell-base64-bytestring' 'haskell-blaze-builder' 'haskell-case-insensitive'
         'haskell-cookie' 'haskell-exceptions' 'haskell-http-types' 'haskell-mime-types'
         'haskell-network' 'haskell-network-uri' 'haskell-random' 'haskell-streaming-commons'
         'haskell-text')
makedepends=('ghc' 'haskell-async' 'haskell-hspec' 'haskell-monad-control' 'haskell-zlib')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha512sums=('ca1d53f4578f06f18223135a552568e2c645cbec2777af056bc3d762e9c2edb966ef9107a2ac3223a859d0fe957f71d510e61c854c41917c286662f1e7b2feca')

build() {
    cd $_hkgname-$pkgver
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir="/usr/share/doc/${pkgname}" --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid -fnetwork-uri
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test
}

package() {
    cd $_hkgname-$pkgver
    
    install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
    install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
    runhaskell Setup copy --destdir="${pkgdir}"
    install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"
}
