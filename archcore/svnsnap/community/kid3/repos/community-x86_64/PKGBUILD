# $Id: PKGBUILD 270511 2017-11-28 19:44:21Z jlichtblau $
# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: Alois Nespor <alois.nespor@gmail.com>

pkgbase=kid3
pkgname=('kid3' 'kid3-qt')
pkgver=3.5.1
pkgrel=1
arch=('x86_64')
url="http://kid3.sourceforge.net/"
license=('GPL')
makedepends=('qt5-tools' 'qt5-multimedia' 'qt5-declarative' 'chromaprint'
            'id3lib' 'taglib' 'libmp4v2' 'kio' 'hicolor-icon-theme' 'docbook-xml'
            'kdoctools' 'extra-cmake-modules' 'python')
changelog=$pkgbase.changelog
source=(https://downloads.sourceforge.net/$pkgbase/$pkgbase-$pkgver.tar.gz{,.asc})
validpgpkeys=('7D09794C2812F62194B081C14CAD34426E354DD2') # Urs Fleisch
sha256sums=('88c20826deb13f81bcdfa7033a4b9ff9ca8957112b2fa2ccc0a9a1076df73926'
            'SKIP')

prepare() {
  cd "${srcdir}"
  [[ ! -d build ]] && mkdir build
  mkdir build-qt
}

build() {
#kid3
  cd "${srcdir}"/build
  cmake \
    ../${pkgbase}-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DWITH_QT5=ON \
    -DWITH_APPS="CLI;KDE" \
    -DWITH_MP4V2=ON \
    -DWITH_FFMPEG=ON \
    -DWITH_GSTREAMER=ON

#kid3-qt
  cd "$srcdir"/build-qt
  cmake \
    ../${pkgbase}-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DWITH_QT5=ON \
    -DWITH_APPS="CLI;Qt" \
    -DWITH_MP4V2=ON \
    -DWITH_FFMPEG=ON \
    -DWITH_GSTREAMER=ON
}

package_kid3() {
pkgdesc="An MP3, Ogg/Vorbis and FLAC tag editor, KDE version"
depends=('qt5-multimedia' 'phonon-qt5' 'chromaprint' 'id3lib' 'taglib'
         'libmp4v2' 'kio' 'desktop-file-utils' 'hicolor-icon-theme')
conflicts=('kid3-qt')

  cd "$srcdir"/build
  make DESTDIR="${pkgdir}" install
}

package_kid3-qt() {
pkgdesc="An MP3, Ogg/Vorbis and FLAC tag editor, Qt version"
depends=('qt5-multimedia' 'chromaprint' 'id3lib' 'taglib' 'libmp4v2'
         'desktop-file-utils' 'hicolor-icon-theme' 'qt5-declarative')
conflicts=('kid3')
provides=('kid3')

  cd "$srcdir"/build-qt
  make DESTDIR="${pkgdir}" install
}
