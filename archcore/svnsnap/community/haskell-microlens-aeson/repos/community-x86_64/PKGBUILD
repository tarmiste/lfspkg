# $Id: PKGBUILD 286391 2018-01-24 13:29:57Z felixonmars $
# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=microlens-aeson
pkgname=haskell-microlens-aeson
pkgver=2.2.0.2
pkgrel=7
pkgdesc="Law-abiding lenses for Aeson, using microlens"
url="https://github.com/fosskers/microlens-aeson/"
license=('MIT')
arch=('x86_64')
depends=('ghc-libs' 'haskell-aeson' 'haskell-attoparsec' 'haskell-microlens' 'haskell-scientific'
         'haskell-text' 'haskell-unordered-containers' 'haskell-vector')
makedepends=('ghc')
source=(https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz)
sha512sums=('debb55f5c66e873cd64712f2e82bcee920db14aaba0fe1f282c1c0209e9db86b2a1a18b6081ac94361a70e5aed1dbb76edc129bfc19609f2454c8d5c7bcba1cb')

build() {
    cd "${srcdir}/${_hkgname}-${pkgver}"
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir="/usr/share/doc/${pkgname}" \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd "${srcdir}/${_hkgname}-${pkgver}"
    
    install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
    install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
    runhaskell Setup copy --destdir="${pkgdir}"
    install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"
}
