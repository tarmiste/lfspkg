# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Michal Bozon <michal.bozon__at__gmail.com>

pkgbase=python-yaml
pkgname=(python-yaml python2-yaml)
pkgver=3.12
pkgrel=3
pkgdesc='Python bindings for YAML, using fast libYAML library'
url='http://pyyaml.org/wiki/PyYAML'
arch=('i686' 'x86_64')
license=('MIT')
makedepends=('python' 'python2' 'libyaml')
source=(http://pyyaml.org/download/pyyaml/PyYAML-${pkgver}.tar.gz)
sha512sums=('e16d8b7f4f026b6a95b11fb59c54ec5f114f6f516294eaa95e718abdf5d37c17a9c4b5e0a0a61fca04e801792d9b7fb801087cf849ff22b9581f6af204b1883a')

prepare() {
  cp -a PyYAML-${pkgver}{,-py2}
}

build() {
  msg2 "Building python..."
  (cd PyYAML-${pkgver}
    python setup.py --with-libyaml build
  )
  msg2 "Building python2..."
  (cd PyYAML-${pkgver}-py2
    python2 setup.py --with-libyaml build
  )
}

check() {
  msg2 "Checking python..."
  (cd PyYAML-${pkgver}
    python setup.py test
  )
  msg2 "Checking python2..."
  (cd PyYAML-${pkgver}-py2
    python2 setup.py test
  )
}

package_python-yaml() {
  depends=('python' 'libyaml')

  cd PyYAML-${pkgver}
  python setup.py  --with-libyaml install --prefix=/usr --root="${pkgdir}" -O1 --skip-build
  install -Dm 644 LICENSE -t "${pkgdir}"/usr/share/licenses/${pkgname}
  install -Dm 644 CHANGES README -t "${pkgdir}"/usr/share/doc/${pkgname}
}

package_python2-yaml() {
  depends=('python2' 'libyaml')

  cd PyYAML-${pkgver}-py2
  python2 setup.py --with-libyaml install --prefix=/usr --root="${pkgdir}" -O1 --skip-build
  install -Dm 644 LICENSE -t "${pkgdir}"/usr/share/licenses/${pkgname}
  install -Dm 644 CHANGES README -t "${pkgdir}"/usr/share/doc/${pkgname}
}

# vim: ts=2 sw=2 et:
