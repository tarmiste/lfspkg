# $Id: PKGBUILD 266875 2017-11-15 14:29:11Z foutrelis $
# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Larry Hajali <larryhaja [at] gmail [dot] com>

pkgname=megaglest
pkgver=3.13.0
pkgrel=4
pkgdesc="Fork of Glest, a 3D real-time strategy game in a fantastic world"
arch=('x86_64')
url="https://megaglest.org"
license=('GPL3')
depends=('curl' 'megaglest-data' 'xerces-c' 'sdl2' 'libvorbis' 'openal' 'libgl' 'lua51' 'icu' 'ftgl' 'glew'
'libircclient' 'miniupnpc' 'wxgtk3' 'glu' 'libx11')
makedepends=('ftjam' 'cmake' 'mesa' 'git')
source=("git+https://github.com/MegaGlest/megaglest-source.git#tag=${pkgver}")
md5sums=('SKIP')

build() {
  cd megaglest-source

  [[ -d build ]] && rm -r build
  mkdir build && cd build

  cmake .. \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_CXX_FLAGS="-lX11" \
    -DWANT_GIT_STAMP=0 \
    -DwxWidgets_CONFIG_EXECUTABLE=/usr/bin/wx-config-gtk3
  make VERBOSE=1
}

package() {
  cd megaglest-source/build

  make DESTDIR="$pkgdir" install
}

# vim: sw=2:ts=2 et:
