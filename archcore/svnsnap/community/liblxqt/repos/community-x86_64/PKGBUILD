# $Id: PKGBUILD 263898 2017-10-21 22:11:10Z jleclanche $
# Maintainer: Jerome Leclanche <jerome@leclan.ch>

pkgname=liblxqt
pkgver=0.12.0
pkgrel=1
pkgdesc="Common base library for LXQt components."
arch=("i686" "x86_64")
url="http://lxqt.org"
license=("GPL2")
depends=("kwindowsystem" "libqtxdg" "libxss")
makedepends=("lxqt-build-tools")
source=(
	"https://github.com/lxde/$pkgname/releases/download/$pkgver/$pkgname-$pkgver.tar.xz"
	"https://github.com/lxde/$pkgname/releases/download/$pkgver/$pkgname-$pkgver.tar.xz.asc"
)
sha256sums=(
	"44bba48b073ded5b0ab3de2ea3575a704bb22d23ce5cc5743f76579c1f084f33"
	"SKIP"
)
validpgpkeys=(
	"169704C6FB490C6892C7F23C37E0AF1FDA48F373"  # Jerome Leclanche <jerome@leclan.ch>
	"7C733BA5F585AAD669E4D23A42C9C8D3AF5EA5E3"  # Alf Gaida <agaida@siduction.org>
)


build() {
	mkdir -p build
	cd build
	cmake "$srcdir/$pkgname-$pkgver" \
		-DCMAKE_INSTALL_PREFIX=/usr \
		-DCMAKE_INSTALL_LIBDIR=lib
	make
}

package() {
	cd build
	make DESTDIR="$pkgdir" install
}
