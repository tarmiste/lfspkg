# Maintainer: Morten Linderud <foxboron@archlinux.org>
# Contibutor: Alexandre Pujol <alexandre@pujol.io>

pkgname='pass-otp'
pkgver=1.0.0
pkgrel=3
pkgdesc='A pass extension for managing one-time-password (OTP) tokens.'
arch=('any')
url='https://github.com/tadfisher/pass-otp'
license=('GPL3')
depends=('pass'
         'oath-toolkit'
	 'qrencode')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/tadfisher/pass-otp/archive/v${pkgver}.tar.gz"
	"otp_algorithm-is-lowercase.patch")
sha256sums=('ed409b50c417814f9feed913bb4c09916a10f635404a6d8c2f4e842319c58052'
            'b6a3ca22b8efcd7936e4c48fdde8b3ff50a7ff6497be001a04632fd823ad2293')

prepare(){
  cd "${pkgname}-${pkgver}"
  patch -Np1 -i "${srcdir}/otp_algorithm-is-lowercase.patch"
}

package() {
  cd "${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
