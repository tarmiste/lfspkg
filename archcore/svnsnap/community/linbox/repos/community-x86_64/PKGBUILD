# $Id: PKGBUILD 273858 2017-12-11 19:51:08Z jlichtblau $
# Maintainer: Antonio Rojas < nqn76sw@gmail.com >

pkgname=linbox
pkgver=1.5.2
pkgrel=2
pkgdesc="A template library for exact, high-performance linear algebra computation with dense, sparse, and structured matrices over the integers and over finite fields"
arch=(x86_64)
url="http://linalg.org/"
license=(LGPL)
depends=(m4rie flint fplll iml fflas-ffpack tinyxml2) 
source=("https://github.com/linbox-team/linbox/releases/download/v$pkgver/$pkgname-$pkgver.tar.gz")
sha256sums=('7b69ab617b15ca6684b29d08531ed8899cd4d07ec75799e5d624c4c0a60dc6b2')

build() {
  cd $pkgname-$pkgver

  ./configure --prefix=/usr --enable-sage --with-fplll=/usr \
    --disable-sse3 --disable-ssse3 --disable-sse41 --disable-sse42 --disable-avx --disable-avx2 --disable-fma
  make
}

check() {
  cd $pkgname-$pkgver
# make check
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
