# $Id: PKGBUILD 122344 2014-11-11 08:16:49Z fyan $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Sebastien Leduc <sebastien@sleduc.fr>
# Contributor: Roberto Catini <roberto.catini@gmail.com>

pkgbase=python-requests-oauthlib
pkgname=('python-requests-oauthlib' 'python2-requests-oauthlib')
pkgver=0.8.0
pkgrel=1
pkgdesc="First-class OAuth library support for Requests"
arch=('any')
url="https://pypi.python.org/pypi/requests-oauthlib"
license=('custom:ISC')
makedepends=('python-setuptools' 'python2-setuptools' 'python-requests' 'python2-requests'
             'python-oauthlib' 'python2-oauthlib' 'git')
checkdepends=('python-pytest' 'python2-pytest' 'python-requests-mock' 'python2-requests-mock'
              'python-mock' 'python2-mock' 'python-pyjwt' 'python2-pyjwt')
source=("git+https://github.com/requests/requests-oauthlib.git#tag=v$pkgver")
md5sums=('SKIP')

prepare() {
  cp -a requests-oauthlib{,-py2}
}

build() {
  cd "$srcdir"/requests-oauthlib
  python setup.py build

  cd "$srcdir"/requests-oauthlib-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/requests-oauthlib
  py.test

  cd "$srcdir"/requests-oauthlib-py2
  py.test2
}

package_python-requests-oauthlib() {
  depends=('python-requests' 'python-oauthlib')

  cd "$srcdir"/requests-oauthlib
  python setup.py install --root="$pkgdir/" --optimize=1
  install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/$pkgname/LICENSE"
}

package_python2-requests-oauthlib() {
  depends=('python2-requests' 'python2-oauthlib')

  cd "$srcdir"/requests-oauthlib-py2
  python2 setup.py install --root="$pkgdir/" --optimize=1
  install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/$pkgname/LICENSE"
}
