# Maintainer: Bruno Pagani <archange@archlinux.org>
# Contributor: SÃ©bastien LEBEAU <sebcbi1 at gmail dot com>
# Contributor: Patrick Griffis <tingpign@fedoraproject.org>

pkgname=transmission-remote-gtk
pkgver=1.3.1
pkgrel=2
pkgdesc="GTK remote control for the Transmission BitTorrent client"
arch=('i686' 'x86_64')
url="https://github.com/transmission-remote-gtk/transmission-remote-gtk"
license=('GPL2')
depends=('curl' 'gtk3' 'libnotify' 'libproxy')
makedepends=('intltool' 'geoip')
optdepends=('geoip: flags for country of peers')
source=("${url}/releases/download/${pkgver}/transmission-remote-gtk-${pkgver}.tar.xz"{,.asc})
sha256sums=('1b29c573b1e205e3e7c2433dc4a48f9574278d97e033845d19bbffa1d7f75345' 'SKIP')
validpgpkeys=('108BF2212A051F4A72B18448B3C7CE210DE76DFC') # Patrick Griffis <tingping@tingping.se>

build() {
	cd ${pkgname}-${pkgver}
	./configure --prefix=/usr --disable-desktop-database-update
	make
}

package() {
	cd ${pkgname}-${pkgver}
	make DESTDIR="${pkgdir}" install
}
