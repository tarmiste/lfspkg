# $Id: PKGBUILD 282305 2018-01-15 01:59:48Z bgyorgy $
# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Anton Bazhenov <anton.bazhenov at gmail>
# Contributor: JD Steffen <jd at steffennet dot org>

pkgname=simutrans-pak64
pkgver=120.3
_pkgver=120-3
pkgrel=1
pkgdesc="Low resolution graphics set for Simutrans"
arch=('any')
url="https://www.simutrans.com/"
license=('custom:Artistic')
options=('!strip')
source=(https://downloads.sourceforge.net/simutrans/simupak64-$_pkgver.zip
        licence.txt)
sha256sums=('b6e94e9d2e2564428bace1f616344437d4a1c26278c97da877fd17e20c446150'
            'dbf7da494c0381290c7af4dfe0a24415c666641a237b401ecb2be6cd7180e183')

package() {
  chmod -R 644 simutrans

  #data
  mkdir -p "$pkgdir/usr/share/games/simutrans"
  cp -r simutrans/pak "$pkgdir/usr/share/games/simutrans"

  find "$pkgdir" -type f -exec chmod 644 {} \;
  find "$pkgdir" -type d -exec chmod 755 {} \;

  #license
  install -Dm644 licence.txt "$pkgdir/usr/share/licenses/simutrans-pak64/licence.txt"
}
