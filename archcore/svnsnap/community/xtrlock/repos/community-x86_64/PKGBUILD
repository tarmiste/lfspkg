# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Michal Hybner <dta081@gmail.com>

pkgname=xtrlock
pkgver=2.8
pkgrel=1
pkgdesc='Minimal X display lock program'
url='https://packages.debian.org/sid/xtrlock'
arch=('i686' 'x86_64')
license=('GPL3')
depends=('libx11')
source=(${pkgname}-${pkgver}.tar.gz::http://ftp.debian.org/debian/pool/main/x/xtrlock/xtrlock_${pkgver}.tar.gz)
sha512sums=('a1388250430f7cb7bfe875aae938c332098f9e88ab7a17d68634b36726acfa7a459bf40f71e6df3c6b1256fb4574a62b2fc72357c6e779c1bc55f10204634068')

build() {
  cd ${pkgname}-${pkgver}
  make -f Makefile.noimake CFLAGS="${CFLAGS} -DSHADOW_PWD" LDLIBS="-lX11 -lcrypt" xtrlock
}

package() {
  cd ${pkgname}-${pkgver}
  install -Dm 4755 xtrlock "${pkgdir}/usr/bin/xtrlock"
  install -Dm 644 xtrlock.man "${pkgdir}/usr/share/man/man1/xtrlock.1"
}

# vim: ts=2 sw=2 et:
