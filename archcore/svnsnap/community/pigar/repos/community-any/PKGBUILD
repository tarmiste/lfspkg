# $Id: PKGBUILD 265732 2017-11-08 12:27:53Z felixonmars $
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=pigar
pkgname=("pigar" "python2-pigar")
pkgver=0.7.1
pkgrel=1
pkgdesc="A fantastic tool to generate requirements for your Python project, and more than that"
arch=('any')
url="https://github.com/Damnever/pigar"
license=('BSD')
makedepends=('python-setuptools' 'python2-setuptools' 'python-colorama' 'python2-colorama'
             'python2-futures' 'python-gevent' 'python2-gevent')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/Damnever/pigar/archive/v$pkgver.tar.gz")
sha512sums=('283ea02dd452a32f1b8b44d4ae19abefc82f2f116b2bdb67fa163b68656c566415bd50fa9aecd5001497e371c2a5aed0d0965d0413bbbf86437fbd787ec7b103')

prepare() {
  cp -a pigar-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/pigar-$pkgver
  python setup.py build

  cd "$srcdir"/pigar-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/pigar-$pkgver
  python -m unittest discover pigar/tests/ -t . -v

  cd "$srcdir"/pigar-$pkgver-py2
  python2 -m unittest discover pigar/tests/ -t . -v
}

package_pigar() {
  depends=('python-setuptools' 'python-colorama' 'python-gevent')

  cd pigar-$pkgver
  python3 setup.py install -O1 --root="$pkgdir"

  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_python2-pigar() {
  depends=('python2-setuptools' 'python2-colorama' 'python2-gevent' 'python2-futures')

  cd pigar-$pkgver-py2
  python2 setup.py install -O1 --root="$pkgdir"
  mv "$pkgdir"/usr/bin/pigar{,2}

  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

# vim:set ts=2 sw=2 et:
