# $Id: PKGBUILD 225021 2017-04-24 12:15:36Z jgc $
# Maintainer: Sergej Pupykin <arch+pub@sergej.pp.ru>
# Maintainer: Christian Hesse <mail@eworm.de>

pkgname=open-isns
pkgver=0.97
pkgrel=3
pkgdesc='iSNS server and client for Linux'
arch=('i686' 'x86_64')
depends=('openssl')
url='https://github.com/gonzoleeman/open-isns'
license=('GPL')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/gonzoleeman/${pkgname}/archive/v${pkgver}.tar.gz")
sha256sums=('c1c9ae740172e55a1ff33bc22151ec3d916562bf5d60c8420cd64496343683a9')

build() {
	cd "$srcdir"/${pkgname}-${pkgver}

	autoreconf -fi
	./configure --prefix=/usr \
		--bindir=/usr/bin \
		--sbindir=/usr/bin \
		 --enable-shared
	make
}

package() {
	cd "$srcdir"/${pkgname}-${pkgver}
	make DESTDIR="${pkgdir}" install install_lib install_hdrs
}
