# $Id: PKGBUILD 276727 2017-12-29 01:03:15Z felixonmars $
# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=text-latin1
pkgname=haskell-text-latin1
pkgver=0.3
pkgrel=10
pkgdesc="Latin-1 (including ASCII) utility functions"
url="https://github.com/mvv/text-latin1"
license=('custom:BSD3')
arch=('x86_64')
depends=('ghc-libs' 'haskell-case-insensitive' 'haskell-data-checked' 'haskell-hashable'
         'haskell-text')
makedepends=('ghc')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha512sums=('a8c321cde140b771c48227ca464ad676b27d8497852468746ecbe37df6f4a5cbd8e3219fae79525290bdc130dc7bc34ded9dad1ddcbe45ff0654dc0eb8f96cee')

build() {
    cd "${srcdir}/${_hkgname}-${pkgver}"

    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir="/usr/share/doc/${pkgname}" \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd "${srcdir}/${_hkgname}-${pkgver}"

    install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
    install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
    runhaskell Setup copy --destdir="${pkgdir}"
    install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"
}
