# $Id: PKGBUILD 271901 2017-12-02 18:03:27Z muflone $
# Maintainer: Muflone http://www.muflone.com/contacts/english/
# Contributor: Balló György <ballogyor+arch at gmail dot com>

pkgname=bluegriffon
pkgver=3.0.1
pkgrel=1
pkgdesc="The next-generation Web Editor based on the rendering engine of Firefox"
arch=('x86_64')
url="http://bluegriffon.org/"
license=('MPL' 'GPL' 'LGPL')
depends=('nss' 'dbus-glib' 'gtk2' 'gtk3' 'hunspell' 'hyphen' 'libxt')
optdepends=('bluegriffon-i18n: language packs for Bluegriffon')
groups=('bluegriffon')
source=("http://bluegriffon.org/freshmeat/${pkgver}/${pkgname}-${pkgver}.Ubuntu16.04-${CARCH}.tar.bz2"
        "icon-16.png"::"https://github.com/therealglazou/${pkgname}/raw/${pkgver}/branding/default16.png"
        "icon-22.png"::"https://github.com/therealglazou/${pkgname}/raw/${pkgver}/branding/default22.png"
        "icon-24.png"::"https://github.com/therealglazou/${pkgname}/raw/${pkgver}/branding/default24.png"
        "icon-32.png"::"https://github.com/therealglazou/${pkgname}/raw/${pkgver}/branding/default32.png"
        "icon-48.png"::"https://github.com/therealglazou/${pkgname}/raw/${pkgver}/branding/default48.png"
        "icon-128.png"::"https://github.com/therealglazou/${pkgname}/raw/${pkgver}/branding/mozicon128.png"
        "icon-256.png"::"https://github.com/therealglazou/${pkgname}/raw/${pkgver}/branding/default256.png"
        "${pkgname}.desktop")
sha256sums=('eafd38c1f1fbec2a50cf7e3904246505704dfa0fe93beaf33f6984018d3b5243'
            '339b371abc23894084a8de27925ba688f760ab6b3b42b7a54ba338b1e8025793'
            '2fcbe4e9631c749f290e4b3c3b177ad463c5dbdf992674499aeb5adbfeb8862a'
            '497e5bde0da375b8a9a2d5f12d008e73d6b1758e712c70432661410b3bc6273c'
            'c03d6e093d43c0bd6b8f25ac77ffae31d2306817b58c074971470186fe998f5a'
            '0ea31be427b462612fde24f852bf6049a2db452320c066c93cfd73ea53524395'
            '146b2e3979949e97e8d092a41275c27a895d11eff995eb6a453c6d79a6760480'
            '8b19d0a8818b1a01dee68dfbda62f192c47ee9dcfd51d90f93c82fb64b40f4d0'
            '882133b77840356985a81e496b4ddc443f8f33c068cfb7ef0858497820adcfff')

prepare() {
  cd "${pkgname}"
  # Remove unused files
  rm "precomplete" "removed-files"
  # Use system wide dictionaries
  rm -r "dictionaries" "hyphenation"
  # Remove all language packs except english
  mv "distribution/extensions/langpack-en-US@${pkgname}.org.xpi" "${srcdir}"
  rm -r "distribution"
  # Use system libraries
  for _file in "libnspr4.so" "libplc4.so" "libplds4.so" "libfreeblpriv3.chk" \
               "libfreeblpriv3.so" "libnss3.so" "libnssckbi.so" "libssl3.so" \
               "libnssutil3.so" "libsmime3.so" "libsoftokn3.chk" "libsoftokn3.so"
  do
    rm "${_file}"
    sed -i "/${_file}/d" "dependentlibs.list"
  done
}

package() {
  # Install files
  install -m 755 -d "${pkgdir}/usr/lib"
  cp -LR "${pkgname}" "${pkgdir}/usr/lib"
  # Install english language pack
  install -m 755 -d "${pkgdir}/usr/lib/${pkgname}/distribution/extensions"
  install -m 644 -t "${pkgdir}/usr/lib/${pkgname}/distribution/extensions" "${srcdir}/langpack-en-US@${pkgname}.org.xpi" 
  # Install executable file
  install -m 755 -d "${pkgdir}/usr/bin"
  ln -sf /usr/lib/${pkgname}/${pkgname} "${pkgdir}/usr/bin/${pkgname}"
  # Install icons
  for _icon in 16 32 48 128 256; do
      install -m 644 -D "icon-${_icon}.png" "${pkgdir}/usr/share/icons/hicolor/${_icon}x${_icon}/apps/${pkgname}.png"
  done
  # Install desktop file
  install -m 644 -D "${pkgname}.desktop" "${pkgdir}/usr/share/applications/${pkgname}.desktop"
  # Use system wide dictionaries
  ln -sf /usr/share/hunspell "${pkgdir}/usr/lib/${pkgname}/dictionaries"
  ln -sf /usr/share/hyphen "${pkgdir}/usr/lib/${pkgname}/hyphenation"
}

