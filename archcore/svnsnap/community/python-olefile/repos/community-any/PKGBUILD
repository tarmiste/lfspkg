# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

pkgbase=python-olefile
pkgname=('python-olefile' 'python2-olefile')
_pyname=olefile
pkgver=0.44
pkgrel=1
pkgdesc='Python library to parse, read and write Microsoft OLE2 files (formerly OleFileIO_PL)'
url='http://www.decalage.info/olefile'
arch=('any')
license=('BSD')
makedepends=('python' 'python2')
source=(https://github.com/decalage2/olefile/releases/download/v$pkgver/$_pyname-$pkgver.tar.gz)
sha512sums=('92b6ad1bced5b2c8e5332a01e5a2e59527ec2303046d0babd665b0f02fe56966574eff56619de168c50f1ea40df2e61ce589ee61b634222146d049b129514c65')

prepare() {
  cp -ra ${_pyname}-${pkgver}{,py2}
}

package_python-olefile() {
  depends=('python')
  replaces=('python3-olefileio')
  conflicts=('python3-olefileio')
  cd ${_pyname}-${pkgver}
  python setup.py install -O1 --root="${pkgdir}" --prefix=/usr
  install -Dm 644 ${_pyname}/LICENSE.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  install -Dm 644 README.md doc/* -t "${pkgdir}/usr/share/doc/${pkgname}/README"

}

package_python2-olefile() {
  depends=('python2')
  replaces=('python2-olefileio')
  conflicts=('python2-olefileio')
  cd ${_pyname}-${pkgver}py2
  python2 setup.py install -O1 --root="${pkgdir}" --prefix=/usr
  install -Dm 644 ${_pyname}/LICENSE.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  install -Dm 644 README.md doc/* -t "${pkgdir}/usr/share/doc/${pkgname}/README"
}

# vim: ts=2 sw=2 et:
