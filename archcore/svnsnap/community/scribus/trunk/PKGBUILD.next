# $Id: PKGBUILD 173822 2016-05-05 09:44:28Z bisson $
# Maintainer: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>
# Contributor: Ronald van Haren <ronald.archlinux.org>
# Contributor: tobias <tobias@archlinux.org>
# Contributor: Ben <ben@benmazer.net>

pkgname=scribus
pkgver=1.5.3
pkgrel=1
pkgdesc='Desktop publishing software'
url='http://www.scribus.net/'
license=('GPL')
arch=('x86_64')
makedepends=('cmake' 'boost')
optdepends=('tk: scripts based on tkinter')
depends=('qt5-quickcontrols' 'qt5-tools' 'boost-libs' 'cairo' 'lcms2'
         'libcups' 'libxml2' 'podofo' 'hunspell' 'harfbuzz-icu'
         'librevenge' 'python2' 'ghostscript' 'boost-libs'
         'graphicsmagick')
validpgpkeys=('757F5E9B13DD648887AD50092D47C099E782504E')
source=("http://downloads.sourceforge.net/${pkgname}/${pkgname}-${pkgver}.tar.xz"{,.asc})
sha256sums=('73a30b4727e19f5d301a936d23a84275cc4f5613a92416cbd843f5167721d74f'
            'SKIP')

prepare() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	sed \
		-e 's|#!/usr/bin/python|#!/usr/bin/python2|' \
		-e 's|#!/usr/bin/env python|#!/usr/bin/env python2|' \
		-i scribus/plugins/scriptplugin/{samples,scripts}/*
	install -d build
}

build() {
	cd "${srcdir}/${pkgname}-${pkgver}/build"
	cmake .. \
		-DCMAKE_INSTALL_PREFIX=/usr \
		-DCMAKE_BUILD_TYPE=Release \
		-DCMAKE_SKIP_RPATH=ON \
		-DLIB_SUFFIX=""
	make
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}/build"
	make DESTDIR="${pkgdir}" install
	install -Dm644 ../scribus.desktop "${pkgdir}/usr/share/applications/scribus.desktop"
}
