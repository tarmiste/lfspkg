# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: SÃ©bastien Luttringer

pkgname=vicious
pkgver=2.3.1
pkgrel=1
pkgdesc='Widgets for the Awesome window manager'
url='https://github.com/Mic92/vicious'
arch=('any')
license=('GPL2')
depends=('lua')
optdepends=(
  'hddtemp: for the HDD Temp widget type'
  'alsa-utils: for the Volume widget type'
  'wireless_tools: for the Wireless widget type'
  'curl: for widget types accessing network resources'
)
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/Mic92/vicious/archive/v${pkgver}.tar.gz)
sha256sums=('101e8ec6a71f7c8da7c653f4fa0f276db20227e2ad98b2d31702ce6735607d95')
sha512sums=('9e89d36d708a7ccdff206451dfb98db8455a16f9d2254167a446af91fadc2103a5274442f38ddf1eb919da23480b6790639cdbbde4a08bffc1cf9221622064c9')

package() {
  cd ${pkgname}-$pkgver
  install -Dm 644 *.lua -t "${pkgdir}/usr/share/lua/5.3/${pkgname}"
  install -Dm 644 widgets/*.lua -t "${pkgdir}/usr/share/lua/5.3/${pkgname}/widgets"
  install -Dm 644 contrib/*.lua -t "${pkgdir}/usr/share/lua/5.3/${pkgname}/contrib"
  install -Dm 644 README.md Changes.md -t "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim:set ts=2 sw=2 et:
