# $Id: PKGBUILD 266875 2017-11-15 14:29:11Z foutrelis $
# Maintainer: Alexander F RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Bastien Dejean <nihilhill@gmail.com>

pkgname=bspwm
pkgver=0.9.3
pkgrel=2
pkgdesc='Tiling window manager based on binary space partitioning'
arch=('x86_64')
url='https://github.com/baskerville/bspwm'
license=('custom:BSD')
makedepends=('git')
depends=('xcb-util' 'xcb-util-wm' 'xcb-util-keysyms')
optdepends=('sxhkd: to define keyboard and pointer bindings'
            'xdo: for the example panel')
            # AUR: bar-aint-recursive, sutils, xtitle
source=("git+https://github.com/baskerville/$pkgname#tag=$pkgver")
md5sums=('SKIP')

build() {
  make -C "$pkgname" PREFIX=/usr
}

package() {
  cd "$pkgname"

  make PREFIX=/usr DESTDIR="$pkgdir" install
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# getver: -u 2 raw.githubusercontent.com/baskerville/bspwm/master/doc/bspwm.1
# vim: ts=2 sw=2 et:
