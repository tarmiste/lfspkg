# $Id: PKGBUILD 145298 2015-10-30 10:35:09Z fyan $
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-pytest-httpbin
pkgname=('python-pytest-httpbin' 'python2-pytest-httpbin')
_commit=e4ffbae0a44895ae8ddfdffdebd1b5e062d1d743
pkgver=0.2.3
pkgrel=2
pkgdesc='A py.test fixture for httpbining code'
arch=('any')
license=('MIT')
url='https://github.com/kevin1024/pytest-httpbin'
makedepends=('python-pytest' 'python2-pytest' 'python-flask' 'python2-flask'
             'python-decorator' 'python2-decorator' 'python-six' 'python2-six'
             'httpbin' 'python2-httpbin' 'git')
checkdepends=('python-requests' 'python2-requests' 'python-pytest-runner' 'python2-pytest-runner')
source=("git+https://github.com/kevin1024/pytest-httpbin.git#commit=$_commit"
        LICENSE)
md5sums=('SKIP'
         '784ac13962be640b76df58e023712ddd')

prepare() {
  cp -a pytest-httpbin{,-py2}
}

build() {
  cd "$srcdir"/pytest-httpbin
  python setup.py build

  cd "$srcdir"/pytest-httpbin-py2
  python2 setup.py build
}

check() {
  # Hack entry points by installing it

  cd "$srcdir"/pytest-httpbin
  python setup.py install --root="$PWD/tmp_install" --optimize=1
  PYTHONPATH="$PWD/tmp_install/usr/lib/python3.5/site-packages:$PYTHONPATH" python setup.py ptr

  cd "$srcdir"/pytest-httpbin-py2
  python2 setup.py install --root="$PWD/tmp_install" --optimize=1
  PYTHONPATH="$PWD/tmp_install/usr/lib/python2.7/site-packages:$PYTHONPATH" python2 setup.py ptr
}

package_python-pytest-httpbin() {
  depends=('python-pytest' 'python-flask' 'python-decorator' 'python-six' 'httpbin')

  cd pytest-httpbin
  python setup.py install --root="$pkgdir"/ --optimize=1
  install -D -m644 ../LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_python2-pytest-httpbin() {
  depends=('python2-pytest' 'python2-flask' 'python2-decorator' 'python2-six' 'python2-httpbin')

  cd pytest-httpbin-py2
  python2 setup.py install --root="$pkgdir"/ --optimize=1
  install -D -m644 ../LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

# vim:set ts=2 sw=2 et:
