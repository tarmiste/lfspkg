# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>
# Contributor: Jens Adam <jra@byte.cx>
# Contributor: Abel Stern <abel.stern AT gmail.com>
# Contributor: Anton Bazhenov <anton.bazhenov at gmail>

pkgname=byacc
pkgver=20170709
pkgrel=1
pkgdesc='Berkeley reimplementation of the Unix parser generator Yacc'
url='http://invisible-island.net/byacc/'
arch=('i686' 'x86_64')
license=('custom')
depends=('glibc')
replaces=('byacc-noconflict')
source=(ftp://invisible-island.net/${pkgname}/${pkgname}-${pkgver}.tgz{,.asc})
sha512sums=('81b6e2dfe674d7e7650e1583c46f67e20cc5ff56b06ef73a54ec2070abd7f4af1a0db7909f31264d0e6157acd3661c19c3478e06c9bab395f53d0371bc1e9097'
            'SKIP')
validpgpkeys=('C52048C0C0748FEE227D47A2702353E0F7E48EDB') # Thomas Dickey <dickey@invisible-island.net>

build() {
  cd ${pkgname}-${pkgver}
  sed -n '/is distributed/,/distributed freely/p' README > LICENSE
  ./configure --prefix=/usr --mandir=/usr/share/man --program-transform=s,^,b,
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim:set ts=2 sw=2 et:
