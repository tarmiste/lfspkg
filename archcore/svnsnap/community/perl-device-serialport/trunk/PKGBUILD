# $Id: PKGBUILD 270675 2017-11-29 12:57:54Z spupykin $
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Ross melin <rdmelin@gmail.com>

pkgname=perl-device-serialport
pkgver=1.04
pkgrel=11
pkgdesc="POSIX clone of Win32::SerialPort"
arch=('x86_64')
url="http://search.cpan.org/dist/Device-SerialPort"
license=('GPL' 'PerlArtistic')
depends=('perl')
options=('!emptydirs')
source=(https://www.cpan.org/authors/id/C/CO/COOK/Device-SerialPort-$pkgver.tar.gz)
sha256sums=('d392567cb39b4ea606c0e0acafd8ed72320311b995336ece5fcefcf9b150e9d7')

build() {
  cd  "$srcdir"/Device-SerialPort-$pkgver
  eval `perl -V:archname`
  PERL_MM_USE_DEFAULT=1 /usr/bin/perl Makefile.PL INSTALLDIRS=vendor
  make
}

package() {
  cd  "$srcdir"/Device-SerialPort-$pkgver
  make DESTDIR="$pkgdir" install
  find "$pkgdir" -name '.packlist' -delete
  find "$pkgdir" -name '*.pod' -delete
# template start; name=perl-binary-module-dependency; version=1;
if [[ $(find "$pkgdir/usr/lib/perl5/" -name "*.so") ]]; then
	_perlver_min=$(perl -e '$v = $^V->{version}; print $v->[0].".".($v->[1]);')
	_perlver_max=$(perl -e '$v = $^V->{version}; print $v->[0].".".($v->[1]+1);')
	depends+=("perl>=$_perlver_min" "perl<$_perlver_max")
fi
# template end;
}
