# $Id: PKGBUILD 270579 2017-11-29 05:36:35Z felixonmars $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Jeremy "Ichimonji10" Audet <ichimonji10 at gmail dot com>
# Contributor: pumpkin <pumpkin at mailoo dot org>
# Contributor: Vsevolod Balashov <vsevolod at balashov dot name>

pkgbase=gunicorn
pkgname=(gunicorn python2-gunicorn)
pkgver=19.7.1
pkgrel=1
pkgdesc='WSGI HTTP Server for UNIX'
arch=('any')
url='http://gunicorn.org/'
license=('MIT')
makedepends=('python-setuptools' 'python2-setuptools')
checkdepends=('python2-mock' 'python-coverage' 'python2-coverage' 'python-pytest' 'python2-pytest'
              'python-pytest-cov' 'python2-pytest-cov' 'python-pytest-runner'
              'python2-pytest-runner')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/benoitc/$pkgbase/archive/$pkgver.tar.gz")
sha512sums=('d06862016937f11064c0d08f2eceb262a4cdecf876f02a0c722e8ff61c439da27890dee78e20b4aeddac9db12b3e04233f16eb320e85c56b70ac2f332433cc14')

prepare() {
  sed -i 's/==/>=/' gunicorn-$pkgver/requirements_test.txt
  cp -a gunicorn-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/gunicorn-$pkgver
  python setup.py build

  cd "$srcdir"/gunicorn-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/gunicorn-$pkgver
  python setup.py pytest

  cd "$srcdir"/gunicorn-$pkgver-py2
  python2 setup.py pytest
}

package_gunicorn() {
  depends=('python')
  optdepends=('python-eventlet: For asynchronous request handling with eventlet.'
              'python-gevent: For asynchronous request handling with gevent.')

  cd $pkgbase-$pkgver
  python setup.py install --root="$pkgdir" --prefix=/usr --optimize=1
  rm -r "$pkgdir"/usr/bin/gunicorn_paster
  install -Dm 644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_python2-gunicorn() {
  depends=('python2')
  provides=('gunicorn-python2')
  conflicts=('gunicorn-python2')
  replaces=('gunicorn-python2')
  optdepends=('python2-eventlet: For asynchronous request handling with eventlet.'
              'python2-gevent: For asynchronous request handling with gevent.')

  cd $pkgbase-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --prefix=/usr --optimize=1
  rm -r "$pkgdir"/usr/bin/gunicorn_paster
  mv "$pkgdir"/usr/bin/gunicorn "$pkgdir"/usr/bin/gunicorn-python2
  install -Dm 644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

# vim:set ts=2 sw=2 et:
