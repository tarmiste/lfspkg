# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

pkgname=sslsplit
pkgver=0.5.1
pkgrel=1
pkgdesc='Tool for man-in-the-middle attacks against SSL/TLS encrypted network connections'
url='https://www.roe.ch/SSLsplit'
arch=('x86_64')
license=('BSD')
depends=('libevent' 'openssl')
checkdepends=('check')
source=(https://mirror.roe.ch/rel/${pkgname}/${pkgname}-${pkgver}.tar.bz2{,.asc})
sha512sums=('89f399e9efa86610ede5d4f1ebaedfe90c2da36fa7d613c92f5b1a83bc385a83fd2775f350e6f320cd637c4546a7cbb97b7a554214ccb639df047370227d8d92'
            'SKIP')
validpgpkeys=('BFF9C7D7EA0EAC7F1AA55B3EFABE3324B5D3397E') # Daniel Roethlisberger <daniel@roe.ch>

build() {
  cd ${pkgname}-${pkgver}
  make
}

check() {
  cd ${pkgname}-${pkgver}
  make -j1 test
}

package() {
  cd ${pkgname}-${pkgver}
  make PREFIX="${pkgdir}/usr" install
  install -Dm 644 LICENSE.md -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm 644 README.md NEWS.md -t "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: ts=2 sw=2 et:
