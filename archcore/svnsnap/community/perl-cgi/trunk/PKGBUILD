# Maintainer: Florian Pritz <bluewind@xinu.at>

pkgname=perl-cgi
pkgver=4.36
pkgrel=3
pkgdesc='Handle Common Gateway Interface requests and responses'
arch=(any)
license=(PerlArtistic GPL)
options=(!emptydirs)
depends=('perl>=5.8.1' perl-html-parser)
checkdepends=('perl-test-deep>=0.11' 'perl-test-warn>=0.3')
url=https://metacpan.org/release/CGI
source=("http://search.cpan.org/CPAN/authors/id/L/LE/LEEJO/CGI-$pkgver.tar.gz")
md5sums=('48566d81f430a7f755bb4d8f426ce35d')
sha512sums=('1fc1c2647be90ac7c75fe29c590c438993baa4b29bf7d6d0b24a7e998955aae86f31c5849eb871be6399e783e166d54f6146723dfc20dd757860e85f7836fb1c')
_ddir="CGI-$pkgver"

build()
(
  cd "$srcdir/$_ddir"
  export PERL_MM_USE_DEFAULT=1 PERL_AUTOINSTALL=--skipdeps
  unset PERL5LIB PERL_MM_OPT
  /usr/bin/perl Makefile.PL
  make
)

check()
(
  cd "$srcdir/$_ddir"
  export PERL_MM_USE_DEFAULT=1
  unset PERL5LIB
  make test
)

package()
(
  cd "$srcdir/$_ddir"
  make install INSTALLDIRS=vendor DESTDIR="$pkgdir"
  find "$pkgdir" -name .packlist -o -name perllocal.pod -delete
)

# Local Variables:
# mode: shell-script
# sh-basic-offset: 2
# End:
# vim:set ts=2 sw=2 et:
