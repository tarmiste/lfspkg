# $Id: PKGBUILD 234865 2017-06-06 09:12:28Z bluewind $
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Francois Charette <firmicus@gmx.net>

pkgname=perl-xml-atom
pkgver=0.41
pkgrel=3
pkgdesc="Atom feed and API implementation "
arch=('any')
url="http://search.cpan.org/dist/XML-Atom"
license=('GPL' 'PerlArtistic')
depends=('perl-class-data-inheritable' 'perl-uri' 'perl-xml-libxml>=1.64' 'perl-xml-xpath')
options=('!emptydirs')
source=(http://www.cpan.org/authors/id/M/MI/MIYAGAWA/XML-Atom-$pkgver.tar.gz)
md5sums=('99eeebbf90c95cc1a24426a0b9f2d6a5')

build() {
  cd  "$srcdir"/XML-Atom-$pkgver
  sed -i "s|recommends('LWP::Authen::Wsse'),||g" Makefile.PL
  PERL_USE_UNSAFE_INC=1 perl Makefile.PL INSTALLDIRS=vendor
  make
}

package() {
  cd  "$srcdir"/XML-Atom-$pkgver
  make install DESTDIR="$pkgdir"
  find "$pkgdir" -name '.packlist' -delete
  find "$pkgdir" -name '*.pod' -delete
}
