# $Id: PKGBUILD 266875 2017-11-15 14:29:11Z foutrelis $
# Maintainer: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: Adam Griffiths <adam_griffithsAATTdart.net.au>

pkgname=ode
pkgver=0.15.1
pkgrel=1
pkgdesc="An open source, high performance library for simulating rigid body dynamics"
arch=('x86_64')
url="https://bitbucket.org/odedevs/ode/"
license=('LGPL' 'BSD')
depends=('gcc-libs' 'bash')
source=("https://bitbucket.org/odedevs/${pkgname}/downloads/${pkgname}-${pkgver}.tar.gz")
md5sums=('9afc11ddd198e751f57bdd8c49b65762')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  ./bootstrap
  ./configure --prefix=/usr \
              --enable-shared \
              --enable-libccd \
              --enable-double-precision
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make DESTDIR=${pkgdir}/ install

  install -Dm0644 ${srcdir}/${pkgname}-${pkgver}/ou/LICENSE-BSD.TXT \
                  ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE-BSD.TXT
  install -Dm0644 ${srcdir}/${pkgname}-${pkgver}/GIMPACT/GIMPACT-LICENSE-BSD.TXT \
                  ${pkgdir}/usr/share/licenses/${pkgname}/GIMPACT-LICENSE-BSD.TXT
  install -Dm0644 ${srcdir}/${pkgname}-${pkgver}/libccd/BSD-LICENSE \
                  ${pkgdir}/usr/share/licenses/${pkgname}/BSD-LICENSE
}
