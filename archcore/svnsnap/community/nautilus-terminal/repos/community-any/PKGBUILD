# $Id: PKGBUILD 159665 2016-01-31 05:33:19Z bgyorgy $
# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=nautilus-terminal
pkgver=1.1
pkgrel=1
pkgdesc="An integrated terminal for Nautilus"
arch=('any')
url="http://projects.flogisoft.com/nautilus-terminal/"
license=('GPL')
depends=('python2-nautilus' 'vte3' 'python2-xdg')
source=(https://launchpad.net/$pkgname/1.x/$pkgver/+download/${pkgname}_$pkgver.tar.gz)
md5sums=('b9417ce4300fea99f8b2bf2d17456858')

prepare() {
  cd "$srcdir/${pkgname}_$pkgver"

  # python2 fix
  sed -i 's|^#!/usr/bin/python$|#!/usr/bin/python2|' src/nautilus_terminal.py
}

package() {
  cd "$srcdir/${pkgname}_$pkgver"
  ./install.sh --package "$pkgdir"
}
