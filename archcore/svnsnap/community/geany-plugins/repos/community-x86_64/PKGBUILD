# $Id: PKGBUILD 271936 2017-12-02 22:12:34Z jgc $
# Maintainer: Alexander F RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Angel 'angvp' Velasquez <angvp@archlinux.org>
# Contributor: Gerardo Exequiel Pozzi <vmlinuz386@gmail.com>
# Contributor: Patrick Melo <patrick@patrickmelo.eti.br>

pkgname=geany-plugins
pkgver=1.32
pkgrel=2
pkgdesc='Plugins for Geany'
arch=('x86_64')
url='https://plugins.geany.org/'
license=('GPL')
depends=("geany>=$pkgver" 'ctpl' 'desktop-file-utils' 'gpgme' 'gtkspell' 'libgit2' 'lua51' 'pygtk' 'python2' 'vte')
makedepends=('cppcheck' 'gdb' 'intltool' 'libgit2' 'vala')
optdepends=('hspell: hebrew spell checker')
source=("https://plugins.geany.org/$pkgname/$pkgname-$pkgver.tar.bz2"
        enchant-2.patch)
sha256sums=('78d4b394fe22fda298e80a22616421efdd4a419eb227b99eadca73f65abac117'
            '99f160f002544550294f36366e3037ec2fe9c91efb9365c106a31a46cda6efb0')

prepare() {
  cd $pkgname-$pkgver
  patch -Np1 -i ../enchant-2.patch
}

build() {
  cd "$pkgname-$pkgver"

  # Looking forward to Python 3 being supported
  export PYTHON=/usr/bin/python2

  ./configure --prefix=/usr --libexecdir=/usr/lib
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

package() {
  make -C "$pkgname-$pkgver" DESTDIR="$pkgdir" install
}

# getver: plugins.geany.org/downloads.html
# vim: ts=2 sw=2 et:
