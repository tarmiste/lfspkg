# Maintainer: Lukas Fleischer <lfleischer@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>
# Contributor: Army

pkgname=vim-latexsuite
_pkgname=vim-latex
pkgver=1.9.0
pkgrel=2
epoch=1
pkgdesc="Tools to view, edit, and compile LaTeX documents within Vim."
arch=('any')
url="http://vim-latex.sourceforge.net"
license=('custom:vim')
depends=('vim')
optdepends=('python2' 'texlive-bin')
groups=('vim-plugins')
source=("https://sourceforge.net/projects/${_pkgname}/files/releases/${_pkgname}-${pkgver}.tar.gz"{,.asc})
md5sums=('7c64cad8f8f8eb3c9b3fe420385abeb5'
         'SKIP')
validpgpkeys=('18A0E3D6A36194E0A6F2C5F06A3A10B31C109517') # Till Maas
install=$pkgname.install
 
build() {
	cd "${srcdir}/${_pkgname}-${pkgver}"
	find . -type f -name *.py -exec sed -i 's:/usr/bin/python:&2:g' {} +
}

package() {
	cd "${srcdir}/${_pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}" PREFIX='/usr' VIMDIR='/usr/share/vim/vimfiles' install
}
