# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Guillermo Ramos <0xwille at gmail dot com>

pkgname=radare2
pkgver=2.2.0
pkgrel=1
pkgdesc='Open-source tools to disasm, debug, analyze and manipulate binary files'
url='https://radare.org'
arch=('x86_64')
license=('GPL3' 'LGPL3')
depends=('sh' 'capstone' 'openssl' 'libzip')
options=('!emptydirs')
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/radare/radare2/archive/${pkgver}.tar.gz)
sha256sums=('99ba2a7c4aae99ae0c70ce0423fa6ed2344fb94a45bc83bf6c8716e2b3e7f1bf')
sha512sums=('e632ca3ffdeefc394e571b5ae7f0619cbbb93c657ff1ef316771e1d10089fbad98dc08b6e7d152e664a4c16e97d4519c1c6445fe737dbd4e3d296bc93c3ee410')

build() {
  cd ${pkgname}-${pkgver}
  ./configure --prefix=/usr \
    --with-syscapstone \
    --with-syszip \
    --with-openssl
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
