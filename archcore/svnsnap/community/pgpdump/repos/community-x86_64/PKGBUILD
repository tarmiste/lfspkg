# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

pkgname=pgpdump
pkgver=0.32
pkgrel=1
pkgdesc='PGP packet visualizer which displays the packet format'
url='http://www.mew.org/~kazu/proj/pgpdump'
arch=('i686' 'x86_64')
license=('BSD')
depends=('zlib' 'bzip2')
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/kazu-yamamoto/${pkgname}/archive/v${pkgver}.tar.gz)
sha256sums=('b5cad57a07ba221049b168dd3baae54b03c6fdedcb4e9ce32e48f88cab01c305')
sha512sums=('a75a6456e36e232473a99b81157d4977a6f5d4f3a6c3c3478ccc09597be1827a6cbfcfeb1add5b724e0cc8331d331a06050a94664c62fffe3c0fc78fa8e205af')

build() {
  cd ${pkgname}-${pkgver}
  ./configure --prefix=/usr
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
  install -Dm 644 COPYRIGHT "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  install -Dm 644 README.md "${pkgdir}/usr/share/doc/${pkgname}/README"
}

# vim: ts=2 sw=2 et:
