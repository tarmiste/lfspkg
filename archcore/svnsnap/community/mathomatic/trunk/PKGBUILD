# $Id: PKGBUILD 266875 2017-11-15 14:29:11Z foutrelis $
# Contributor: G_Syme <demichan(at)mail(dot)upb(dot)de>
# Contributor: Stefan Husmann <stefan-husmann@t-online.de>
# Maintainer:  Thorsten TÃ¶pper <atsutane-tu@freethoughts.de>

pkgname=mathomatic
pkgver=16.0.5
pkgrel=4
pkgdesc='General purpose Computer Algebra System written in C'
arch=('x86_64')
license=('LGPL')
url="http://mathomatic.orgserve.de/math/"
depends=('readline')
makedepends=('time')
optdepends=('python2: for running /usr/share/doc/mathomatic/factorial/factorial'
            'bash: for running a test script'
            'm4: for complex operations using the matho script'
            'rlwrap: for readline editing'
            'gnuplot: for plotting')
# The original author is dead and the projects site no longer exists. Uploaded a backup copy.
source=('https://github.com/Atsutane/mathomatic/raw/master/mathomatic-16.0.5.tar.bz2')
md5sums=('dd04913a98a5073b56f3bc78a01820f3')

build() {
  cd "$srcdir"/$pkgname-$pkgver

  # python2 fix
  for file in primes/{primorial,matho-sum,matho-mult} examples/factorial; do
    sed -i 's_#!.*/usr/bin/python_#!/usr/bin/python2_' $file
    sed -i 's_#!.*/usr/bin/env.*python_#!/usr/bin/env python2_' $file
  done

  make READLINE=1
  make test
  cd "$srcdir"/$pkgname-$pkgver/primes
  make
  make test
}

package() {
  cd "$srcdir"/$pkgname-$pkgver
  make prefix="$pkgdir"/usr m4install
  sed -i 's+/build/pkg++' "$pkgdir"/usr/bin/matho

  cd "$srcdir"/$pkgname-$pkgver/primes
  make prefix="$pkgdir"/usr install
  sed -i "s+$pkgdir++" "$pkgdir/usr/bin/matho"
}

# vim:set ts=2 sw=2 et:
