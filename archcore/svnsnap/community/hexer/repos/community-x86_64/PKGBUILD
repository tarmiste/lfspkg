# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Magnus Therning <magnus@therning.org>
# Contributor: Anton Bazhenov <anton.bazhenov at gmail>

pkgname=hexer
pkgver=1.0.3
pkgrel=2
pkgdesc='Multi buffer editor for binary files with vi-like interface'
url='https://devel.ringlet.net/editors/hexer/'
arch=('i686' 'x86_64')
license=('custom')
depends=('ncurses')
source=(https://devel.ringlet.net/files/editors/${pkgname}/${pkgname}-${pkgver}.tar.gz{,.asc})
sha256sums=('74149464a713efc34d6bae1cb95e82727ead12dc8a8444f9dfd66f6e459c8191'
            'SKIP')
sha512sums=('5c08374d0879ca61d7d7968bf374d6a136a514708790cc1ff502a6c0cab689397044b30924955eb63b3eae64fb7872a291e182a2a7fadaeec6e51b5c6a573c14'
            'SKIP')
validpgpkeys=('2EE7A7A517FC124CF115C354651EEFB02527DF13') # Peter Pentchev <roam@ringlet.net>

build() {
  cd ${pkgname}-${pkgver}
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" PREFIX=/usr MANDIR=/usr/share/man/man1 install
  install -Dm 644 COPYRIGHT "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

# vim: ts=2 sw=2 et:
