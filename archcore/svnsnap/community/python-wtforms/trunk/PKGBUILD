# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: nblock <nblock [/at\] archlinux DOT us>
# Contributor: neithere <neithere at gmail>
# Contributor: mitsuse <mitsuse at gmail>

_pyname=wtforms
pkgbase=python-wtforms
pkgname=('python-wtforms' 'python2-wtforms')
pkgver=2.1
pkgrel=3
pkgdesc='Flexible forms validation and rendering library for python web development'
url='http://wtforms.simplecodes.com/'
arch=('any')
license=('BSD')
makedepends=('python-setuptools' 'python-sphinx'
             'python2-setuptools' 'python2-sphinx')
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/wtforms/wtforms/archive/${pkgver}.tar.gz)
sha512sums=('fbffa933d33707f5758393458bafdfb923d9dbfdbd1d669987a845d6720f8e2fa635dd15a5831f16838da05690e48084756206349bfdb24f2e9ef4e895fa18cf')

prepare() {
  cp -a ${_pyname}-${pkgver}{,-py2}
}

build() {
  (cd ${_pyname}-${pkgver}
    make -C docs html SPHINXBUILD=sphinx-build
  )
  (cd ${_pyname}-${pkgver}-py2
    make -C docs html SPHINXBUILD=sphinx-build2
  )
}

package_python-wtforms() {
  depends=('python')
  cd ${_pyname}-${pkgver}
  python setup.py install --root="${pkgdir}" -O1
  install -Dm 644 LICENSE.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  install -Dm 644 CHANGES.rst README.md  -t "${pkgdir}/usr/share/doc/${pkgname}"
  cp -r docs/_build/html "${pkgdir}/usr/share/doc/${pkgname}/html"
}

package_python2-wtforms() {
  depends=('python2')
  cd ${_pyname}-${pkgver}-py2
  python2 setup.py install --root="${pkgdir}" -O1
  install -Dm 644 LICENSE.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  install -Dm 644 CHANGES.rst README.md  -t "${pkgdir}/usr/share/doc/${pkgname}"
  cp -r docs/_build/html "${pkgdir}/usr/share/doc/${pkgname}/html"
}

# vim: ts=2 sw=2 et:
