# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Patrick LÃ¼hne <patrick-arch@luehne.de>

_gemname=sass-listen
pkgname=ruby-${_gemname}
pkgver=4.0.0
pkgrel=5
pkgdesc='Listens to file modifications and notifies you about the changes'
url='https://github.com/sass/listen'
arch=('any')
license=('MIT')
depends=('ruby' 'ruby-rb-inotify')
options=('!emptydirs')
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/sass/listen/archive/v${pkgver}.tar.gz)
sha256sums=('5b09daf811df4ae3d8657cf919976b9795d275a3792ab08bdf3b1cccaecd3f4b')
sha512sums=('799e1e70f86b4be671c3286ae860cbcec83d8a1311d67fb37956dc527e2961888094439fd8377d50c78798c494f19ddbed3a412f02e76e429dcc1717c93b7e19')

prepare() {
  cd listen-${pkgver}
  sed -r 's|~>|>=|g' -i ${_gemname}.gemspec # don't give a fuck about rubys bla bla
  sed -r '/rb-fsevent/d' -i ${_gemname}.gemspec # remove OSX stuff
  sed 's|git ls-files -z|find -type f -print0\|sed "s,\\\\./,,g"|' -i ${_gemname}.gemspec
}

build() {
  cd listen-${pkgver}
  gem build ${_gemname}.gemspec
}

package() {
  cd listen-${pkgver}
  local _gemdir="$(gem env gemdir)"
  gem install --ignore-dependencies --no-user-install -i "${pkgdir}${_gemdir}" -n "${pkgdir}/usr/bin" ${_gemname}-${pkgver}.gem
  install -Dm 644 README.md CHANGELOG.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 LICENSE.txt -t "${pkgdir}/usr/share/licenses/${pkgname}"
  rm "${pkgdir}/${_gemdir}/cache/${_gemname}-${pkgver}.gem"
}

# vim: ts=2 sw=2 et:
