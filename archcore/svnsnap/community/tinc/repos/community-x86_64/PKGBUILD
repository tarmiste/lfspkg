# $Id: PKGBUILD 266125 2017-11-10 16:07:12Z demize $
# Maintainer: Johannes LÃ¶thberg <johannes@kyriasis.com>
# Contributor: Timothy Redaelli <timothy.redaelli@gmail.com>
# Contributor: helios
# Contributor: Lothar Gesslein
# Contributor: Dominik George <nik@naturalnet.de>

pkgname=tinc
pkgver=1.0.33
pkgrel=1

pkgdesc="VPN (Virtual Private Network) daemon"
url="http://www.tinc-vpn.org/"
arch=('x86_64')
license=('GPL')

depends=('lzo' 'openssl' 'zlib')

source=(https://www.tinc-vpn.org/packages/tinc-$pkgver.tar.gz{,.sig})

md5sums=('f9080dc5f8b1c135da43b9533f69a0bb'
         'SKIP')
validpgpkeys=('D62BDD168EFBE48BC60E8E234A6084B9C0D71F4A')

build() {
  cd tinc-$pkgver

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var --sbindir=/usr/bin --with-systemdsystemunitdir=/usr/lib/systemd/system
  make
}

package() {
  cd tinc-$pkgver

  make DESTDIR="$pkgdir/" install
  install -dm755 "$pkgdir"/usr/share/tinc/examples
  cp -a doc/sample-config/. "$pkgdir"/usr/share/tinc/examples/
  find "$pkgdir"/usr/share/tinc/examples -type f -exec chmod 644 {} +
  find "$pkgdir"/usr/share/tinc/examples -type d -exec chmod 755 {} +
}
