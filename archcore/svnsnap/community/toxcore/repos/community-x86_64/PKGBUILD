# Maintainer: Jiachen Yang <farseerfc@gmail.com>
# $Id: PKGBUILD 278868 2018-01-05 06:16:21Z farseerfc $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Håvard Pettersson <mail@haavard.me>
# Contributor: naxuroqa <naxuroqa at gmail.com>
# Contributor: Boohbah <boohbah at gmail.com>
# Contributor: Kevin MacMartin <prurigro at gmail dot com>

pkgname=toxcore
_pkgname=c-toxcore
epoch=1
pkgver=0.1.11
pkgrel=1
pkgdesc='Secure, configuration-free, P2P Skype replacement backend'
arch=('x86_64')
url='https://tox.chat'
license=('GPL3')
depends=('systemd' 'libconfig' 'libsodium' 'libvpx' 'opus')
makedepends=('check' 'cmake')
conflicts=("tox")
provides=("tox")
backup=('etc/tox-bootstrapd.conf')
install=$pkgname.install
source=(
	#"${_pkgname}-v${pkgver}.tar.gz::https://github.com/TokTok/${_pkgname}/archive/v${pkgver}.tar.gz"
	"${_pkgname}-v${pkgver}.tar.gz::https://github.com/TokTok/${_pkgname}/releases/download/v${pkgver}/${_pkgname}-${pkgver}.tar.gz"
	"${_pkgname}-v${pkgver}.tar.gz.asc::https://github.com/TokTok/${_pkgname}/releases/download/v${pkgver}/${_pkgname}-${pkgver}.tar.gz.asc"
	#"${_pkgname}-v${pkgver}.tar.lz::https://github.com/TokTok/${_pkgname}/releases/download/v${pkgver}/${_pkgname}-v${pkgver}.tar.lz"
	#"${_pkgname}-v${pkgver}.tar.lz.asc::https://github.com/TokTok/${_pkgname}/releases/download/v${pkgver}/${_pkgname}-v${pkgver}.tar.lz.asc"
	#"pr504.patch::https://github.com/TokTok/c-toxcore/commit/d4414ffa3acf9e7ab5158e6c7a69c115ccb1a308.patch"
        'toxcore.conf')
sha512sums=('4cc853c549c9ea9e2b48cf144b2a5f5d43e79c1103911beb0e8a827d24e40520907c2cef3c1eb05a37a7d0223b33dee352a3fc7a48e76a1a4947989b17f44e88'
            'SKIP'
            'aa1dcfbdf9b613f5d89e238ff5d01e0ea150ad3162792acb806f51ce07fd9ade1270b310b1285a828dcdf578549b95a89fd9bd198fb205c83f5a5be2c969ea63')
validpgpkeys=(
'15D3B9A6B3951DF9854FCA93E786548AE0A0B56B'  # RobinLindén<mail+gitlab@robinlinden.eu>
'B7008FF12C079BF6E6EA19D1601A604B7E605776'  # Robin Lindén <mail@robinlinden.eu>
# GPG key fingerprints taken from https://github.com/qTox/qTox/blob/$_gitver/README.md
'DA262CC93C0E1E525AD21C8596775D454B8EBF44'  # sudden6 <sudden6@gmx.at>
'BA7883E22F9D35945BA3376053137C3033F09008'  # Zetok Zalbavar <zetok@openmailbox.org>
'1157616BBD860C539926F8139591A163FF9BE04C'  # - antis81
'31039166FA902CA50D05D6085AF9F2E29107C727'  # - Diadlo
'C7A2552D0B250F983827742C133203A3AC399151'  # - initramfs
'2880C860D95C909D3DA45C687E086DD661263264'  # - tux3
)

prepare() {
  cd $_pkgname-$pkgver
  sed -i "s|/usr/local|/usr|" other/bootstrap_daemon/tox-bootstrapd.service
  sed -i "/Rpath/d;/RPATH/d" CMakeLists.txt
}

build() {
  cd $_pkgname-$pkgver
  #autoreconf -if
  #./configure \
  #  --prefix=/usr \
  #  --enable-daemon \
  #  --disable-ntox \
  #  --enable-tests
  cmake -DCMAKE_INSTALL_PREFIX=/usr \
        -DDHT_BOOTSTRAP=on \
        -DBOOTSTRAP_DAEMON=on \
        -DBUILD_NTOX=off
  make
}

check() {
  cd $_pkgname-$pkgver
  #make check
  make test ## test fail on i686 
}

package() {
  cd $_pkgname-$pkgver
  make DESTDIR="$pkgdir" install
  install -Dm644 "$srcdir/toxcore.conf" "$pkgdir/usr/lib/sysusers.d/toxcore.conf"
  install -Dm644 ./other/bootstrap_daemon/tox-bootstrapd.service "$pkgdir/usr/lib/systemd/system/tox-bootstrapd.service"
  install -Dm644 ./other/bootstrap_daemon/tox-bootstrapd.conf "$pkgdir/etc/tox-bootstrapd.conf"
}
