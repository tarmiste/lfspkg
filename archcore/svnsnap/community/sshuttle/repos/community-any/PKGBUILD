# $Id: PKGBUILD 243740 2017-07-10 16:13:20Z felixonmars $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: alphazo <alphazo@gmail.com>

pkgname=sshuttle
pkgver=0.78.3
pkgrel=1
pkgdesc='Transparent proxy server that forwards all TCP packets over ssh'
arch=('any')
url="https://github.com/sshuttle/sshuttle"
license=('GPL2')
depends=('python-setuptools' 'iptables' 'openssh' 'net-tools')
makedepends=('python-sphinx' 'python-setuptools_scm')
checkdepends=('python-pytest-runner' 'python-mock')
backup=('etc/sshuttle/tunnel.conf' 'etc/sshuttle/prefixes.conf')
source=("$pkgname-$pkgver.tar.gz::https://github.com/sshuttle/$pkgname/archive/v$pkgver.tar.gz"
        'sshuttle.service' 'prefixes.conf' 'tunnel.conf')
sha512sums=('5c2aaed1c1cda6bd6f0b0c81f0f0c1a6b717c83fa9279d9d13f5e754d7ce9a6b00ca4705868c4971e80a9846477db2ccb88b2927f0ff2c11851633dc12292fa1'
            '655ac580a02ea75376701fd40d0a340b2385efe07016b0930eef9cce98faac60e7c96e492b5894f0897c311196bebf514f9dd2a0be1cc8cc21f18fb0bfeab057'
            'cc5a5dee7991c7641fa8a4fb356e1977aa9d1cbfed63cb5ebf8732c4de76be841f66d9267472e70578b176f528d8cfb4f75e634d5d915f4c8bcdc0b801db8a13'
            'acd3d0024d4604cc6a96609286c3f27bce2f0b70a068f58a2110dacc235b22ba7cd83c8989f46d0a9391eda7d272040bb3b1b2c547ae3fa0c9f61bef0a187dda')

prepare() {
  export SETUPTOOLS_SCM_PRETEND_VERSION=$pkgver
}

build() {
  cd $pkgname-$pkgver
  python setup.py build

  cd docs
  make man
}

check() {
  cd $pkgname-$pkgver
  python setup.py pytest
}

package() {
  cd $pkgname-$pkgver
  python setup.py install --root="$pkgdir" -O1

  install -Dm644 docs/_build/man/sshuttle.1 "$pkgdir/usr/share/man/man1/sshuttle.1"

  install -d "$pkgdir/etc/sshuttle"
  install -m644 "$srcdir"/{tunnel.conf,prefixes.conf} "$pkgdir/etc/sshuttle"
  install -Dm644 "$srcdir/sshuttle.service" "$pkgdir/usr/lib/systemd/system/sshuttle.service"
}
