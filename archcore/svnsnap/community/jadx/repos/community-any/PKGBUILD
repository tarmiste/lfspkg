# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Nicolas Hureau <archlinux@kalenz.net>

pkgname=jadx
pkgver=0.6.1
pkgrel=1
pkgdesc='Command line and GUI tools to produce Java source code from Android Dex and APK files'
url='https://github.com/skylot/jadx'
arch=('any')
license=('Apache')
depends=('java-runtime' 'bash' 'fontconfig' 'xorg-font-utils')
makedepends=('java-environment' 'gradle')
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/skylot/${pkgname}/archive/v${pkgver}.tar.gz)
sha512sums=('05b62710164d4062ff3cd15529b571fa6c0d4aeb9466ec54c4ccc9985dbcce6d6ef4f22b18a6ce1a795a87ed917da199043c3b56a2f6e7984af6b371d0d99705')

build() {
  cd ${pkgname}-${pkgver}
  gradle --gradle-user-home=. dist
}

check() {
  cd ${pkgname}-${pkgver}
  gradle --gradle-user-home=. test
}

package() {
  cd ${pkgname}-${pkgver}/build/jadx

  install -Dm 755 bin/jadx "${pkgdir}/usr/share/java/${pkgname}/bin/jadx"
  install -Dm 755 bin/jadx-gui "${pkgdir}/usr/share/java/${pkgname}/bin/jadx-gui"
  install -Dm 644 lib/* -t "${pkgdir}/usr/share/java/${pkgname}/lib"

  install -d "${pkgdir}/usr/bin"
  ln -s /usr/share/java/${pkgname}/bin/jadx "${pkgdir}/usr/bin/jadx"
  ln -s /usr/share/java/${pkgname}/bin/jadx-gui "${pkgdir}/usr/bin/jadx-gui"

  install -Dm 644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  install -Dm 644 NOTICE "${pkgdir}/usr/share/doc/${pkgname}/NOTICE"
  install -Dm 644 README.md "${pkgdir}/usr/share/doc/${pkgname}/README.md"
}

# vim: ts=2 sw=2 et:
