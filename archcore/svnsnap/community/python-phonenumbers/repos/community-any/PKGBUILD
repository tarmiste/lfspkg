# $Id: PKGBUILD 281354 2018-01-11 17:38:41Z felixonmars $
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-phonenumbers
pkgname=(python-phonenumbers python2-phonenumbers)
pkgver=8.8.9
pkgrel=1
pkgdesc="Python version of Google's common library for parsing, formatting, storing and validating international phone numbers"
arch=('any')
url="https://github.com/daviddrysdale/python-phonenumbers"
license=('Apache')
makedepends=("python-setuptools" "python2-setuptools")
source=("$pkgbase-$pkgver.tar.gz::https://github.com/daviddrysdale/python-phonenumbers/archive/v$pkgver.tar.gz")
sha512sums=('b65ede4b8dd6c40ade04fa794dc1c5e642fd6991aa039255bfb076b178479c387a279addab1491e42b363901caea7e09db4b0ab7508eace7df87a46f0348bc67')

prepare() {
  cp -a python-phonenumbers-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/python-phonenumbers-$pkgver
  python setup.py build

  cd "$srcdir"/python-phonenumbers-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/python-phonenumbers-$pkgver
  python setup.py test

  cd "$srcdir"/python-phonenumbers-$pkgver-py2
  python2 setup.py test
}

package_python-phonenumbers() {
  depends=('python')

  cd python-phonenumbers-$pkgver
  python setup.py install -O1 --root "$pkgdir"
}

package_python2-phonenumbers() {
  depends=('python2')

  cd python-phonenumbers-$pkgver-py2
  python2 setup.py install -O1 --root "$pkgdir"
}
