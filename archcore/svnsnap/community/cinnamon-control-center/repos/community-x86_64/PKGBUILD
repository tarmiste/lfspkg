# $Id: PKGBUILD 267623 2017-11-20 17:58:48Z arojas $
# Maintainer: Alexandre Filgueira <alexfilgueira@cinnarch.com>
# Based on gnome-control-center:
# Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Jan de Groot <jgc@archlinux.org>

pkgname=cinnamon-control-center
pkgver=3.6.5
pkgrel=1
pkgdesc="The Control Center for Cinnamon"
arch=('x86_64')
depends=('cinnamon-settings-daemon' 'cinnamon-translations' 'cinnamon-menus'
         'libgnomekbd'  'libmm-glib' 'nm-connection-editor' 'colord' 'gnome-online-accounts')
optdepends=('gnome-color-manager: for color management tasks')
makedepends=('intltool' 'gnome-common' 'docbook-xsl')
url="https://github.com/linuxmint/cinnamon-control-center"
license=('GPL')
options=('!emptydirs')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxmint/cinnamon-control-center/archive/$pkgver.tar.gz")
sha256sums=('298daf3f0f01af11b63a57c59f0ee62cc0cb16a12b498bdd45fd92e6d32754aa')

build() {
  cd $pkgname-$pkgver

  ./autogen.sh --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var --enable-systemd

  #https://bugzilla.gnome.org/show_bug.cgi?id=656229
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0 /g' -e 's/    if test "$export_dynamic" = yes && test -n "$export_dynamic_flag_spec"; then/      func_append compile_command " -Wl,-O1,--as-needed"\n      func_append finalize_command " -Wl,-O1,--as-needed"\n\0/' libtool
  make
}

package() {
  cd $pkgname-$pkgver

  make DESTDIR="$pkgdir" install

  # Remove unused stuff
  make -C shell DESTDIR="$pkgdir" uninstall-binPROGRAMS uninstall-directoryDATA uninstall-uiDATA
  rm "$pkgdir"/usr/share/applications/cinnamon-{control-center,region-panel}.desktop
  rm -R "$pkgdir/etc"

  install -d -o root -g 102 -m 750 "$pkgdir/usr/share/polkit-1/rules.d"
}
