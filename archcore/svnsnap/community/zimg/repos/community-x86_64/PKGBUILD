# $Id: PKGBUILD 281049 2018-01-10 16:26:06Z alucryd $
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Gustavo Alvarez <sl1pkn07@gmail.com>

pkgname=zimg
pkgver=2.7
pkgrel=1
pkgdesc='Scaling, colorspace conversion, and dithering library'
arch=('x86_64')
url='https://github.com/sekrit-twc/zimg'
license=('custom:WTFPL')
depends=('gcc-libs' 'glibc')
provides=('libzimg.so')
options=('!emptydirs')
source=("zimg-${pkgver}.tar.gz::https://github.com/sekrit-twc/zimg/archive/release-${pkgver}.tar.gz")
sha256sums=('afc15350bd0072a24dd0ed1eaae832ce606245e3c1f8edd81a81bb6f1bfdda2d')

build() {
  cd zimg-release-${pkgver}

  ./autogen.sh
  ./configure \
    --prefix='/usr' \
    --enable-x86simd \
    --disable-static
  make
}

package(){
  cd zimg-release-${pkgver}

  make DESTDIR="${pkgdir}" install
  install -Dm 644 COPYING -t "${pkgdir}"/usr/share/licenses/zimg/
}

# vim: ts=2 sw=2 et:
