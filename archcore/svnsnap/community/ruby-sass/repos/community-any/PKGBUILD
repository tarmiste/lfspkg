# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

_gemname=sass
pkgname=ruby-sass
pkgver=3.5.5
pkgrel=1
pkgdesc='Sass makes CSS fun again'
url='http://sass-lang.com/'
arch=('any')
license=('MIT')
depends=('ruby' 'ruby-sass-listen' 'ruby-yard' 'ruby-maruku')
options=('!emptydirs')
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/sass/sass/archive/${pkgver}.tar.gz)
sha256sums=('53766fd5a410d01f98d6aecfa948519b2b8004751d4a35a607fc3d5ab8abe654')
sha512sums=('a1df63244d4b186a797fa7bfddd59961945b529a24960a39685079c30b580bd90059460d3bbbb86f396fd586568430ef1282e0d3d789cb842fd01042518ef590')

prepare() {
  cd ${_gemname}-${pkgver}
  sed -r 's|~>|>=|g' -i ${_gemname}.gemspec # don't give a fuck about rubys bla bla
}

build() {
  cd ${_gemname}-${pkgver}
  gem build ${_gemname}.gemspec
}

package() {
  cd ${_gemname}-${pkgver}
  local _gemdir="$(gem env gemdir)"
  gem install --ignore-dependencies --no-user-install -i "${pkgdir}${_gemdir}" -n "${pkgdir}/usr/bin" ${_gemname}-${pkgver}.gem
  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 MIT-LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  rm "${pkgdir}/${_gemdir}/cache/${_gemname}-${pkgver}.gem"
}

# vim: ts=2 sw=2 et:
