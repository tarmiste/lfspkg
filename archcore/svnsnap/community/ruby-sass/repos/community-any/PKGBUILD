# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

_gemname=sass
pkgname=ruby-sass
pkgver=3.5.4
pkgrel=2
pkgdesc='Sass makes CSS fun again'
url='http://sass-lang.com/'
arch=('any')
license=('MIT')
depends=('ruby' 'ruby-sass-listen' 'ruby-yard' 'ruby-maruku')
options=('!emptydirs')
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/sass/sass/archive/${pkgver}.tar.gz)
sha256sums=('1d25bf0e11f4390e132aeba9c4def193a3bf447ea99e3c7f4bdefafc716ba18b')
sha512sums=('5da2235d9b3abdc1c07d788005647f8a1a3fb6b77a9bfd68748d40446b88583918880dd5e6f8a692e9e4fef3d9b7870020eed4f7e0a3b3d643c685a5bc4f3ad0')

prepare() {
  cd ${_gemname}-${pkgver}
  sed -r 's|~>|>=|g' -i ${_gemname}.gemspec # don't give a fuck about rubys bla bla
}

build() {
  cd ${_gemname}-${pkgver}
  gem build ${_gemname}.gemspec
}

package() {
  cd ${_gemname}-${pkgver}
  local _gemdir="$(gem env gemdir)"
  gem install --ignore-dependencies --no-user-install -i "${pkgdir}${_gemdir}" -n "${pkgdir}/usr/bin" ${_gemname}-${pkgver}.gem
  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 MIT-LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  rm "${pkgdir}/${_gemdir}/cache/${_gemname}-${pkgver}.gem"
}

# vim: ts=2 sw=2 et:
