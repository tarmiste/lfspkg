# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Changaco <changaco ατ changaco δοτ net>

pkgname=slowhttptest
pkgver=1.7
pkgrel=1
pkgdesc='Highly configurable tool that simulates some Application Layer Denial of Service (DoS) attacks'
url='https://github.com/shekyan/slowhttptest'
arch=('x86_64' 'i686')
license=('Apache')
depends=('openssl' 'gcc-libs')
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/shekyan/slowhttptest/archive/v${pkgver}.tar.gz)
sha256sums=('9fd3ce4b0a7dda2e96210b1e438c0c8ec924a13e6699410ac8530224b29cfb8e')
sha512sums=('f3dcab977ce538f8b74bee0649e8941d2928d8497a3611ecb6a6367b193b3a8251e90368bdbf4e58096f37afd268aff12d5c60981eb560fc08465a5fedebf069')

prepare() {
  cd ${pkgname}-${pkgver}
  autoreconf -fiv
}

build() {
  cd ${pkgname}-${pkgver}
  ./configure --prefix=/usr
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
