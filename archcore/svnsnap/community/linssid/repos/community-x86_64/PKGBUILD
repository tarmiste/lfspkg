# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Jacques Fontaine <waitnsea@free.fr>
# Contributor: Ryan Young <ryan31415926535@gmail.com>

pkgname=linssid
pkgver=2.9
pkgrel=3
pkgdesc='Graphical wireless scanner for Linux'
url='https://sourceforge.net/projects/linssid/'
arch=('x86_64')
license=('GPL3')
depends=('qt5-base' 'qt5-svg' 'qwt' 'libxkbcommon-x11' 'iw' 'wireless_tools' 'xdg-utils' 'boost-libs'
         'desktop-file-utils' 'gtk-update-icon-cache')
makedepends=('boost')
optdepends=('sudo: privilege escalation to use wireless tools')
source=(${pkgname}-${pkgver}.tar.gz::https://downloads.sourceforge.net/${pkgname}/${pkgname}_${pkgver}.orig.tar.gz)
sha256sums=('3745beed51a210a79d0b0ae9071c75e312734650bda01a269435ba4ae5903f1b')
sha512sums=('687912acfaa5d2d2e39f05cd01730f0727ad6e3b01687c392dbfa5aee32ff9133c27a5d8fd9c223ab56f4f234d8afaec8b7ea826e45ed035dcf552d4be699ae6')

prepare() {
  cd ${pkgname}-${pkgver}
  sed -r 's|libqwt-qt5.so|libqwt.so|g' -i linssid-app/linssid-app.pro
}

build() {
  cd ${pkgname}-${pkgver}
  qmake-qt5
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make INSTALL_ROOT="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
