# Maintainer: Benjamin Bukowski <benjamin.bukowski@gmail.com>
# Contributor: Johannes LÃ¶thberg <demizide@gmail.com>

pkgname=atftp
pkgver=0.7.1
pkgrel=4
pkgdesc='Client/server implementation of the TFTP protocol that implements RFCs 1350, 2090, 2347, 2348, and 2349'
arch=(i686 x86_64)
url='http://sourceforge.net/projects/atftp/'
license=(GPL)
depends=(pcre readline)
backup=(etc/conf.d/atftpd)
source=("https://downloads.sf.net/sourceforge/atftp/atftp-$pkgver.tar.gz"
        'atftp-0.7-cflags.patch'
        'atftpd.service'
        'atftpd.conf')
sha256sums=('ae4c6f09cadb8d2150c3ce32d88f19036a54e8211f22d723e97864bb5e18f92d'
            '56a9bdf1c4f9452db041fce1bc4c994e84ce44c6093ef48588f8d8bc93adaf24'
            'c7f115241777155265cae9760bbadd7299e49630e5be69d0d1d349e2bd4062bf'
            'b9942e43206ddd4337454d6ed27a97976c0b84b6218b09a1a6549eec6b3023e6')

prepare() {
  cd atftp-$pkgver
  # Respect our CFLAGS; needed below for building with -std=gnu89
  patch -Np1 -i ../atftp-0.7-cflags.patch
  autoreconf -vi
}

build() {
  cd atftp-$pkgver
  CFLAGS+=' -std=gnu89'
  ./configure --prefix=/usr --mandir=/usr/share/man --sbindir=/usr/bin \
              --enable-libreadline --disable-libwrap
  make
}

package() {
  cd atftp-$pkgver
  make DESTDIR="$pkgdir" install

  install -Dm644 ../atftpd.service "$pkgdir"/usr/lib/systemd/system/atftpd.service
  install -Dm644 ../atftpd.conf "$pkgdir"/etc/conf.d/atftpd
  install -dm775 --group=nobody "$pkgdir"/srv/atftp 

  # remove in.tftpd link as it conflicts with extra/tftp-hpa
  rm "${pkgdir}/usr/share/man/man8/in.tftpd.8"
  rm "${pkgdir}/usr/bin/in.tftpd"
}
