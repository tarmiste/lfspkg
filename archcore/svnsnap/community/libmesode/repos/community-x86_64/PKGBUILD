# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

pkgbase=libmesode
pkgname=('libmesode' 'libmesode-doc')
pkgver=0.9.1
pkgrel=3
pkgdesc='Simple, lightweight C library for writing prifanity XMPP client'
url='https://github.com/boothj5/libmesode'
arch=('i686' 'x86_64')
license=('GPL3' 'MIT')
makedepends=('openssl' 'expat' 'doxygen')
checkdepends=('check')
options=('staticlibs')
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/boothj5/libmesode/archive/${pkgver}.tar.gz openssl-1.1.0.patch)
sha512sums=('b549c17e61aa3e1d80a93783ac47d5a9e87767876173500c30613e9326d10ed413f284c7435b4f24c028a51f8a8d9be913cd7de9baaa5b358cac5951496d9076'
            'a7025d7d6913f3f1d7b266587c4c4ee26b06bf3f7c2754b8e1fbcf6153e48de6a87f326927c7bb613fd9c67d8e0f5040cb6ee2b594f555733b270e1e87bc6396')

prepare() {
  cd ${pkgbase}-${pkgver}
  patch -Np1 -i $srcdir/openssl-1.1.0.patch
  ./bootstrap.sh
}

build() {
  cd ${pkgbase}-${pkgver}
  ./configure --prefix=/usr
  make
  doxygen
}

check() {
  make -C ${pkgbase}-${pkgver} check
}

package_libmesode() {
  depends=('openssl' 'expat')
  cd ${pkgbase}-${pkgver}
  make DESTDIR="${pkgdir}" install
  install -Dm 644 MIT-LICENSE.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE-MIT"
}

package_libmesode-doc() {
  pkgdesc+=' (documentation and samples)'
  options=('!strip')
  optdepends=('libmesode')
  cd ${pkgbase}-${pkgver}
  install -Dm 644 docs/html/* -t "${pkgdir}/usr/share/doc/${pkgname}/html"
  install -Dm 644 examples/{README.md,*.c} -t "${pkgdir}/usr/share/doc/${pkgname}/examples"
  install -Dm 644 MIT-LICENSE.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE-MIT"
}

# vim: ts=2 sw=2 et:
