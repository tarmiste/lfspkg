# $Id: PKGBUILD 276032 2017-12-25 19:59:11Z demize $
# Maintainer: Johannes Löthberg <johannes@kyriasis.com>
# Contributor:  Bartłomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Andreas Radke <andyrtr@archlinux.org>
# Contributor: Peter Lewis <plewis@aur.archlinux.org>
# Contributor: Sebastian Köhler <sebkoehler@whoami.org.uk>

# This must be built against the version of dovecot being used,
# else mail delivery will fail.
# Specify the version of dovecot to be used here:
_dcpkgver=2.3.0
# Make sure to bump pkgrel if changing this.

pkgname=pigeonhole
pkgver=0.5.0
pkgrel=2

pkgdesc='Sieve implementation for Dovecot'
url='http://pigeonhole.dovecot.org/'
arch=('x86_64')
license=('LGPL')

depends=('dovecot='$_dcpkgver)

conflicts=('dovecot-sieve' 'pigeonhole-hg')

source=("https://pigeonhole.dovecot.org/releases/2.3/dovecot-2.3-pigeonhole-$pkgver.tar.gz"{,.sig}
        dovecot.conf
        sieve-extprograms-segfault.patch::https://github.com/dovecot/pigeonhole/commit/321a39be974deb2e7eff7b2a509a3ee6ff2e5ae1.patch)

sha256sums=('54c887a493605322e5b968b86f7ba0cf5c503f68513a8cbcd866185f2b237954'
            'SKIP'
            'a457a1691cfa82495fc0503bfa4b61e54b149e63400fe0f568dff2c24a3f7858'
            'SKIP')
validpgpkeys=('42F3CD50D4F25A41833BEE3704D62B1E3DFBB4F4') # Stephan Bosch <stephan@rename-it.nl>

prepare() {
  cd dovecot-2.3-pigeonhole-$pkgver

  patch -p1 <"$srcdir"/sieve-extprograms-segfault.patch
}

build() {
  cd dovecot-2.3-pigeonhole-$pkgver

  ./configure --prefix=/usr \
    --with-dovecot=/usr/lib/dovecot \
    --with-moduledir=/usr/lib/dovecot/modules \
    --disable-static
  make
}

check() {
  cd dovecot-2.3-pigeonhole-$pkgver
  make check
}

package() {
  cd dovecot-2.3-pigeonhole-$pkgver

  make DESTDIR="$pkgdir" install
  install -Dm644 "$srcdir/dovecot.conf" "$pkgdir/etc/ld.so.conf.d/dovecot.conf"
}
