# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Doug Newgard <scimmia at archlinux dot info>
# Contributor: Andrey Mivrenik <gim at fastmail dot fm>
# Contributor: Glen Oakley <goakley123@gmail.com>

pkgname=cool-retro-term
pkgver=1.0.1
pkgrel=1
pkgdesc='A good looking terminal emulator which mimics the old cathode display'
arch=('i686' 'x86_64')
url='https://github.com/Swordfish90/cool-retro-term'
license=('GPL3')
depends=('qmltermwidget' 'qt5-quickcontrols' 'qt5-graphicaleffects' 'hicolor-icon-theme')
source=("https://github.com/Swordfish90/cool-retro-term/archive/$pkgver.tar.gz")
sha256sums=('45cd756c61bb6ee3ae7796f221da8d2b137e47f00c3d24839d0ac01ed19a6b42')

prepare() {
  cd "$srcdir/$pkgname-$pkgver"

  sed -i '/qmltermwidget/d' cool-retro-term.pro
}

build() {
  cd "$srcdir/$pkgname-$pkgver"

  qmake
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make INSTALL_ROOT="$pkgdir" install
}
