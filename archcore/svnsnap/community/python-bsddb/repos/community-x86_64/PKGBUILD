# $Id: PKGBUILD 284453 2018-01-20 21:46:04Z bgyorgy $
# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Kaiting Chen <kaitocracy@gmail.com>
# Contributor: Stéphane Gaudreault <stephane@archlinux.org>
# Contributor: Douglas Soares de Andrade <dsandrade@gmail.com>
# Contributor: William Rea <sillywilly@gmail.com>

pkgbase=python-bsddb
_pkgbase=bsddb3
pkgname=(python-bsddb python2-bsddb)
pkgver=6.2.5
pkgrel=2
pkgdesc='Python interface for BerkeleyDB'
arch=('x86_64')
url='https://www.jcea.es/programacion/pybsddb.htm'
license=('BSD')
makedepends=('python2' 'python3')
source=(https://files.pythonhosted.org/packages/source/b/$_pkgbase/$_pkgbase-$pkgver.tar.gz)
sha256sums=('784bf40ad935258507594a89b32ea11f362cde120751c8b96de163955ced7db8')

prepare() {
  cp -a $_pkgbase-$pkgver{,-python2}

  cd $_pkgbase-$pkgver-python2
  sed -i 's@^#!.*python$@#!/usr/bin/python2@' Lib/bsddb/{dbshelve,test/test_dbtables}.py
}

build () {
  # Build python 3 module
  cd $_pkgbase-$pkgver
  python3 setup.py --berkeley-db=/usr build

  # Build python 2 module
  cd ../$_pkgbase-$pkgver-python2
  python2 setup.py --berkeley-db=/usr build
}

package_python-bsddb() {
  depends=('db' 'python')

  cd $_pkgbase-$pkgver
  python3 setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE.txt
}

package_python2-bsddb() {
  depends=('db' 'python2')

  cd $_pkgbase-$pkgver-python2
  python2 setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE.txt
}
