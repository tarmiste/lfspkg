# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
pkgname=forge
pkgdesc="High Performance Visualizations"
url='https://github.com/arrayfire/forge'
pkgver=1.0.1
arch=('i686' 'x86_64')
pkgrel=2
epoch=1
license=('BSD')
makedepends=('cmake' 'doxygen' 'graphviz' 'git' 'python' 'glm' 'gcc6' 'boost')
depends=('glfw' 'freetype2' 'fontconfig' 'freeimage')
source=(https://github.com/arrayfire/forge/archive/v${pkgver}.tar.gz)
sha512sums=('08cf80d6020f85a44fc505bf47bbfcad201b75d5ee15a004ff485e9965896cca4e24e706b789b0a41bc0acf9296112368a2e98dbead0bd57dca1ff9fca57927d')
options=('!buildflags')

build() {
  cd forge-${pkgver}

  rm -rf build
  mkdir build && cd build

  export CC=gcc-6
  export CXX=g++-6
  cmake .. \
      -DCMAKE_INSTALL_PREFIX=/usr \
      -DBUILD_EXAMPLES=ON \
      -DBUILD_DOCUMENTATION=ON

  make
}

package() {
  cd forge-${pkgver}

  install -Dm644 LICENSE ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE

  cd build

  make DESTDIR="${pkgdir}/" install
}
