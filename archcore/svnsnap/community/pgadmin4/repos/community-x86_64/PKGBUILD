# $Id$
# Maintainer: Jerome Leclanche <jerome@leclan.ch>

pkgname=pgadmin4
pkgver=2.0
pkgrel=1
pkgdesc="Comprehensive design and management interface for PostgreSQL"
arch=("i686" "x86_64")
url="https://www.pgadmin.org/"
license=("custom")
depends=("qt5-webengine" "python" "python-psycopg2" "postgresql-libs" "hicolor-icon-theme")
makedepends=("python-setuptools" "imagemagick")
source=(
	"https://ftp.postgresql.org/pub/pgadmin/pgadmin4/v$pkgver/source/$pkgname-$pkgver.tar.gz"{,.sig}
	"pgAdmin4.desktop"
	"config_distro.py"
	"config_local.py"
	# https://www.postgresql.org/message-id/CAMQ-g0dHUN2SFioNg93hCTJkRR3LsVZ-ZvhUh6in%2BO4UAU2Qow%40mail.gmail.com
)
sha256sums=(
	"a8c8052d9e61f3ae357810bcabc8ffc8b02a0c39730e1f14d9a920ae7cae6ccb"
	"SKIP"
	"9a39a321fa5a7fcf4f7f7abf6872de6de537800212e9f6f4902ed37b806765b7"
	"510c11bf26fa335548ec4c582bc9f5a5242b8e03f3cb0b7c19e6bfd12b6d663c"
	"e3b0c44298fc1c149afbf4c8996fb92427ae41e4649b934ca495991b7852b855"
)
validpgpkeys=(
	"E0C4CEEB826B1FDA4FB468E024ADFAAF698F1519"
)


prepare() {
	cd "$pkgname-$pkgver"
	local PYTHONVERSION="$(python -c 'import sys; print("{}.{}".format(sys.version_info.major, sys.version_info.minor))')"
	sed 's|value("PythonPath")|value("PythonPath", "/usr/lib/pgadmin4/venv/lib/python'${PYTHONVERSION}'/site-packages:/usr/lib/python'${PYTHONVERSION}'/site-packages:/usr/lib/python'${PYTHONVERSION}'")|g' -i runtime/{pgAdmin4.cpp,Server.cpp,BrowserWindow.cpp}
	sed -r 's|(venv/lib/python)|\1'${PYTHONVERSION}'|g' runtime/Server.cpp
}


build() {
	export LANG=en_US.UTF-8
	export LC_ALL=en_US.UTF-8

	cd "$pkgname-$pkgver"
	convert runtime/pgAdmin4.{ico,png}

	python -m venv "$srcdir/$pkgname-$pkgver/venv"
	"$srcdir/$pkgname-$pkgver/venv/bin/python" -m pip install -r "$srcdir/$pkgname-$pkgver/requirements.txt"

	python -m venv "$srcdir/$pkgname-$pkgver/venv-docs"
	"$srcdir/$pkgname-$pkgver/venv-docs/bin/python" -m pip install -r "$srcdir/$pkgname-$pkgver/requirements.txt"
	"$srcdir/$pkgname-$pkgver/venv-docs/bin/python" -m pip install sphinx

	PATH="$srcdir/$pkgname-$pkgver/venv-docs/bin:$PATH" make docs

	cd runtime
	qmake CONFIG+=release
	make
}


package() {
	cd "$pkgname-$pkgver"

	install -D runtime/pgAdmin4 "$pkgdir/usr/lib/pgadmin4/runtime/pgAdmin4"

	cp -a docs web "$pkgdir/usr/lib/pgadmin4"
	cp -a venv "$pkgdir/usr/lib/pgadmin4/venv"

	install -Dm644 "$srcdir/config_distro.py" "$pkgdir/usr/lib/pgadmin4/web/config_distro.py"
	install -Dm644 "$srcdir/config_local.py" "$pkgdir/usr/lib/pgadmin4/web/config_local.py"

	### fixup some python wonkyness
	local PYTHONVERSION="$(python -c 'import sys; print("{}.{}".format(sys.version_info.major, sys.version_info.minor))')"
	rm -r "${pkgdir}/usr/lib/pgadmin4/venv/lib/python${PYTHONVERSION}/site-packages/psycopg2"
	ln -s /usr/lib/python3.6/lib-dynload "${pkgdir}/usr/lib/pgadmin4/venv/lib/python${PYTHONVERSION}/lib-dynload"

	### Launcher

	install -Dm644 runtime/pgAdmin4-0.png "$pkgdir/usr/share/icons/hicolor/256x256/apps/pgAdmin4.png"
	install -Dm644 runtime/pgAdmin4-1.png "$pkgdir/usr/share/icons/hicolor/48x48/apps/pgAdmin4.png"
	install -Dm644 runtime/pgAdmin4-2.png "$pkgdir/usr/share/icons/hicolor/32x32/apps/pgAdmin4.png"
	install -Dm644 runtime/pgAdmin4-3.png "$pkgdir/usr/share/icons/hicolor/16x16/apps/pgAdmin4.png"

	install -Dm644 "$srcdir/pgAdmin4.desktop" "$pkgdir/usr/share/applications/pgAdmin4.desktop"

	install -D /dev/stdin "$pkgdir/usr/bin/pgadmin4" <<END
#!/bin/sh
cd /usr/lib/pgadmin4
exec runtime/pgAdmin4 "\$@"
END
	install -D /dev/stdin "$pkgdir/usr/bin/pgadmin4-server" <<END
#!/bin/sh
cd /usr/lib/pgadmin4
PYTHONPATH="/usr/lib/pgadmin4/venv/lib/python${PYTHONVERSION}/site-packages" python web/pgAdmin4.py "\$@"
END

	install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
