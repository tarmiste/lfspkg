# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Dmitry Kharitonov <darksab0r@gmail.com>
# Contributor: Anatol Pomozov <anatol.pomozov@gmail.com>
# Contributor: eagletmt <eagletmt@gmail.com>

_gemname=mini_portile2
pkgname=ruby-mini_portile2
pkgver=2.3.0
pkgrel=3
pkgdesc='Simple autoconf builder for developers'
url='https://github.com/flavorjones/mini_portile'
arch=('any')
license=('MIT')
depends=('ruby')
options=('!emptydirs')
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/flavorjones/mini_portile/archive/v${pkgver}.tar.gz)
sha256sums=('123d785b208946c6ed23771988ac75f047dab802cb5f9f8c288b5eb69ed19604')
sha512sums=('3b787db952899172a93010ecaa65425639bbdffe2f268d761a54de02271a542cfc0764f56eefaa4f4875d271231872c8a2da3177853dea70e3d3c349b5c3617f')

prepare() {
  cd mini_portile-${pkgver}
  sed -r 's|~>|>=|g' -i ${_gemname}.gemspec # don't give a fuck about rubys bla bla
  sed 's|git ls-files -z|find -type f -print0|' -i ${_gemname}.gemspec
}

build() {
  cd mini_portile-${pkgver}
  gem build ${_gemname}.gemspec
}

package() {
  cd mini_portile-${pkgver}
  local _gemdir="$(gem env gemdir)"
  gem install --ignore-dependencies --no-user-install -i "${pkgdir}${_gemdir}" -n "${pkgdir}/usr/bin" ${_gemname}-${pkgver}.gem
  rm "${pkgdir}/${_gemdir}/cache/${_gemname}-${pkgver}.gem"
  install -Dm 644 LICENSE.txt -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm 644 README.md CHANGELOG.md -t "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: ts=2 sw=2 et:
