# $Id: PKGBUILD 257236 2017-09-13 05:48:00Z felixonmars $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Max Meyer <dev@fedux.org>

pkgbase=pacparser
pkgname=('pacparser' 'python2-pacparser')
pkgver=1.3.7rc6
pkgrel=1
arch=(i686 x86_64)
url="https://github.com/pacparser/pacparser"
license=('LGPL')
makedepends=('python2-setuptools')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/pacparser/pacparser/archive/$pkgver.tar.gz")
sha512sums=('4489d3ca1d2121013373cf473a449ee09d6eb33840241739c988657335e9c9be98a39dd1cb4d690bbc98f8e7a444a31829f8b99ab64685252b9aed750f5fa768')

build() {
  cd $pkgbase-$pkgver
  PYTHON=python2 make -j1 all pymod -C src
}

package_pacparser() {
  pkgdesc="Library to parse proxy auto-config (PAC) files"

  cd $pkgbase-$pkgver
  make -C src DESTDIR="$pkgdir/" install
}

package_python2-pacparser() {
  depends=('python2')
  pkgdesc="Python 2.x pacparser module"

  cd $pkgbase-$pkgver
  PYTHON=python2 make -C src DESTDIR="$pkgdir/" install-pymod
}

# vim:set ts=2 sw=2 et:
