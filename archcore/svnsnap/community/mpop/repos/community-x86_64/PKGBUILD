# Maintainer: Lukas Fleischer <lfleischer@archlinux.org>
# Contributor: Israel Herraiz <isra@herraiz.org>
# Contributor: Roberto Alsina <ralsina@kde.org>

pkgname=mpop
pkgver=1.2.6
pkgrel=2
pkgdesc="A small, fast POP3 client suitable as a fetchmail replacement"
arch=('x86_64')
url="http://mpop.sourceforge.net/"
license=('GPL3')
depends=('gnutls' 'libidn')
install=mpop.install
source=("https://downloads.sourceforge.net/mpop/mpop-$pkgver.tar.xz") # {,.sig})
md5sums=('2298ddcf1defbf8e7097a1869c8d39d3')
validpgpkeys=('2F788CDEF4181652720EF132F4926138953E5294') # revoked key

build() {
  cd "$srcdir/mpop-$pkgver"

  ./configure --prefix=/usr --with-ssl=gnutls
  make
}

package() {
  cd "$srcdir/mpop-$pkgver"

  make DESTDIR="$pkgdir" install
  install -Dm644 scripts/vim/mpop.vim "$pkgdir/usr/share/vim/vimfiles/syntax/mpop.vim"
  install -Dm644 doc/mpoprc.example   "$pkgdir/usr/share/doc/mpop/mpoprc.example"
}
