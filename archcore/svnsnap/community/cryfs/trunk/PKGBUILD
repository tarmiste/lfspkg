# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andy Weidenbaum <archbaum@gmail.com>
# Contributor: kaylyn
# Contributor: Giuseppe Cal√† <jiveaxe@gmail.com>

pkgname=cryfs
pkgver=0.9.8
pkgrel=1
pkgdesc="Cryptographic filesystem for the cloud"
arch=(x86_64)
depends=(crypto++ fuse2 curl)
makedepends=(cmake python boost)
url="https://www.cryfs.org"
license=(LGPL3)
source=($pkgname-$pkgver.tar.gz::"https://codeload.github.com/cryfs/$pkgname/tar.gz/$pkgver")
sha256sums=('98f977536ccb3094691ef43682e02c434cc7e606f1926c6c588b70c0c298f86f')

prepare() {
  mkdir -p build
}

build() {
  cd build

  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DBUILD_TESTING=OFF \
    -DCRYFS_UPDATE_CHECKS=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install

  install -Dm 644 "$srcdir"/$pkgname-$pkgver/LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname
  install -Dm 644 "$srcdir"/$pkgname-$pkgver/README.md -t "$pkgdir"/usr/share/doc/$pkgname
}
