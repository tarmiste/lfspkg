# $Id: PKGBUILD 285089 2018-01-22 18:24:55Z jlichtblau $
# Maintainer: Laurent Carlier <lordheavym@gmail.com>
# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: gumper <gumper1034@gmail.com>

pkgname=libalkimia
pkgver=7.0
pkgrel=2
pkgdesc="A library with common classes and functionality used by finance applications for the KDE SC"
arch=('x86_64')
url="http://kde-apps.org/content/show.php/libalkimia?content=137323"
license=('LGPL')
depends=('kdelibs')
makedepends=('cmake' 'automoc4' 'doxygen' 'extra-cmake-modules' 'qt4')
source=(http://download.kde.org/stable/alkimia/$pkgver/src/alkimia-$pkgver.tar.xz{,.sig})
sha256sums=('0c70d0d3b6c020d5bcf92b6f1c395f3bad53a08d4e2b2c17e70c2079e9f43158'
            'SKIP')
validpgpkeys=('D69A745A55331F44F404D8258D4DE062AA2EB01C') #Thomas Baumgart <thb@net-bembel.de>

build() {
  cd "${srcdir}"

  mkdir build
  cd build

  cmake ../alkimia-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DCMAKE_BUILD_TYPE=Release \
    -DBUILD_QT4=1 \
    -Wno-dev
  make
}

package() {
  cd "${srcdir}"/build

  make DESTDIR="${pkgdir}" install
}

