# $Id: PKGBUILD 162811 2016-02-20 15:24:54Z arodseth $
# Maintainer: Alexander F RÃ¸dseth <xyproto@archlinux.org>

pkgname=duktape
pkgver=2.2.0
pkgrel=1
pkgdesc='Embeddable Javascript engine'
arch=('x86_64' 'i686')
url='http://duktape.org/'
makedepends=('setconf')
depends=('glibc')
license=('MIT')
source=("http://duktape.org/duktape-$pkgver.tar.xz" # no https available
        'duktape.pc')
sha256sums=('62f72206427633077cb02e7ccd2599ace4d254db409334593b86d262c0d50c14'
            '7d06661869537a34f3aaf30146f8fbf76b29ef1fc43b6d1371dbb621f1a375b6')

prepare() {
  cd "$pkgname-$pkgver"

  mv Makefile.sharedlibrary Makefile
  sed 's/-Wall -Wextra/$(CFLAGS)/g' -i Makefile
  setconf Makefile INSTALL_PREFIX="$pkgdir/usr"
  setconf "$srcdir/duktape.pc" Version="$pkgver"
}

build() {
  CFLAGS="$CFLAGS -D DUK_USE_FASTINT -w" make -C "$pkgname-$pkgver"
}

package() {
  cd "$pkgname-$pkgver"

  install -d "$pkgdir/usr/lib" "$pkgdir/usr/include"
  make install
  install -Dm644 "$srcdir/duktape.pc" "$pkgdir/usr/lib/pkgconfig/duktape.pc"
  install -Dm644 LICENSE.txt "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# getver: github.com/svaarala/duktape
# vim: ts=2 sw=2 et:
