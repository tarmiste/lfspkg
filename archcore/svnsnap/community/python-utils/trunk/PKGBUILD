# Maintainer: Maxim Andersson <thesilentboatman@gmail.com>
# Co-Maintainer: NicoHood <archlinux {cat} nicohood {dog} de>
# PGP ID: 97312D5EB9D7AE7D0BD4307351DAE9B7C1AE9161

_pkgname=python-utils
_pypiname=python-utils
pkgbase=python-utils
pkgdesc="A module with some convenient utilities not included with the standard Python install"
pkgname=('python-utils' 'python2-utils')
pkgver=2.2.0
pkgrel=1
url="https://github.com/WoLpH/python-utils"
license=('BSD')
arch=("any")
makedepends=('python' 'python-setuptools' 'python2' 'python2-setuptools')
source=("${_pkgname}-${pkgver}.tar.gz::https://pypi.org/packages/source/p/${_pypiname}/${_pypiname}-${pkgver}.tar.gz"
        "${_pkgname}-${pkgver}.tar.gz.asc::https://pypi.org/packages/source/p/${_pypiname}/${_pypiname}-${pkgver}.tar.gz.asc")
sha512sums=('02b3b7f3ebdab8b8f82221634336e1e54ebee58e2b3a8348cbad218ff8a1b55efdace5df8dd105d77b6eecbf30876e7b4bca3ca7cda7b34c10993741dda8570e'
            'SKIP')
validpgpkeys=('149325FD15904E9C4EB89E95E81444E9CE1F695D') # Rick van Hattem <wolph@wol.ph>

prepare() {
    cp -a "${srcdir}/${_pypiname}-${pkgver}"{,-py2}
}

build() {
    cd "${srcdir}/${_pypiname}-${pkgver}"
    python setup.py build

    cd "${srcdir}/${_pypiname}-${pkgver}-py2"
    python2 setup.py build
}

package_python-utils() {
    depends=('python' 'python-six')

    cd "${srcdir}/${_pypiname}-${pkgver}"
    python setup.py install --skip-build --root="${pkgdir}" --optimize=1
    install -Dm644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

package_python2-utils() {
    depends=('python2' 'python2-six')

    cd "${srcdir}/${_pypiname}-${pkgver}-py2"
    python2 setup.py install --skip-build --root="${pkgdir}" --optimize=1
    install -Dm644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
