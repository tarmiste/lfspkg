# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Mitch Bigelow <ipha00@gmail.com>

pkgname=pixiewps
pkgver=1.4.1
pkgrel=1
pkgdesc='Offline bruteforce of the WPS pin exploiting the low or non-existing entropy of some APs'
url='https://github.com/wiire/pixiewps'
arch=('x86_64')
license=('GPL3')
depends=('glibc' 'openssl')
optdepends=('reaver: authentication session key support'
            'bully: authentication session key support')
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/wiire-a/pixiewps/archive/v${pkgver}.tar.gz)
sha512sums=('83f20655c2ad1a67554e6c8cd03ae0dc9484534e3013eb6808032c403f7fbf06c394bbd11005d8705dc0bff83a31a819e6fd6f592286b35dd1d224ee17994249')

build() {
  cd ${pkgname}-${pkgver}
  make -C src OPENSSL=1
}

package() {
  cd ${pkgname}-${pkgver}
  make -C src DESTDIR="${pkgdir}" PREFIX=/usr install
  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 pixiewps.1 -t "${pkgdir}/usr/share/man/man1"
}

# vim: ts=2 sw=2 et:
