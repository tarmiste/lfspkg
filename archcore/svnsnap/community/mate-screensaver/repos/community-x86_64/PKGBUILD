# Maintainer:
# Contributor: Martin Wimpress <code@flexion.org>

pkgname=mate-screensaver
pkgver=1.18.2
pkgrel=1
pkgdesc='Screensaver for MATE'
url="https://mate-desktop.org"
arch=('i686' 'x86_64')
license=('GPL')
depends=('libmatekbd' 'libnotify' 'libxss' 'mate-desktop' 'mate-menus' 'mate-session-manager')
makedepends=('intltool' 'systemd')
optdepends=('rss-glx: Really slick screensavers')
groups=('mate-extra')
conflicts=('mate-screensaver-gtk3')
replaces=('mate-screensaver-gtk3')
source=("https://pub.mate-desktop.org/releases/${pkgver%.*}/${pkgname}-${pkgver}.tar.xz")
sha256sums=('57f820c1b2898f65fc73b0d8003b372fe558620358a9bc12892501ceb53eea0f')

build() {
    cd ${pkgname}-${pkgver}
    ./configure \
        --prefix=/usr \
        --libexecdir=/usr/lib/${pkgname} \
        --sysconfdir=/etc \
        --with-xscreensaverdir=/usr/share/xscreensaver/config \
        --with-xscreensaverhackdir=/usr/lib/xscreensaver \
        --with-mit-ext \
        --with-libnotify \
        --enable-locking \
        --without-console-kit
    make
}

package() {
    cd ${pkgname}-${pkgver}
    make DESTDIR="${pkgdir}" install
}
