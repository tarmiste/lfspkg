# $Id: PKGBUILD 129624 2015-03-20 12:27:56Z arodseth $
# Maintainer: Alexander F RÃ¸dseth <xyproto@archlinux.org>

pkgname=libsvgtiny
pkgver=0.1.6
pkgrel=1
pkgdesc='SVG Tiny library'
arch=('x86_64')
url='http://www.netsurf-browser.org/projects/libsvgtiny/'
license=('MIT')
makedepends=('netsurf-buildsystem' 'git' 'libwapcaplet>=0.4.0' 'libdom>=0.3.2' 'gperf')
depends=('libdom>=0.3.2')
source=("git://git.netsurf-browser.org/libsvgtiny.git#tag=release/$pkgver")
md5sums=('SKIP')

prepare() {
  sed 's:_BSD_SOURCE:_DEFAULT_SOURCE:' -i "$pkgname/Makefile"
}

build() {
  export CFLAGS="$CFLAGS -Wno-error=unused-but-set-variable -w"

  make -C "$pkgname" \
    INCLUDEDIR=include \
    LIBDIR=lib \
    PREFIX=/usr \
    COMPONENT_TYPE="lib-shared"
}

package() {
  cd "$pkgname"

  make install \
    INCLUDEDIR=include \
    LIBDIR=lib \
    PREFIX=/usr \
    DESTDIR="$pkgdir" \
    COMPONENT_TYPE="lib-shared"

  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}

# getver: source.netsurf-browser.org/libsvgtiny.git
# vim: ts=2 sw=2 et:
