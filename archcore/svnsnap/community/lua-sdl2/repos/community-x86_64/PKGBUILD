# $Id: PKGBUILD 135876 2015-06-25 20:39:55Z arodseth $
# Maintainer: Alexander F RÃ¸dseth <xyproto@archlinux.org>

pkgname=lua-sdl2
pkgver=v2.0.5_6.0
pkgrel=1
_lua=5.3
pkgdesc='Pure C binding of SDL 2.0 for Lua'
arch=('x86_64' 'i686')
url='https://github.com/Tangent128/luasdl2'
license=('MIT')
depends=('lua' 'sdl2_gfx' 'sdl2_image' 'sdl2_mixer' 'sdl2_net' 'sdl2_ttf')
makedepends=('lua' 'git' 'sdl2_gfx' 'sdl2_image' 'sdl2_mixer' 'sdl2_net' 'sdl2_ttf' 'cmake' 'ninja')
source=('git+https://github.com/Tangent128/luasdl2#commit=98602738ba87b2c7f1ce299134d0f10b49092350')
md5sums=('SKIP')

build() {
  mkdir -p build
  cd build
  cmake ../luasdl2 \
    -DWITH_LUAVER=${_lua/./} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -G Ninja
  ninja
}

package() {
  DESTDIR="$pkgdir" ninja -C build install
  install -Dm644 luasdl2/LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  mv "$pkgdir/usr/share/docs/Lua-SDL2" "$pkgdir/usr/share/docs/$pkgname"
}

# vim:set ts=2 sw=2 et:
