# $Id: PKGBUILD 265355 2017-11-03 16:51:28Z kkeen $
# Maintainer: Kyle Keen <keenerd@gmail.com>

pkgname=gnu-apl
pkgver=1.7
pkgrel=4
url="http://www.gnu.org/software/apl/"
pkgdesc="An (almost) complete implementation of ISO standard 13751"
arch=('i686' 'x86_64')
depends=('ncurses' 'postgresql-libs' 'gcc-libs' 'sqlite')
license=('GPL3')
source=("https://ftpmirror.gnu.org/apl/apl-${pkgver}.tar.gz")
md5sums=('a06456f4aa9e349700da51c8efd14150')

prepare() {
    cd "$srcdir/apl-$pkgver"
    # The default color scheme is black-on-black in some terminals
    sed -i '64 s/^.*Color.*ANSI/#&/' gnu-apl.d/preferences{,.in}
    sed -i '65 s/^#.*Color.*CURSES/Color\tCURSES/' gnu-apl.d/preferences{,.in}

    # SVN 908 (remove in 1.8)
    sed -i '888 s/ || / || nc == /g' src/Symbol.cc
}

build() {
    cd "$srcdir/apl-$pkgver"
    CFLAGS='-std=gnu99' ./configure --prefix=/usr --sysconfdir=/etc
    make PREFIX=/usr -j1
}

package() {
    cd "$srcdir/apl-$pkgver"
    make PREFIX=/usr DESTDIR="$pkgdir" install
}

