# $Id: PKGBUILD 253906 2017-08-26 11:54:02Z foutrelis $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Brian Bidulock <bidulock@openss7.org>

pkgname=perl-canary-stability
_realname=Canary-Stability
pkgver=2012
pkgrel=3
pkgdesc="CPAN/Canary-Stability - canary to check perl compatability for schmorp's modules"
arch=('any')
license=('PerlArtistic' 'GPL')
options=('!emptydirs')
depends=('perl')
url="https://metacpan.org/release/${_realname}"
source=("http://cpan.metacpan.org/authors/id/M/ML/MLEHMANN/${_realname}-${pkgver}.tar.gz")
md5sums=('88516c29e3d7807a5943ce3a3bdcdb31')

build() {
  cd "$srcdir/$_realname-$pkgver"
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd "$srcdir/$_realname-$pkgver"
  make test
}

package() {
  cd "$srcdir/$_realname-$pkgver"
  make install DESTDIR="$pkgdir"
}

# vim:set ts=2 sw=2 et:
