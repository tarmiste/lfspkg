# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

_gemname=gpgme
pkgname=ruby-gpgme
pkgver=2.0.16
pkgrel=1
pkgdesc='Ruby interface to GnuPG Made Easy (GPGME)'
url='https://github.com/ueno/ruby-gpgme'
arch=('x86_64')
license=('GPL2')
depends=('ruby' 'ruby-mini_portile2')
options=('!emptydirs')
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/ueno/ruby-gpgme/archive/v${pkgver}.tar.gz)
sha256sums=('e2712cb0a94dea827023afd63e1fa273d9204fc3685f351b926404b1beed1fca')
sha512sums=('719be5387fa7077b25f2f36d334ee4a1a830cd00e69952880cd4f829a2182bd98edcab25a805485aba4f3def415be170feaaf577589195d27deef55cdbd152f3')

prepare() {
  cd ${pkgname}-${pkgver}
  sed -r 's|~>|>=|g' -i ${_gemname}.gemspec # don't give a fuck about rubys bla bla
}

build() {
  cd ${pkgname}-${pkgver}
  gem build ${_gemname}.gemspec
}

package() {
  cd ${pkgname}-${pkgver}
  local _gemdir="$(gem env gemdir)"
  gem install --ignore-dependencies --no-user-install -i "${pkgdir}${_gemdir}" -n "${pkgdir}/usr/bin" ${_gemname}-${pkgver}.gem
  install -Dm 644 README.rdoc NEWS -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 examples/* -t "${pkgdir}/usr/share/doc/${pkgname}/examples"

  rm "${pkgdir}/${_gemdir}/cache/${_gemname}-${pkgver}.gem"
  find "${pkgdir}" \( -name gem_make.out -o -name mkmf.log \) -delete
}

# vim: ts=2 sw=2 et:
