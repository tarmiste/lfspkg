# Maintainer:
# Contributor: Nate Simon <aurpkg (at natesimon.net)>

pkgname=xreader
pkgver=1.6.2
pkgrel=1
url='https://github.com/linuxmint/xreader'
pkgdesc="Document viewer for files like PDF and Postscript. X-Apps Project."
arch=(x86_64)
license=(GPL)
depends=(poppler-glib djvulibre libspectre webkit2gtk)
makedepends=(mate-common yelp-tools gobject-introspection caja nemo)
optdepends=('nemo: nemo extension'
    'caja: caja extension'
    'gtk3-print-backends: printer support in gtk3 apps'
    'texlive-bin: support for dvi files')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/linuxmint/${pkgname}/archive/${pkgver}.tar.gz")
sha256sums=('b925007199b156943a568e2da1b7058f99e328cdd3f03f447d280e9bf7356cab')

build() {
    cd $pkgname-$pkgver
    ./autogen.sh --prefix="/usr" \
        --localstatedir="/var" \
        --libexecdir="/usr/lib/${pkgname}" \
        --enable-introspection
    make
}

package(){
    cd $pkgname-$pkgver
    make DESTDIR="$pkgdir" install
}
