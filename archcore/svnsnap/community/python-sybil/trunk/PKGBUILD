# $Id: PKGBUILD 287165 2018-01-26 17:45:49Z felixonmars $
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-sybil
pkgname=('python-sybil' 'python2-sybil')
pkgver=1.0.7
pkgrel=1
pkgdesc='Automated testing for the examples in your documentation.'
arch=('any')
license=('MIT')
url='https://github.com/cjw296/sybil'
makedepends=('python-setuptools' 'python2-setuptools')
checkdepends=('python-pytest-runner' 'python2-pytest-runner' 'python-nose' 'python2-nose')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/cjw296/sybil/archive/$pkgver.tar.gz")
sha512sums=('a8677029b929dda76bbdf1e4880cd820e5d999edd8757ac125d6c223e665ee57c6946468c0e13a24f2c28ff7523b351761d3624fc6f9a8d91e7513eecbe9170a')

prepare() {
  cp -a sybil-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/sybil-$pkgver
  python setup.py build

  cd "$srcdir"/sybil-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/sybil-$pkgver
  python setup.py pytest

  cd "$srcdir"/sybil-$pkgver-py2
  python2 setup.py pytest
}

package_python-sybil() {
  depends=('python')

  cd sybil-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}

package_python2-sybil() {
  depends=('python2')

  cd sybil-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
}

# vim:set ts=2 sw=2 et:
