# $Id: PKGBUILD 266875 2017-11-15 14:29:11Z foutrelis $
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Gustavo Alvarez <sl1pkn07@gmail.com>

pkgname=vapoursynth-plugin-mvtools
pkgver=19
pkgrel=1
pkgdesc='MVTools plugin for VapourSynth'
arch=('x86_64')
url='https://github.com/dubhater/vapoursynth-mvtools'
license=('GPL2')
depends=('fftw' 'gcc-libs' 'glibc' 'vapoursynth')
makedepends=('yasm')
source=("vapoursynth-plugin-mvtools-${pkgver}.tar.gz::https://github.com/dubhater/vapoursynth-mvtools/archive/v${pkgver}.tar.gz")
sha256sums=('41848bf526f1807e6894513534d5243bbce5b796d798a3cf47f617229d7b6e9e')

build() {
  cd vapoursynth-mvtools-${pkgver}

  ./autogen.sh
  ./configure \
    --prefix='/usr' \
    --libdir='/usr/lib/vapoursynth'
  make
}

package(){
  cd vapoursynth-mvtools-${pkgver}

  make DESTDIR="${pkgdir}" install

  install -dm 755 "${pkgdir}"/usr/share/doc/vapoursynth/plugins/mvtools
  install -m 644 readme.rst "${pkgdir}"/usr/share/doc/vapoursynth/plugins/mvtools/README
}

#vim: ts=2 sw=2 et:
