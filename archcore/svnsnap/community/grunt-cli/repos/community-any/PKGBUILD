# $Id: PKGBUILD 193388 2016-10-26 01:31:52Z anthraxx $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Mario Finelli <mario dot finelli at yahoo dot com>
# Contributor: Andy Weidenbaum <archbaum at gmail dot com>
# Contributor: Peter Wu <lekensteyn at gmail dot com>

pkgname=grunt-cli
pkgver=1.2.0
pkgrel=3
pkgdesc="The grunt command line interface."
arch=('any')
url="http://gruntjs.com/"
license=('MIT')
depends=('nodejs')
makedepends=('npm')
provides=('nodejs-grunt-cli')
conflicts=('nodejs-grunt-cli')
replaces=('nodejs-grunt-cli')
install=${pkgname}.install
source=("https://registry.npmjs.org/$pkgname/-/$pkgname-$pkgver.tgz")
noextract=($pkgname-$pkgver.tgz)
sha256sums=('fdb1d4bd83435b3f70614b608e0027a0d75ebfda151396bb99c46405334a01d8')

package() {
  npm install -g --user root --prefix "$pkgdir"/usr "$srcdir"/$pkgname-$pkgver.tgz
  rm -r "$pkgdir"/usr/etc
  mkdir -p "$pkgdir/usr/share/licenses/$pkgname"
  ln -s "../../../lib/node_modules/$pkgname/LICENSE-MIT" "$pkgdir/usr/share/licenses/$pkgname/"
}
