# Maintainer: Bruno Pagani (a.k.a. ArchangeGabriel) <archange@archlinux.org>
# Contributor: Francesco Colista <francesco.colista@gmail.com>
# Contributor: Pizon <pizon@pizon.org>

pkgname=dkimproxy
pkgver=1.4.1
pkgrel=7
pkgdesc="An SMTP-proxy that signs and/or verifies emails, using the Mail::DKIM module."
arch=('any')
url="http://dkimproxy.sourceforge.net/"
license=('GPL')
depends=('perl-mail-dkim' 'perl-net-server' 'perl-error')
options=('!emptydirs')
source=("https://downloads.sourceforge.net/${pkgname}/${pkgname}-${pkgver}.tar.gz"
        'dkimproxy_in.service'
        'dkimproxy_out.service'
        'dkimproxy.sysusers') 
sha256sums=('e5345a1d3cefd32d1fb0face9fa73490118132767253b0ce643463f1e86185bd'
            'ac360adea15b8d4ce40efdccfe4a50268e34452ae91d4d892b0ebe34a6f58571'
            '7933ac07d47bdf3a294c247c76e21b95a1d0cc5c7464a59ed2f7ab60d6e419e6'
            '3a1c6b37c6550c8556c941e230ba38a3eaa40cfb77059a81fc3231dae1db5b5a')

build() {
    cd  dkimproxy-${pkgver}
    ./configure --prefix=/usr --sysconfdir=/etc/dkimproxy
    make
}

package() {
    cd dkimproxy-${pkgver}
    make DESTDIR="${pkgdir}" install
    cd "${srcdir}"
    install -Dm644 ${pkgname}.sysusers "${pkgdir}"/usr/lib/sysusers.d/${pkgname}.conf
    install -Dm644 ${pkgname}_{in,out}.service -t "${pkgdir}"/usr/lib/systemd/system/
}
