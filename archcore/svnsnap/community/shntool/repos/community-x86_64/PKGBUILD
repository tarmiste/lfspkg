# Maintainer: schuay <jakob.gruber@gmail.com>
# Contributor: Michal Hybner <dta081@gmail.com>

pkgname=shntool
pkgver=3.0.10
pkgrel=4
pkgdesc="A multi-purpose WAVE data processing and reporting utility"
arch=('i686' 'x86_64')
url="http://www.etree.org/shnutils/shntool/"
license=('GPL')
options=(!emptydirs)
depends=('glibc')
optdepends=('mac: support for ape format'
            'flac: support for flac format'
            'wavpack: support for wv format')
source=("http://www.etree.org/shnutils/shntool/dist/src/${pkgname}-${pkgver}.tar.gz"
        'shntool-3.0.10-large-size.diff'
        'shntool-3.0.10-large-times.diff')

# Patches taken from https://bugs.debian.org/cgi-bin/bugreport.cgi?bug=684600

prepare() {
	cd "${srcdir}"/${pkgname}-${pkgver}
  patch -Np1 < "${srcdir}/shntool-3.0.10-large-size.diff"
  patch -Np1 < "${srcdir}/shntool-3.0.10-large-times.diff"
}

build() {
	cd "${srcdir}"/${pkgname}-${pkgver}
	./configure --prefix=/usr
	make
}

package() {
	cd "${srcdir}"/${pkgname}-${pkgver}
	make DESTDIR="${pkgdir}" install
}

md5sums=('5d41f8f42c3c15e3145a7a43539c3eae'
         '4265935ef1d684a4b49041278ffda7de'
         '6f0d61ddbf8cbee5c0b51a99e987ddda')

# vim:set ts=2 sw=2 et:
