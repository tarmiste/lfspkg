# $Id: PKGBUILD 253377 2017-08-25 14:43:05Z alucryd $
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: jtts <jussaar@mbnet.fi>
# Contributor: josephgbr <rafael.f.f1@gmail.com>
# Contributor: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Ionut Biru <ibiru@archlinux.org>

pkgname=lib32-colord
pkgver=1.4.1
pkgrel=1
pkgdesc='System daemon for managing color devices'
arch=('x86_64')
url='https://www.freedesktop.org/software/colord'
license=('GPL2')
depends=('colord' 'lib32-dconf' 'lib32-libgudev' 'lib32-libgusb' 'lib32-lcms2'
         'lib32-polkit' 'lib32-sqlite')
makedepends=('argyllcms' 'gcc-multilib' 'gobject-introspection' 'intltool'
             'meson' 'python2' 'sane' 'vala')
source=("https://www.freedesktop.org/software/colord/releases/colord-${pkgver}.tar.xz")
sha256sums=('2b068fc8298265a7a3b68e7516c7a263394cff57579af0d1c0fb6b7429230555')

prepare() {
  if [[ -d build ]]; then
    rm -rf build
  fi
  mkdir build
}

build() {
  cd build

  export CC='gcc -m32'
  export CXX='g++ -m32'
  export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'

  meson setup ../colord-${pkgver} \
    --buildtype='release' \
    --prefix='/usr' \
    --libdir='/usr/lib32' \
    --libexecdir='/usr/lib32/colord' \
    --localstatedir='/var' \
    -Denable-bash-completion='false' \
    -Denable-docs='false' \
    -Denable-libcolordcompat='true' \
    -Denable-man='false' \
    -Denable-systemd='false' \
    -Denable-tests='false' \
    -Denable-vala='true' \
    -Dwith-daemon-user='colord'
  ninja
}

package() {
  cd build

  DESTDIR="${pkgdir}" ninja install
  rm -rf "${pkgdir}"/{etc,var,usr/{bin,include,lib,share}}
}

# vim: ts=2 sw=2 et:
