# $Id: PKGBUILD 224934 2017-04-24 12:10:20Z jgc $
# Maintainer: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>
# Maintainer: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Jeff Mickey <jeff@archlinux.org>
# Contributor: Grigorios Bouzakis <grbzks@gmail.com>

pkgname=fdm
pkgver=1.9
pkgrel=3
pkgdesc='Program to fetch and deliver mail'
url='https://github.com/nicm/fdm'
license=('BSD')
arch=('i686' 'x86_64')
depends=('openssl' 'tdb' 'zlib')
source=("https://github.com/nicm/fdm/releases/download/${pkgver}/fdm-${pkgver}.tar.gz"
        'openssl-1.1.0.patch')
sha256sums=('16416c38a9a7e32d187220cc5ae61a51463d5e4e47419c5c513f422523d39914'
            '334122758841e7d03d691267a634d5b3fda3ab7dbd9852a810349c4ea618b0cb')

options=('!makeflags')

prepare() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	patch -p1 -i ../openssl-1.1.0.patch
	autoreconf -fi
}

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	./configure --prefix=/usr
	make
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"

	make DESTDIR="${pkgdir}" install

	install -d "${pkgdir}/usr/share/licenses/${pkgname}"
	sed '3,17p;d' fdm.c > "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"

	install -d "${pkgdir}/usr/share/doc/${pkgname}"
	cp -a examples "${pkgdir}/usr/share/doc/${pkgname}"
}
