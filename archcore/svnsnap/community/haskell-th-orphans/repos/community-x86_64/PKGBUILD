# $Id: PKGBUILD 277331 2017-12-30 15:09:42Z felixonmars $
# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=th-orphans
pkgname=haskell-th-orphans
pkgver=0.13.4
pkgrel=7
pkgdesc="Orphan instances for TH datatypes"
url="https://github.com/ekmett/th-orphans/"
license=('custom:BSD3')
arch=('x86_64')
depends=('ghc-libs' 'haskell-mtl' 'haskell-th-lift' 'haskell-th-lift-instances' 'haskell-th-reify-many')
makedepends=('ghc')
source=(https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz)
sha512sums=('66707368ac9649802dec954b6c5867b380b275b407c1f2d8db79bcf178a212b8f5e50b8f9f73edb4a09619b3097f6663007f9f4aaf0f33d15102cf80fb883eb4')

build() {
    cd "${srcdir}/${_hkgname}-${pkgver}"
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir="/usr/share/doc/${pkgname}" \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd "${srcdir}/${_hkgname}-${pkgver}"
    
    install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
    install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
    runhaskell Setup copy --destdir="${pkgdir}"
    install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"
}
