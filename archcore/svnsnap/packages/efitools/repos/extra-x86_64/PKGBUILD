# $Id: PKGBUILD 294446 2017-04-24 12:00:42Z jgc $
# Maintainer: Tobias Powalowski <tpowa@archlinux.org>
# Contributor: Mirco Tischler <mt-ml at gmx dot de>
# Contributor: Keshav Amburay <(the ddoott ridikulus ddoott rat) (aatt) (gemmaeiil) (ddoott) (ccoomm)>

pkgname="efitools"
pkgver=1.7.0.5.g0649468
pkgrel=1
pkgdesc="Tools for manipulating UEFI secure boot platforms"
url="http://git.kernel.org/cgit/linux/kernel/git/jejb/efitools.git"
arch=('x86_64' 'i686')
replaces=('prebootloader')
license=('GPL' 'LGPL2.1')
options=('!strip' '!makeflags')
makedepends=('gnu-efi-libs' 'help2man' 'sbsigntools' 'perl-file-slurp' 'git')
depends=('openssl')
_commit=0649468475d20d8ca5634433c4912467cef3ce93  # master
source=("git+https://git.kernel.org/pub/scm/linux/kernel/git/jejb/$pkgname.git#commit=$_commit"
        openssl11.patch)
md5sums=('SKIP'
         '08672dd4dc23daf7345a6f2535960fc1')

pkgver() {
	cd "${pkgname}"
	echo "$(git describe --tags)" | sed -e 's|-|\.|g' -e 's|^v||g'
}

prepare() {
	cd "${pkgname}"
	git clean -x -d -f
	patch -Np1 -i ../openssl11.patch
	sed -i "s/-O2/${CFLAGS} -Wno-pointer-sign/" Make.rules
}

build() {
	cd "${pkgname}"
	# fix PreLoader.efi building on x86_64 #49314
	export ARCH="${CARCH}"
	make
}

package() {
	# http://www.rodsbooks.com/efi-bootloaders/secureboot.html#prebootloader
	# http://blog.hansenpartnership.com/uefi-secure-boot/
	# http://blog.hansenpartnership.com/linux-foundation-secure-boot-system-released
	cd "${pkgname}"
	make DESTDIR="${pkgdir}" install
}
