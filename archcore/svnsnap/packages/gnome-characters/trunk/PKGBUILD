# $Id: PKGBUILD 310007 2017-11-15 14:11:34Z foutrelis $
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=gnome-characters
pkgver=3.26.2
pkgrel=1
pkgdesc="A character map application"
arch=(x86_64)
url="https://git.gnome.org/browse/gnome-characters"
license=(GPL2)
depends=(gjs gtk3 libunistring gnome-desktop)
makedepends=(intltool gobject-introspection appstream-glib git gperf)
groups=(gnome-extra)
_commit=f84930f30778174a91bbfbdfe4f9e28b569f8ff6  # tags/v3.26.2^0
source=("git+https://git.gnome.org/browse/gnome-characters#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/^v//;s/-/+/g'
}

prepare() {
  cd $pkgname
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $pkgname
  ./configure --prefix=/usr
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
