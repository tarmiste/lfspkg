# $Id: PKGBUILD 315395 2018-01-24 12:06:49Z heftig $
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=pipewire
pkgver=0.1.8
pkgrel=1
pkgdesc="Server and user space API to deal with multimedia pipelines"
url="https://pipewire.org"
license=(LGPL2.1)
arch=(x86_64)
depends=(gstreamer gst-plugins-base v4l-utils sbc)
makedepends=(git meson doxygen graphviz xmltoman ffmpeg jack2 libva valgrind)
optdepends=(ffmpeg jack2 libva)
install=pipewire.install
_commit=1804e47a9167e38ce340d10db2ecad3b363f97ac  # tags/0.1.8
source=("git+https://github.com/PipeWire/pipewire#commit=$_commit")
sha256sums=('SKIP')


pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  mkdir build
  cd $pkgname
  # Reduce docs size
  printf '%s\n' >>doc/Doxyfile.in \
    HAVE_DOT=yes DOT_IMAGE_FORMAT=svg INTERACTIVE_SVG=yes
}

build() {
  arch-meson $pkgname build \
    -Denable_docs=true \
    -Denable_man=true \
    -Denable_gstreamer=true
  ninja -C build
}

check() {
  cd build
  meson test
}

package() {
  DESTDIR="$pkgdir" ninja -C build install
}
