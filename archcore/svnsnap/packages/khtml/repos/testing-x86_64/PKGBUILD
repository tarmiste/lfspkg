# $Id: PKGBUILD 314756 2018-01-13 14:10:51Z arojas $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=khtml
pkgver=5.42.0
pkgrel=1
pkgdesc='KHTML APIs'
arch=('x86_64')
url='https://community.kde.org/Frameworks'
license=('LGPL')
depends=('giflib' 'kparts' 'kjs')
makedepends=('extra-cmake-modules' 'python' 'gperf')
groups=('kf5-aids')
source=("https://download.kde.org/stable/frameworks/${pkgver%.*}/portingAids/${pkgname}-${pkgver}.tar.xz"{,.sig})
sha256sums=('7464be060190427d1bda39e30257ea712bdec3af820b91fffba0a9dd9ba5daad'
            'SKIP')
validpgpkeys=(53E6B47B45CEA3E0D5B7457758D0EE648A48B3BB) # David Faure <faure@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
