# $Id: PKGBUILD 304349 2017-08-30 14:32:27Z arojas $
# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=libmediawiki
pkgver=5.37.0
pkgrel=1
_commit=700d8512caeb4d0116a4ad39b6b377adf2899007
pkgdesc="A KDE C++ interface for MediaWiki based web service as wikipedia.org"
arch=(i686 x86_64)
url='https://www.digikam.org/'
license=(GPL)
depends=(kcoreaddons)
makedepends=(extra-cmake-modules)
source=("https://download.kde.org/stable/libmediawiki/libmediawiki-$pkgver.tar.xz"{,.sig})
sha256sums=('da167259c49d012359ad65175c365613f0c42699fb99ac7fb9d1f1c8c48292c1'
            'SKIP')
validpgpkeys=(7C35920F1CE2899E8EA9AAD02E7C0367B9BFA089) # Scarlett Clark <sgclark@kde.org>

prepare() {
  mkdir -p build 
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
