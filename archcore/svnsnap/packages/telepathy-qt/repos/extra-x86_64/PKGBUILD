# $Id: PKGBUILD 284532 2016-12-23 14:32:18Z arojas $
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>

pkgname=telepathy-qt
pkgver=0.9.7
pkgrel=1
arch=(i686 x86_64)
url="http://telepathy.freedesktop.org/wiki/"
license=(LGPL)
pkgdesc="A library for Qt-based Telepathy clients"
depends=(qt5-base telepathy-farstream)
makedepends=(cmake doxygen python2 qt5-tools)
conflicts=(telepathy-qt5)
provides=(telepathy-qt5)
replaces=(telepathy-qt5)
source=("https://telepathy.freedesktop.org/releases/$pkgname/$pkgname-$pkgver.tar.gz"{,.asc})
md5sums=('86470dd3d7f6de8b8f4f36165486d42f'
         'SKIP')
validpgpkeys=('AA33B0D27868E36C151780F0FE0B6D736B1195ED') # Alexandr Akulich

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DPYTHON_EXECUTABLE=/usr/bin/python2 \
    -DDESIRED_QT_VERSION=5 \
    -DENABLE_EXAMPLES=OFF \
    -DENABLE_TESTS=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
