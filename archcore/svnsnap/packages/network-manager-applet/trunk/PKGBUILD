# $Id: PKGBUILD 313546 2017-12-22 20:14:34Z heftig $
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Arjan Timmerman <arjan@archlinux.org>
# Contributor: Wael Nasreddine <gandalf@siemens-mobiles.org>
# Contributor: Tor Krill <tor@krill.nu>
# Contributor: Will Rea <sillywilly@gmail.com>

pkgbase=network-manager-applet
pkgname=(nm-connection-editor network-manager-applet)
pkgdesc="Applet for managing network connections"
url="https://wiki.gnome.org/Projects/NetworkManager/"
pkgver=1.8.11dev+7+ga52ccb2f
pkgrel=1
arch=(x86_64)
license=(GPL2 LGPL2.1)
depends=(libnm-glib libgudev gtk3 mobile-broadband-provider-info iso-codes gcr)
makedepends=(libsecret libnotify libmm-glib intltool gobject-introspection git gtk-doc meson)
options=(!emptydirs)
_commit=a52ccb2fe170558fc0aab4dd1d15ba8808b10951  # master
source=("git+https://git.gnome.org/browse/network-manager-applet#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgbase
  git describe --tags | sed 's/-dev/dev/;s/-/+/g'
}

prepare() {
  mkdir -p build nma/etc/xdg nma/usr/bin \
    nma/usr/share/{applications,icons/hicolor/22x22/apps,man/man1,nm-applet}
  cd $pkgbase
}

build() {
  cd build
  arch-meson ../$pkgbase -Dselinux=false
  ninja
}

check() {
  cd build
  meson test
}

package_nm-connection-editor() {
  pkgdesc="NetworkManager GUI connection editor and widgets"
  provides=("libnm-gtk=$pkgver-$pkgrel" "libnma=$pkgver-$pkgrel")
  conflicts=(libnm-gtk)
  replaces=(libnm-gtk)

  cd build
  DESTDIR="$pkgdir" ninja install

### Split network-manager-applet
  cd ../nma
  mv "$pkgdir"/etc/xdg/autostart etc/xdg/
  mv "$pkgdir"/usr/bin/nm-applet usr/bin/
  mv "$pkgdir"/usr/share/GConf usr/share/

  cd usr/share
  mv "$pkgdir"/usr/share/applications/nm-applet.desktop applications/
  mv "$pkgdir"/usr/share/man/man1/nm-applet.1 man/man1/

  cd icons/hicolor/22x22
  mv "$pkgdir"/usr/share/icons/hicolor/22x22/apps/nm-{adhoc,mb-roam,wwan-tower}.png apps/
  mv "$pkgdir"/usr/share/icons/hicolor/22x22/apps/nm-{secure,vpn-active}-lock.png apps/
  mv "$pkgdir"/usr/share/icons/hicolor/22x22/apps/nm-{signal,tech}-*.png apps/
  mv "$pkgdir"/usr/share/icons/hicolor/22x22/apps/nm-*connecting*.png apps/
}

package_network-manager-applet() {
  depends=(nm-connection-editor libmm-glib libnotify libsecret networkmanager)
  mv nma/* "$pkgdir"
}
