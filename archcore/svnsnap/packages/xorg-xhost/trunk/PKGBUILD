# $Id: PKGBUILD 310007 2017-11-15 14:11:34Z foutrelis $
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=xorg-xhost
pkgver=1.0.7
pkgrel=1
pkgdesc="Server access control program for X"
arch=('x86_64')
url="https://xorg.freedesktop.org/"
license=('custom')
depends=('libx11' 'libxmu')
makedepends=('xorg-util-macros')
groups=('xorg-apps' 'xorg')
source=(https://xorg.freedesktop.org/archive/individual/app/xhost-${pkgver}.tar.bz2{,.sig})
sha256sums=('93e619ee15471f576cfb30c663e18f5bc70aca577a63d2c2c03f006a7837c29a'
            'SKIP')
validpgpkeys=('4A193C06D35E7C670FA4EF0BA2FB9E081F2D130E') # Alan Coopersmith <alan.coopersmith@oracle.com>

build() {
  cd xhost-${pkgver}
  ./configure --prefix=/usr
  make
}

package() {
  cd xhost-${pkgver}
  make DESTDIR="${pkgdir}" install
  install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}
