# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Dan McGee <dan@archlinux.org>
# Contributor: Michael Irwin <6d6469@gmail.com>

pkgname=memcached
pkgver=1.5.3
pkgrel=1
pkgdesc='Distributed memory object caching system'
url='https://memcached.org/'
arch=('i686' 'x86_64')
license=('GPL')
depends=('libevent')
optdepends=('perl: for memcached-tool usage')
install=memcached.install
source=(https://www.memcached.org/files/${pkgname}-${pkgver}.tar.gz
        memcached.service
        memcached.tmpfiles)
sha256sums=('258cc3ddb7613685465acfd0215f827220a3bbdd167fd2c080632105b2d2f3ce'
            'fd60fde92b959dc4160facc0d165f04319d2ece4d2c59b68d8ae24824abea7dd'
            'c4d0ae2218b99a276ff6e0084ae81e66add0ca9347e4bde70e9172db6e44002a')
sha512sums=('b28282c579891e7c5508eb5ab4680b933aa76e8e4a8fe575c996fd4e750f2b4b35ee734d01ae0f280cf4cf6e0530f7f6a95c2e07be3660b07c5ade6ad3c835e2'
            '5b006064b3ab31a6982f5c7b1ab4a49d64118a459913bd4be18ca63bf606dcae3550121d05a34ac8932d28b367e18fa76699c46e311b0b6a22f36ab1885ebebe'
            '960705ff74d25afed477e0b2a5872a3a4fb49ed3105a351f0d0224abc947778f9dbda81e80be94ab636da4a8411a9dd56a8fd4513e5b86a3096a14fa67f1548b')

build() {
  cd ${pkgname}-${pkgver}
  ./configure --prefix=/usr
  make
}

check() {
  cd ${pkgname}-${pkgver}
  make test
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
  install -Dm 755 scripts/memcached-tool -t "${pkgdir}/usr/bin"
  install -Dm 644 "${srcdir}/memcached.service" -t "${pkgdir}/usr/lib/systemd/system"
  install -Dm 644 "${srcdir}/memcached.tmpfiles" "${pkgdir}/usr/lib/tmpfiles.d/memcached.conf"
}

# vim: ts=2 sw=2 et:
