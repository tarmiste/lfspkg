# $Id: PKGBUILD 310007 2017-11-15 14:11:34Z foutrelis $
# Maintainer: Eric BÃ©langer <eric@archlinux.org>

pkgname=figlet
pkgver=2.2.5
pkgrel=2
pkgdesc="A program for making large letters out of ordinary text"
arch=('x86_64')
url="http://www.figlet.org/"
license=('BSD')
depends=('glibc' 'sh')
source=(ftp://ftp.figlet.org/pub/figlet/program/unix/${pkgname}-${pkgver}.tar.gz)
sha1sums=('dda696958c161bd71d6590152c94c4f705415727')

build() {
  cd ${pkgname}-${pkgver}
  make DEFAULTFONTDIR=/usr/share/figlet/fonts all
}

check() {
  cd ${pkgname}-${pkgver}
  make check
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" BINDIR=/usr/bin MANDIR=/usr/share/man \
    DEFAULTFONTDIR=/usr/share/figlet/fonts install 
  install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
