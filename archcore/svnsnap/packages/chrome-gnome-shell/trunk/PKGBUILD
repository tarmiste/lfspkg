# $Id$
# Maintainer: Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>

pkgname=chrome-gnome-shell
pkgver=9
pkgrel=1
pkgdesc='Native browser connector for integration with extensions.gnome.org'
arch=(any)
url='https://wiki.gnome.org/Projects/GnomeShellIntegrationForChrome'
license=(GPL)
depends=(gnome-shell python-requests python-gobject)
makedepends=(cmake jq git)
_commit=35851c792fb68a180d42ee875dadea88089351b1  # tags/v9^0
source=(git+https://git.gnome.org/browse/chrome-gnome-shell#commit=$_commit)
md5sums=('SKIP')

prepare() {
  cd $pkgname
  mkdir -p build
}

build() {
  cd $pkgname/build
  cmake .. \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DBUILD_EXTENSION=OFF
}

package() {
  make -C $pkgname/build DESTDIR="$pkgdir" install
}
