# $Id: PKGBUILD 313621 2017-12-25 22:37:34Z jgc $
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=gnome-calendar
pkgver=3.26.2+19+g954c48d7
pkgrel=1
pkgdesc="Simple and beautiful calendar application designed to perfectly fit the GNOME desktop"
url="https://wiki.gnome.org/Apps/Calendar"
arch=(x86_64)
license=(GPL)
depends=(evolution-data-server gsettings-desktop-schemas evolution gnome-control-center)
makedepends=(python appstream-glib git meson gtk-doc)
groups=(gnome-extra)
_commit=954c48d7a093d4c4e65dc761eb2f395e90819848  # gnome-3-26
source=("git+https://git.gnome.org/browse/gnome-calendar#commit=$_commit"
        gnome-calendar-3.26.2-libical-3.0.patch)
sha256sums=('SKIP'
            '8f4fd0aba6780413c1956a82ebd7a49afcae1ca46b364fb0df7da476c4fb96d5')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  mkdir build
  cd $pkgname
  patch -Np1 -i ../gnome-calendar-3.26.2-libical-3.0.patch
}

build() {
  cd build
  arch-meson ../$pkgname -Denable-gtk-doc=true
  ninja
}

package() {
  cd build
  DESTDIR="$pkgdir" ninja install
}

# vim:set ts=2 sw=2 et:
