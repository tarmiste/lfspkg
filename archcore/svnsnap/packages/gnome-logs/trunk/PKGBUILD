# $Id: PKGBUILD 310007 2017-11-15 14:11:34Z foutrelis $
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=gnome-logs
pkgver=3.26.2
pkgrel=1
pkgdesc="A log viewer for the systemd journal"
arch=(x86_64)
url="https://wiki.gnome.org/Apps/Logs"
license=(GPL)
groups=(gnome-extra)
depends=(systemd gtk3 gsettings-desktop-schemas)
makedepends=(intltool itstool gnome-common appstream-glib git)
_commit=0306fcee341a86c448174e1e7c3b2da8315e5763  # tags/gnome-logs-3.26.2^0
source=("git+https://git.gnome.org/browse/gnome-logs#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/^gnome-logs-//;s/-/+/g'
}

prepare() {
  cd $pkgname
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $pkgname
  ./configure --prefix=/usr
  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}
