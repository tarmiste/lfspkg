# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor:  Hyacinthe Cartiaux <hyacinthe.cartiaux at free.fr>
# Contributor: Bartlomiej Piotrowski <nospam at bpiotrowski dot pl>
# Contributor: Patrick McCarty <pnorcks at gmail dot com>

pkgname=discount
pkgver=2.2.2
pkgrel=5
pkgdesc='A Markdown implementation written in C'
arch=(x86_64)
url="https://www.pell.portland.or.us/~orc/Code/discount/"
license=('custom:BSD')
source=("http://www.pell.portland.or.us/~orc/Code/$pkgname/$pkgname-$pkgver.tar.bz2")
sha256sums=('ec7916731e3ef8516336333f8b7aa9e2af51e57c0017b1e03fa43f1ba6978f64')
depends=(glibc)
conflicts=(markdown)
provides=(markdown)
replaces=(markdown)

prepare() {
  cd $pkgname-$pkgver
  sed -e '/test.*librarian.sh/d' -i configure.inc
}

build() {
  cd $pkgname-$pkgver
  ./configure.sh --prefix=/usr --enable-all-features --with-fenced-code --shared --pkg-config
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install.everything "$pkgdir"/usr/lib/pkgconfig/libmarkdown.pc
  install -Dm644 COPYRIGHT "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
