# $Id: PKGBUILD 310007 2017-11-15 14:11:34Z foutrelis $
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: Sarah Hay <sarahhay@mb.sympatico.ca>

# This package must not by "any" arch.
# Requires rebuild with every change to sdl-config

pkgname=perl-alien-sdl
_realname=Alien-SDL
pkgver=1.446
pkgrel=4
pkgdesc="Building, finding and using SDL binaries"
arch=(x86_64)
license=(PerlArtistic GPL)
url="http://search.cpan.org/dist/Alien-SDL/"
depends=('sdl=1.2.15' perl-file-sharedir perl-capture-tiny)
makedepends=(perl-file-which perl-text-patch perl-archive-extract perl-archive-zip perl-module-build)
options=(!emptydirs)
_author=FROGGS
source=(http://search.cpan.org/CPAN/authors/id/${_author::1}/${_author::2}/$_author/$_realname-$pkgver.tar.gz)
sha256sums=('c9aa2c9dc3c63d89773c7d7203f2a46d1b924d0c72d9f801af147a3dc8bc512a')

prepare() {
  cd $_realname-$pkgver

  # Workaround bug with --with-sdl-config
  sed -i '/^GetOptions/d' Build.PL
}

build() {
  cd $_realname-$pkgver

  # install module in vendor directories
  perl Build.PL --with-sdl-config
  perl Build
}

package() {
  cd $_realname-$pkgver
  perl Build install installdirs=vendor destdir="$pkgdir"
}
