# $Id: PKGBUILD 308779 2017-10-31 17:41:45Z heftig $
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgbase=gdm
pkgname=(gdm libgdm)
pkgver=3.26.2.1
pkgrel=1
pkgdesc="Display manager and login screen"
arch=(i686 x86_64)
license=(GPL)
url="https://wiki.gnome.org/Projects/GDM"
depends=(gnome-shell gnome-session upower xorg-xrdb xorg-server xorg-server-xwayland xorg-xhost)
makedepends=(yelp-tools intltool gobject-introspection git docbook-xsl)
checkdepends=(check)
_commit=029e0c2237bbc91da9cdd64b417d11251a7d599d  # tags/3.26.2.1^0
source=("git+https://git.gnome.org/browse/gdm#commit=$_commit"
        0002-Xsession-Don-t-start-ssh-agent-by-default.patch
        gdm.sysusers)
sha256sums=('SKIP'
            '9449da0b6ee58aa3fde65e6d8d1f30513e4176a7dc6d176f17f320ce82cb1d82'
            '6d9c8e38c7de85b6ec75e488585b8c451f5d9b4fabd2a42921dc3bfcc4aa3e13')

pkgver() {
  cd $pkgbase
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgbase
  patch -Np1 -i ../0002-Xsession-Don-t-start-ssh-agent-by-default.patch
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $pkgbase
  ./configure \
    --prefix=/usr \
    --sbindir=/usr/bin \
    --sysconfdir=/etc \
    --libexecdir=/usr/lib/gdm \
    --localstatedir=/var \
    --disable-static \
    --disable-schemas-compile \
    --enable-gdm-xsession \
    --enable-ipv6 \
    --with-at-spi-registryd-directory=/usr/lib/at-spi2-core \
    --with-check-accelerated-directory=/usr/lib/gnome-session \
    --with-default-pam-config=arch \
    --with-default-path=/usr/local/bin:/usr/local/sbin:/usr/bin \
    --with-gnome-settings-daemon-directory=/usr/lib/gnome-settings-daemon \
    --without-plymouth \
    --without-tcp-wrappers

  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

check() {
  cd $pkgbase
  make check
}

package_gdm() {
  depends+=(libgdm)
  optdepends=('fprintd: fingerprint authentication')
  backup=(etc/pam.d/gdm-autologin etc/pam.d/gdm-fingerprint etc/pam.d/gdm-launch-environment
          etc/pam.d/gdm-password etc/pam.d/gdm-smartcard etc/gdm/custom.conf
          etc/gdm/Xsession etc/gdm/PostSession/Default etc/gdm/PreSession/Default)
  groups=(gnome)
  install=gdm.install

  cd $pkgbase
  make DESTDIR="$pkgdir" install

  chown -R 120:120 "$pkgdir/var/lib/gdm"

  # Unused or created at start
  rm -r "$pkgdir"/var/{cache,log,run}

  install -Dm644 ../gdm.sysusers "$pkgdir/usr/lib/sysusers.d/gdm.conf"

### Split libgdm
  make -C libgdm DESTDIR="$pkgdir" uninstall
  mv "$pkgdir/usr/share/glib-2.0/schemas/org.gnome.login-screen.gschema.xml" "$srcdir"
}

package_libgdm() {
  pkgdesc="GDM support library"
  depends=(systemd glib2 dconf)

  cd $pkgbase
  make -C libgdm DESTDIR="$pkgdir" install
  install -Dt "$pkgdir/usr/share/glib-2.0/schemas" -m644 \
    "$srcdir/org.gnome.login-screen.gschema.xml"
}
