# $Id: PKGBUILD 311575 2017-12-03 22:29:59Z jgc $
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Aaron Griffin <aaron@archlinux.org>

pkgname=goffice
pkgver=0.10.37
pkgrel=1
pkgdesc="A GLib/GTK+ set of document-centric objects and charting library"
url="https://git.gnome.org/browse/goffice"
arch=(x86_64)
license=(GPL)
depends=(librsvg gtk3 libgsf libspectre libxslt lasem)
makedepends=(intltool gtk-doc gobject-introspection autoconf-archive git)
_commit=c804cf97229bb8cc6ce81b5fd1c92ee7d5ec4c88  # tags/GOFFICE_0_10_37^0
source=("git+https://git.gnome.org/browse/goffice#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/^GOFFICE_//;s/_/./g;s/-/+/g'
}

prepare() {
  cd $pkgname
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $pkgname
  ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var --disable-static --enable-introspection --enable-gtk-doc

  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

package() {
  cd $pkgname
  make DESTDIR="${pkgdir}" install
}
