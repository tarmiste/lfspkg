# $Id: PKGBUILD 314380 2018-01-11 16:27:16Z arojas $
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Laurent Carlier <lordheavym@gmail.com>

pkgname=telepathy-kde-send-file
_pkgname=ktp-send-file
pkgver=17.12.1
pkgrel=1
pkgdesc='A File manager plugin to launch a file transfer job with a specified contact'
arch=(x86_64)
url='https://community.kde.org/Real-Time_Communication_and_Collaboration'
license=(GPL)
groups=(kde-applications kdenetwork telepathy-kde)
depends=(telepathy-kde-common-internals)
makedepends=(extra-cmake-modules kdoctools python)
source=("https://download.kde.org/stable/applications/$pkgver/src/$_pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('97348cf2a644d52be17364be86e1285917d988515690f5680b3803da4a70183a'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$_pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
