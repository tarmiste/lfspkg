# $Id: PKGBUILD 306961 2017-10-06 12:01:26Z heftig $
# Maintainer: Jan de Groot <jan@archlinux.org>

pkgname=gnome-menus
pkgver=3.13.3+29+g7f0844e
pkgrel=1
pkgdesc="GNOME menu specifications"
arch=('i686' 'x86_64')
depends=('glib2')
makedepends=('intltool' 'gobject-introspection' 'python2' git gnome-common)
license=('GPL' 'LGPL')
url="https://www.gnome.org"
_commit=7f0844e1c933249faccb3999aacaf44b155813e4  # master
source=("git+https://git.gnome.org/browse/gnome-menus#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

check() {
  cd $pkgname
  make check
}

prepare() {
  cd $pkgname
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd "$pkgname"
  ./configure \
      --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var  --disable-static
  make
}

package(){
  cd "$pkgname"
  make DESTDIR="$pkgdir" install
}
