# $Id: PKGBUILD 310007 2017-11-15 14:11:34Z foutrelis $
# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>
# Contributor: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Alexander Fehr <pizzapunk gmail com>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>

pkgname=audacious
pkgver=3.9
pkgrel=1
pkgdesc="Lightweight, advanced audio player focused on audio quality"
arch=('x86_64')
url="http://audacious-media-player.org/"
license=('BSD')
depends=('gtk2' 'glib2' 'libguess' 'libsm' 'audacious-plugins'
         'gnome-icon-theme' 'hicolor-icon-theme' 'desktop-file-utils')
makedepends=('python') # for gdbus-codegen
optdepends=('unzip: zipped skins support')
provides=('audacious-player')
conflicts=('audacious-player')
replaces=('audacious-player')
source=(http://distfiles.audacious-media-player.org/$pkgname-$pkgver.tar.bz2)
sha256sums=('2d8044673ac786d71b08004f190bbca368258bf60e6602ffc0d9622835ccb05e')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  ./configure \
    --prefix=/usr \
    --with-buildstamp='Arch Linux'
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim:set ts=2 sw=2 et:
