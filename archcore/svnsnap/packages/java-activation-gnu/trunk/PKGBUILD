# Maintainer: Guillaume ALAUX <guillaume@archlinux.org>
_libname=activation
pkgname=java-${_libname}-gnu
pkgver=1.1.1
pkgrel=2
pkgdesc="JavaBeans Activation Framework (JAF), framework for declaring what beans operate on what MIME type data"
arch=('any')
url="https://www.gnu.org/s/classpathx/jaf/jaf.html"
license=('LGPL')
depends=('java-runtime-headless')
makedepends=('apache-ant')
source=(https://ftp.gnu.org/gnu/classpathx/${_libname}-${pkgver}.tar.gz{,.sig})
validpgpkeys=('2875BCCCAE65860C6087CA8FE9D9750C4A8782B8') # Chris Burdess
sha256sums=('b1b5ef560d30fcb11fbf537246857d14110ce4eb2b200d4c54690472305d87b7'
            'SKIP')

build() {
  cd "${srcdir}/${_libname}-${pkgver}"

  ant dist
}

package() {
  install -Dm644 ${srcdir}/${_libname}-${pkgver}/activation.jar \
                 ${pkgdir}/usr/share/java/${_libname}-${pkgver}.jar

  ln -s ${_libname}-${pkgver}.jar ${pkgdir}/usr/share/java/${_libname}.jar
}
