# $Id: PKGBUILD 311969 2017-12-09 12:23:59Z bpiotrowski $
# Maintainer: Dan McGee <dan@archlinux.org>
# Contributor: Geoffroy Carrier <geoffroy.carrier@koon.fr>

pkgname=nilfs-utils
pkgver=2.2.7
pkgrel=1
pkgdesc="A log-structured file system supporting continuous snapshotting (userspace utils)"
arch=('x86_64')
url="http://nilfs.sourceforge.net/"
license=('GPL2' 'LGPL2.1')
backup=('etc/nilfs_cleanerd.conf')
depends=('util-linux')
source=(http://nilfs.sourceforge.net/download/$pkgname-$pkgver.tar.bz2)
md5sums=('de16b06a7d04ca94b56dcfa37e569064')

build() {
  cd $pkgname-$pkgver
  sed -i -e 's#root_sbindir=/sbin#root_sbindir=/usr/bin#' configure
  ./configure --sbindir=/usr/bin --with-libmount
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" sbindir=/usr/bin install LDCONFIG=/bin/true
}
