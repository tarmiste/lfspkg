# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>

pkgname=libpqxx
pkgver=6.0.0
pkgrel=1
pkgdesc='C++ client API for PostgreSQL'
url='http://pqxx.org/development/libpqxx/'
arch=('x86_64')
license=('custom')
depends=('postgresql-libs>=9.1')
makedepends=('python2' 'xmlto' 'doxygen')
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/jtv/libpqxx/archive/${pkgver}.tar.gz)
sha256sums=('81cac92458efd799fadb0374107464320d93eba71de05aedf21afb9c8dda7c3a')
sha512sums=('f237cc03c01a8262eee44a9428206c1cc11b6034dddf540afef145f58eee5c32b880d84832563480d73d834c24311170e2ef6789e100793afbe0b6e393bd4169')

prepare() {
  cd ${pkgname}-${pkgver}
  sed 's|python|python2|' -i tools/splitconfig
}

build() {
  cd ${pkgname}-${pkgver}
  ./configure --prefix=/usr --enable-shared
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make prefix="${pkgdir}/usr" install
  install -Dm 644 COPYING -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: ts=2 sw=2 et:
