# $Id: PKGBUILD 315117 2018-01-18 21:11:55Z bpiotrowski $
# Maintainer: Ray Rashif <schiv@archlinux.org>
# Contributor: Juergen Hoetzel <juergen@archlinux.org>
# Contributor: see .contrib

pkgname=snd
pkgver=17.9
pkgrel=1
pkgdesc="An advanced sound editor"
arch=('x86_64')
url="http://ccrma.stanford.edu/software/snd/"
license=('custom:free')
depends=('jack' 'gamin' 'gsl' 'fftw' 'gtk2' 'libmpc')
makedepends=('ladspa' 'wavpack' 'flac' 'speex' 'gmp'
             'timidity++' 'mpg123' 'vorbis-tools')
optdepends=('wavpack' 'flac' 'speex' 'mpg123'
            'timidity++' 'vorbis-tools')
source=("ftp://ccrma-ftp.stanford.edu/pub/Lisp/${pkgname}-${pkgver}.tar.gz")
md5sums=('8e04c2eaa794c294a3a2d82a7ef20448')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  LDFLAGS+=" -Wl,-z,noexecstack"

  ./configure --prefix=/usr \
              --with-alsa \
              --with-jack \
              --with-gtk \
              --with-ladspa \
              --with-gmp
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make prefix="$pkgdir/usr" install

  install -Dm644 COPYING \
    "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}

# vim:set ts=2 sw=2 et:
