# $Id: PKGBUILD 310475 2017-11-20 16:42:07Z anatolik $
# Maintainer: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>

pkgname=strace
pkgver=4.20
pkgrel=1
pkgdesc='A diagnostic, debugging and instructional userspace tracer'
arch=(i686 x86_64)
url='http://sourceforge.net/projects/strace/'
license=(BSD)
depends=(perl libunwind)
source=(http://downloads.sourceforge.net/$pkgname/${pkgname}-${pkgver}.tar.xz{,.asc})
md5sums=('f2271ab0fac49ebee9cbd7f3469227cb'
         'SKIP')
validpgpkeys=('296D6F29A020808E8717A8842DB5BD89A340AEB7') # Dmitry V. Levin <ldv@altlinux.org>

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr \
    --with-libunwind
  make
}

check() {
  # tests do not work in chroot environment. TODO: fixit.
  # make -C $pkgname-$pkgver check
  true
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
