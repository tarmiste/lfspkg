# $Id: PKGBUILD 313394 2017-12-20 17:44:52Z foutrelis $
# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>
# Contributor: St√©phane Gaudreault <stephane@archlinux.org>
# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: Andreas Radke <andyrtr@archlinux.org>

pkgname=dialog
pkgver=1.3_20171209
pkgrel=1
epoch=1
pkgdesc="A tool to display dialog boxes from shell scripts"
arch=('x86_64')
url="https://invisible-island.net/dialog/"
license=('LGPL2.1')
depends=('sh' 'ncurses')
provides=('libdialog.so')
source=(https://invisible-mirror.net/archives/$pkgname/$pkgname-${pkgver/_/-}.tgz)
sha256sums=('20139442119e2eff5c35236e8e5e313c901539008d9cccf8c8ab3851b41267e6')

build() {
  cd "$srcdir/$pkgname-${pkgver/_/-}"

  ./configure \
    --prefix=/usr \
    --enable-nls \
    --with-libtool \
    --with-ncursesw
  make
}

package() {
  cd "$srcdir/$pkgname-${pkgver/_/-}"
  make DESTDIR="$pkgdir" install-full
}

# vim:set ts=2 sw=2 et:
