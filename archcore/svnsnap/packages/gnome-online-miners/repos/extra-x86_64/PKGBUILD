# $Id: PKGBUILD 306966 2017-10-06 12:01:40Z heftig $
# Contributor: Ionut Biru <ibiru@archlinux.org>

pkgname=gnome-online-miners
pkgver=3.26.0
pkgrel=1
pkgdesc="Crawls through your online content"
arch=(i686 x86_64)
url="https://wiki.gnome.org/Projects/GnomeOnlineMiners"
license=(GPL)
depends=(libgdata gnome-online-accounts grilo libzapojit tracker gfbgraph dleyna-server)
makedepends=(python gnome-common git)
_commit=186cfa6ea0d2fa1db8b7d75d386a6a4e9da3a2f9  # tags/3.26.0^0
source=("git+https://git.gnome.org/browse/gnome-online-miners#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname
  NOCONFIGURE=1 ./autogen.sh
}
  
build() {
  cd $pkgname
  ./configure --prefix=/usr --libexecdir=/usr/lib/$pkgname --disable-static
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
