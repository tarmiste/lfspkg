# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Donald Carr <sirspudd@gmail.com>
# Contributor: Jake Petroules <jake.petroules@qt.io>
pkgname=qbs
pkgver=1.10.0
pkgrel=1
pkgdesc='Cross platform build tool'
arch=('x86_64')
url='https://wiki.qt.io/Qbs'
license=('LGPL')
depends=('qt5-base' 'qt5-script')
source=($pkgname-$pkgver.tar.gz::"https://github.com/qbs/qbs/archive/v$pkgver.tar.gz")
sha512sums=('3c95891527e189e6f29e59cc9b39badac5c9ac008d598a9467d22ed2d579bc87af331fe5b3baa952dc216a2dc9e5b3dca9a4947dddfe754e9e4e22c9679b700e')

build() {
  cd qbs-$pkgver
  qmake QBS_INSTALL_PREFIX=/usr \
    QBS_LIBEXEC_INSTALL_DIR=/usr/lib/qbs \
    QBS_RELATIVE_LIBEXEC_PATH=../lib/qbs \
    CONFIG+=qbs_enable_project_file_updates \
    qbs.pro
  make
}

package() {
  cd qbs-$pkgver
  INSTALL_ROOT="$pkgdir" make install
}
