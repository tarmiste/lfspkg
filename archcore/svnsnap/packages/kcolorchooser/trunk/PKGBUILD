# $Id: PKGBUILD 314380 2018-01-11 16:27:16Z arojas $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kcolorchooser
pkgver=17.12.1
pkgrel=1
pkgdesc="Color Chooser"
url="https://kde.org/applications/graphics/kcolorchooser/"
arch=(x86_64)
license=(GPL LGPL FDL)
groups=(kde-applications kdegraphics)
depends=(kxmlgui hicolor-icon-theme)
makedepends=(extra-cmake-modules python)
conflicts=(kdegraphics-kcolorchooser)
replaces=(kdegraphics-kcolorchooser)
source=("https://download.kde.org/stable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('a35f8bc57700b5cc0107af7ed8b4350c4865c5a8078429e4b7654975f8e21fab'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DBUILD_TESTING=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
