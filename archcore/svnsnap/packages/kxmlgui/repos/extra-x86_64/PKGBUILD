# $Id: PKGBUILD 314882 2018-01-15 22:05:28Z arojas $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kxmlgui
pkgver=5.42.0
pkgrel=1
pkgdesc='User configurable main windows'
arch=('x86_64')
url='https://community.kde.org/Frameworks'
license=('LGPL')
depends=('kglobalaccel' 'ktextwidgets' 'attica-qt5')
makedepends=('extra-cmake-modules' 'python' 'mesa' 'doxygen' 'qt5-tools')
groups=('kf5')
source=("https://download.kde.org/stable/frameworks/${pkgver%.*}/${pkgname}-${pkgver}.tar.xz"{,.sig})
sha256sums=('c01715eb7ba54e10ef3754ac2fd2de580ad6b12568dbe657d17ec2cc5197dd4d'
            'SKIP')
validpgpkeys=(53E6B47B45CEA3E0D5B7457758D0EE648A48B3BB) # David Faure <faure@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DCMAKE_INSTALL_LIBEXECDIR=lib \
    -DBUILD_TESTING=OFF \
    -DBUILD_QCH=ON
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
