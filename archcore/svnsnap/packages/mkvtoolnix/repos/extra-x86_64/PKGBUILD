# $Id: PKGBUILD 314162 2018-01-06 19:38:06Z bpiotrowski $
# Maintainer: Giovanni Scafora <giovanni@archlinux.org>
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: xduugu <xduugu@gmx.com>

pkgbase=mkvtoolnix
pkgname=('mkvtoolnix-cli' 'mkvtoolnix-gui')
pkgver=19.0.0
pkgrel=2
pkgdesc='Set of tools to create, edit and inspect Matroska files'
arch=('x86_64')
url='https://www.bunkus.org/videotools/mkvtoolnix/'
license=('GPL')
makedepends=('boost' 'boost-libs' 'docbook-xsl' 'file' 'flac' 'libebml'
             'libmatroska' 'libogg' 'libvorbis' 'qt5-multimedia' 'ruby' 'zlib')
source=("http://www.bunkus.org/videotools/mkvtoolnix/sources/mkvtoolnix-${pkgver}.tar.xz")
sha256sums=('4b4af84cfdf3062f8ca4ab3c37854be7cd9a71bcf5c46b8ca805a101d414b6a1')

build() {
  cd mkvtoolnix-${pkgver}

  ./configure \
    --prefix='/usr' \
    --disable-qt
  rake apps:mkvinfo $MAKEFLAGS
  mv src/mkvinfo{,-cli}

  ./configure \
    --prefix='/usr' \
    --disable-update-check
  rake $MAKEFLAGS
}

package_mkvtoolnix-cli() {
  depends=('boost-libs' 'file' 'flac' 'gcc-libs' 'glibc' 'libogg' 'zlib'
           'libebml.so' 'libmatroska.so' 'libvorbis.so')

  cd mkvtoolnix-${pkgver}

  rake DESTDIR="${pkgdir}" install
  install -m 755 src/mkvinfo-cli "${pkgdir}"/usr/bin/mkvinfo
  rm -rf "${pkgdir}"/usr/share/{applications,icons,mime,mkvtoolnix}
  find "${pkgdir}" -name mkvtoolnix-gui* -delete
}

package_mkvtoolnix-gui() {
  depends=('desktop-file-utils' 'hicolor-icon-theme' 'mkvtoolnix-cli'
           'qt5-multimedia' 'shared-mime-info' 'xdg-utils')
  replaces=('mkvtoolnix-gtk')

  cd mkvtoolnix-${pkgver}

  rake DESTDIR="${pkgdir}" install
  rm -rf "${pkgdir}"/usr/share/locale
  for t in mkv{extract,info,merge,propedit}; do
    find "${pkgdir}" -name $t* -delete
  done
  # The GUI is disabled in our mkvinfo
  rm "${pkgdir}"/usr/share/applications/org.bunkus.mkvinfo.desktop
}

# vim: ts=2 sw=2 et:
