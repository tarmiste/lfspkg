# $Id: PKGBUILD 310003 2017-11-15 09:32:30Z felixonmars $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: kevin <kevin@archlinux.org>
# Contributor: Tobias Kieslich <tobias@justdreams.de>

pkgname=perl-libintl-perl
pkgver=1.29
pkgrel=1
pkgdesc="Perl Module: Localization support"
arch=('x86_64')
license=('LGPL')
url="http://search.cpan.org/dist/libintl-perl"
depends=('coreutils' 'gettext' 'perl')
options=('!emptydirs')
source=("http://www.cpan.org/authors/id/G/GU/GUIDO/libintl-perl-$pkgver.tar.gz")
sha512sums=('fc3b3e526aa9f5fc9f69da4cbd6653c27fbc380f0139bc5e642c5299def533209cab0112702b1faa13a52b1392f028dbb943b09974bdbc09812f11dee0ccfc7f')

build() {
  cd libintl-perl-$pkgver
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd libintl-perl-$pkgver
  make test
}

package() {
  cd libintl-perl-$pkgver
  PERL_USE_UNSAFE_INC=1 \
  make install DESTDIR="$pkgdir"
# template start; name=perl-binary-module-dependency; version=1;
if [[ $(find "$pkgdir/usr/lib/perl5/" -name "*.so") ]]; then
	_perlver_min=$(perl -e '$v = $^V->{version}; print $v->[0].".".($v->[1]);')
	_perlver_max=$(perl -e '$v = $^V->{version}; print $v->[0].".".($v->[1]+1);')
	depends+=("perl>=$_perlver_min" "perl<$_perlver_max")
fi
# template end;
}
