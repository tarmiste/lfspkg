# $Id: PKGBUILD 306958 2017-10-06 12:01:06Z heftig $
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=gnome-klotski
pkgver=3.22.2+4+ga445c76
pkgrel=1
pkgdesc="Slide blocks to solve the puzzle"
url="https://wiki.gnome.org/Apps/Klotski"
arch=(i686 x86_64)
license=(GPL)
depends=(gtk3 librsvg libgnome-games-support)
makedepends=(intltool itstool gobject-introspection yelp-tools appstream-glib vala git)
groups=(gnome-extra)
_commit=a445c76244d086653d080c52c9c7a63282339398  # master
source=("git+https://git.gnome.org/browse/gnome-klotski#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $pkgname
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
      --disable-schemas-compile
  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}
