# $Id: PKGBUILD 308211 2017-10-18 22:22:40Z heftig $
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Nikolay Rysev <mad.f3ka@gmail.com>

pkgname=fprintd
pkgver=0.8.0
pkgrel=1
pkgdesc="D-Bus service to access fingerprint readers"
arch=(i686 x86_64)
url="https://www.freedesktop.org/wiki/Software/fprint/fprintd"
license=(GPL)
depends=(libfprint dbus-glib polkit)
makedepends=(intltool gtk-doc gnome-common git)
groups=(fprint)
_commit=8eba30992053d045bed534546586bb4f79ff8814  # tags/V_0_8_0^0
source=("git+https://anongit.freedesktop.org/git/libfprint/fprintd#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/^V_//;s/_/./g;s/-/+/g'
}

prepare() {
  cd $pkgname
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $pkgname
  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --libexecdir=/usr/lib \
    --localstatedir=/var \
    --disable-static \
    --enable-gtk-doc
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
  install -d -m 700 "$pkgdir/var/lib/fprint"
}
