# $Id: PKGBUILD 310007 2017-11-15 14:11:34Z foutrelis $
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=xorg-xinput
pkgver=1.6.2
pkgrel=1
pkgdesc="Small commandline tool to configure devices"
arch=(x86_64)
url="https://xorg.freedesktop.org/"
license=('custom')
depends=('libx11' 'libxi>=1.5.99.2' 'xorg-xrandr' 'libxinerama')
makedepends=('xorg-util-macros' 'inputproto' 'libxfixes')
groups=('xorg-apps' 'xorg')
source=(https://xorg.freedesktop.org/archive/individual/app/xinput-${pkgver}.tar.bz2{,.sig})
sha256sums=('3694d29b4180952fbf13c6d4e59541310cbb11eef5bf888ff3d8b7f4e3aee5c4'
            'SKIP')
validpgpkeys=('3C2C43D9447D5938EF4551EBE23B7E70B467F0BF') # "Peter Hutterer (Who-T) <office@who-t.net>"

build() {
  cd xinput-${pkgver}
  ./configure --prefix=/usr
  make
}

package() {
  cd xinput-${pkgver}
  make DESTDIR="${pkgdir}" install
  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
}
