# $Id: PKGBUILD 310007 2017-11-15 14:11:34Z foutrelis $
# Contributor: Eric Belanger <eric@archlinux.org>
# Maintainer:

pkgname=ispell
pkgver=3.4.00
pkgrel=1
pkgdesc="An interactive spell-checking program for Unix"
arch=('x86_64')
url="http://ficus-www.cs.ucla.edu/geoff/ispell.html"
license=('BSD')
depends=('ncurses')
options=('!makeflags')
source=(http://fmg-www.cs.ucla.edu/geoff/tars/${pkgname}-${pkgver}.tar.gz 
	license.txt)
sha256sums=('5dc42e458635f218032d3ae929528e5587b1e7247564f0e9f9d77d5ccab7aec2'
            '359eca8bfb77db63785c4c304ecd3568b26cdf736946388e509933fc8a3f66bd')

build() {
	cd ${srcdir}/${pkgname}-${pkgver}

	sed -i -e 's/#undef USG/#define USG/' \
	       -e 's|/usr/local|/usr|' \
               -e 's|/lib|/lib/ispell|' local.h.linux
	cp local.h.linux local.h
	make TMPDIR=/tmp all
}

package() {
	cd ${srcdir}/${pkgname}-${pkgver}

        # creating directory structure
        install -dm755 ${pkgdir}/usr/{bin,lib/ispell,share/man/man{1,5}}

	# Installing binary tools
	for b in buildhash findaffix icombine ijoin ispell iwhich munchlist tryaffix; do
		install -m755 $b ${pkgdir}/usr/bin/
	done

	# Installing man pages
	install -m644 ispell.1 ${pkgdir}/usr/share/man/man1/ispell.1
	install -m644 ispell.5 ${pkgdir}/usr/share/man/man5/ispell.5

	# Installing dictionnaries
	install -m644 languages/american/americanmed.hash \
	    ${pkgdir}/usr/lib/ispell/americanmed.hash
	install -m644 languages/english/english.aff \
	    ${pkgdir}/usr/lib/ispell/english.aff
	ln -s americanmed.hash ${pkgdir}/usr/lib/ispell/american.hash
	ln -s americanmed.hash ${pkgdir}/usr/lib/ispell/english.hash

	# Installing license
	install -Dm644 ${srcdir}/license.txt \
	    ${pkgdir}/usr/share/licenses/${pkgname}/license.txt
}
