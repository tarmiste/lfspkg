# $Id: PKGBUILD 314683 2018-01-13 13:44:15Z arojas $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kglobalaccel
pkgver=5.42.0
pkgrel=1
pkgdesc='Add support for global workspace shortcuts'
arch=('x86_64')
url='https://community.kde.org/Frameworks'
license=('LGPL')
depends=('kdbusaddons' 'kconfig' 'kcrash')
makedepends=('extra-cmake-modules' 'doxygen' 'qt5-tools')
groups=('kf5')
source=("https://download.kde.org/stable/frameworks/${pkgver%.*}/${pkgname}-${pkgver}.tar.xz"{,.sig})
sha256sums=('e6f5e6588bfcbbbdc62de2756143db34a92ee07adb5ec8a14fa76110c7519f5a'
            'SKIP')
validpgpkeys=(53E6B47B45CEA3E0D5B7457758D0EE648A48B3BB  # David Faure <faure@kde.org>
              2D1D5B0588357787DE9EE225EC94D18F7F05997E) # Jonathan Riddell

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF \
    -DBUILD_QCH=ON
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
