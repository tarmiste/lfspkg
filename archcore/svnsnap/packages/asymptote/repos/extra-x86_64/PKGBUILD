# $Id: PKGBUILD 302876 2017-08-19 14:28:12Z arojas $
# Maintainer: RÃ©my Oudompheng <remy@archlinux.org>
# Contributor: Firmicus <francois.archlinux.org>
# Contributor: bender02 at gmx dot com

pkgname=asymptote
pkgver=2.41
pkgrel=2
pkgdesc="A vector graphics language (like metapost)"
arch=('i686' 'x86_64')
url="http://asymptote.sourceforge.net/"
license=("GPL3")
depends=('texlive-core' 'gc' 'freeglut' 'glu' 'gsl' 'fftw' 'libsigsegv')
makedepends=('ghostscript' 'imagemagick'
             'mesa'               # For OpenGL headers
             'texlive-plainextra' # For texinfo
            )
optdepends=('python2:           for the xasy GUI'
            'python2-pillow:    for the xasy GUI'
            'tix:               for the xasy GUI')
source=("http://downloads.sourceforge.net/sourceforge/${pkgname}/${pkgname}-${pkgver}.src.tgz")
sha1sums=('ee33febaab5b42e0f04375f4a40512102d01a1d5')

prepare() {
  # Disable test that fails in chroot, but works locally
  rm $pkgname-$pkgver/tests/gs/ghostscript.asy && touch $pkgname-$pkgver/tests/gs/ghostscript.asy
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  ./configure --enable-gc=/usr \
              --prefix=/usr \
              --with-latex=/usr/share/texmf/tex/latex \
              --with-context=/usr/share/texmf/tex/context \
              --enable-offscreen
  make all
}

check() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make check-all
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make -j1 DESTDIR="${pkgdir}" install-all
  sed -i -e 's@env python@env python2@' ${pkgdir}/usr/share/asymptote/GUI/*.py
  # this dir contains png files that are already embedded in the pdf documentation:
  rm -rf ${pkgdir}/usr/share/info/asymptote

  # copy some data files to their correct location
  mkdir -p "$pkgdir"/usr/share/emacs/site-lisp
  cp "$pkgdir"/usr/share/asymptote/*.el "$pkgdir"/usr/share/emacs/site-lisp
  mkdir -p "$pkgdir"/usr/share/vim/vimfiles/syntax
  cp "$pkgdir"/usr/share/asymptote/*.vim "$pkgdir"/usr/share/vim/vimfiles/syntax
  mkdir -p "$pkgdir"/usr/share/org.kde.syntax-highlighting/syntax
  cd "$pkgdir"/usr/share/asymptote/
  sh asy-kate.sh
  mv asymptote.xml "$pkgdir"/usr/share/org.kde.syntax-highlighting/syntax
}
