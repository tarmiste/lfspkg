# $Id: PKGBUILD 314394 2018-01-11 16:40:39Z arojas $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=libkmahjongg
pkgver=17.12.1
pkgrel=1
pkgdesc="Common code, backgrounds and tile sets for games using Mahjongg tiles"
url="https://www.kde.org/applications/games/"
arch=(x86_64)
license=(GPL LGPL FDL)
depends=(kconfigwidgets kcompletion qt5-svg)
makedepends=(extra-cmake-modules python)
source=("https://download.kde.org/stable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('986b2b8ec2aa59256e48aaad200486072edfb97897904bec05be1a731b5d1ff5'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DBUILD_TESTING=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
