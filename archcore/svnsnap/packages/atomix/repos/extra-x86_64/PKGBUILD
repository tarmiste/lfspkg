# $Id: PKGBUILD 278358 2016-10-12 09:07:41Z jgc $
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=atomix
pkgver=3.22.0
pkgrel=1
pkgdesc="Build molecules out of single atoms"
url="https://wiki.gnome.org/Apps/Atomix"
arch=(x86_64 i686)
license=(GPL2)
depends=(gtk3)
makedepends=(intltool autoconf-archive appstream-glib git)
groups=(gnome-extra)
_commit=6fb4a9840df2f1e51ed00076e61509ced8c67d00  # tags/3.22.0^0
source=("git://git.gnome.org/atomix#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $pkgname
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var
  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}
