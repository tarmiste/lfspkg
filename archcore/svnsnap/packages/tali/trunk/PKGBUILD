# $Id: PKGBUILD 310007 2017-11-15 14:11:34Z foutrelis $
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=tali
pkgver=3.22.0
pkgrel=1
pkgdesc="Beat the odds in a poker-style dice game"
url="https://wiki.gnome.org/Apps/Tali"
arch=(x86_64)
license=(GPL)
depends=(gtk3 librsvg)
makedepends=(intltool itstool gobject-introspection yelp-tools appstream-glib git)
groups=(gnome-extra)
_commit=985406dcd0f037a0dd279e2fab1a93a233220aad
source=("git://git.gnome.org/tali#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $pkgname
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
      --disable-schemas-compile
  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}
