# $Id: PKGBUILD 306975 2017-10-06 12:02:06Z heftig $
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=gnome-system-monitor
pkgver=3.26.0+1+g97b9578d
pkgrel=1
pkgdesc="View current processes and monitor system state"
url="https://wiki.gnome.org/Apps/SystemMonitor"
arch=(i686 x86_64)
license=(GPL)
depends=(libgtop gtkmm3 librsvg systemd)
makedepends=(itstool docbook-xsl intltool desktop-file-utils appdata-tools gnome-common git)
options=(!emptydirs)
groups=(gnome)
_commit=97b9578d867232b6ba2257bdc66e149f1c7929b3  # master
source=("git+https://git.gnome.org/browse/gnome-system-monitor#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $pkgname
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
    --libexecdir=/usr/lib/$pkgname --enable-systemd
  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}
