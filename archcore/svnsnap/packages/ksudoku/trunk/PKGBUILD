# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=ksudoku
pkgver=17.12.1
pkgrel=1
pkgdesc="A logic-based symbol placement puzzle"
url="https://kde.org/applications/games/ksudoku/"
arch=(x86_64)
license=(GPL LGPL FDL)
groups=(kde-applications kdegames)
depends=(libkdegames glu hicolor-icon-theme)
makedepends=(extra-cmake-modules kdoctools)
conflicts=(kdegames-ksudoku)
replaces=(kdegames-ksudoku)
source=("https://download.kde.org/stable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('75982970efc5d72f99faee5a21cbe0e6dd5f865f2861be27e5df07cf37c8a299'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
