# $Id: PKGBUILD 311989 2017-12-10 03:08:41Z heftig $
# Maintainer: Ionut Biru <ibiru@archlinux.org>

pkgname=gnome-user-docs
pkgver=3.26.2.1
pkgrel=1
pkgdesc="User documentation for GNOME"
url="https://www.gnome.org/"
arch=(any)
license=(FDL)
depends=(yelp)
makedepends=(yelp-tools git gnome-common)
groups=(gnome)
_commit=41ff9395e68de07ce11908da39bba1d05c0b19fc  # tags/3.26.2.1^0
source=("git+https://git.gnome.org/browse/gnome-user-docs#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname

# Fixup missing tag...
  git rev-parse --verify -q 3.22.0 || git tag 3.22.0 01d8e67cc466c667e2f02f605999444a9c17e10c

  NOCONFIGURE=1 ./autogen.sh
}
  

build() {
  cd $pkgname
  ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var
  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}
