# $Id: PKGBUILD 310007 2017-11-15 14:11:34Z foutrelis $
# Maintainer: Eric BÃ©langer <eric@archlinux.org>

pkgname=fvwm
pkgver=2.6.7
pkgrel=2
pkgdesc="A multiple large virtual desktop window manager originally derived from twm"
arch=('x86_64')
url="http://www.fvwm.org"
license=('GPL' 'custom')
depends=('fribidi' 'perl' 'libstroke' 'libxpm' 'librsvg' 'libxinerama' 'libxcursor' 'python2-xdg')
makedepends=('libxslt')
optdepends=('perl-tk: for ClickToFocus support'
            'perl-x11-protocol: for ClickToFocus support')
options=('!emptydirs' '!makeflags')
source=(https://github.com/fvwmorg/fvwm/releases/download/${pkgver}/fvwm-${pkgver}.tar.gz fvwm.desktop)
sha1sums=('c50d530c75f549d2f4270f54ae8ba89a6634bcec'
          '6fd8fd767f32f88cc8b4743a2ba49bcfcdb32bcb')

prepare() {
  cd ${pkgname}-${pkgver}
  # fix symlinks
  sed -e 's|ln -sf|ln -sfr|g' -i default-config/Makefile.am
  autoreconf -vi
}

build() {
  cd ${pkgname}-${pkgver}
  PYTHON=/usr/bin/python2 ./configure --prefix=/usr --sysconfdir=/etc --libexecdir=/usr/lib --with-stroke-library
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
  install -d "${pkgdir}/usr/share/doc/fvwm"
  install -D -m644 ../fvwm.desktop "${pkgdir}/usr/share/xsessions/fvwm.desktop"
  install -D -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}
