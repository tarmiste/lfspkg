# $Id: PKGBUILD 315279 2018-01-23 04:19:04Z heftig $
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=gnome-shell-extensions
pkgver=3.26.2+9+g683b5ee
pkgrel=1
pkgdesc="Extensions for GNOME shell, including classic mode"
url="https://wiki.gnome.org/Projects/GnomeShell/Extensions"
arch=(any)
license=(GPL)
depends=(gnome-shell)
makedepends=(intltool git gnome-common)
optdepends=('nautilus: gnome-classic session')
groups=(gnome)
_commit=683b5eee754d63da43c5d7a94fa5e126f7430050  # gnome-3-26
source=("git+https://gitlab.gnome.org/GNOME/gnome-shell-extensions.git#commit=$_commit"
        "git+https://git.gnome.org/browse/gnome-shell-sass")
sha256sums=('SKIP'
            'SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname

  git submodule init
  git config --local submodule.data/gnome-shell-sass.url "$srcdir/gnome-shell-sass"
  git submodule update

  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $pkgname
  ./configure --prefix=/usr --libexecdir=/usr/lib/gnome-session --enable-extensions=all
  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}
