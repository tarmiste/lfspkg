# $Id: PKGBUILD 315064 2018-01-18 09:41:49Z heftig $
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=gnome-2048
pkgver=3.26.1
pkgrel=1
pkgdesc="Obtain the 2048 tile"
url="https://wiki.gnome.org/Apps/2048"
arch=(x86_64)
license=(GPL3)
depends=(clutter-gtk libgee libgnome-games-support)
makedepends=(intltool vala itstool yelp-tools appstream-glib git)
_commit=0a4020fbb5a8dd0c41c275507b767be1142b47e8  # tags/3.26.1^0
source=("git+https://git.gnome.org/browse/gnome-2048#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $pkgname
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var
  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}
