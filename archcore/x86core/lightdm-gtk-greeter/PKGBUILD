# $Id: PKGBUILD 310007 2017-11-15 14:11:34Z foutrelis $
# Maintainer: Maxime Gauduin <alucrydarchlinux.org>
# Contributor: Padfoot <padfoot@exemail.com.au>

pkgname=lightdm-gtk-greeter
pkgver=2.0.3
pkgrel=1
epoch=1
pkgdesc='GTK+ greeter for LightDM'
arch=('x86_64')
url='https://launchpad.net/lightdm-gtk-greeter'
license=('GPL3' 'LGPL3')
depends=('gtk3' 'lightdm')
makedepends=('exo' 'gobject-introspection' 'intltool')
replaces=('lightdm-gtk2-greeter' 'lightdm-gtk3-greeter')
backup=('etc/lightdm/lightdm-gtk-greeter.conf')
source=("https://launchpad.net/lightdm-gtk-greeter/${pkgver%.?}/${pkgver}/+download/lightdm-gtk-greeter-${pkgver}.tar.gz")
sha256sums=('3db39542cffd54d84c2e1632c1a1668f4f63d8596a6d8fd9fd1649fc7d15db30')

build() {
  cd lightdm-gtk-greeter-${pkgver}

  export CFLAGS="$CFLAGS -Wno-declaration-after-statement"

  ./configure \
    --prefix='/usr' \
    --libexecdir='/usr/lib/lightdm' \
    --sbindir='/usr/bin' \
    --sysconfdir='/etc' \
    --with-libxklavier \
    --enable-kill-on-sigterm \
    --disable-libido \
    --disable-libindicator \
    --disable-static
  make
}

package() {
  cd lightdm-gtk-greeter-${pkgver}

  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
