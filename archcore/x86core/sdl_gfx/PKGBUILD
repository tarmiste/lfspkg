# $Id: PKGBUILD 310007 2017-11-15 14:11:34Z foutrelis $
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>

pkgname=sdl_gfx
pkgver=2.0.26
pkgrel=1
pkgdesc="SDL graphics drawing primitives and other support functions"
url="http://www.ferzkopp.net/wordpress/2016/01/02/sdl_gfx-sdl2_gfx/"
arch=(x86_64)
license=(LGPL)
depends=(sdl)
source=(http://www.ferzkopp.net/Software/SDL_gfx-2.0/SDL_gfx-${pkgver}.tar.gz)
sha256sums=('7ceb4ffb6fc63ffba5f1290572db43d74386cd0781c123bc912da50d34945446')

build() {
  cd SDL_gfx-$pkgver
  ./configure --prefix=/usr \
    $([[ $CARCH == x86_64 ]] && echo --disable-mmx)
  make
}

package() {
  cd SDL_gfx-$pkgver
  make DESTDIR="$pkgdir" install
}
